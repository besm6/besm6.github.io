шифр 419999 зсх^
лен 42(2099)^
лен 43(2099-зп)^
еев1а3
*name spe66.b6
*bemsh
ВВД$$$
СПЭ66 СТАРТ '14000'
 GВLА &В
&В SЕТА 0
 УПОТР СПЭ66(М7)
 АIF (&В ЕQ 1).М40
.М40 АNОР
 КОНД В'0'
 КОНД М41В'3'
 КОНД (21)В'0'
 КОНД В'0'   НЕ ЗАНИМАТЬ
 КОНД В'3775674'
 КОНД В'3365470'
 КОНД В'2755264'
 КОНД В'2345060'
 КОНД В'1734654'
 КОНД В'1324450'
 КОНД В'714244'
 КОНД В'304040'
 КОНД М15В'1'
 КОНД М47В'1'
СЛЭК ПАМ 1
СОХРСП ПАМ 1
НОМ ПАМ 1
R1 ПАМ 1
R2 ПАМ 1
R3 ПАМ 1
 И П77
 УИ М5
 СЧ ТУСП-1(М5)
 УИ М3            АДРЕС ИПЗ ЗАДАЧИ
 СДА 64-П
 И Е48П43
 ЗП R3
 СЧ 0
 ЗП R6
 УИА (М2)
 УИА (М11)
 МОДА ЧУЖЛИС
 УИА (М15)
 СЧИ М15
 ЗП РАБ
* ПВ ЗАПВЫХ(М15)
 СЧ 4(М3)
 УИ М15
 УИА '2000'
 СЧ -1(М15)
 УИА 3
 ЗП R2
 ЗП R4
 УИА -СП+1(М17)
 СДА 64-26         ЕСЛИ СП МАТ,ТО С 21Р(N Н,УСТР,ТР(М)
 СДА 64+6          ЕСЛИ СП В СИСТ,ТО С 21Р- N СП
 ИЛИ R3            ШИФР ЗАДАЧИ(48-43Р)
 ЗП R3
 АIF (&В ЕQ 0).Е1
 СЧ R2
 ЗП ИНФ-32(М5)
 И Е23
 ПО Н
 СЧ Ш1
 ИЛИ Е48-1(М5)
 ЗП Ш1
 СЧ 1(М3)
 ИЛИ П23
 СЛЦ П3
 ЗП 1(М3)
 СЧ Е6
 ИЛИ Е40
 ПВ ЗАПВЕТ(М16)
 ПБ ИСКЛ1
.Е1 АNОР
Н СЧ R2
 И Е24
 ЗП R6
СПДА СЧ СПНОМ+СП-1(М17)
 ПО СП5
 НТЖ R3
 И Е48П16
 ПЕ СВ
 СЧ R6
 ПЕ СП10    СП В ОЗУ ОСТАВИТЬ
 ЗП СПНОМ+СП-1(М17)
 СЧ R
 ВЧ Е1
 ЗП R
 ПБ СПНЕОЗ   НА ВЫЗОВ СП
СП5 СЧИ М17
 ЗП R7
СВ МОДА СПДА          НА ДАЛЬН.ПОИСК
 ЦИКЛ (М17)
СП5Б СЧ R             СЧЕТЧИК СП,НАХОД.В ОЗУ
 СЛЦ Е1
 ЗП R             ЧИСЛО СП В ОЗУ+1=К
 ВЧОБ КСП
 ПО СП20          ЧИСЛО СП В ОЗУ <40(8)
 пб рано
СПНЕОЗ СЧ '11'(М3)
 УИА 34(М17)
 ПО СПОШ          НЕВЕРН.ОБРАЩ.К СП
 УИ М4
 УИА 0
 СЧ (М4)
 СЧ 8(М4)
 УИА 3
 СЧ 0
 ПВ ТБУФ(М15)     ЗАПРОС ЛИСТА
 УИА 3
 СДА 64-8
 УИ М2            АДРЕС НАЧАЛА ЛИСТА
 СДА 64-2
 ИЛИ Е19
 ЗП НОМ
 СЧ R2
 И Е23
 ПЕ рано        было СП26М         СП ИЗ БИБЛ.МАТЕМ.
 СЧИ М2
 СДА 64-2
 ИЛИ NСП1          ТАБЛ.N СП ИЗ БИБЛ.ДИСП.
 СЛЦ НОММЛ1
 ИЛИ Е18
 ЗП NСП           КУС ДЛЯ ЧТЕН.ТАБЛИЦЫ С МБ
 ЗП НОМ
 ПВ ФИЗОБМ(М16)   ЧТЕНИЕ ТАБЛ.НОМЕРОВ СП
 УИА 3
 СЧ R2
 И П377
 УИ М15           N СП
 ПО СП26А         N СП=0
 МОДА (М2)
 СЧ -1(М15)       АДРЕС СП
 ЗП R4
СП26А УИА 35(М17)
 ПО СП1Б          НА ВЫД.ОШИБКИ И СНЯТИЕ ЗАД.
 СДА 64+12
 И П777          ОТНОСИТ.N ЗОНЫ
 СДА 64-24
 СЛЦ НОММЛ1
 СЛЦ NСП1
 ЗП НОМ
СП26 СЧ '11'(М3)      А НАЧ.СП
 УИ М12
 УИА '2000'
 ЗП 5(М12)        5-Я ЯЧ.СП
 УИА 3
 СЧ 4(М3)         М32
 УИА '2000'
 ЗП 1(М12)        В 1-Ю ЯЧ.СП А ВОЗВР.(М32)
 УИА 3
 СЧ 1(М3)
 УИА '2000'
 ЗП 2(М12)
 УИА 3
 СЧ R4
 И П1777
 УИ М13           А НАЧ.СП В ЗОНЕ
 ВЧОБ Е11
 УИ М10
 СЧ РАБ
 ПО ММ
 СЧИ М2
 СДА 64-2
 ИЛИ НОМ
 ИЛИ Е18
 ЗП НОМ
 ПВ ФИЗОБМ(М16)   ЧТЕНИЕ 1-Й ЧАСТИ СП
ММ УИА 3
 МОДА (М2)
 СЧ (М13)         1-Е УКАЗЫВАЮЩ.СЛОВО СП
 И П77777        (ДЛИНА СП НА МБ)
 УИ М14
 ПО СП26А         ДЛ.СП=0
 СЧ R6
 ПО СП9Б          СП НЕ ОСТАВЛ.В ОЗУ
 МОДА (М2)
 СЧ (М13)
 СДА 64+15         16-30 Р.1 УКАЗЫВ.СЛОВА-ДЛИНА
 УИА '2000'        СП ВМЕСТЕ С РАБ.ЯЧ.
 СЛЦ 5(М12)
 ЗП 5(М12)        АДРЕС СВОБ.ЯЧ.
СП9Б УИА 3
 СЧИ М13
 НТЖ П1777
 ПО СП28
 СЧ R4
 МОДА (М2)          В0 2-М УКАЗ.СЛОВЕ-А СП НА МБ
 НТЖ 1(М13)
 И П1777
 УИА 34(М17)       НЕВЕРНО ОБРАЩ.К СП
 ПЕ СП1Б          НА ВЫДАЧУ ОШ-КИ
*СП ЕСТЬ В КАТАЛОГЕ
СП28 МОДА (М2)
 СЧ (М13)
 СЛИА 1(М13)
 УИА '2000'
 ЗП 6(М12)        УКАЗЫВ.СЛОВО СП
 УИА '2003'
 РЕГ '101'
 УИА '2000'
 ЗП 6(М12)
 СЛИА 1(М12)
 УИА 3
 СЛИА -1(М14)
 СЛИА -1(М10)
 МОДА СП28А         ВСЮ СП ПЕРЕНЕСЛИ В ПАМЯТЬ
 ПИО (М14)
 МОДА СП28
 ПИНО (М10)         НЕ ПЕРЕН.ВЫЗВАН.АБЗ.В ОЗУ
 СЧ R2
 И Е23
 ПЕ рано          СП МАТЕМАТИЧ.
 СЧ НОМ
 СЛЦ Е25
СП28В ЗП НОМ
 УИА (М13)
 УИА 1024(М10)
 ПВ ФИЗОБМ(М16)   ВЫЗОВ СЛЕДУЮЩ.АБЗАЦА
 УИА 3
 ПБ СП28
рано ноп
 уиа 3(м17)
СПОШ УИИ М15(М3)
 МОДА ИСКЛ
 УИА (М16)
 ПБ АВОСТА
ЧУЖЛИС УИА 58(М17)
 МОДА СПОШ
 ПИО (М2)
СП1Б СЧ НОМ
 УИИ М2(М17)
 ПВ ТБУФ(М15)     ОТДАТЬ ЛИСТ
 УИИ М17(М2)
 ПБ СПОШ
СП28А СЧ '11'(М3)
 СЛЦ Е4
 ЗП 3(М3)         М33
 ПБ СП7Б          НАИСКЛ.НЕРЕЗ.ИВЫХОД В СП
СП7А ЗП R2            А СП
 СЛЦ Е4
 УИ М16           ВХОД В 10-Ю ЯЧ.СП
 ЗП 3(М3)         ВХОД В 10-Ю ЯЧ.СП
 СЧ 1(М3)         РК СОХР.
 УИА '2000'
 ЗП -6(М16)
 УИА 3
 СЧ '11'(М3)      А СВОБ.ЯЧ.
 УИА '2000'
 ЗП -3(М16)
 УИА 3
 СЧ R2
 ЗП '11'(М3)      А СП
 СЧ 4(М3)         М32(ВОЗВРАТ ПОСЛЕ СП)
 УИА '2000'
 ЗП -7(М16)
 УИА 3
 МОДА ИСКЛ
 ПИО (М2)          БУФЕР НЕ ПРОСИЛИ
СП7Б СЧ НОМ
 ПВ ТБУФ(М15)
ИСКЛ СЧ Е48-1(М5)
 ПВ ВШГП(М15)
ИСКЛ1 СЧ 33(М7)
 НТЖ Е48           + ВРЕМЯ РАБОТЫ ЭКСТРАК.
 СЛЦ 33(М3)
 ЗП 33(М3)
 СЧ Е48
 ЗП 33(М7)
* СЧ Е28
 ПБ висп16      было БЛИСКВ
СП20 СЧ R3
 ИЛИ '11'(М3)
 МОД R7
 ЗП СПНОМ+СП-1
 ПБ СПНЕОЗ
СП10 СЧ СПНОМ+СП-1(М17)
 ПБ СП7А
КСБ КОНД М10В'1777'
NСП1 КОНД М24В'530'
НОМ1 КОНД М17В'1'В'170'     36Т ОС
NСП ПАМ 1
R4 ПАМ 1
R5 ПАМ 1
R6 ПАМ 1
R7 ПАМ 1
РАБ ПАМ 1
 М
 Е
дисп99 ВНЕШ (R,СПR),П777,П3,П377,П7777,П77777,СПНОМ
дисп99 ВНЕШ НОММЛ1,Е39П25,ТУС,ТБУФ,Е48П43
дисп99 ВНЕШ СТРЕЛ,СП,АВОСТА,П7,П1777
дисп99 ВНЕШ П177,КСП,
дисп99 ВНЕШ Е48П16
дисп99 ВНЕШ П77,ТУСП
дисп99 ВНЕШ ВШГП,висп16
дисп99 ВНЕШ Е48П17
дисп99 ВНЕШ ФИЗОБМ,П
СОСТАВ ВНЕШ ТОП,ТБД,ТБДТР,ШЗНМБ,ССТБД
*
*       $Log: spe66.bs,v $
*       Revision 1.1  1999/02/02 03:35:27  mike
*       Initial revision
*
*
*ЧП
 ФИНИШ ,             В ВАШЕМ ФИНИШЕ ОШИБКА ИЛИ ЕГО НЕТ
КВЧ$$$
ТРН$$$
ЧТКОМП420070
БТМАЛФ
ЗОНМОД430150  0001
КНЦ$$$
*end file
``````
ЕКОНЕЦ

* ОТЛАДЧИК ОС ДИСПАК. БЛОК ОБРАБОТКИ И ВЫПОЛНЕНИЯ ДИРЕКТИВ
* ПОЛЬЗОВАТЕЛЯ. ВЫЗЫВАЕТСЯ В РАБОТУ БЛОКОМ НРОТ.
 Б
ОВД СТАРТ ’10000’
 УПОТР ОВД(М6)
* БЛОК РАСШИФРОВКИ ОПЕРАЦИИ
РОП1 СЧ 0             Б Л И С К В  -  ! ! ! ! !
 ЗП R
 ЗП R1
 ЗП R2
 МОДА ПР3
 ПВ (М12)         ПРОГОН ПРОБЕЛОВ
 СДА 64+3
 ПЕ РОП5          1-Й СИМВОЛ - НЕ ЦИФРА
 УИА (М16)         * ОПЕРАЦИЯ <АДР> *
 МОДА АДР
 УИА (М14)
 ПБ РRNТ
РОП5 УИА -2(М16)       ОПЕРАЦИЯ ОПРЕДЕЛЯЕТСЯ ПО 1÷3 СИМВ.
 УИА SW-ЕNDSW(М4)
 МОДА ПР2
 ПВ (М11)
 НТЖ П377
 ПО РОП2А         ЕТХ ≡ ОПЕРАЦИЯ ВЫБРАНА
 СЧ СИМВ
 СДА 64+5
 ПО РОП2А         НЕ БУКВА (≤’37’) ≡ ОПЕРАЦИЯ ВЫБРАНА
 МОДА ПР1
 ЦИКЛ (М16)         ЗА ОЧЕРЕДНЫМ СИМВОЛОМ ОПЕРАЦИИ
РОП2А УИА Е24П1(М11)    СКОЛЬКО СИМВОЛОВ В ’ОПЕРАЦИИ’
 МОДА РОП2
 ПИО (М16)         ТРИ
 УИИ М11(М16)
 СЛИА 1(М11)
 МОДА РОП6
 ПИО (М11)         ДВА
 СЧ R
 СДА 64+5
 ПО ОШ-НРОТ(М7)   НЕ БУКВА - КЧ БОЛЬШЕ НЕ БУДЕТ
 СЧ ШРОП
 МОД R
 И Е48-’40’
 ПО ОШ-НРОТ(М7)   ’ОПЕРАЦИЯ’ НЕ ОПРЕДЕЛЯЕТСЯ ПО 1 СИМВ.
 УИА -8(М11)
РОП6 СЧ R
 СДА 64-8(М11)
 ЗП R
 СЧ Е24П1
 СДА 64-8(М11)
 И Е24П1
 ЗП И16
 МОДА И16
 УИА (М11)
РОП2 СЧ R             ПОИСК ОПЕРАЦИИ
 МОДА ЕNDSW
 НТЖ (М4)
 И (М11)
 ПЕ РОП3          НЕ НАШЛИ ОПЕРАЦИЮ В ПЕРЕКЛЮЧАТЕЛЕ
 УИИ М14(М4)
 МОДА ЕNDSW
 СЛИА (М14)
 СЛИА ЕNDSW-SW(М4)
 МОДА РRNТ
 УИА (М12)
 ПБ ПРОГОН
РОП3 МОДА РОП2
 ЦИКЛ (М4)          ПРОСМОТР СПИСКА ОПЕРАЦИЙ НЕ ОКОНЧЕН
 СЧ СИМВ
 ЗП И16
 СЧ R
 СДА 64+16
 ЗП СИМВ
 УИА НЕАД-ОВД(М17) ДЛЯ ОПЕРАЦИИ =
 МОДА РШКЛЧ
 ПВ (М12)         РАСШИФРОВКА ОПЕРАЦИЙ СМ,МР,РК
 СЧ И16
 ЗП СИМВ
 МОДА ПР3+1
 УИА (М11)
 МОДА ПР3+1
 ПВ (М12)
РОП7 МОДА ВЫПР5
 УИА (М14)
 МОДА РRNТ
 УИА (М12)
РОП4 МОДА ПР3           РАСШИФРОВКА ОПЕРАЦИЙ := И =
 УИА (М11)
 НТЖ П37
 ПО С             :=М4=0 - ПРОГОН ДО ПРАВОЙ ЧАСТИ
 НТЖ П12
 ПЕ ОШ-НРОТ(М7)   ОПЕРАЦИИ НЕТ
 УИА 1(М4)
 ПБ ПР3           = М4≠0 - ПРОГОН ДО ВИДА ВЫДАЧИ
РRNТ МОДА КНЦ           ’АДУХВН’:=<КНЦ>.
 УИА (М15)
 СЧИ М15
 ПВ ЗАПВЫХ(М15)
 УИА ’2000’        ТРЕБУЕТСЯ ЛИ ВЫДАЧА НА АЦПУ *
 СЧ 4(М2)
 УИА 3
 ЗП РЕЖРАБ
 РЖА ’13’
 ПЕ (М14)         <ПВА> НЕТ
 СЧИ М16
 ЗП И16
 СЧ НТ-НРОТ(М7)   ВЫДАЧА ПРИНЯТОЙ ДИРЕКТИВЫ НА АЦПУ
 СДА 64-8
 СЧМР
 УИ М15
 СЧ ТСЛ(М15)
 СДА 64+10
 И П37
 ЗП Э64-НРОТ(М7)
 СЧ ТСЛ(М15)
 И П1777
 УИ М15
 СЧИ М6
 ИЛИ Э64-НРОТ(М7)
 ЗП Э64-НРОТ(М7)
 Э64 РRINТ-ОВД+2048
 МОД И16
 УИА (М16)
 ПБ (М14)
* ПЕРЕКЛЮЧАТЕЛЬ БЛОКА РАСШИФРОВКИ ОПЕРАЦИЙ
SW ПБ ОСТ
 КОНК П’УЗЕ’
 ПБ ОСТ
 КОНК П’КРА’
 ПБ ОСТ
 КОНК П’ЗП0’
 ПБ ОСТ
 КОНК П’СЧИ’
 ПБ ВЫП
 КОНК П’ВЫП’
 ПБ СНЯТЬ
 КОНК П’СНЯ’
 ПБ АДР
 КОНК П’АДР’
 ПБ ИДИ
 КОНК П’ИДИ’
 ПБ ИР
 КОНК П’ИР0’
 ПБ АЦПУ
 КОНК П’АЦП’
 ПБ ПРО
 КОНК П’ПРО’
 ПБ ЛИН
 КОНК П’ЛИН’
 ПБ ЦИКЛ
 КОНК П’ЦИК’
 ПБ ПВ
 КОНК П’ПВ0’
 ПБ Э70
 КОНК П’Э70’
 ПБ ДИС
 КОНК П’VТ0’
 ПБ ДИП74
 КОНК П’ЛИС’
 ПБ ПВА
 КОНК П’ПВА’
 ПБ ИПО
 КОНК П’ИПО’
 ПБ ВЫЗКД
 КОНК П’НАЙ’
 ПБ ГДЕ-НРОТ(М7)
 КОНК П’ГДЕ’
 ПБ НОВ
 КОНК П’НОВ’
ЕNDSW ПБ КНЦ1
 КОНК П’КНЦ’
ВЫЗКД УИА 0(М10)   ПРИЗНАК ПОИСКА
 УИА 1(М5)   ПЕРЕКЛ.В НРКОД-Е
 ПБ ВЫЗКОД-НРОТ(М7)
ЗПУЗ УИА ’2000’        ЗАПИСЬ ПРИЗНАКА КОНЦА ИПО (0)
 ЗП (М2)          УСТАНОВКА УЗЛА -
 СЧ (М16)         - ЗАПИСЬ КОМАНДЫ ЧИСЛОМ - БУДЕТ КК
 ЗП (М16)
 УИА 3
 УИИ М2(М14)
 СЧ КТУС-НРОТ(М7)
 УИ М3
ОСТ УИА (М17)         М17=0 - РАСШИФРОВКА АДРЕСА ОСТАНОВА
 СЧ
 ЗП R1
 ЗП R2
 МОДА РИТА
 ПИО (М4)          <УЗЕЛ>
 СЧ СИМВ
 НТЖ П377
 ПО РА3А          СНЯТИЕ КРА ИЛИ ЗПСЧ
* БЛОК РАСШИФРОВКИ ИНФОРМАЦИИ ТИПА’АДРЕС’
РИТА МОДА ВОИН1
 ПВ (М12)         R:= АДРЕС
 СЧ R
 МОДА РА1А
 ПИНО (М17)
 ПО ОШ-НРОТ(М7)   АДРЕС=0
РА1А СДА 64(М17)
 ИЛИ R1
 ЗП R1
РА1 СЧ СИМВ
 НТЖ СКОБЛ
 ПО РА2           КРАТНЫЙ ОСТАНОВ ИЛИ АДРЕС С ИР-ОМ
 МОДА РА3А
 ПИО (М17)         ПРОСТОЙ ОСТАНОВ
 ПБ (М5)          АДРЕС БЕЗ ИР-А
РА2 МОДА ПР3
 ПВ (М12)         ПРОПУСК СКОБКИ
 СДА 64+3
 ПЕ УСЛОВ         ОСТАНОВ ПО УСЛОВИЮ
КРАТИР МОДА ВОИН1
 ПВ (М12)         R:= КРАТНОСТЬ ОСТАНОВА ИЛИ N ИР-А
 МОДА ИРЕГ
 ПИНО (М17)         <R> = N ИР-А
 СЧ СИМВ          (=) *
 НТЖ П25
 ПЕ ИРЕГ          ’НОРМАЛЬНАЯ’ КРАТНОСТЬ
 СЧ R1            (Е41) УСТАНОВКА ОСТАНОВА
 ИЛИ Е41           ’ПО ИСЧЕРПАНИЮ КРАТНОСТИ’
 ЗП R1
 МОДА С
 ПВ (М11)         ПРОГОН (=)
ИРЕГ МОДА ПРОКС
 ПВ (М12)         ПРОГОН ПРАВОЙ СКОБКИ
ИНД СЧ R
 СДА 64-15(М17)
 ИЛИ R1
 ЗП R1
 МОДА РА3
 ПИО (М17)         КРАТНЫЙ ОСТАНОВ
 СЧ R
 ПО ОШ-НРОТ(М7)   N ИР-А=0
 СДА 64+4
 ПЕ ОШ-НРОТ(М7)   N ИР-А > ’17’
 СЛИА -15(М17)
 ПБ (М5)
УСЛОВ МОДА ОШ-НРОТ(М7)
 ПИНО (М17)         N ИР-А ≠ ЦИФРА
* БЛОК РАСШИФРОВКИ УСЛОВИЯ ОСТАНОВА
 МОДА УСЛАД1
 УИА (М5)
 МОДА УСЛ1
 УИА (М12)         ВЫХОД ИЗ РШК
РШКЛЧ УИА (М16)         РАСШИФРОВКА ЛЕВОЙ ЧАСТИ
 СЧ РМСИА         РК,МР,СМ,ИР,АД
РШК ЗП R3            РАСШИФРОВКА ПО ШКАЛЕ
 И П377
 НТЖ СИМВ
 ПО (М12)         ВЫХОД
 СЛИА 1(М16)
 СЧ R3
 СДА 64+8
 ПО ОШ-НРОТ(М7)
 ПБ РШК
УСЛ1 МОДА ПР3           ПРОПУСК ИДЕНТИФИКАТОРА ЛЕВОЙ ЧАСТИ
 ПВ (М12)
 НТЖ П377
 ПО ОШ-НРОТ(М7)   КОНЕЦ СООБЩЕНИЯ
 СЧ СИМВ
 СДА 64+5
 ПЕ С             ПРОПУСКАЕМ БУКВУ
 УИА -15(М17)
 СЧИ М16
 ЗП R3
 ПО РИТА          АД
 НТЖ Е1
 ПЕ УСЛ3          НЕ ИР
 МОДА УСЛ3
 УИА (М5)
ИНДЕР МОДА ИНД           СБОРКА N ИР-А
 УИА (М12)
 УИА -2(М16)
 ПБ ВОИН
УСЛ3 СЧ R3            ФОРМИРОВАНИЕ СЛОВА ИПО
 СДА 64-3
 УИ М16
 СЧ ШКЗНУ
 МОДА РШК
 ПВ (М12)         РАСШИФРОВКА ЗНАКА УСЛОВИЯ
 МОДА ПР3
 ПВ (М12)         ПРОГОН ДО НАЧАЛА ПРАВОЙ ЧАСТИ
 СЛИА ’300’(М16)
 УИИ М15(М16)
 МОДА ЛИТ
 ПВ (М5)          R2:= ПРАВАЯ ЧАСТЬ
 МОДА ПРОКС
 ПВ (М12)         ПРОГОН ПРАВОЙ СКОБКИ
 МОДА УСЛ4
 ПИО (М17)         ПРАВАЯ ЧАСТЬ - СОNSТ
 СЛИА -’300’(М15)
 СЧИ М15
 СДА 64+3
 ПО УСЛ4          СРАВНЕНИЕ <АДРЕС> С Ч
 НТЖ Е2
 ПО УСЛ4          СРАВНЕНИЕ <СМ> С Ч
 ПБ ОШ-НРОТ(М7)   ПОПЫТКА СРАВНЕНИЯ РК,РМР,ИР С Ч
УСЛ4 СЧИ М15           КОНЕЦ ФОРМИРОВАНИЯ
 СДА 64-35
 ИЛИ R1
 ИЛИ Е48           Е48 - ПРИЗНАК УСЛОВИЯ
 ЗП R1
РА3 И П77777        R:= АДРЕС ОСТАНОВА
 ЗП R
РА3А СЧИ М4
 ПО РА5           УЗЕЛ
 И Е2            М2 :=АДРЕС ИПО ДЛЯ КРА,ЗПСЧ
 УИ М16
 СЛИ М2(М16)
 ПБ РА6
РА5 МОДА СНЯТЬ1        ГАШЕНИЕ НА ИПО ИНФОРМАЦИИ К УЗЛУ
 ПВ (М12)         <R>, ЕСЛИ ОН БЫЛ УСТАНОВЛЕН РАНЕЕ
РА5А УИИ М2(М10)       М2:=АДРЕС ИПО ДЛЯ УЗЛА
 УИИ М16(М2)
РА6 УИА -1(М17)
 СЧ R1
 ПО РА4А          СНЯТИЕ КРА ИЛИ ЗПСЧ
 И П77777        ПОСЛЕДНЯЯ ПРОВЕРКА
 ЗП R4
 СЧ АВОС(М3)
 СДА 64-15
 СЧМР
 СЛЦ Е1            ВНИМАНИЕ: СЛЦ ’ДЕЛЬТА ИПО’
 ВЧОБ R4
 СДА 64+1
 ПО ОШ-НРОТ(М7)   ОСТАНОВ В 0-Ю ИЛИ 1-Ю ЯЧЕЙКУ ИПО
 СЛИА 2(М16)
 СЧ R1
 СДА 64+15
 ПЕ РА6Б          ОСТАНОВ НЕ ПРОСТОЙ
 МОДА РА4А
 ПИНО (М4)          КРА,ЗПСЧ - ГАШЕНИЕ ИПО
ЗПОСТ СЧ R1            ЗАПИСЬ ОСТАНОВА
 УИ М16
 МОДА ПРОЧЛИ
 ПВ (М5)          ЧУЖОЙ ЛИСТ*
ЗПОСТ1 СЧИ М4
 ПО ЗПУЗ          УЗЕЛ
 И Е2
 ПО КЗС           КРА
 СЛИА -2(М4)
 СЧ 5(М3)
 ИЛИ Е20           УСТАНОВКА ПРИЗНАКА ЗП(М29)-5Р М17e
 МОДА ЗП1
 ПИО (М4)
 НТЖ Е20           ГАШЕНИЕ ПРИЗНАКА ЗП(М29)
ЗП1 ЗП 5(М3)
 СЛИА 1(М3)
КЗС УИА ’2003’
 СЧ 22(М3)        ЗАПИСЬ В ИПЗМ НОВОГО ОСТАНОВА
 И Е48П16        ПО КРА, ЗПСЧ (М28e, М29e)
 ЗП R
 СЧ R1
 И П77777
 ИЛИ R
 ЗП 22(М3)
 УИА 3
 ПБ ДВ-НРОТ(М7)   СТРОКУ ОБРАБОТАЛИ
РА6Б И Е33
 ПЕ РА6А          ОСТАНОВ ПО УСЛОВИЮ
 СЛИА -1(М16)
 УИА (М17)
РА6А МОДА РА4
 ПИНО (М4)          НЕ УЗЕЛ
 МОДА ПРОЧЛИ
 ПВ (М5)          НЕ ПЕРЕПОЛЗЛО ЛИ ИПО В Ч.ЛИСТ
РА4 СЧ R1
РА4А УИА ’2000’        ЗАПИСЬ ИНФОРМАЦИИ НА ИПО
 ЗП (М2)
 УИА 3
 СЛИА 1(М2)
 СЧ R2
 МОДА РА4А
 ЦИКЛ (М17)
 СЧ R1
 ПЕ ЗПОСТ         НЕ СНЯТИЕ
 СЧИ М4
 ПО ОШ-НРОТ(М7)   УЗЕЛ =0
 И Е2
 ПО КЗС           СНЯТИЕ КРА
 УИА ’2000’        ГАШЕНИЕ ХРАНЯЩЕГОСЯ В 1-М
 СЧ -5(М2)        СЛОВЕ ИПО СОДЕРЖИМОГО М35
 УИА 3
 И Е48П16
 УИА ’2000’
 ЗП -5(М2)
 УИА 3
 СЛИА 1(М3)
 ПБ КЗС           СНЯТИЕ ЗПСЧ
ВЫП УИА -24(М17)      НЕСТАНДАРТНОЕ ДЕЙСТВИЕ
 СЛИА -2(М2)
 УИИ М10(М2)
 СЧ
 ЗП R3
 МОДА ЛИТ8
 ПВ (М4)          ЗАПИСЬ КОМАНДЫ НА ИПО
ВЫПНД СЧИ М2            ИЗМЕНЕНИЕ М33
 НТЖ 3(М3)
 ПЕ ВЫПНД2        АВОСТ У ПОЛЬЗ.ИЛИ ПОВТОРНАЯ <ВЫП>
 УИА ’2000’        <М33> = АИПО - ПЛАНОВЫЙ ОСТАНОВ
 СЧ 1(М2)
 УИА 3
 УИ М15
 СДА 64+24
 УИ М16           ПОДПРАВКА М33:= АП
 СЛИА -1(М16)
 МОДА ВЫПНД4
 ПИО (М15)         БЫЛ УЗЕЛ,КРА ИЛИ УЖЕ СНЯТЫЙ ЗПСЧ
 СЧ 23(М3)        ОСТАНОВ ПО ЗПСЧ НЕ СНЯЛСЯ
 И П77777
 ПЕ ВЫПНД4        УСТАНОВЛЕН НОВЫЙ ОСТАНОВ ПО ЗПСЧ
 СЧИ М15           ВОССТАНОВЛЕНИЕ М35
 ИЛИ 23(М3)
 ЗП 23(М3)
ВЫПНД4 СЧИ М16           ВОССТАНОВЛЕНИЕ М33
 ЗП 3(М3)
ВЫПНД2 СЧ 3(М3)         ЗАПИСЬ М33 НА ИПО
 И П77777
ВЫПНД3 УИА ’2000’        М33:= АИПО
 ЗП 1(М2)
 УИА 3
 СЧИ М2
 ЗП 3(М3)
 СЧ 5(М3)         ГАШЕНИЕ ПРК И М20
 И Е48П16
 ЗП 5(М3)
 СЧ
 ЗП 6(М3)
 МОДА ОТК2-НРОТ(М7)
 УИА (М13)
 ПБ ОТКР          ОТКРЫТИЕ ЗАДАЧИ
ИПО СЧ НЗ-НРОТ(М7)   М14:= N ЗАДАЧИ
 НЕД
 УИ М14
 УИА -15(М17)
 МОДА РИТА
 ПВ (М5)          ЗА НОВЫМ АДРЕСОМ
 СЧ R1
 СДА 64+15
 И П77777
 ПО ОШ-НРОТ(М7)
 УИ М16
 СДА 64+10
 МОДА ПРОЧЛИ        ПРОВЕРКА НА ЧУЖОЙ ЛИСТ
 ПВ (М5)
 СЧ АВОС(М3)
 СДА 64-15         ЗАМЕНА   А И П О
 СДА 64+15
 ЗП АВОС(М3)
 УИИ М2(М16)
 СЧ R1
 СДА 64-18
 ИЛИ АВОС(М3)
 ЗП АВОС(М3)
 ПБ РИПО1-НРОТ(М7)
ПРО СЧ АВОС(М3)      ПРОПУСТИТЬ ВЫДАЧУ НА АЦПУ
 ИЛИ Е32
 ЗП АВОС(М3)
АЦПУ СЧ НЗ-НРОТ(М7)   ПЕЧАТЬ НАКОПЛЕННОЙ ИНФОРМАЦИИ
 НЕД 0             NЗАДАЧИ->ИПЗ+25, 00 064 0000
 УИ М16
 СЛИА ПЗ(М16)
 ЗП ИЗАХВ(М7)
 Э64
     ПБ ДВ-НРОТ(М7)
НОВ СЧ АВОС(М3)      ’ПОВТОРНЫЙ ВВОД’ ЗАДАЧИ
 ИЛИ Е33           Е33 -> АВОС; ГАШЕНИЕ ПРИЧИНЫ СНЯТИЯ.
 ЗП АВОС(М3)
 СЧ ’44’(М3)
 СДА 64-7
 СДА 64+7
 ЗП ’44’(М3)
 ПБ КНЦ
СН3 УИИ М2(М14)       ВОССТАНОВЛЕНИЕ М2
СНЯТЬ МОДА ВОИН1
 ПВ (М12)         R:= АДРЕС СНИМАЕМОГО ОСТАНОВА
 СЛИА СН3-СНЯТЬ-1(М12)
 СЧ R
 УИ М16
 МОДА СНЯТЬ1
 УИА (М4)
ЗПКОМ УИА ’2000’        БЛОК ПЕРЕЗАПИСИ КОМАНДОЙ
 СЧ (М16)
 УИА ’2003’
 РЕГ ’101’
 УИА ’2000’
 ЗП (М16)
 УИА 3
 РЕГ ’107’
 МОДА ДВ-НРОТ(М7)
 ПИО (М4)          ДИРЕКТИВА ПРИСВАИВАНИЯ
 ПБ (М4)
СНЯТЬ1 УИИ М14(М2)       УПРЯТЫВАНИЕ М2
 СЛИА 5(М2)
СН1 УИА ’2000’        R2:= ОЧЕРЕДНОЙ ОСТАНОВ С ИПО
 СЧ (М2)
 УИА 3
 УИИ М10(М2)
 ПО (М12)         НЕТ ОСТАНОВА <R> V ИПО=>ОН ПРОСТОЙ
 СЛИА 1(М2)
 НТЖ Е48
 ПО СН1           СЛЕД ОТ ОСТАНОВА СНЯТОГО В <ОБОС>
 НТЖ R             АНАЛИЗ <R2>
 РЖА ’13’
 ПО СН2           ОСТАНОВ КРАТНЫЙ
 СЛИА 1(М2)
СН2 И П77777
 ПЕ СН1           <R> ≠ <R2>
* БЛОК УПЛОТНЕНИЯ ИНФОРМАЦИИ ОБ УЗЛАХ НА БУФЕРЕ МАТЕМАТИКА
УПИ УИА ’2000’
 СЧ (М2)
 УИА 3
 ПО УПИ1          ЗАПИСЬ ПРИЗНАКА КОНЦА ИПО (0)
 НТЖ Е48
 ПО УПИ2          СНЯТ УЗЕЛ
УПИ1 УИА ’2000’        ПЕРЕСЫЛКА ↑
 СЧ (М2)
 ЗП (М10)
 УИА 3
 ПО (М12)         ВЫХОД
 СЛИА 1(М10)
УПИ2 СЛИА 1(М2)         ПРОПУСК СЛОВА
 ПБ УПИ
ВОИН1 СЧ СИМВ          СБОРКА АДРЕСА
 НТЖ П377
 ПО ДВ-НРОТ(М7)   СТРОКУ ОБРАБОТАЛИ
 СЧ СИМВ
 СДА 64+3
 ПЕ ОШ-НРОТ(М7)   НЕ ЦИФРА
 УИА -5(М16)
 ПБ ВОИН
ЛИН УИА (М4)          ТРАССИРОВКА ПРОГРАММЫ ПОЛЬЗОВАТЕЛЯ
ИДИ СЧ ’44’(М3)      ОПЕРАЦИЯ ПЕРЕДАЧИ УПРАВЛЕНИЯ
 СДА 64+41
 ПО ИДИ5А         БЫЛ КЗ
 УИ М16           М16:= N ПРИЧИНЫ ОСТАНОВА
 МОДА ИДИ5
 ПИНО (М4)          <ИДИ>
 СЧ РАЗТРА        <ЛИН> (КК,КРА)
 И Е48(М16)
 ПЕ ИДИ5          ТРАССИРОВКА РАЗРЕШЕНА (М4=0)
 УИА -1(М4)        ИДИ ≡ ЛИН (М4≠0)
ИДИ5 СЛИА -14(М16)      ВИД ОСТАНОВА
 СЧ ФАТОШ
 И Е48(М16)
 ПЕ КНЦ           ФАТАЛЬНАЯ ОШИБКА
 СЛИА -63(М16)
 МОДА КНЦ
 ПИО (М16)         НЕТ ИНФОРМАЦИИ ДЛЯ ОТЛАДЧИКА
 СЛИА 63(М16)
ИДИ5А СЛИА 5(М2)         ВЫПОЛНЕНИЕ ДИРЕКТИВЫ <ИДИ>
 УИИ М10(М2)
 СЧ СИМВ
 СДА 64+3
 УИА (М12)
 ПЕ ИДИ1          НЕ ЦИФРА - ПРОДОЛЖИТЬ
 СЛИА -7(М16)
 СЧИ М16
 СДА 64+1
 ПЕ ИДИ1Б         ОСТАНОВ НЕ ПО ЗПСЧ
 СЧ 23(М3)
 И П77777
 ПЕ ИДИ1Б         УСТАНОВЛЕН НОВЫЙ ОСТАНОВ ПО ЗПСЧ
 УИА ’2000’        ВОССТАНОВЛЕНИЕ М35
 СЧ -6(М2)
 УИА 3
 И П77777
 ИЛИ 23(М3)
 ЗП 23(М3)
ИДИ1Б МОДА ВОИН1
 ПВ (М12)         ЗА АДРЕСОМ ВХОДА
 СЧ R
 УИ М16
 МОДА ПРОЧЛИ
 ПВ (М5)          АДРЕС ВХОДА В Ч.ЛИСТЕ*
 СЧ R             ИЗМЕНЕНИЕ М33, ГАШЕНИЕ М20
 ЗП 3(М3)
 СЧ
 ЗП 6(М3)
ИДИ1 СЧ СИМВ          ВХОД НА ЛЕВУЮ ИЛИ ПРАВУЮ *
 УИ М16
 СЛИА -’57’(М16)
 СЧ 5(М3)
 ИЛИ Е9
 МОДА ИДИ1А
 ПИО (М16)         НА ПРАВУЮ - УСТАНОВКА ПРК
 МОДА ИДИ2
 ПИО (М12)         <ИДИ≡> - ГДЕ СТОИМ
 НТЖ Е9            НА ЛЕВУЮ - ГАШЕНИЕ ПРК
ИДИ1А ЗП 5(М3)         ЗАПИСЬ В М27 (ИПЗМ)
 СЧИ М16
 ПЕ ИДИ2          ИДЕМ НА ЛЕВУЮ
 ЗП 6(М3)         ГАШЕНИЕ М20 В СЛУЧАЕ <ИДИ П≡>
ИДИ2 МОДА УПИ
 ПВ (М12)         УПЛОТНЕНИЕ ИНФОРМАЦИИ ОБ УЗЛАХ
 СЧ 5(М3)
 И Е9
 ПО ИДИ2Б         ВХОД НА ЛЕВУЮ
 СЧ 3(М3)         СРАВНЕНИЕ М33 С М34(КРА)
 НТЖ 22(М3)
 И П77777
 ПЕ ИДИ2Б         М33 ≠ М34
 СЧ 22(М3)        СНЯТИЕ КРА -
 И Е48П16        -ОСОБЕННОСТИ АППАРАТУРЫ
 ЗП 22(М3)
ИДИ2Б МОДА ИДИ2А
 ПИНО (М4)          ВЫПОЛНИЛИ ДИРЕКТИВУ <ИДИ>
* ВЫПОЛНЕНИЕ ДИРЕКТИВЫ <ЛИН>: УСТАНОВКА УЗЛА В БЛИЖАЙШЕЕ
* СЛОВО, СОДЕРЖАЩЕЕ ПЕРЕДАЧУ УПРАВЛЕНИЯ (ПУ).
 МОД 3(М3)
 УИА (М16)
 УИА (М4)
 СЧ АВОС(М3)
 СДА 64-15
 СЧМР
 СЛЦ Е1            В Н И М А Н И Е - ДЕЛЬТА ИПО
 УИ М2
 НТЖ 3(М3)
 ПЕ ЛУЧ1          КОМАНДА НЕ НА ИПО
 УИА 0             ПОЛУЧЕНИЕ ДЕЙСТВИТЕЛЬНОГО
 СЧ 1(М2)         АДРЕСА ОСТАНОВА
 УИА 3
 СДА 64+24
 УИ М16
 СЛИА -1(М16)
ЛУЧ1 УИА (М14)
 СЧ 5(М3)
 И Е5
 ПО ЛУЧ2          ПРИК(РК) =0, М14:=0 ≡ М20=0
 СЧ 6(М3)         М14:=<М20>
 УИ М14
ЛУЧ2 СЧ 5(М3)         ПРК*
 И Е9
 УИА 24(М12)
 ПЕ СОК1          ВХОД НА ПРАВУЮ
 УИА 48(М12)
 ПБ СОК1
СОК3 МОДА СОК2
 ПИНО (М12)         ЗА ПРАВОЙ КОМАНДОЙ
СОК СЛИА 1(М16)        СЧИТЫВАНИЕ ОЧЕРЕДНОЙ КОМАНДЫ
 УИА 48(М12)
 УИА 1(М4)
СОК1 МОДА ПРОЧЛИ        IF КОМАНДА В ЧУЖОМ ЛИСТЕ,  GО ТО ’ОШ’
 ПВ (М5)          R4:=КОМАНДА
СОК2 СЛИА -24(М12)      АНАЛИЗ КОМАНДЫ
 СЧ R4
 СДА 64(М12)
 И Е20
 ПО КО1С          КОМАНДА 1Й СТРУКТУРЫ
 СЧ R4
 СДА 64+15(М12)
 И П37
 ВЧ П26
 ПЕ СОК3          НЕ ПУ
АПУ УИ М1            АНАЛИЗ КОМАНДЫ ПЕРЕДАЧИ УПРАВЛЕНИЯ
 МОДА АПУ2
 ПИНО (М4)          НАШЛИ КОНЕЦ ВЕТВИ
 СДА 64+1          ОСТАНОВ БЫЛ НА ПУ
 ПО АПУ1          ПО,ПЕ
 НТЖ П3
АПУ1 УИ М10           М10=0 - ПРИЗНАК УСЛОВНОЙ ПУ. ->
 НТЖ П7
 ПЕ ВЕGLIN        НЕ ЦИКЛ
 СЧ РЕЖРАБ        ЦИКЛ РАСКРУЧИВАТЬ *
 И Е6
 УИ М10
 ПО ВЕGLIN        ДА, М10:=0
 ПБ СОК3          НЕТ
ЛУЧ3 УИ М16           ПОИСК НАЧАЛА ВЕТВИ,IF ПУ - ПРАВАЯ
 СДА 64+24
 УИ М11
 СДА 64+20
 ЗП R3
 СЧ R4
 СДА 64-4
 РЖА ’13’
 ПЕ К1С           ЛЕВАЯ КОМАНДА 1Й СТРУКТУРЫ
 СДА 64+43
 ВЧОБ П25
 ПЕ ПОИСК         ЛЕВАЯ КОМАНДА - ПУ -УЖЕ ОБРАБОТАЛИ
 УИ М5
 НТЖ Е2
 ПЕ ЛУЧ4          НЕ МОД (23)
 СЛИ М11(М14)      АИСП:= А + <М20>
 СЧ R3
 ПО МОД2          МОД БЕЗ ИР-А
 НТЖ П17
 УИ М5
 ПЕ МОД1          N ИР-А ≠’17’
 МОДА МОД1
 ПИНО (М11)         АИСП ≠ 0
 УИА -1(М11)       МОД С МАГАЗИНОМ, АИСП:=-1
МОД1 МОДА 7(М3)         АИСП:= АИСП + <ИР>
 МОД (М5)
МОД2 СЛИА (М11)
 УИА 0
 МОД (М11)
 СЛИА (М16)
 УИА 3
 ПБ ПОИСК         НАШЛИ НАЧАЛО ВЕТВИ
ЛУЧ4 НТЖ Е1
 ПО МОДА          ЛЕВАЯ КОМАНДА - МОДА (22)
 СЧИ М15           ЛЕВАЯ КОМАНДА - СЛИА(25) ИЛИ УИА(24)
 ПО ПОИСК         ПУ БЕЗ ИР-А ИЛИ ПВ,ПИО,ПИНО,ЦИКЛ
 СЛИ М11(М14)      АИСП(24,25):= А + <М20>
 НТЖ R3
ПУСИ ПЕ ПОИСК         ИР-Ы РАЗНЫЕ
 МОДА ПУСИ1
 ПИО (М5)          СЛИА,СЛИ
 СЧ R4            БУДЕТ УСТАНОВКА НА ИР УКАЗАННЫЙ В ПУ
 УИ М16           ВОССТАНОВЛЕНИЕ М16 (А(ПУ))
ПУСИ1 СЛИ М16(М11)      М16:= М16 + ХХХХХ
 ПБ ПОИСК         УИА,УИ,УИМ,УИИ
К1С МОДА ПОИСК
 ПИО (М15)         ПУ БЕЗ ИР-А ИЛИ ПВ (31)
 СЧ R4            АНАЛИЗ ВЛИЯНИЯ КОМАНД 1Й СТРУКТУРЫ
 СДА 64+36
 И П377
 НТЖ Е6
 ПО К1С1          УИ (040)
 НТЖ Е1
К1С1 УИ М5            М5=0 - УИ,УИМ
 ПО К1С2          УИМ (041)
 НТЖ Е3
 ПО К1С2          СЛИ (045)
 НТЖ Е1
 ПЕ ПОИСК         НЕ УИИ (044) - НЕТ ВЛИЯНИЯ
К1С2 СЛИ М14(М11)      АИСП:= А + <М20>
 СЧ R3
 ПО К1С4          КОМАНДА БЕЗ ИР-А (УИ,УИМ)
 НТЖ П17           М11:= <ИР>
 УИ М11
 МОДА 7(М3)
 СЧ (М11)
 УИ М11
 МОДА К1С3
 ПИНО (М5)          СЛИ,УИИ
 СЛИ М14(М11)      УИ,УИМ С ИР-ОМ; АИСП:= АИСП + <ИР>
К1С4 СЧ (М3)          М11:= <М20>
 УИ М11
К1С3 СЧИ М14           СРАВНЕНИЕ ИР-ОВ
 ЗП R3
 СЧИ М15
 НТЖ R3
 СЛИА -4(М5)
 ПБ ПУСИ
АПУ2А МОДА СОК3
 ПИО (М4)          Э70 НЕ НА ВЕТВИ
АПУ2 СЧ РЕЖРАБ        УЗЕЛ СТАВИТЬ *
 И Е15(М1)
 ПЕ СОК3          НЕ СТАВИТЬ - ТАКОВ РЕЖИМ
 СЧ R4            УСТАНОВКА УЗЛА
 УИА ’2000’
 ЗП (М16)
ЕNDLIN УИА 3             ВЕТВЬ ЗАКОНЧИЛИ
 УИА (М4)
 МОДА ИДИ2А
 ПИНО (М10)         ВСЕ УЗЛЫ ПОСТАВИЛИ
 УИА 1(М10)        ЗАКОНЧИЛИ ВЕТВЬ ПО ДИСП
 УИИ М16(М17)      ВОССТАНОВЛЕНИЕ КООРДИНАТ
 УИИ М12(М13)
 ПБ СОК3          М4:=0 - ПОИСК НАЧАЛА НОВОЙ ВЕТВИ
ВЕGLIN СЧ R4            ПОИСК НАЧАЛА ВЕТВИ
 УИИ М17(М16)      УПРЯТЫВАНИЕ КООРДИНАТ
 УИИ М13(М12)
 СДА 64+20(М12)
 И П17
 УИ М15
 НТЖ П17
 УИ М11
 СЧИ М1
 ВЧ П3
 ПЕ НВ1           НЕ ПВ,ПИО,ПИНО,ЦИКЛ
 УИА (М15)         М15:=0 - ПРИЗНАК ПУ БЕЗ ИР-А
НВ1 СЧ R4
 СДА 64(М12)
 МОДА НВ
 ПИО (М15)         ПУ БЕЗ ИР-А
 МОДА 7(М3)         М16:= М16 + <ИР>
 СЛЦ (М11)
НВ МОДА ЛУЧ3
 ПИО (М12)         ПУ - ПРАВАЯ
 УИ М16
МОДА СЛИ М16(М14)      АИСП(ПУ):= А + <М20>
ПОИСК УИА 1(М4)         ВЫШЛИ НА ВЕТВЬ
 СЛИА -1(М16)
 ПБ СОК
КО1С СЧ R4            ПРОВЕРКА НА Э70,Э74
 СДА 64+12(М12)
 И П377
 УИА 14(М1)
 НТЖ П60+1
 ПО АПУ2А         Э70
 НТЖ Е3
 ПЕ СОК3          НЕ Э74
 МОДА ЕNDLIN
 ПИНО (М4)          ШЛИ ПО ВЕТВИ - КОНЧАЕМ
ИДИ2А МОДА ОТКР
 ПВ (М13)         НА ОТКРЫТИЕ ЗАДАЧИ
 УИА (М16)
 ПБ ОТК2-НРОТ(М7) ДИРЕКТИВУ ВЫПОЛНИЛИ
ПВА УИА -36(М15)      Е48 ≡ ПВА-ПАРАЛЛЕЛЬНАЯ ВЫДАЧА НА АЦПУ
 ПБ ЛИН1
ДИС УИА -29(М15)      Е41 ≡ ДИС -ВЫДАЧА ПОЛНОЙ СИТУАЦИИ
 ПБ ЛИН1
* ОПЕРАЦИИ УСТАНОВКИ РЕЖИМОВ ТРАССИРОВКИ
ЦИКЛ УИА 6(М15)        37: Е6
 ПБ ЛИН1
Э70 УИА 11(М15)       070: Е1
 ПБ ЛИН1
ПВ УИА (М15)         31: Е12
ЛИН1 СЧ РЕЖРАБ        УСТАНОВКА(ОТМЕНА) РЕЖИМОВ РАБОТЫ
 НТЖ Е12(М15)
 УИА ’2000’
 ЗП 4(М2)
 УИА 3
 ПБ ДВ-НРОТ(М7)
КНЦ1 СЧ СИМВ          КОНЕЦ РАБОТЫ С ОТЛАДЧИКОМ
 СДА 64+3
 ПЕ КНЦ           СНЯТИЕ ЗАДАЧИ ПОЛЬЗОВАТЕЛЯ
 СЛИА -2(М2)        КНЦ С АДРЕСОМ - ЗАДАЧУ ПРОДОЛЖИТЬ
 МОДА ВОИН1
 ПВ (М12)         ЗА АДРЕСОМ ВХОДА
 СЧ R
 ЗП 3(М3)
КНЦ2 УИ М16           ГАШЕНИЕ ПРК,М20;М2:=0
 СЧ 5(М3)         СНЯТИЕ КРА,ЗПСЧ
 ИЛИ Е9
 НТЖ Е9
 ЗП 5(М3)
 СЧ
 ЗП 6(М3)
 СЧ 22(М3)
 И Е48П16
 ЗП 22(М3)
 СЧ 23(М3)
 И Е48П16
 ЗП 23(М3)
 УИА (М2)
 МОДА ЗПКОМ         СНЯТИЕ УЗЛА А.ВХОДА
 ПВ (М4)          (НА ВСЯКИЙ СЛУЧАЙ)
КНЦ3 СЧ ТЕРЕЗА(М3)    ОСВОБОЖДЕНИЕ ТЕРМИНАЛА:
 УИА ’2003’        ’0’ -> ШЗНКТ,ШЗПРИМ,ТЕРЕЗА
 И ШКОТЛ
 ЗП R
 НТЖ ТЕРЕЗА(М3)
 ЗП ТЕРЕЗА(М3)
 СЧ R
 НТЖ ШЗНКТ
 ЗП ШЗНКТ
 СЧ R
 НТЖ ШЗПРИМ
 ЗП ШЗПРИМ
 УИА 3
КНЦ СЧ НТ-НРОТ(М7)   <КНЦ>. IF М2=0 - ЗАДАЧУ М.НЕ СНИМАТЬ
 СДА 64+40
 УИ М16
 МОДА КНЦ4
 УИА (М13)
 СЧ НЗ-НРОТ(М7)
 НЕД
 УИ М15
ОТКР УИА ’2003’        БЛОК ОТКРЫТИЯ ЗАДАЧИ ПОЛЬЗОВАТЕЛЯ
 СЧ ШКРДО         ЗАПРЕТ НАБОРА ДИРЕКТИВ.Е ЗАДАЧИ->МШГ
 НТЖ НЗ-НРОТ(М7)
 ЗП ШКРДО
 СЧ МШГП
 ИЛИ НЗ-НРОТ(М7)
 ЗП МШГП
 СЧ ДСТОП
 И НЗ-НРОТ(М7)
 ПО ОТКР1         <СТОП> НЕ БЫЛО
 НТЖ ДСТОП         ГАШЕНИЕ ПРИЗНАКА <СТОП>
 ЗП ДСТОП
 ПБ (М13)
ОТКР1 СЧ НЗ-НРОТ(М7)
 УИИ М4(М15)
 ПВ ВШГП(М15)
 УИИ М15(М4)
 ПБ (М13)
КНЦ4 СЧ (М1)          УСТАНОВКА ПРИЧИНЫ СНЯТИЯ
 И П377
 ПО КНЦ6
 НТЖ =В’115’
 ПЕ КНЦ6
 СЧМР
 ПБ КНЦ5
КНЦ6 СЧ ’44’(М3)      ПРИЧИНА ИЗ ИПЗ
 СДА 64+41
КНЦ5 УИ М13           N ПРИЧИНЫ -> М13
 СДА 64+2
 НТЖ П5
 ПЕ ИСКМ-НРОТ(М7) НЕ УЗЕЛ, КРА, ЗПСЧ
 УИА (М13)         ИСКЛЮЧИТЬ АВОСТНУЮ ВЫДАЧУ
 ПБ ИСКМ-НРОТ(М7) НА СНЯТИЕ
* БЛОК РАСШИФРОВКИ ОПЕРАЦИЙ ВЫДАЧИ И ПРИСВАИВАНИЯ
АДР УИА -15(М17)      АДР - М17≠0
 УИА (М16)
 ПБ ВЫПР1
ИР СЧ СИМВ          ИР - М17=0
 УИА 15(М17)
 СДА 64+3
 ПЕ ВСЕМ          ВЫДАЧА ВСЕХ ИР-ОВ
ВЫПР1 УИА (М4)
 МОДА ВЫПР1А
 УИА (М5)
 МОДА РИТА
 ПИО (М16)         РАСШИФРОВКА АДРЕСА
 ПБ ИНДЕР         РАСШИФРОВКА ИР-А
ВЫПР1А СЧ R1
 МОДА ВЫПР2
 ПИНО (М17)         АДРЕС
 УИ М14           М14:= N ИР-А
 НТЖ П17
 УИ М10           М10:= N ИР-А В ОБРАТНОМ КОДЕ
 СЛИА ПИР-ВЫПР1А(М5) М5:=ВЫХОД ПРИ :=
 МОДА ВЫПР6
 УИА (М12)
 ПБ ВЫПР4
ВЫПР2 МОДА МАД
 ПВ (М5)          А НАЧ
 УИА (М14)
 УИИ М10(М16)
 СЧ СИМВ
 НТЖ П13
 МОДА ВЫПР9
 УИА (М5)
 УИА ВЫДАД-ОВД(М17) ДЛЯ ОПЕРАЦИИ =
 ПО ВЫПР7         ЗАДАН ДИАПАЗОН: А НАЧ - А КОН
 СДА 64+1
 ПЕ ВЫПР3         ЗАДАН ОДИН АДРЕС: А
 МОДА ВЫПР8
 УИА (М5)          ЗАДАН ДИАПАЗОН: А + N
ВЫПР7 УИА -15(М17)
 ЗП R1
 МОДА ПР3
 ПВ (М12)         ПРОПУСК -,+
 ПБ РИТА          ЗА А КОН ИЛИ N
ВЫПР8 СЧИ М10           А + N = А КОН
 СДА 64-15
 СЛЦ R1
 ЗП R1
ВЫПР9 СЧ R1
 УИА ВЫДАД-ОВД(М17) ДЛЯ ОПЕРАЦИИ =
 МОДА МАД
 ПВ (М5)          А КОН
 СЧИ М16
 ЗП R2
 СЧИ М10
 ВЧ R2
 УИ М14
 ПЕ ВЫПР3         А НАЧ < А КОН
 НТЖ П77777
 УИ М14
 СЛИА 1(М14)
 УИИ М12(М10)
 УИИ М10(М16)
 УИИ М16(М12)
ВЫПР3 МОДА ПАД           М5:= ВЫХОД ПРИ :=
 УИА (М5)
 МОДА ВЫПР3В
 УИА (М12)
ВЫПР4 СЧ СИМВ          ЗАКОНЧИЛИ РАСШИФРОВКУ ЛЕВОЙ ЧАСТИ
 ПБ РОП4          := ИЛИ =
ВЫПР3В МОДА ВЫДТТ
 ПИНО (М4)          ВЫДАЧА
 МОДА (М2)          <М10> = А НАЧ, <М16> = А КОН
 УИА 4(М12)
ВЫПР3А УИА ’2000’        ОПРЕДЕЛЕНИЕ КОНЕЧНОГО АДРЕСА ИПО
 СЧ 1(М12)
 УИА 3
 СЛИА 1(М12)
 ПЕ ВЫПР3А        ≠ 0
 СЧИ М12           <М12> = АИПО + L(ИПО)
 ЗП R1
 СЧИ М10
 ВЧОБ R1
 ПЕ РАСЛО         АИПО + L(ИПО) < А НАЧ
 СЛИА ДИП+2(М16)
 СЧИ М2
 ЗП R1
 СЧИ М16
 ВЧ R1
 ПО ОШ-НРОТ(М7)   ПОПЫТКА ЗАПИСИ НА ИПО
 ПБ РАСЛО         А КОН < АИПО
ПСМ ЗП (М3)          ЗАПИСЬ НА СМ
 ПБ ДВ-НРОТ(М7)
ПМР ЗП 2(М3)         ЗАПИСЬ НА МР
 ПБ ДВ-НРОТ(М7)
ПРК И П77           ЗАПИСЬ НА РК
 СДА 64-41
 ЗП 1(М3)
 ПБ ДВ-НРОТ(М7)
ПИР И П77777        ИЗМЕНЕНИЕ ИР-А
 МОДА 7(М3)
 ЗП (М10)
 УИА -1(М15)
ПРИС СЧ СИМВ          ПРИСВАИВАНИЕ ЗАКОНЧИЛИ *
 НТЖ П377
 ПО ДВ-НРОТ(М7)   ДА
 СЧИ М12           В СТРОКЕ ЕЩЕ ЕСТЬ ИНФОРМАЦИЯ
 ПО БЦИН          ЗАПИСЬ ТЕКСТА
 МОДА ПРИС
 УИА (М12)
 СЧ СИМВ
 НТЖ П17
 ПО ПР3           ПРОГОН ПРОБЕЛА
 НТЖ Е2            АДР: М10:=М10+1
 СЛИ М10(М15)      ИР: М10:=М10-1
 ПО ПР3           ПРОГОН ЗАПЯТОЙ
 УИИ М16(М10)      ПРОВЕРКА КОРРЕКТНОСТИ ПРИСВАИВАНИЯ
 СЧИ М16
 МОДА ПРИСА
 ПИО (М14)         АДРЕС
 НТЖ П77777
 ПО ОШ-НРОТ(М7)   N ИР-А > 15e
 ПБ РАСЛО
ПРИСА И П1777
 ПЕ РАСЛО         ЗАПИСЬ В ТОТ ЖЕ ЛИСТ
 МОДА ПРОЧЛИ
 ПВ (М5)          ПРОВЕРКА НА Ч.ЛИСТ
ПРИСА1 СЛИА ПАД-ПРИСА1(М5) ВОССТАНОВЛЕНИЕ М5
 ПБ РАСЛО
ПАД УИА ’2000’        ЗАПИСЬ ПО АДРЕСУ (М10)
 ЗП (М10)
 УИА 3
 СЛИА 1(М10)
 МОДА ПАД
 ЦИКЛ (М14)
 УИА 1(М15)
 СЛИА -1(М10)
 ПБ ПРИС
ВЫПР6 МОДА РАСЛО
 ПИО (М4)          ПРИСВАИВАНИЕ
* БЛОК ВЫДАЧИ НА ТЕРМИНАЛ ПО ДИРЕКТИВЕ ПОЛЬЗОВАТЕЛЯ
ВЫДТТ СЧ СДКБ
 СДА 64+40
 НТЖ СИМВ
 ПО СОХПЧ  СОХР.ПОЗИЦ.ЧТЕНИЯ ДЛЯ НРКОД-А
 УИА 0(М1)
 УИА -48(М13)
СОХПЧ НОП ,
 МОДА ВНПС
 УИА (М12)
 МОДА ВИ
 ПИО (М17)         ВЫДАЧА ИР-А
 СЛИ М17(М6)
 ПБ (М17)
НЕАД СЛИА -1(М16)       ВЫДАВАТЬ НЕ МАССИВ
 СЧИ М16
 ПО ВСЕМ          ВЫДАТЬ ВСЕ ИР-Ы
 НТЖ П3
 ПО ВЫДРК
 НТЖ Е2
 УИА 0(М10)       ПРИЗН.СМ,РМР ДЛЯ НРКОДА
 УИА 0(М14)       ПРИЗН.СМ
 ПО ВЫДСМ
 УИА 2(М14)       ПРИЗН.РМР
 МОДА РВВ
 ПВ (М11)
 МОДА ВНПС
 УИА (М12)
 СЧ 2(М3)
 ПБ ВТТ2
ВЫДСМ МОДА РВВ
 ПВ (М11)         ВИД ВЫДАЧИ СМ
 МОДА ВНПС
 УИА (М12)
 СЧ (М3)
 ПБ ВТТ2
ВЫДРК СЧ 1(М3)
 СДА 64+8
 РЗБ КРК2
 ПБ ВРК
ЗАПНО МОДА ОШ-НРОТ(М7)
 ПИО (М17)    НЕ АДРЕС
 УИА 1(М5)
 ПБ ВЫЗКОД-НРОТ(М7)
СЧНОВ УИА 0(М5)  ПРИЗН.ЧТ-ЗП
 МОДА ВЫЗКОД-НРОТ(М7)
 УИА (М11)
 ПБ С    ПРОГОН БУКВЫ Н
ВЫДАД МОДА РВВ
 ПВ (М11)         ВИД ВЫДАЧИ МАССИВА
 СЛИА -1(М14)
 СЛИ М16(М6)
 УИА -8(М2)
 СЧ ТЕРЕЗА(М3)
 И ШКОТЛ
 И ШКВИД
 ПО ВЫДАДР        НЕ VIDЕОТОN
 УИА -11(М2)       ДРУГИЕ РАЗМЕРЫ МАССИВА
 СЛИА 5(М16)
ВЫДАДР СЧИ М2            ДЛИНА МАССИВА *
 ЗП СИМВ
 СЧИ М14
 ВЧОБ МАХДВ-ОВД(М16)
 ПЕ ВТТ           ДИАПАЗОН В РАЗРЕШЕННЫХ ПРЕДЕЛАХ
 СЧ МАХДВ-ОВД(М16) ДИАПАЗОН > ДОПУСТИМОГО
 УИ М14           УСТАНОВКА МАХ ЗНАЧЕНИЯ
 ПБ ВТТ
РВВ УИА (М16)         РАСШИФРОВКА ВИДА ВЫДАЧИ
 СЧ СДКБ
 МОДА РШК
 ПВ (М12)
 МОДА (М16)
 ПБ РВВ1          НА ПЕРЕКЛЮЧАТЕЛЬ
РВВ1 УИА БРС-ОВД(М5)
 ПБ РВВ2          СОNSТ
 УИА БРЧ-ОВД(М5)
 ПБ РВВ2          ЧИСЛА
 УИА БЦИ-ОВД(М5)
 ПБ РВВ2          БЦ-ИНФОРМАЦИЯ
 УИА БРК-ОВД(М5)
 ПБ РВВ2          КОМАНДЫ
 УИА БРС-ОВД(М5)   СОNSТ
 ПБ РВВ2
 ПБ СЧНОВ  ЧТЕНИЕ ПО-НОВОМУ
РВВ2 СЛИ М5(М6)        ВЫХОД
 ПБ (М11)
ВТТ1 МОДА ВТТ           М12:= ВЫХОД ИЗ БЛОКОВ РАСПАКОВКИ
 УИА (М12)
 МОДА НЕИ
 ПИНО (М5)          ВЫДАЧА МАССИВА
ВИ МОДА 7(М3)         ВЫДАЧА ИНДЕКСОВ
 СЧ (М10)
 СЛИА -1(М10)
 ПБ БРИ
НЕИ УИА ’2000’        ВЫБОРКА ОЧЕРЕДНОГО СЛОВА
 СЧ (М10)
 УИА 3
 СЛИА 1(М10)
ВТТ2 ЗП R             R:= <СЛОВО>
 ПБ (М5)          НА БЛОКИ РАСПАКОВКИ
ВСЕМ УИА -15(М14)      ВЫДАЧА ВСЕХ ИР-ОВ
 УИА 14(М10)
 УИА (М5)
 УИА 1(М1)
 СЧ ВСЕПР
 ЗП БУФТТ
ВТТ МОДА ВТТ1
 ЦИКЛ (М14)         ВЫДАЧА МАССИВА НЕ КОНЧИЛАСЬ
 МОДА ДВ-НРОТ(М7)
 ПИО (М1)          ДИРЕКТИВУ ВЫПОЛНИЛИ
ВНПС СЛИА -1(М1)        ВЫДАЧА НЕ ПОЛНОЙ СТРОКИ
 МОДА ДВ-НРОТ(М7)
 УИА (М11)
 ПБ ТТ
БРИ СДА 64-33         РАСПАКОВКА ИР-А
 РЗБ КРК2
ВРК ИЛИ Е44П41
 МОДА БУФТТ
 ЗП (М1)
 СЛИА 1(М1)
ВС8 СЧИ М1            СТРОКА ИЗ 8 СЛОВ
 НТЖ Е4
 ПЕ (М12)
 СЛИА -1(М1)
 УИИ М11(М12)
 ПБ ТТ
БРЧ УИА 15(М16)
 МОДА БРЧ1
 УИА (М11)
 УИА -3(М17)
БРЧ2 СЧИ М16           ЗАНЕСЕНИЕ СИМВОЛА (М16)
 ПБ ЗС
БРЧ1 МОДА БРЧ2
 ЦИКЛ (М17)         ЗАПИСЬ 4-Х ПРОБЕЛОВ
 СЧ R
 И Е41
 ПО БРЧ3А         ЧИСЛО R>0
 УИА 11(М16)       М16:=МИНУС
БРЧ3А СЛИА БРЧ3-БРЧ1(М11)
 ПБ БРЧ2          ЗАПИСЬ ЗНАКА ЧИСЛА
БРЧ3 СЧ R             НОРМАЛИЗАЦИЯ |R|
 РЖА ’40’          БЛОКИРОВКА АВОСТА
 ВЧАБ
 РЖА 4             ЛОГИЧЕСКАЯ ГР.
 УИА -9(М15)
 СЛИА БРЧ7-БРЧ3(М11)
 ПО БРЧ6          R=0
 УИА 1(М17)        М17:=1
БРЧ4 ЗП R
 ВЧ Ч10Д
 ПЕ БРЧ5          R<10
 СЛИА 10(М17)       М17:=М17+10
 СЧ R
 УМН Ч10М10
 ПБ БРЧ4          R:=R∧e-10
БРЧ5 СЧ R
 ВЧ Ч1Д
 ПО БРЧ6          R≥1
 СЛИА -1(М17)       М17:=М17-1
 СЧ R
 УМН Ч10Д
 ЗП R
 ПБ БРЧ5          R:=R∧10
БРЧ6 РЖА 3             ПЕРЕВОД МАНТИССЫ -10 ЦИФР
 СЧ R
 СЛ ПЕ64
 И П17
 УИ М16
 ПБ ЗС            ЗАПИСЬ ЦИФРЫ
БРЧ7 СЧИ М16
 ИЛИ ПЕ64
 РЖА 0
 ВЧОБ R
 УМН Ч10Д
 ЗП R
 МОДА БРЧ6
 ЦИКЛ (М15)         ЗА ОЧЕРЕДНОЙ ЦИФРОЙ
 УИА 10(М16)       М16:=ПЛЮС
 СЛИА БРЧ8А-БРЧ7(М11)
 РЖА 3
 СЧИ М17
 И Е6
 ПО БРЧ2          ПОРЯДОК>0
 УИА 11(М16)       М16:=МИНУС
 СЧИ М17
 НТЖ П77777
 УИ М17
 СЛИА 1(М17)
 ПБ БРЧ2          ЗАПИСЬ МИНУСА
БРЧ8А УИИ М16(М17)      ПОЛУЧЕНИЕ 1-ОЙ ЦИФРЫ ПОРЯДКА
 СЛИА -10(М16)
 СЧИ М16
 СДА 64+4
 И Е1
 НТЖ Е1
 ПЕ БРЧ9          ПОРЯДОК ≥10
 СЛИА 10(М16)
БРЧ9 МОДА ЗС            ЗАПИСЬ 1-Й ЦИФРЫ ПОРЯДКА
 ПВ (М11)
 УИИ М11(М12)      М11:=ВЫХОД
 ПБ БРЧ2          ЗАПИСЬ 2-Й ЦИФРЫ ПОРЯДКА
БРС УИА -16(М17)      БЛОК РАСПАКОВКИ СОNSТАNТ
 СЧ П17
 МОДА ЗС
 ПВ (М11)         ЗАПИСЬ ПРОБЕЛА
 СЧ R             ВЫДЕЛЕНИЕ ЦИФРЫ
 СДА 64-3
 ЗП R
 СЧМР
 МОДА ЗС
 ЦИКЛ (М17)         ЗАПИСЬ ЦИФРЫ
 УИИ М11(М12)      М11:=ВЫХОД
 СЧ П17
ЗС СЛИА 8(М13)        ЗАНЕСЕНИЕ СИМВОЛА НА БУФЕР (М1,М13)
 СДА 64(М13)
 МОДА БУФТТ
 ИЛИ (М1)
ЗС1 МОДА БУФТТ
 ЗП (М1)
 МОДА (М11)
 ПИНО (М13)         ВЫХОД - НЕПОЛНОЕ СЛОВО
 УИА -48(М13)      СЛОВО ЗАПОЛНИЛОСЬ
 СЛИА 1(М1)
 МОДА (М11)
 ЦИКЛ (М2)          БУФЕР ЕЩЕ НЕ ЗАПОЛНЕН
 МОД СИМВ          ВОССТАНОВЛЕНИЕ’РАЗМЕРА СТРОКИ’
 УИА (М2)
 СЛИА -1(М1)
 ПБ ТТ            ВЫДАТЬ СТРОКУ !
БРК УИА -1(М17)       БЛОК РАСПАКОВКИ КОМАНДЫ
 СЧ R
БРК1 И Е44
 МОДА КРК2
 УИА (М15)
 УИА Е44П41(М11)
 ПЕ БРК2          КОМАНДА 2-Й СТРУКТУРЫ
 СЛИА -1(М15)
 МОДА Е36П33
 УИА (М11)
БРК2 СЧ R
 РЗБ КРК
 ИЛИ ПР14
 МОДА БУФТТ
 ЗП (М1)
 СЧ R
 СДА 64-9
 РЗБ (М15)
 ИЛИ (М11)
 МОДА БУФТТ
 ЗП 1(М1)
 СЛИА 2(М1)
 СЧ R
 СДА 64-24
 ЗП R
 МОДА БРК1
 ЦИКЛ (М17)         РАСПАКОВКА ПРАВОЙ КОМАНДЫ
 МОДА БРК3
 ПИО (М2)          В СТРОКЕ УЖЕ 2 КОМАНДЫ
 МОДА БРК3
 ПИО (М14)         ПОСЛЕДНЯЯ КОМАНДА
 СЧ ПРОБ6         6 ПРОБЕЛОВ ДЛЯ КРАСОТЫ
 ЗП БУФТТ+4
 СЛИА 1(М1)
 УИА (М2)
 ПБ (М12)
БРК3 УИИ М11(М12)      ВЫДАЧА СТРОКИ КОМАНД
 СЛИА -1(М1)
 УИА -1(М2)
 ПБ ТТ
БЦИ УИА (М13)
 УИИ М11(М12)
 ПБ ЗС1
ТТ МОДА БУФТТ         БЛОК ВЫДАЧИ
 УИА (М16)
 СЧ СТРЕЛА
 МОДА БУФТТ
 ЗП 1(М1)
 МОДА ТТ3
 УИА (М15)
 УИИ ’32’(М15)
 СЧ БУФТТ
 НТЖ Е44П41
 И Е48П41
 ПО ТТ1           1Й СИМВОЛ-ПРОБЕЛ
 СЧ ПУСТО         5 ПУСТЫШЕК (143)
 СЛИА -1(М16)
 ПБ ТТ2
ТТ1 СЧ БУФТТ
 НТЖ Е44П41
ТТ2 ИЛИ НТ-НРОТ(М7)   ЗАПИСЬ N ТТ
 ЗП (М16)
 СЧ Е23
 ПБ ТВ40          НА ВЫДАЧУ
ТТ3 СЧ РЕЖРАБ
 РЖА ’13’
 ПЕ ТТ3Б          НА АЦПУ НЕ ВЫДАЕМ
 СЧ (М16)         ВЫДАЧА - Э64
 И Е40П1
 ИЛИ Е44П41
 ЗП (М16)
 СЧИ М6
 ЗП Э64-НРОТ(М7)
 Э64 ПЕЧ-ОВД+2048
ТТ3Б УИА -13(М16)      РОСПИСЬ ВЫДАННОГО БУФЕРА
 СЧ 0
ТТ3А МОДА БУФТТ-1
 ЗП 13(М16)
 МОДА ТТ3А
 ЦИКЛ (М16)
 УИА (М1)
 ПБ (М11)
ВЫПР5 МОДА ВЫДТТ
 ПИНО (М4)          ВЫДАЧА СМ,РК,РМР
 УИИ М5(М16)       М5:= ВЫХОД ИЗ РАСЛО
 МОДА ПСМ-2
 СЛИА (М5)
 УИА (М17)
* БЛОК РАСШИФРОВКИ ЛИТЕРАЛЬНОГО ОПЕРАНДА
РАСЛО СЧ СИМВ
 УИ М16
 СДА 64+4
 ПО ЛИТ           УКАЗАТЕЛЬ ПРИСВАИВАНИЯ НЕ ЗАДАН
 СЛИА -’41’(М16)
 СЧИ М16
 СЛИА -3(М16)
 ПЕ ЛИТ7          НЕ БЦ-ИНФОРМАЦИЯ
 МОДА С
 ПВ (М11)         ЗА 1-ЫМ СИМВОЛОМ
БЦИ1А СЛИА БЦИ2-БЦИ1А(М11)
 СЧ
БЦИ1 ЗП R             R:=0, М16:=-6
 УИА -6(М16)
БЦИ2 СЧ СИМВ          СБОРКА СЛОВА
 НТЖ П377
 ПО БЦИ3          КОНЕЦ БЦ-ИНФОРМАЦИИ
 МОДА ПР2
 ЦИКЛ (М16)
 УИА (М12)         М12:=0 - ЗАПИСЬ ТЕКСТА
 МОДА ОШ-НРОТ(М7)
 ПИНО (М14)         РОСПИСЬ МАССИВА,ЗАПИСЬ НА СМ,МР,ИР
 СЧ R             ЗАПИСЬ ОЧЕРЕДНОГО СЛОВА ТЕКСТА
 ПБ (М5)
БЦИ3 МОДА ЛИТНЕЧ
 ПИНО (М12)         ВЫХОД - ОДНО НЕПОЛНОЕ СЛОВО
 СЧИ М16           ПОСЛЕДНЕЕ СЛОВО ТЕКСТА - НЕПОЛНОЕ
 СДА 64-3          ДОПОЛНЯЕМ ПРОБЕЛАМИ СПРАВА
 УИ М16
 СЧ R
 СДА 64(М16)
 ЗП R
 СЛИА 48(М16)
 СЧ ВСЕПР
 СДА 64(М16)
 ИЛИ R
 ПБ (М5)
БЦИН СЛИА 1(М10)        М10:= М10 +1
 ПБ БЦИ1
ЛИТ7 МОДА ПР3
 ПВ (М12)         ПРОГОН УКАЗАТЕЛЯ ПРИСВАИВАНИЯ
 СЧИ М16
 ПО ЛИТ           ЧИСЛА
 НТЖ П6
 ПО ЛИТ6          КОМАНДА
 НТЖ П17
 ПО ЗАПНО   Н - ЗАПИСЬ ПО-НОВОМУ
 НТЖ Е3
 ПЕ ОШ-НРОТ(М7)   НЕВЕРНО ЗАДАН УКАЗАТЕЛЬ
ЛИТ1 УИА -16(М16)      СБОРКА СОNSТ
 МОДА ЛИТНЕЧ
 УИА (М12)
 ПБ ВОИН
ЛИТ6 МОДА ОШ-НРОТ(М7)
 ПИО (М17)         ЗАПИСЬ КОМАНДЫ НЕ ПО АДРЕСУ
 УИА -24(М17)
 СЧ
 ЗП R2
 ЗП R3
 СЧ СИМВ
 НТЖ П53
 ПЕ ЛИТ6А         НЕ ЛЕВАЯ КОМАНДА
 СЧ Е24П1
 ЗП R3
 ПБ ЛИТ8А
ЛИТ6А НТЖ Е3
 ПЕ ЛИТ8          ПОЛНАЯ КОМАНДА
 УИА (М17)
 СЧ ВСЕЕД
 НТЖ Е24П1
 ЗП R3
ЛИТ8А МОДА ПР3
 ПВ (М12)         ПРОГОН СИМВОЛОВ П,Л
ЛИТ8 СЧ СИМВ          СБОРКА ЗАПИСЫВАЕМОЙ КОМАНДЫ
 СДА 64+1
 ПЕ ОШ-НРОТ(М7)   1-Я ЦИФРА >1
 УИА -2(М16)
 МОДА ВОИН
 ПВ (М12)
 СЛИА ВОИН2-ПР3-1(М11)
 СДА 64+2
 ПЕ ОШ-НРОТ(М7)   3-Я ЦИФРА >3
 УИА -6(М16)
 СЧ R
 СДА 64-2
 МОДА ПР4
 ПВ (М12)
 МОДА ВОИН4
 ПВ (М12)         ПРОПУСК ПРОБЕЛА МЕЖДУ КОП И А
 МОДА ОШ-НРОТ(М7)
 ПИНО (М16)         КОМАНДА СОДЕРЖИТ МЕНЬШЕ 9 ЦИФР
 СЧ R
 СДА 64(М17)
 ИЛИ R2
 ЗП R2
 СЛИА 24(М17)
 СЧ СИМВ
 СДА 64+3
 ПО ЛИТ8          ЗАПИСЬ ПОЛНОЙ КОМАНДЫ
 МОДА ЛИТ9А
 ПИО (М4)          ОПЕРАЦИЯ :=
 СЧ R2            КОМАНДА ДЛЯ НЕСТАНДАРТНОГО ДЕЙСТВИЯ
 И Е24П1
 ПЕ ЛИТ9А         ЭТО ПОЛНАЯ КОМАНДА
 СЧ ЛЕМОДА        ЗАПИСЬ 22-Й В ПРАВУЮ ПОЛОВИНУ
 ИЛИ R2
 ЗП R2
ЛИТ9А СЧ R3
 ПО ЛИТ9          ЗАПИСЬ ПОЛНОЙ КОМАНДЫ
 УИА 0
 И (М10)
ЛИТ9 УИА 3             ЗАПИСЬ ЧИСЛОМ
 ИЛИ R2
 УИА ’2000’
 ЗП (М10)
 УИИ М16(М10)
 УИА 3
 ПБ ЗПКОМ         НА ЗАПИСЬ КОМАНДОЙ
ЛИТНЕЧ СЧ R             М17=0 ПРИЗНАК НЕ ЧИСЛА
 ЗП R2
 УИА (М17)
 ПБ (М5)          ВЫХОД
ЛИТ УИИ М12(М13)      УПРЯТЫВАНИЕ ДЛЯ ПРОВЕРКИ ЧИСЛО ЛИ ЭТО
 УИИ М16(М1)
 СЧ СИМВ
 ЗП R
 НТЖ П13
 ПО ЛИТ2          МИНУС
 НТЖ Е1
 ПЕ ЛИТ3          НЕ ПЛЮС
ЛИТ2 МОДА С             ПРОПУСК СИМВОЛА
 ПВ (М11)
ЛИТ3 УИА 1(М17)
 СЧ СИМВ
 НТЖ П16
 ПО ЛИТ4          ВСТРЕТИЛАСЬ ТОЧКА => ЭТО ЧИСЛО
 СЧ СИМВ
 ВЧ П12
 ПЕ ЛИТ2          ЦИФРА
 УИА (М11)
ЛИТ4 СЧ R             ВОССТАНОВЛЕНИЕ ПОСЛЕ ПРОВЕРКИ
 ЗП СИМВ
 УИИ М13(М12)
 УИИ М1(М16)
 МОДА ЛИТ1
 ПИО (М11)         ЭТО СОNSТАNТА
ЛИТ5 УИА 1(М17)        М17=1 - ПРИЗНАК ЧИСЛА
 УИА ’75’(М12)     Ю
 СЧ ТЕРЕЗА(М3)
 И Е36Е35
 ПО ЧИСЛО         РАБОТА С ТТ
 УИА ’20’(М12)
* ПЕРЕВОД ЧИСЕЛ ИЗ 10 -> 2.
ЧИСЛО МОДА Ч2            ЧИСЛА ИМЕЮТ ВИД: ЧeП, П-ПОРЯДОК
 УИА (М11)         Ч=Х.У - ТОЧКА ОБЯЗАТЕЛЬНА
Ч1 СЧ 0             ОБРАБОТКА ЗНАКА
 ЗП R2
 СЧ Ч10Д
 ЗП R3            R3 = 10
 СЧ СИМВ
 НТЖ П12
 ПО С             ПРОПУСК ЗНАКА +
 НТЖ Е1
 ПЕ (М11)         ЗНАКА НЕТ ≡ ЕСТЬ +
 СЧ R3
 ЗНАК Е41
 ЗП R3            R3 = -10
 ПБ С             ПРОПУСК ЗНАКА -
Ч2 СЧИ М12           ОБРАБОТКА Ч
 НТЖ СИМВ
 ПО Ч4            ЧИСЛО С ПОРЯДКОМ
 МОДА Ч3
 ПИО (М16)         ОБРАБАТЫВАЕМ ДРОБНУЮ ЧАСТЬ
 СЧ СИМВ
 НТЖ П16
 ПЕ Ч3            ОБРАБАТЫВАЕМ ЦЕЛУЮ ЧАСТЬ
 УИА (М16)
 ПБ С             ЗА ПЕРВОЙ ЦИФРОЙ ДРОБНОЙ ЧАСТИ
Ч3 РЖА 3             R2,R3
 СЧ СИМВ
 ВЧ П12
 РЖА ’22’          ГР.СЛОЖЕНИЯ,БЛ.ОКРУГЛЕНИЯ
 ПО Ч5            СИМВОЛ-НЕ ЦИФРА
 СЧ СИМВ          R2=Ч∧e(К+1),К-ЧИСЛО ЦИФР ПОСЛЕ ТОЧКИ
 СЛЦ ПЕ64
 СЛ R2
 УМН Ч10Д
 ЗП R2
 МОДА С
 ПИНО (М16)         ЗА ОЧЕРЕДНОЙ ЦИФРОЙ ЦЕЛОЙ ЧАСТИ
 СЧ R3            R3=e(К+1)
 УМН Ч10Д
 ЗП R3
 ПБ С             ЗА ЦИФРОЙ ДРОБНОЙ ЧАСТИ
Ч5 СЧ R2            R2:=<R2>:<R3>
 РЖА ’22’
 ДЕЛ R3
 ЗП R2
Ч5А РЖА 3
 ПБ (М5)          ВЫХОД
Ч4 УИИ М12(М5)       ОБРАБОТКА ПОРЯДКА. М12:=ВЫХОД
 МОДА Ч6
 УИА (М5)
 УИА Ч01Д(М16)
 СЧ R2
 ПЕ Ч5            Ч≠0
 СЧ Ч1Д
 ЗНАК R3
Ч6 ЗП R             R:=Ч
 СЛИА Ч6А-Ч6(М5)
 МОДА С
 ПВ (М11)         ЗА ЗНАКОМ ПОРЯДКА
Ч6Б СЛИА Ч3-Ч6Б(М11)
 ПБ Ч1            НА ПЕРЕВОД ПОРЯДКА
Ч6А РЖА ’20’
 ПЕ Ч7            ПОРЯДОК<0
 МОДА Ч10Д
 УИА (М16)
Ч7 СЧ R2            П:=П-1
 ВЧАБ Ч1Д
 ЗП R2
 ПО Ч8            П≠0
 СЧ R             ПЕРЕВОД ЗАКОНЧЕН
 ЗП R2
 УИИ М5(М12)
 ПБ Ч5А           ВЫХОД
Ч8 СЧ R             УМНОЖЕНИЕ(ДЕЛЕНИЕ) НА 10
 УМН (М16)
 ЗП R
 ПБ Ч7
* БЛОК ВЫБОРКИ ОЧЕРЕДНОГО СИМВОЛА ИЗ СТРОКИ
С МОДА СИ
 ПИНО (М13)
 СЛИА 1(М1)
 УИА 48(М13)
СИ СЛИА -8(М13)
 СЧ (М1)
 СДА 64(М13)
 И П377
 ЗП СИМВ
 ПБ (М11)
ПРОГОН МОДА ПР1           БЛОК ПРОГОНА
 УИА (М11)
ПР1 СЧ СИМВ
 НТЖ П17
 ПО ПР3           ПРОБЕЛ
 СЧ СИМВ
 НТЖ П377
 ПО (М12)         КОНЕЦ СТРОКИ
ПР2 СЧ R
 СДА 64-8
ПР4 НТЖ СИМВ
 ЗП R
 ПБ С
ВОИН3 СЧ СИМВ
 ВЧ П12
 ПЕ ОШ-НРОТ(М7)   ДЕСЯТИЧНАЯ ЦИФРА
 НТЖ Е3
 ПО ОШ-НРОТ(М7)   ТОЧКА ≡ ЧИСЛО
 СЧ СИМВ
 НТЖ П17
 ПЕ (М12)         ВЫХОД: СИМВ - ЗНАЧАЩИЙ (≠ ПРОБЕЛУ)
ПР3 МОДА С             ПРОГОН СИМВ И ПОСЛЕДУЮЩИХ ПРОБЕЛОВ
 ПВ (М11)
 НТЖ П17
 ПО С
ПР3А СЧ СИМВ
 ПБ (М12)
* БЛОК СБОРКИ ВОСЬМИРИЧНОЙ ИНФОРМАЦИИ
ВОИН СЧ 0             М16=-(ОЖИДАЕМОЕ ЧИСЛО ЦИФР В ГРУППЕ)
 ЗП R
ВОИН4 МОДА ВОИН2
 УИА (М11)
ВОИН2 СЧ СИМВ
 СДА 64+3
 ПЕ ВОИН3         КОНЕЦ ГРУППЫ ВОСЬМИРИЧНЫХ ЦИФР
 СЧ R
 СДА 64-3
 МОДА ПР4
 ЦИКЛ (М16)         ЗАПИСЬ ОЧЕРЕДНОЙ ЦИФРЫ В R
 ПБ ОШ-НРОТ(М7)   ЧИСЛО ЦИФР > ОЖИДАЕМОГО
ПРОКС МОДА ПРОКС+1       ПРОГОН КАВЫЧКИ И ПРАВОЙ СКОБКИ
 УИА (М11)
 СЧ СИМВ
 НТЖ П17
 ПО С             ПРОПУСК ПРОБЕЛА
 СЧ СИМВ
 НТЖ П377
 ПО (М12)         КОНЕЦ СТРОКИ - ВЫХОД
 СЧ СИМВ
 НТЖ П23
 ПО ПР3           ПРОГОН СКОБКИ
 НТЖ Е4
 ПО ПР3           ПРОГОН КАВЫЧКИ
 ПБ ОШ-НРОТ(М7)   НЕТ НИ СКОБКИ,НИ КАВЫЧКИ
УСЛАД1 СЛИА УСЛ3-УСЛАД1(М5)
 СЧ R1
МАД СДА 64+15         ПОЛУЧЕНИЕ МАТЕМАТИЧЕСКОГО АДРЕСА
 УИ М16
 СДА 64+15
 И П17
 ПО ПРОЧЛИ        НЕТ ИР-А
 НТЖ П17
 УИ М15
 МОДА 7(М3)
 МОД (М15)
 СЛИА (М16)
ПРОЧЛИ УИА ОШ-НРОТ(М15)
 СЛИ М15(М7)
 СЧИ М15
 ПВ ЗАПВЫХ(М15)
 СЧИ М16
 СДА 64+10
 НТЖ П37
 УИ М15
 СЧ ’26’(М3)
 НТЖ ’40’(М3)
 И Е48(М15)
 ПЕ ОБМЕН-НРОТ(М7) ЛИСТ В ОБМЕНЕ
 УИА ’2000’        СЧИТЫВАНИЕ СЛОВА, ЗАПИСЬ В R4
 СЧ 7(М16)
 СЧ (М16)
 УИА 3
 ЗП R4
 ПБ (М5)
КРК1 КОНД П’707777’
КРК2 КОНД П’077777’
КРК КОНД П’017037’
Е36П33 КОНД М32В’17’
ПР14 КОНД П’ 00 00’
Ч10Д КОНД Е’10’
Ч10М10 КОНД Е’Е-10’
ВСЕПР КОНД П’      ’
П13 КОНД В’13’
П16 КОНД В’16’
П25 КОНД В’25’
П26 КОНД В’26’
П33 КОНД В’33’
П53 КОНД В’53’
ЛЕМОДА КОНД М15В’22’
РЕЖРАБ КОНД В’0’
************** А ВГ   ЗИ КЛ Н   С У  Ц     Э             V   *
ШРОП КОНД Ю’101100011011010001010010000010000000000000100000’
СКОБЛ КОНД В’22’
РМСИА КОНД П’РМСИА’
ШКЗНУ КОНД П’<>≠=’
Е36Е35 КОНД М34В’3’
РАЗТРА КОНД М24В’11’
* ФАТОШ - ШКАЛА АВОСТОВ,ПОСЛЕ КОТОРЫХ ЗАДАЧА СНИМАЕТСЯ ПО ДИРЕ-
* ТИВЕ <ИДИ> : NN АВОСТОВ = 19,37÷40,42,48
ФАТОШ КОНД М42В’1’М19В’75’М13В’1’
СДКБ КОНД П’Н0КБДС’М32В’377’
ПРОБ6 КОНД П’      ’
ПУСТО КОНД М32В’143’М24В’143’М16В’143’М8В’143’В’143’
СТРЕЛА КОНД М40П’↑’
МАХДВ КОНД М15В’-1’В’-14’ СОNSТ
 КОНД М15В’-1’В’-14’ ЧИСЛА
 КОНД М15В’-1’В’-44’ БЦИ
 КОНД М15В’-1’В’-10’ КОМАНДЫ
 КОНД М15В’-1’В’-16’   ПО-НОВОМУ
* ДЛЯ VIDЕОТОN-А  -  11 СТРОК
 КОНД М15В’-1’В’-54’  С
 КОНД М15В’-1’В’-54’  Ч
 КОНД М15В’-1’В’-204’ Б
 КОНД М15В’-1’В’-26’  К
 КОНД М15В’-1’В’-16’   ПО-НОВОМУ
R КОНД В’0’
R1 КОНД В’0’
R2 КОНД В’0’
R3 КОНД В’0’
R4 КОНД В’0’
СИМВ КОНД В’0’
 КОНД В’0’          НЕ РАЗДЕЛЯТЬ С БУФТТ
БУФТТ КОНД (13)В’0’
РRINТ ЗП Н(М15)
 ЗП Н(М15)
 КОНД М23В’1’
И16 КОНД В’0’
ПЕЧ ЗП БУФТТ-ОВД+2048
 ЗП БУФТТ-ОВД+2048
 КОНД М23В’1’
 Е
 М
ДИСП70 ВНЕШ П3,П5,П6,П7,П17,П37,П60,П77,П377,П1777
ДИСП70 ВНЕШ П77777,П12,П23,Е24П1,Е40П1,Е44П41,Е48П16,ВСЕЕД
ДИСП70 ВНЕШ Ч1Д,Ч01Д,(ПЕ64,ЭК9),ТВ40,ТЕРЕЗА,ШКИВ,АВОС,ТСЛ
КИТ ВНЕШ Е48П41,ШКРДО,ДСТОП
МОТТ ВНЕШ ШЗПРИМ,ШКОТЛ,ШЗНКТ,ШКВИД,Н,ДИП
БОЭК1 ВНЕШ ЗАПВЫХ
НРОТ ВНЕШ НРОТ,ИСКМ,ОШ,НЗ,НТ,(ДВ,ОТКПР),КТУС
НРОТ ВНЕШ ОТК2,Э64,(ОБМЕН,NОSТОР),РИПО1
НРОТ ВНЕШ ГДЕ,ВЫЗКОД
СОСТАВ ВНЕШ ИЗАХВ
 ВХОД РОП1
ДИП74 СЧ ЗОНЫ(М3)      ДЛЯ ОТЛАДКИ ПО ЛИСТИНГУ,ИР16:=АИПО
 УИА ’2000’        АИПО: 16 070 0001, АИПО+1: ИНФ.СЛОВО
 ЗП ’4000’
 УИА 3
 СЛИА -2(М2)
 СЧИ М2
 ЗП 8(М3)
 УИИ М16(М2)
 СЧ 36(М3)
 ИЛИ Е41
 ЗП 36(М3)        ДЛЯ ПЕРЕПИСИ ПОСЛЕДНЕЙ ЗОНЫ АЦПУ
 СЧ ГАЛЕ1
 УИА ’2000’
 ЗП (М16)
 УИА 3
 МОДА ЗПКОМ
 ПВ (М4)
 СЧ ГАЛЕ2
 ПБ ВЫПНД3
ГАЛЕ1 КОНД М24В’70700001’
ГАЛЕ2 КОНД В’370002’М39В’1’
ВИСП ВНЕШ ВШГП
ДИСП70 ВНЕШ ЗОНЫ,МШГП,ПЗ
*КОНЕЦ ЧАСТИ

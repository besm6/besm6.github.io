ПРОРОК СТАРТ ’22000’
 Е
 М
 УПОТР ПРОРОК(М3)
 УИА 3
 СЧ (М2)
 МОДА ДШ
 ПБ (М14)
ДШ СЧ 9(М2)
 ПБ Н   А37
 УИА 1027
 ПБ А47
 СЧ 0
 ПБ ЭКВЫХ
 СЧ 23(М2)
 ПБ А51
Н СЧ НОМАРС
 И Е48-1(М1)
 ПЕ ОТМАРС
 СЧ ОСПОД
 МОД LS-П(М7)
 И Е48-1
 ПО НВ
ОТМАРС СЧ 9(М2)
 И П77
 УИ М13
 ВЧ П30
 ПЕ ЗН ЗАПР НАПР
 ВЧОБ П37
 ПЕ ЗН
 СЧИ М13
 И Е1
 НТЖ Е1
 УИ М17
 НТЖ Е1
 СДА 64-2
 УИ М16
 НТЖ Е3
 УИ М14
 СЧИ М13
 СДА 64+1
 УИ М15
 УИИ М6(М15)
 СЛИ М6(М2)
 СЛИ М15(М4)
 СЧ 9(М2)
 И Е13
 УИ М11
 СЧ ШЗНМБ-12(М6)
 СДА 64+8(М16)
 И П17
 ЗП Р1-П(М7)
 МОДА ОТНТОМ
 ПИНО (М10) ОТНЯТИЕ ТОМА
 СЧ АРХМЛ(М2)
 И Е48-24(М13)
 ПЕ АРЛ
 СЧ Р1-П(М7)
 ПО НЕТЛ НЕТ ТОМА У ЗАДАЧИ
АРЛ НОП
 СЧ ШЗНМБ-12(М15)
 СДА 64+8(М16)
 И П17
 ПЕ РЕСТЬ У ПОДЧ УЖЕ ЕСТЬ
 СЧ АРХМЛ(М4)
 И Е48-24(М13)
 ПЕ РЕСТЬ    ТОМ УЖЕ ЕСТЬ
 СЧ АРХМЛ(М2)
 И Е48-24(М13)
 ПЕ АРХ37
ПТОМ СЧ Р1-П(М7)
 ЗП РЯ1
 И П3
 УИ М5
 СЧ Р1-П(М7)
 СДА 64+2
 УИ М12 НОМ СТРОКИ
 МОДА -1(М5)
 СЧ 28(М2)
 МОДА (М12)
 СДА 64+20(М12)
 И П3
 ЗП Р1-П(М7)
 СЛИ М12(М2)
 СЧ ОБЩТОМ(М12)
 СД КТОМ-1(М5)
 И Е16П1
 ЗП Р2-П(М7)
 УИА 3
 СД КТОМЛ-1(М5)
 ЗП Р3-П(М7)
 УИА -3(М16)
ПСВ2 УИА -2(М5)
ПСВМ1 МОДА (М4) ПОИСК СВОБ МЕСТА
 СЧ ОБЩТОМ+3(М16)
 СД КТОМ+2(М5)
 И Е16П1
 ПО СВМ
ПР МОДА ПСВМ1
 ЦИКЛ (М5)
 МОДА ПСВ2
 ЦИКЛ (М16)
НР СЧ Е44
 ПБ ЭКВЫХ
ЗН СЧ Е37
 ПБ ЭКВЫХ
НЕТЛ СЧ Е47 НЕТ РЕС У ГЛАВН
 ПБ ЭКВЫХ
НЕЛ МОД Р16
 УИА (М16)
 СЛИА -3(М5)
 ПБ ПР
РЕСТЬ СЧ Е45 РЕСУРС УЖЕ ЕСТЬ
 ПБ ЭКВЫХ
СВМ СЛИА 3(М5)
 СЧИ М16
 ЗП Р16
 СЛИА 3(М16)
 СЧИ М16
 СДА 64-10
 ЗП Р
 СЧИ М5
 СДА 64-8
 ИЛИ Р
 ЗП Р
 СДА 64-4
 ИЛИ Р
 ЗП Р
 УИА -15(М16)
Ц МОДА (М16)
 НТЖ ШЗНМБ+15(М4)
 СДА 64+8
 И П377
 ЗП РР
 И П17
 ПО НЕЛ
 СЧ РР
 СДА 64+4
 И П17
 ПО НЕЛ
 СЧ Р
 МОДА Ц
 ЦИКЛ (М16)
 МОД Р16
 УИА (М16)
 СЛИА -3(М5)
 СЧ Р2-П(М7)
 СД КТОМЛ+2(М5)
 УИА ’2003’
 МОДА (М4)
 ИЛИ ОБЩТОМ+3(М16)
 МОДА (М4)
 ЗП ОБЩТОМ+3(М16)
 СЛИА 3(М5)
 СЛИА 3(М16)
 СЧИ М16
 СДА 64-2
 ЗП Р2-П(М7)
 СЧИ М5
 ИЛИ Р2-П(М7)
 ЗП РЯ
 СДА 64-12(М14)
 ИЛИ ШЗНМБ-12(М15)
 ИЛИ Е8(М17)
 НТЖ Е8(М17)
 ЗП ШЗНМБ-12(М15)
 СЧИ М11
 СДА 64+5(М17)
 ИЛИ ШЗНМБ-12(М15)
 ЗП ШЗНМБ-12(М15)
 СЧ П17
 СДА 64-12(М14)
 НТЖ ВСЕЕД
 И ШЗНМБ-12(М6)
 ЗП ШЗНМБ-12(М6)
 СЧ Р3-П(М7)
 НТЖ ВСЕЕД
 И ОБЩТОМ(М12)
 ЗП ОБЩТОМ(М12)
 СЧ ГОД
 И Е46П48
 ПО НЕ
 СЧ РЯ1
 ИЛИ Е48
 УМН П3
 СЧМР 64
 ВЧОБ 0
 УИ М17
 МОД ТУСП-1(М1)
 СЧ ОБЩМЛ
 СДА 64(М17)
 И Е46П48
 ПО НЕ
 ЗП РЯ1
 СЧ РЯ
 ИЛИ Е48
 УМН П3
 СЧМР 64
 УИ М17
 МОД LS-П(М7)
 УИА (М11)
 СЧ П7
 СДА 64(М17)
 НТЖ ВСЕЕД
 МОД ТУСП-1(М11)
 И ОБЩМЛ
 МОД ТУСП-1(М11)
 ЗП ОБЩМЛ
 СЧ РЯ1
 СДА 64(М17)
 МОД ТУСП-1(М11)
 ИЛИ ОБЩМЛ
 МОД ТУСП-1(М11)
 ЗП ОБЩМЛ
НЕ СЧИ М16
 ВЧОБ 0
 УИ М16
 СЛИ М16(М16)
 СЧ Р1-П(М7)
 СДА 64-20(М16)
 ЗП Р1-П(М7)
 СЧ П3
 СДА 64-20(М16)
 НТЖ ВСЕЕД
 МОДА -1(М5)
 И 28(М4)
 ИЛИ Р1-П(М7)  ЗЩ
 МОДА -1(М5)
 ЗП 28(М4)
 СЧ Е48-8(М13)
 И УКАЗД(М2)
 ИЛИ УКАЗД(М4)
 ЗП УКАЗД(М4)
 СЧ Е48-8(М13)
 И МДР(М2)
 ИЛИ МДР(М4)
 ЗП МДР(М4)
 СЧ Е48-8(М13)
 ИЛИ УКАЗД(М2)
 НТЖ Е48-8(М13)
 ЗП УКАЗД(М2)
 СЧ МДР(М2)
 ИЛИ Е48-8(М13)
 НТЖ Е48-8(М13)
 ЗП МДР(М2)
 СЧ LS-П(М7)
 ИЛИ Е36
 ПВ ЗАПВЕТ(М16)
 СЧ 0
 МОДА ЭКВЫХ
 ПИО (М10)ПЕРЕДАЧА
 УИИ М2(М4)
ПРОЦЕС ВНЕШ (ЭКВЫХХ,ЭКВЫХ)
ЭКВЫХ МОДА ЭКВЫХХ-П(М7)
 УИА (М13)
 ПБ ЭКВЫХ1
* ОТНЯТИЕ ТОМА
ОТНТОМ НОП
 СЧ Р1-П(М7)
 ПЕ РЕСТЬ    ТОМ УЖЕ ЕСТЬ
 СЧ АРХМЛ(М2)
 И Е48-24(М13)
 ПЕ РЕСТЬ
 СЧ АРХМЛ(М4)
 И Е48-24(М13)
 ПЕ АРХ37
 УИА ’2003’
 СЧ ШЗНМБ-12(М15)
 СДА 64+8(М16)
 И П17
 ПО НР НЕТ ТОМА У ПОДЧ ЗАДАЧИ
 ЗП Р1-П(М7)
 УИИ М16(М15)
 УИИ М15(М6)
 УИИ М6(М16)
 УИИ М16(М2)
 УИИ М2(М4)
 УИИ М4(М16)
 ПБ ПТОМ
АРХ37 СЧ СОБА40
 ПВ ЗАПВЕТ(М16)
 СЧ LS-П(М7)
 ИЛИ Е36
 ПВ ЗАПВЕТ(М16)
 СЧ Е48-1(М1)
 ИЛИ Ш1
 ЗП Ш1
 СЧ 1(М2)
 И Е48П9
 ИЛИ П12
 ИЛИ Е1
 ЗП 1(М2)
 СЧ LS-П(М7)
 ЗП ИНФ(М2)
ЖДИТЕ МОДА ЖДИ-П(М7)
 УИА (М13)
ЭКВЫХ1 ЗП Р1-П(М7)
 МОД НЗАДА-П(М7)
 УИА (М1)
 МОД ТУСП-1(М1)
 УИА (М2)
 ПБ ВОЗ-П(М7)
А47 СЧ ШЗБУДП
 И Е48-1(М13)
 ПО НИ
 УИИ М16(М13)
 УИА ПП(М17)
 СЧ Е2
 ПВ ТАЙМ(М15)
НОРМА НОП
 СЧ 0
 ПБ ЭКВЫХ
НИ СЧ Е2
 ПБ ЭКВЫХ
НВ СЧ Е3
 ПБ ЭКВЫХ
НЕТГЛ СЧ Е1
 ПБ ЭКВЫХ
А51 СДА 64+15
 И П177
 НТЖ ПРИЧУП
 ПЕ НЕТГЛ
 ЗП Р16
 ЗП Р
 СЧ (М2)
 ПО СОХЛИС        N РАБ = 0
 НТЖ Е1
 ПО ЭТОЛУП        N РАБ = 1
 НТЖ Е2
 ПО ЭКВОС         N РАБ = 3
 СЧ 11(М2)
 ПО ОТДБУФ
 СЧ НЗАДА-П(М7)
 ЗП Р16
ОТДБУФ НОП
 СЧИ М1
 СДА 64-6
 ИЛИ 10(М2)
 И П7777
 ПВ ТБУФ(М15)     ЗАПРОС БУФЕРНОГО ЛИСТА
 СДА 64+2
 СЛЦ НЛОС
 ЗП РЯ
 УИИ М17(М1)
 ПВ ЗАНПРП(М15)
 УИА 3
 СЧ РЯ
А51А УИ М11           М11:= ФИЗ. НОИМЕР БУФ.ЛИСТА
 СДА 64-38
 ЗП РЯ
 МОДА А51В
 УИА (М14)
А51Б НОП
 СЧ 9(М2)
 И П3
 УИ М6
 СЧ 9(М2)
 СДА 64+2
 НТЖ П7
 УИ М5
 СЛИ М5(М2)
 СЧ 9(М2)
 СДА 64-10
 УИ М16
 УИА 1024
 СЧ 20(М16)
 УИА 1027
 СЧ 24(М5)
 И ПРИП(М6)
 ЗП РР
 НТЖ 24(М5)
 ЗП 24(М5)
 СЧ РР
 СБР ПРИП(М6)
 СДА 64+38
 УИ М15
 ПБ (М14)
А51В НОП
 СЧ РЯ
 РЗБ ПРИП(М6)
 ИЛИ 24(М5)
 ЗП 24(М5)
 ПВ УПРИ(М10)
 СЧ Р
 МОДА ИЗМ
 УИА (М17)
ИЗМ1 СДА 64-ТОПНЗ
 УИА 1026
 НТЖ ТОП(М11)
 УИА 1027
 И Е48П42
 УИА 1026
ИЗ1 НОП
 НТЖ ТОП(М11)
 ЗП ТОП(М11)
 УИА 1027
 ПБ (М17)
ИЗМ УИА 1026
 СЧ ТОП(М15)
 УИА ИЗ3-ПРОРОК(М17)
 ПБ ИЗ2
ИЗ3 УИИ М11(М15)
 СЧ Р16
 МОДА ИЗМ1
 ПВ (М17)
 СЧ 10(М2)
 ПЕ НЕМЕН
 УИА ’46’(М17)
 СЧИ М17
НЕМЕН УИА ИЗ4-ПРОРОК(М17)
 СДА 64-СДВМЛ
 ИЛИ ЕОТКЛ
ИЗ2 УИА 1026
 НТЖ ТОП(М11)
 И ВИОЛПМ
 СЛИ М17(М3)
 ПБ ИЗ1
ИЗ4 СЧ 10(М2)
 ПО ЗПФИЗА
 СЧ Р16
 ПЕ КОНЭ51        ЛИСТ НУЖЕН
 СЧ Е5
 ПВ ЗАХЗАН(М15)
 СЧ 10(М2)
 СДА 64-СДВМЛ
 ИЛИ ЕОТКЛ
 ПВ ИСКБЛ(М16)
 СЧ Е5
 ПВ ГАШЗАН(М15)
КОНЭ51 НОП
 УИА 3
 ПБ НОРМА
ЭТОЛУП СЧ НЗАДА-П(М7)
 ЗП Р
 ЗП Р16
 УИА 1027
 СЧ Е48-1(М1)
 ЗП ШЗУПР
СОХЛИС СЧ 11(М2)
 ПБ А51А
ЗПФИЗА СЧИ М11
 ЗП 11(М2)
 ПБ КОНЭ51
ЭКВОС МОДА А51Б
 ПВ (М14)
 УИИ М11(М15)
 ПВ УПРИ(М10)
 СЧ 10(М2)
 СДА 64-СДВМЛ+ТБДТР
 ИЛИ 12(М2)        М12 ПОЛЬЗ. - НОМЕР ВИРТ.ЛИСТА
 СДА 64-ТБДТР
 УИА ИЗ5-ПРОРОК(М17)
 ПБ ИЗ2
ИЗ5 СЧ 10(М2)
 И П3
 УИ М6
 СЧ 10(М2)
 СДА 64+2
 НТЖ П7
 УИ М5
 СЛИ М5(М2)
 СЧИ М11           ФИЗ.ЛИСТ
 СДА 64-38
 РЗБ ПРИП(М6)
 ИЛИ 24(М5)
 ЗП 24(М5)
 СЧ 9(М2)
 НТЖ П37
 УИ М5
 СЧ 10(М2)
 НТЖ П37
 УИ М6
 СЧ Е48(М5)
 НТЖ Е48(М6)
 ЗП РР
 НТЖ 22(М2)
 ЗП 22(М2)
 СЧ РР
 НТЖ 32(М2)
 ЗП 32(М2)
 СЧ РР
 НТЖ ЛИСТМБ(М2)
 ЗП ЛИСТМБ(М2)
 СЧ 11(М2)
 ПЕ КОНЭ51        М33 НЕ МЕНЯТЬ
 СЧ 10(2)
 СДА 64-10
 ЗП РР
 СЧ 3(М2)
 И П1777
 ИЛИ РР
 ЗП 3(М2)
 ПБ КОНЭ51
ПРИЧУП КОНД В’103’
Р КОНД В’0’
РР КОНД В’0’
Р16 КОНД В’0’
РЯ КОНД В’0’
РЯ1 КОНД В’0’
БОЭК1 ВНЕШ ИСКБЛ
ДИСП70 ВНЕШ П3,ГОД,Е46П48,ТАЙМ,П60,Е40П1,ШИФРМ
ДИСП70 ВНЕШ ТВ40,П17,ЗАПВЕТ,ЗАНПРП
ДИСП70 ВНЕШ ГАШЗАН,ЗАХЗАН,НОМАРС
ДИСП70 ВНЕШ УКАЗД,ВСЕЕД,П377,ТУСП
ДИСП70 ВНЕШ ОБЩМЛ,П7,П77,П30,П37
ДИСП70 ВНЕШ Е48П9,П12,Ш1,ОСПОД,П7777,ИНФ
ДИСП70 ВНЕШ НЛОС
ДИСКИ ВНЕШ Е16П1,КТОМ,КТОМЛ
МОТТ ВНЕШ ШКУСТР,ШКАС,ШКОПТТ
ПРОЦЕС ВНЕШ ВОЗ,(П,ПРОЦЕС),Р1,Р2,Р3,LS
ПРОЦЕС ВНЕШ ЖДИ,НЗАДА
ВЗУ ВНЕШ (ПП,П)
ДИСП70 ВНЕШ (ЛИСТУП,П50),ТБУФ,Е48П7,П177,ПРИП,ШКИВ,ШЗУПР
ДИСП70 ВНЕШ ТЗФСТР,ШЗБУДП,СОБА40,МИНЗ,НЗАРХ,МАКЗ,П1777
КАЧКА ВНЕШ ШКРЗ,R2
СОСТАВ ВНЕШ СДВМЛ,ЕОТКЛ
КИТ ВНЕШ Е48П42
ГЕНС1 ВНЕШ ВИОЛПМ
СОСТАВ ВНЕШ ТОП,ТОПНЗ,ТБДТР,ОБЩТОМ,АРХМЛ,МДР,ШЗНМБ,ЛИСТМБ
ХЛАМ ВНЕШ УПРИ
*КОНЕЦ ЧАСТИ

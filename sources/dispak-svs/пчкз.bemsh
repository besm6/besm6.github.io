 Б
ПЧКЗ СТАРТ ’10000’
 УПОТР ПЧКЗ(М5)
 ПАМ 48  ССЫЛКИ ,ПЕЧАТЬ
Н И Е42П1
 ЗП Р-АВРАСП(М7)
 СЧИ М5
 ЗП ДЛЯПЧ-АВРАСП(М7)
 СЧИ М14
 ЗП ЗМ14
 СЧ ЗМ14
 ПЕ Н1    АВОСТ  В ЗАДАЧУ
 СЧ 23(М1)
 СДА 64+15
 И П177
 УИ М15     ПРИЧИНА СНЯТИЯ
 ПО ЗАП1    КЗ
 СЛИА -13(М15)
 УИИ М16(М15)
 СЛИ М15(М15)
 СЛИ М15(М16)
ЗАП1 МОДА РАСПЕЧ-АВРАСП(М7)
 СЛИА (М15)   АДР РАСПЕЧ В ТАБЛ РАСПЕЧ
 УИА -2(М16)
ЗАП СЧ (М15)
 МОДА (М16)
 ЗП ШПЭГ+2(М1)
 СЛИА 1(М15)
 МОДА ЗАП
 ЦИКЛ (М16)    УПРЯТАЛ РАСПЕЧ В ИПЗ
Н1 НОП ------
 СЧ 0           ДАЙ ЛИСТ
       ПВ    ТБУФ(М15)     ДАЙ БУФЕР
       СДА   64-8
       УИА   3
 УИ М10  БАЗА М.К.
 УИИ М12(М5)    БАЗА ПЧКЗ
       СДА   64-2
 ЗП КУС15З        КУС15З := АДР БУФ
 СЧ ШКРЗ
 СДА 64+17
 ПО ЧТМК
 СЧ Е19
ЧТМК ИЛИ КУС15З
       ИЛИ   МАЛКАТ
       ПВ    ФИЗОБМ(М16)   ЧТ.КАТАЛ.ЗАДАЧ ДЛЯ ПЛАНИРОВ.
 УИА 3
 УИИ М15(М6)
 СЛИА 1(М15)
 СЧИ М15
 СДА 64+4
 ВЧОБ Е2
 СДА 64-8
 УИ М16  А СЕКТОРА
 СЛИ М16(М10)
 СЧИ М15
 И П17
 УИ М15
 СЛИ М16(М15)
 СЧ СТПМК(М16)
 ИЛИ СТП
 ЗП СТП
 СЧ ИН(М16)
 ЗП ССЫЛКА
 СЧ ТЕР(М16)
 ЗП КЗ4
 СЧ ФИО(М16)
 ЗП ФИОК
 СЧ ФИО1(М16)
 ЗП ФИОК+1
 СЧ ШКРЗ
 НТЖ Е48(М6)
 ЗП РЯ2
 СЧ 0
 ЗП СТАТЗ+15
 УИА -МЧПЗ(М16)
СУМКОМ СЧ РЯ2
 И Е48+НПЗ+МЧПЗ(М16)
 ПО СУМКО2
 НТЖ РЯ2
 ЗП РЯ2
 МОД ТУСП+НПЗ+МЧПЗ(М16)
 УИА (М15)         АИПЗ
 СЧ 23(М15)
 СДА 64+15
 И П177          ПРИЧ.СНЯТИЯ
 ПО СУМКО1
 ЗП ПРСБО
 СЧ Е48
 МОД ПРСБО
 СДА 64-24
 И КСБОЙ
 ПЕ СУМКО2
СУМКО1 СЧ 35(М15)
 СДА 64+32
 ЗП РЯ1      МЛ.РАЗ.ЗАК.ВРЕМ.
 СЧ ЯЧВР(М15)
 СДА 64+39
 СДА 64-16
 ИЛИ РЯ1
 ЗП РЯ1
 ЗП РЯ1
 СЧ 39(М15)
 СДА 64-25
 СЛЦ 39(М15)
 СЛЦ 33(М15)
 СДА 64+23
 СЛЦ РЯ1
 СЛЦ Е3
 И Е1П25
 СЛЦ СТАТЗ+15
 ЗП СТАТЗ+15
 СЧ ЖДЕТ(М15)
 И Е24П1
 СЛЦ СТАТЗ+15
 ЗП СТАТЗ+15
СУМКО2 МОДА СУМКОМ
 ЦИКЛ (М16)
 СЧ КУС15З
 ИЛИ КУСБК
 СЛЦ НОММЛ1
 ЗП КУС15З
 ПВ ФИЗОБМ(М16)
 УИА 3
 СЧ ПОРНЗ(М10)
 СДА 64+24
 И П7777
 ПВ ПЕРЕВ(М15)
 СДА 64-16
 ИЛИ ПОРНОМ
 ЗП ПОРНОМ
 СЧ ССЫЛКА
 СДА 64+37
 И П1770         А З-ЧИ В Б.К.
 УИ М15
 СЛИ М15(М10)
       УИА   -4(М16)
 УИИ М10(М12)
ПЕРЕНК МОДА (М15)
       СЧ    4(М16)
 ЗП СТАТ+1(М10)   5 СЛОВ ИЗ Б.К.
 СЛИА 1(М10)
       МОДА  ПЕРЕНК
       ЦИКЛ  (М16)
       И     Е48П17
 ЗП СТАТ+5(М12)
 СЧ 7(М15)
       И     Е48П17
 ЗП СТАТ+9(М12)
 СЧ 5(М15)
       И     Е48П17
 И Н36П25      МЕСТО ПОД МЛ.Р-ДЫ РАЗДЕЛ.ВРЕМЕНИ
 ЗП СТАТ+8(М12)
 СЧ 6(М15)
 ЗП СЛОВО6
 И Е30
 ЗП СТАТ+12(М12)
 ЗП КЗ4+3
 СЧ 6(М15)
 СДА 64+41
 СДА 64-16
 ЗП СТАТ+6(М12)
 СЧ (М15)
 СДА 64+29
 И П777
 СДА 64-9
 ИЛИ СТАТ+6(М12)
 ЗП СТАТ+6(М12)
 СЧ КУС15З
 ПВ ТБУФ(М15)  ОТКАЗ ОТ ЛИСТА ПОД Б.К.
       УИА   3
 СЧ ССЫЛКА
       СДА   64+25
 И П37777
 ИЛИ СТАТ+9(М12)
 ЗП СТАТ+9(М12)
 СЧ СТАТ+2(М12)
 И ЕКТ
 ПО НЕТ      *НЕТ КОНТР.ТОЧКИ
 СЧ 35(М1)
 СДА 64+32
 ЗП РЯ1      МЛ.Р.ЗАК.ВР.
 СЧ ЯЧВР(М1)
 СДА 64+39
 СДА 64-16
 ИЛИ РЯ1
 СДА 64+5
 ЗП РЯ1
 СЧ СТАТ+1(М12)
 СДА 64+20
 СДА 64-20
 ИЛИ РЯ1
 ЗП СТАТ+1(М12)
 СЧ 23(М1)
 СДА 64+15
 И П177      *ПРИЧИНА СНЯТИЯ ЗАДАЧИ
 ЗП ПРСБО
 СЧ Е48
 МОД ПРСБО
 СДА 64-24
 И КСБОЙ
 ПО НЕТ
ЕСТЬ СЧ Е1
 ЗП РКТ      *ПРИЗНАК КТ И БЫЛ СБОЙ
 СЧ 0
 ЗП СТАТ+10(М12)  *ВРЕМЯ СЧЕТА ЗАДАЧИ ОБНУЛЯЕМ ПРИ СБОИ
 ПБ ОБХВР
НЕТ СЧ СТАТ+1(М12)
 СДА 64-5
 ЗП СТАТ+10(М12)
 СЧ 39(М1)
       СДА   64-25
 СЛЦ 39(М1)
 СЛЦ 33(М1)
       СДА   64+23
 СЛЦ СТАТ+10(М12)
       СЛЦ   Е3
       И     Е1П25
 ЗП СТАТ+10(М12)  ВР.СЧЕТА З-ЧИ
ОБХВР СЧ ЯЧТР(М1)
 СДА 64+СДТ
 И П777
 ЗП R2 ЗАКАЗ ТРАК
 СЧ СТАТ+1(М12)
 СДА 64+20
 И П777
 ЗП РЯ1
 ВЧ R2
 ПЕ ОБХ1
 СЧ РЯ1
 ЗП R2          НАЧАЛЬНЫЙ ЗАКАЗ БОЛЬШЕ
ОБХ1 СЧ СТАТ+1(М12)
 СДА 64+20
 НТЖ R2
 И П777
 СДА 64-20
 НТЖ СТАТ+1(М12)
 ЗП СТАТ+1(М12)   ЗАК.ТР.МБ
       СЧ    ДАТА
       СДА   64+24
       СДА   64-37
 ИЛИ СТАТ+6(М12)
 ЗП СТАТ+6(М12)
 СЧ 35(М1)
       И     Е16П32
 ЗП СТАТ+11(М12)  ОБР.К МБ
 СЧ ЯЧОБМЛ(М1)
       СДА   64+36
 ИЛИ СТАТ+11(М12)
 ЗП СТАТ+11(М12)  ОБР.К МЛ
 СЧ 35(М1)
 СДА 64-33
 ИЛИ СТАТ+11(М12)
 ЗП СТАТ+11(М12)  ОБР.К МД
 СЧ ШИФРМ(М1)
 ЗП СТАТ(М12)
 СЧ ЯЧСЧМБ(М1)
       И     Е46П48
       СДА   64+18
 ИЛИ СТАТ+6(М12)
 ЗП СТАТ+6(М12)
 СЧ 23(М1)
       СДА   64+15
       СДА   64-17         ПРИЧИНА СНЯТИЯ,ЧИСЛО П/К ВЫВОДА
 ЗП СТАТ+7(М12)   НА ПИ И ПЛ
 СЧ СТАТ+8(М12)
       СДА   64+16
НОВ И П377
       ЗП    ЦО
 ПО АЦП0          ЗАК ’АЦП 0’
 УМН КЗ101 100 В ПОР;В МЛ.РАЗР.360(ЧИСЛО СТРОК
 СЧМР  64   В 1 МЕТРЕ АЦПУ
 СЛЦ 32(М1) ЧИСЛО ВЫДАН.СТРОК НА АЦПУ
 И Е16П1
       ИЛИ   Л64
 ДЕЛ КЗ102         N СТРОК В СМ В НОРМ.ВИДЕ
 СЛ Л64
АЦП0 И П77777
       ЗП    ЦМ
 ИЛИ СТАТ+8(М12)
 ЗП СТАТ+8(М12)   ВЫД.СМ АЦПУ
 СЧ ЯЧЧТР(М1)
 СДА 64+СДТ
 СЛЦ R2
 И П777
 ЗП ЦТР
 ИЛИ СТАТ+6(М12)    ИСП ТР
 ЗП СТАТ+6(М12)
 СЧ 33(М1)
 СДА 64-20
 И Е30П25
 ПЕ ОБХОД
 СЧ АВОС(М1)
 И Е32Е30
 ПЕ ОБНМ
 СЧ РКТ
 ПЕ ОБНМ
 СЧ ЗОНЫ(М1)
 И Е41
       ПО    ОБХОД
ОБНМ СЧ СТАТ+8(М12)
 И Е48П17
 ЗП СТАТ+8(М12)
 СЧ 0
 ЗП РКТ
 ЗП ЦМ
ОБХОД  СЧ    ВРЕМЯ
       ЗП    ВКСЧЕТ   ВРЕМЯ К-ЦА СЧЕТА
       СДА   64-23
 И Е48П30
 ИЛИ СТАТ+10(М12)
 ЗП СТАТ+10(М12)  КОНЕЦ СЧЕТА ДЕЛ.64
 УИА КЗ4-НОВ+7(М11)
       СЧ    К9
ОСТА МОДА НОВ-КЗ4-3(М11)
 ЗП КЗ4
       МОДА  ОСТА
       ЦИКЛ  (М11)
 СЧ ЯЧЛИС(М1)
 И П7777
       ЗП    ЦОЗУ  ****УЧЕТ ЛИСТОВ ОЗУ****
       ПВ    ПЕРЕВ(М15)    В СИМВ.УПП
       ИЛИ   К1
 ЗП КЗ4+61  ЧИСЛО ЛИСТОВ ЗАК. В ЗАДАЧЕ
 СЧ ЛИСОСТ(М1)
 СЛЦ ЦОЗУ
 И П7777
 ЗП КЗ4+71
 СЛЦ П37
 НТЖ СТАТ+10(М12)
 И П37
 НТЖ СТАТ+10(М12)  ИСП.ОЗУ-1
 ЗП СТАТ+10(М12)
 СЧ КЗ4+71
 ПВ ПЕРЕВ(М15)
 ИЛИ К1
 ЗП КЗ4+71  ЧИСЛО ИСП. ЛИСТОВ В ЗАД.
 СЧ ЦТР     ИСП-НО ТР.
       ПВ    ПЕРЕВ(М15)    В СИМВОЛЫ УПП
       ИЛИ   К3
 ЗП КЗ4+72  ЧИСЛО ИСП. ТРАКТОВ
 СЧ R2 ЧИСЛО ЗАКАЗ.ТРАКТОВ ЗАДАЧЕЙ
       ПВ    ПЕРЕВ(М15)    В СИМВОЛЫ УПП
       ИЛИ   К3
 ЗП КЗ4+62 ЧИСЛО ЗАК. ТРАКТОВ
 МОДА КОЛПОД
 СЧ (М6)
 СДА 64+24
 ПВ ПЕРЕВ(М15)
 ЗП ПОДКМБ
 МОДА КОЛПОД
 СЧ (М6)
 И Е24П1
 ПВ ПЕРЕВ(М15)
 ЗП ПОДКОП
 МОДА КЗ4+87
 УИА (М17)
 УИА -31(М13)
 УИА 24(М14)
НУМ СЧ АРХМЛ(М1)
 И Е48-24(М14)
 ПЕ НЕТНУ
 СЧИ М14
 СДА 64+1
 УИ М10
 СЧИ М14
 И Е1
 НТЖ Е1
 УИ М2
 НТЖ Е1
 СДА 64-2
 УИ М11
 МОДА (М1)
 СЧ ШЗНМБ-12(М10)
 СДА 64+8(М11)
 И П17
 ПО НЕТНУ
 МОДА (М1)
 СЧМ ШЗНМБ-12(М10)
 СБР Е8(М2)
 СДА 64+47
 УИМ М2
 СДА 64+2
 УИ М10
 СЧМР
 СДА 64+46
 УИ М11           М11:=N КВАНТА В СЛОВЕ ОБЩТОМ
 МОДА (М1)
 СЧ ОБЩТОМ(М10)
 СД КТОМ-1(М11)
 И Е16П1
 ЗП РАЯ          ДЕЛЬТА+НУ(ФИЗ)
 И П77
 УИ М10          М10:=НУ ФИЗ
 СЧ МДР(М1)
 И Е48-8(М14)
 ПО ДАЛ
 СЧ НОМЛД
 СБР НАПРМГ
 СДА 64+42
 СЧМ РАЯ
 СДА 64+12
 СДА 64-6
 ИЛИ (М17)
 ЗП РАЯ
 СЧ СЕГМ
 ПБ СИ1
ДАЛ СЧ УКАЗД(М1)
 И Е48-8(М14)
 ПО ЛЕНТА
 СЛИА 56(М10)
ЛЕНТА СЧ ТЗНМЛ-24(М10)
 И Е47
 ПЕ СИ
 СЧ ТЗНМЛ-24(М10)
 И П7777
 ПВ ПЕРЕВ(М15)
СИ1 СДА 64-16
 МОДА (М2)
 ИЛИ КЗ103
 СЧИМ М14
 СДА 64-6
 СЧМ РАЯ
 И П77
 ИЛИ (М17)
 СДА 64-36
 РЗБ КЗ106
 ИЛИ КЗ104
 СЧМ РАЯ
 СДА 64-32
 РЗБ КЗ107
 ИЛИ КЗ105
 НТЖ КЗ105
 ПО ПРОБ
 НТЖ КЗ105
ВВ НОП 0
 ЗП (М17)         МАГАЗИН
НЕТНУ СЛИА 1(М14)
 МОДА НУМ
 ЦИКЛ (М13)
       СЧ    ГОД1
       И     П7    НОМЕР ЭВМ
       СДА   64-30
 ИЛИ СТАТ+6(М12)
 ЗП СТАТ+6(М12)
       РЗБ   К5
       ИЛИ   К2
       ИЛИ   К4
 ЗП КЗ4+21 ДАТА:ЧИСЛО И МЕСЯЦ
 СЧ СТАТ(М12)
       РЗБ   К9
 ЗП КЗ4+124   6 СТАР.СИМВ.ШИФРА
 СЧ СТАТ(М12)
       СДА   64-24
       РЗБ   К9
 ЗП КЗ4+125   6 МЛ.СИМВ.ШИФРА
 СЧ СТАТ+9(М12)
 СДА 64+23
       И     П77777
       СДА   64-10
 МОДА ПВР1
 УИА (М10)
 УИА КЗ4+25-ПЧКЗ(М11)   НАЧ ВВД
ПВР МОДА КЗ97
 ПВ (М13)
 СДА 64+32
 СЛИ М11(М5)
       ИЛИ   К1
 ЗП (М11)
 СЧ РЯ3
       СДА   64-16
       РЗБ   КЗ111
       ИЛИ   К2
 ЗП 1(М11)
 ПБ (М10)
ПВР1 СЧ СТАТ+9(М12)
       СДА   64-1
       И     П77777
       СДА   64-9
       ЗП    ВНСЧЕТ  ВРЕМЯ НАЧАЛА СЧЕТА
 УИА КЗ4+27-ПЧКЗ(М11)
 МОДА ПВР
 ПВ (М10)    НАЧ РЕШ
       СЧ    П7
       СДА   64-21
       И     МГРП
       СДА   64-13
 ИЛИ СТАТ+6(М12)
 ЗП СТАТ+6(М12)   N СМЕНЫ 35-37 РР.
       СДА   64-11
       СДА   64+45
       ИЛИ   ДВЕРС+1
 ЗП КЗ4+24 НОМЕР СМЕНЫ
 СЧ СТАТ+10(М12)
       СДА   64+23
 УИА КЗ4+29-ПЧКЗ(М11)
 МОДА ПВР
 ПВ (М10)    КНЦ РЕШ
 СЧ РЯ3
 РЗБ КЗ111
 ИЛИ =П’ 00.00’
 ЗП СТАТ+14(М12)
 СЧ СТАТ+1(М12)
 СДА 64-5
 И Е1П25
 УИА КЗ4+63-ПЧКЗ(М11)
 МОДА ПВР
 ПВ (М10)     ЗАК РЕШ
 СЧ СТАТ+10(М12)
       И     Е1П25
       ЗП    ЦВ
 УИА КЗ4+73-ПЧКЗ(М11)
 МОДА ПВР
 ПВ (М10)     РЕШ ЗАД
 СЧ ЗМ14
 ПЕ КЗ23Б1
 СЧ РЯ3
 РЗБ КЗ111
 ИЛИ =П’000.00’М40В’14’
 ЗП ИНФ(М1)    ВРЕМ РЕШ
КЗ23Б1 НОП ------
       СЧ    ГОД1
 И П7
       СДА   64-16
       ИЛИ   К6
 ЗП КЗ4+33 ЭВМ РЕШЕНИЯ
 СЧ УКАЗД(М1)
 СДА 64-10
 РЗБ НОМЭ
 ИЛИ НОМИ
 ЗП КЗ4+34
 СЧ ГОД
 ЗП КЗ4+22 МЛ. ЦИФРА ГОДА
 СЧ СТАТ+11(М12)
       И     Е16П32
       СДА   64+15
 ЗП ЦМБ
 СЧ ЯЧСЧМБ(М1)
       И     Е46П48
       СДА   64+28
 ИЛИ ЦМБ
       ЗП    ЦМБ
       ПВ    ПЕРЕВ(М15)    В СИМВОЛЫ УПП
 ЗП КЗ4+82 ЧИСЛО ОБР. К МБ В ЗАДАЧЕ
 СЧ ПРМЗОН(М1)
 СДА 64+1
       И     Е1П23
       ЗП    ЦПР  ***УЧЕТ ПЕРЕМОТОК***
       ПВ    ПЕРЕВ(М15)
       ИЛИ   К4
 ЗП КЗ4+85 ПРМЗОН
 СЧ ЦПР
 И Е17П1
 ИЛИ СТАТ+7(М12)
 ЗП СТАТ+7(М12)
       СЧ    ЦМ
       ПВ    ПЕРЕВ(М15)    В СИМВОЛЫ УПП
       СДА   64-8
       РЗБ   ДЛЯЗП
       ИЛИ   К7
 ЗП КЗ4+76 ЧИСЛО ИСП. САНТИМЕТРОВ
 СЧ СТАТ+8(М12)
       СДА   64+36
       ЗП    ЦВУ
       ПВ    ПЕРЕВ(М15)    В СИМВОЛЫ УПП
       ИЛИ   К8
 ЗП КЗ4+77 ЧИСЛО П/К С ВУ
       СЧ    ЦО
       ПВ    ПЕРЕВ(М15)    В СИМВОЛЫ УПП
       СДА   64-24
       ИЛИ   К7
 ЗП КЗ4+66 ЗАК.МЕТРЫ
 СЧ ЦВУ
 ПЕ ОБХ3
 СЧ СЛОВО6
 И Е29
 ПЕ ОБХ3
 СЧ =П’   FS ’
 ЗП ВЛАД+9
ОБХ3 СЧ СТАТ+7(М12)
       СДА   64+24
       И     П7777
       ЗП    ЦПЛ
       ПВ    ПЕРЕВ(М15)    В СИМВОЛЫ УПП
       ИЛИ   К8
 ЗП КЗ4+80 ЧИСЛО П/К НА ПЛ
 СЧ СТАТ+7(М12)
       СДА   64+36
       ЗП    ЦПИ
       ПВ    ПЕРЕВ(М15)    В СИМВОЛЫ УПП
       ИЛИ   К8
 ЗП КЗ4+79 ЧИСЛО П/К ВЫВОДА НА ПИ
 СЧ СТАТ+1(М12)
 СДА 64-10
 РЗБ =П’0А3770’
 ИЛИ =П’ 0000 ’
 ЗП КЗ4+81      ПРИЗНАК АС И N ТЕРМ.
 СЧ ЖДЕТ(М1)
 И Е24П1
 СЛЦ СТАТ+10(М12)
 И Е1П25
 ЗП ТКОМ          КОМ.ВРЕМЯ
 МОДА БУФТРВ
 УИА (М15)
 ПВ СЧЕТРВ(М16)
 СЧ ШЗНМБ+ТРВ(М1)
 СДА 64+16
 ИЛИ Л64
 РЖА 0
 ЗНАК 0           НОРМАЛИЗ.
 НТЖ 0
 ПЕ ОБХ01
 СЧ Л641
ОБХ01 НОП
 ЗП РЯ1         ВЗВЕШ.ВРЕМЯ КОНКУРЕНЦИИ
 РЖА 3
 СЧ ШЗНМБ+ТРВ+1(М1)
 СДА 64+16
 ВЧОБ УЧЕТРВ
 ЗП РЯ2         ВР.РЕШ.З-ЧИ ПО ЖИЗНИ ОС
 ИЛИ Л64
 РЖА 0
 ЗНАК 0           НОРМАЛИЗ.
 ДЕЛ РЯ1
 РЖА 3
 ЗП РЯ1         1/ВЗВЕШ.СРЕДНЕЕ ЧИСЛО КОНКУР.
 ВЧ ГРАНЬ
 ПЕ ОБХ2        ДОСТАТОЧНО КОНКУРЕНТОВ
 СЧ ГРАНЬ
 ЗП РЯ1
ОБХ2 СЧ ТКОМ          КОМ.ВРЕМЯ
 ВЧОБ РЯ2
 ПЕ ОБНТРВ        ТSS<ТКОМ
 ИЛИ Л64
 РЖА 0
 ЗНАК 0          НОРМАЛИЗ.
 УМН РЯ1
 РЖА 3
 СЛ Л64
 И Е21П1
БЕЗОПЛ ЗП ЦТЕР
 СЧ ШЗНМБ+ТРВР(М1)
 СДА 64+16
 СЛЦ ЦТЕР
 И Е21П1
 ЗП ЦТЕР
 СЛЦ Е5
 СДА 64+5
 ЗП РЯ1
 И П7777
 СДА 64-24
 ИЛИ СТАТ+8(М12)
 ЗП СТАТ+8(М12)
 СЧ РЯ1
 СДА 64+12
 СДА 64-25
 ИЛИ СТАТ+10(М12)
 ЗП СТАТ+10(М12)  СТ.Р-ДЫ ТРВ
 СЧ ЦТЕР
 ИЛИ Л64
 РЖА 0
 ДЕЛ =Е’50’
 РЖА 3
 СЛ Л64
 И Е17П1
 ПВ ПЕРЕВ(М15)
 ИЛИ К4
 ЗП КЗ4+78      РАЗДЕЛЕННОЕ ВРЕМЯ
 СЧ ЖДЕТ(М1)
 СЛЦ ЦТЕР          ПРОСТОИ+ТРВ
 СЛЦ Е5
 СДА 64+5
 И Е16П1
 ИЛИ СТАТ+5(М12)
 ЗП СТАТ+5(М12)
 СЧ ТКОМ
 СЛЦ ЦТЕР          МПР.ВРЕМЯ
 УИА КЗ4+31-ПЧКЗ(М11)
 МОДА ПВР
 ПВ (М10)    МПР
 СЧ СТАТ+11(М12)
       СДА   64+33
       ЗП    ЦМД
       ПВ    ПЕРЕВ(М15)    В СИМВОЛЫ УПП
 УИА -ДЛИНВ(М14)
       ИЛИ   К4
 ЗП КЗ4+83 МД
 СЧ 33(М1)
       СДА   64-20
 И Е30П25
       ИЛИ   НОМВЫД
       ЗП    НОМВЫД   ЗАПИСЫВАЕМ НОМЕР ВЫДАЧИ
 СЧ СТАТ+11(М12)
       И     П77777
       ЗП    ЦМЛ
       ПВ    ПЕРЕВ(М15)    В СИМВОЛЫ УПП
       ИЛИ   К4
 ЗП КЗ4+84 ЧИСЛО ОБР.К МЛ В ЗАД.
 СЧ КЗ4
 СДА 64-24
 РЗБ =П’000077’
 ИЛИ СУМВВД
 ЗП СУМВВД
 СЧ КЗ4
 СДА 64-30
 РЗБ =П’777777’
 ЗП СУМВВД+1
 ЗП КЗ4+139
 СЧ КЗ4+3
 НТЖ Е30
 ПО НЕВВД       ПОВТОРЯТЬ НЕ НАДО
 СЧ СУМВВД+2
 ЗП НЕВВОД
 ЗП НЕВВОД+1
НЕВВД СЧ АВОС(М1)
       И     Е31
       ПО    КЗ23А         ПОДЛЕЖИТ УЧЕТУ
       СЧ    Е25
 ИЛИ СТАТ+6(М12)
 ЗП СТАТ+6(М12)   ПРИЗН:НЕ НАДО РОСПИСЬ
       СЧ    К9
       ЗП    ПОЛУЧ
       ЗП    ПОЛУЧ+1
       ЗП    ПОЛУЧ+2
КЗ23А  МОДА  ДВЕРС-1
       СЧ    (М14)         ПЕРЕС.ПОСМЕРТН.ВЫДАЧИ ЗАДАЧИ НА КЗ
       МОДА  ДЛИНВ(М14)
 ЗП КЗ4
       МОДА  КЗ23А
       ЦИКЛ  (М14)
       УИА   -18(М14)   19 СЛОВ
       СЧ    ДВЕРС  ЗАПИС. ДАТУ ВЕРСИИ
 ЗП КЗ4+23
КЗА    МОДА  ЗАК+1
       СЧ    (М14)
       МОДА  (М14)
 ЗП КЗ4+60
       МОДА  КЗА
       ЦИКЛ  (М14)
       УИА   -3(М14)
 СЧ 0
 ЗП РЯ3
 ЗП СОТЧАС
КЗБ    МОДА  ИСП+1
       СЧ    (М14)
       МОДА  (М14)
 ЗП КЗ4+70
       МОДА  КЗБ
       ЦИКЛ  (М14)
       СЧ    БОБ
 ЗП КЗ4+86
       СЧ    ШФР
 ЗП КЗ4+123
 СЧ СТП
 ЗП КЗ4+126   НОМЕР СТ.ПАСПОРТА
 СЧ Т
 СЛЦ ЖИЗОС
 МОДА КЗ97
 ПВ (М13)
 РЗБ КЗ111
 ИЛИ ВРЖИЗ
 ЗП ВРЖИЗ
 СЧ СОТЧАС
 СДА 64-40
 ИЛИ ВРЖИЗ
 ЗП ВРЖИЗ
 СЧ РЯ3
 СДА 64-32
 РЗБ КЗ111
 ИЛИ ВРЖИЗ+1
 ЗП ВРЖИЗ+1
 УИА -21(М15)
КЗВ МОДА НОМВЫД+21
 СЧ (М15)
 МОДА (М15)
 ЗП КЗ4+149
       МОДА  КЗВ
 ЦИКЛ (М15)
 СЧ ПРЕДЕЛ
 И Е32
 ПЕ БЮД
ПЧКОН СЧ ЗМ14
 УИ М14
 СЧИ М5
 ЗП ДЛЯПЧ-АВРАСП(М7)
 Э64 КЗ112-ПЧКЗ+2048
***  А Р Х И В  ***
 СЧ ШЗНМБ(М1)
 И АХ
 ПО СТВИПЗ
 СЧ ССЫЛКА
 И П1777
 СДА 64-24
 СЛЦ НОММЛ2
 ЗП РАЯ
 СЧ 0
 ПВ ТБУФ(М15)
 УИА 3
 СДА 64-8
 УИ М10
 СДА 64-2
 ИЛИ Е18
 ИЛИ РАЯ
 ЗП РАЯ
 ПВ ФИЗОБМ(М16)
 УИА 3
 СЧ ШКАНУ(М10)
 И Е48П17
 ПО КПЧ1
 ЧЕД
 УИ М4             ЧИСЛО АРХМЛ
 СЛИА ИМЕНА(М10)
 МОДА ПОЛЕПЧ
 УИА (М14)
 УИА -40(М15)
ЦПЧОБЛ СЧ (М10)
 И Е48П43
 ПО КЦПЧ
 УИИ М12(М10)
 СЛИА LЗГЛВВ(М12)
ЦИМЯ УИА 40(М13)
ЦСИМВ МОДА ЧТСИМВ
 ПВ (М16)
 НТЖ П377
 ПО КОБЛ
 НТЖ П377
 МОДА ЗПСИМВ
 ПВ (М16)
 ПБ ЦСИМВ
КОБЛ СЛИА 40(М15)
 МОДА ДЛЯОБ
 ПИО (М15)
 СЛИА -40(М15)
 СЧ =В’143’
 МОДА КОБЛ
 УИА (М16)
 ПБ ЗПСИМВ
ДЛЯОБ СЧ (М10)
 СЛЦ Е1
 СДА 64-32
 РЗБ ДЛЯАР2
 ЗП 1(М14)
 СЧ (М10)
 И Е40
 СДА 64+39
 УИ М16
 СЧ (М10)
 РЗБ ДЛЯАР1
 ЗП РАЯ1
 СЧ (М10)
 И Е17
 НТЖ Е17
 ПО ЕСТЬПЛ
 СЧ (М10)
 И Е41
 СДА 64+40
 ПЕ ЕСТЬПЛ
 СЧ К9
 ЗП 1(М14)
 СЧ П17
ЕСТЬПЛ МОДА (М16)
 ИЛИ ДЛЯАР
 ИЛИ РАЯ1
 ЗП (М14)
 СЛИА 2(М14)
 УИА -40(М15)
 СЧ П17
 НТЖ Е2
 МОДА ЗПСИМВ
 ПВ (М16)
 СЛИА -1(М4)
КЦПЧ СЧ (М10)
 СДА 64+СДВЧУР
 И П77
 УИ М11
 СЛИ М10(М11)
 МОДА ЦПЧОБЛ
 ПИНО (М4)
 СЧ П377
 МОДА ЗПСИМВ
 ПВ (М16)
 СЧ РАЯ
 ПВ ТБУФ(М15)
 СЧИ М5
 ЗП ДЛЯПЧ-АВРАСП(М7)
 Э64 ПЧОБЛ-ПЧКЗ+2048
 МОД ЗМ14
 УИА (М14)
 ПБ СТВИПЗ
КПЧ1 СЧ РАЯ
 ПВ ТБУФ(М15)
 ПБ СТВИПЗ
КЗ112 ЗП КЗ4-ПЧКЗ+2048
 ЗП КЗ4-ПЧКЗ+2197
 КОНД М23В’1’
КЗ97 УИА (М17)
 УИА -2(М14)
КЗ97А ЗП РЯ2
       МОДА  КЗ108
       ВЧ    2(М14)        -1 ЧАС(1 МИН.ИЛИ 1 СЕК)
       ПЕ    КЗ97Б
       СЛИА  1(М17)        ЧИСЛО ЧАС.(МИН.ИЛИ СЕК)
       ПБ    КЗ97А
КЗ97Б СЧ РЯ3
       СДА   64-16
 ЗП РЯ3
 СЧМР
 ПО КЗ97В
 ЗП СОТЧАС
КЗ97В СЧИ М17
       ПВ    ПЕРЕВ(М15)    В СИМВОЛЫ УПП
 ИЛИ РЯ3
 ЗП РЯ3 РЕЗУЛЬТАТ ПЕРЕВОДА
 СЧ РЯ2
       УИА   (М17)
       МОДА  КЗ97А
       ЦИКЛ  (М14)
 СЧ РЯ3
       ПБ    (М13)         ВЫХОД ИЗ П/П
ОБНТРВ СЧ 0
 ПБ БЕЗОПЛ
ПРОБ СЧ К9
 ПБ ВВ
СИ СЧ СИСТ
 ПБ СИ1
СТВИПЗ МОДА ВАВР
 ПИНО (М14)
 УИА -15(М15)
ПСТА МОДА СТАТЗ
 СЧ 15(М15)
 МОДА 15(М15)
 ЗП 4(М1)
 МОДА ПСТА  СТАТ 16 ЯЧ В ИПЗ (20-1 ИР)
 ЦИКЛ (М15)
 СЧ ССЫЛКА  ИН ИЗ МАЛ КАТ
 ЗП 21(М1)
 СЧ СТП  СТ ПАСП
 И П77
 ЗП 20(М1)
ВАВР СЧ Р-АВРАСП(М7)
 МОДА ВПЧКЗ-АВРАСП(М7)
 УИА (М15)
 ПБ ТБУФ
СЕГМ КОНД П’00СЕГМ’
СИСТ КОНД П’00СИСТ’
ВЕР    КОНД  П’ДАТА Р’
       КОНД  П’ЕШ.  В’
       КОНД  П’ЕРСИЯ ’
ВЕ     КОНД  П’130 СМ’
       КОНД  П’Е  НАЧ’
       КОНД  П’.ВВОДА’
       КОНД  П’   НАЧ’
       КОНД  П’. РЕШ.’
       КОНД  П’   КОН’
       КОНД  П’. РЕШ.’
 КОНД П’   КОМ’
       КОНД  П’.ВРЕМ.’
       КОНД  П’ Э.РЕШ’
       КОНД  П’. Э.ВВ’
       КОНД  П’.  ОСТ’
       КОНД  П’.ЭКС. ’
       КОНД  П’   ОСТ’
       КОНД  П’.ЛИМ. ’
       КОНД  П’   ОСТ’
 КОНД П’.ДЕЖ. ’
       КОНД  П’ОСТ.Б0’В’175’
ДВЕРС КОНД П’16.08.’
 КОНД П’89   0’
ГОД КОНД П’.00   ’
 ВХОД ГОД,ВЕ
ВЛАД КОНД П’0 ОС Д’М40В’175’
 КОНД П’ИСПАК ’
       КОНД  П’  00ОЗ’М24В’173’М16В’43’
 КОНД  П’У ТРАК’
       КОНД  П’ТЫ ВРЕ’
       КОНД  П’М.РЕШ.’
       КОНД  П’ КАТЕГ’
       КОНД  П’. АЦПУ’
       КОНД  П’   ВУ ’
 КОНД П’  ТРВ ’
       КОНД  П’   ПИ ’
       КОНД  П’   ПЛ ’
       КОНД  П’ ТЕРМ ’
       КОНД  П’  МБ  ’
       КОНД  П’  МД  ’
       КОНД  П’  МЛ  ’
ПРМЗ   КОНД  П’ПРМЗОН’
ЗАК    КОНД  П’000ЗАК’М40В’175’М32В’173’М24В’26’
       КОНД  П’АЗАНО:’
ОРГ1   КОНД  П’0ИПМ А’М40В’175’
 КОНД В’0’
ИСП    КОНД  П’00ИСПО’М40В’173’М32В’25’
       КОНД  П’ЛЬ-НО:’
БОБ    КОНД  П’0 БОБ:’М40В’175’
ШФР    КОНД  П’000   ’М40В’175’М32В’173’М24В’26’
СТП КОНД П’   З00’
К1 КОНД П’  0000’
К2     КОНД  П’.00.00’
К3     КОНД  П’   000’
К4     КОНД  П’ 00000’
К5     КОНД  П’03 01 ’ ДЛЯ ДАТЫ
К6     КОНД  П’  00  ’
К7     КОНД  П’000.00’
К8     КОНД  П’  0000’
К9     КОНД  П’      ’
НОМВЫД КОНД  Х’7Д0Ф000Ф0Ф0Ф’
ПОЛУЧ  КОНД  П’   ПОД’
       КОНД  П’ЛЕЖИТ ’
       КОНД  П’УЧЕТУ ’
       КОНД  М24П’   ’Х’7В1919’
ФИОК  КОНД (2)В’0’
 КОНД П’*     ’
НЕВВОД КОНД П’НЕ ВВО’
 КОНД П’ДИТЬ! ’
СУМВВД КОНД П’ S= 00’
 КОНД П’000000’
 КОНД П’      ’
ВРЖИЗ КОНД П’000.00’
 КОНД П’.00   ’
ПОРНОМ КОНД П’0000 П’
 КОНД П’ОДКАЧК’
 КОНД П’И: ОП=’
ПОДКОП ПАМ 1
 КОНД П’,  МБ=’
ПОДКМБ ПАМ 1
 КОНД М40В’172’
КЗ103 КОНД П’ :’
 КОНД П’*:’
КЗ104 КОНД П’00(00)’
КЗ105 КОНД П’-0000,’
КЗ106 КОНД М40В’7’М32В’7’М16В’7’М8В’7’
КЗ107 КОНД М32В’1’М24В’7’М16В’7’М8В’7’
КЗ108  КОНД  В’537440’     1 ЧАС С ЧАСТ.ПРЕРЫВ.50
       КОНД  В’5670’       1 МИН
       КОНД  В’62’         1 СЕК
КЗ111  КОНД  Х’ФФФФ00ФФФФ’
ДЛЯАР КОНД П’  :00+’
 КОНД П’ *:00+’
ДЛЯАР1 КОНД М16В’7’М8В’7’
ДЛЯАР2 КОНД М40В’1’М32В’7’М24В’7’М16В’7’М8В’7’В’7’
СОТЧАС КОНД В’0’
ВР1 КОНД В’0’
МЕТР1 КОНД В’0’
К144 КОНД М47В’1’В’144’
КУСЛИМ КОНД М24В’610’М17В’1’ КУС.КТЛИМ.
КУСЛМ КОНД В’0’
БЮД СЧ СТАТ+6(М12)
 СДА 64-29
 И Е46П48
 ПО ПЧКОН      З-ЧА ВНЕ Б.С.
 ЗП РЯ1
 СЧ СТАТ+1(М12)
 СДА 64-5
 И Е1П25
 ЗП РЯ2           ЗАК.ВР.
 СЧ ЦО
 УМН К144
 СЧМР 64
 ЗП ЦО            ЗАК.СМ АЦПУ
 СЧ СТАТ+7(М12)
 СДА 64+17
 И П177
 ПО БЮДКЗ         НОРМ.КЗ
 ЗП ПРСБО
 СЧ Е48
 МОД ПРСБО
 СДА 64-24
 И КСБОЙ
 ПЕ БЮДЧП
* НОРМ.КЗ И АВОСТЫ ПОЛЬ-ЛЯ
БЮДКЗ СЧ РЯ2
 ВЧ ЦВ
 ПЕ ОШВР          ВОЗВРАТ=0
 ЗП ВР1           ВР.ЗАК.-ВР.ИСП.
ОШВР НОП
 СЧ ЦО
 ВЧ ЦМ
 ПЕ БЮДЖ          ВОЗВРАТ=0
 ЗП МЕТР1         СМ ЗАК.-СМ ИСП.
 ПБ БЮДЖ
БЮДЧП СЧ РЯ2
 ЗП ВР1           ВОЗВРАТ=ЗАКАЗ
БЮДИНК СЧ ЦО
 ЗП МЕТР1         ВОЗВРАТ=ЗАКАЗ
БЮДЖ СЧ 0
       ПВ    ТБУФ(М15)
       УИА   3
       СДА   64-8
       УИ    М4
       СДА   64-2
       ИЛИ   КУСЛИМ
       СЛЦ   НОММЛ1
       ЗП    КУСЛМ
       ПВ    ФИЗОБМ(М16)
 УИА 3
* ОПР.КАТ.ЗАК.(М13) И КАТ.РЕШ.(М10)
 СЧ РЯ1
 НЕД П77777
 УИ М10        КАТ.РЕШ.
 УИИ М13(М10)
 СЧ Е48(М10)
 НТЖ РЯ1
 ПО ЗАПИС      КАТ.РЕШ.=КАТ.ЗАК.
 ЗП РЯ1
 НЕД П77777
 УИ М13
 СЧ Е48(М13)
 НТЖ РЯ1
 ПО ЗАПИС
 УИА 2(М13)      КАТ.ЗАК.’ЭКС’
ЗАПИС  МОДА  (М13)
       СЧ    ДЕЖУРН  ЗАКАЗАНА СООТВ.КАТЕГОР.
 ЗП КЗ4+65
       МОДА  (М10)
       СЧ    ДЕЖУРН  КАТ.РЕШ.ЗАДАЧИ
 ЗП КЗ4+75
       СЧ    0
       ЗП    ЦЭ ВРЕМЯ ЭКСПР.
       ЗП    ЦЛ ВРЕМЯ ЛИМИТНОЕ
       ЗП    ЦО   ТАМ БЫЛА БУМАГА
       ЗП    ЦД ВРЕМЯ ДЕЖ.
       СЧ    ЦВ  ВРЕМЯ СЧЕТА
       МОДА  (М10)  ПЕРЕНОС ПО СООТВЕТСТВ. КАТЕГ.
       ЗП    ЦД
******ПОЛУЧЕНИЕ СТОИМОСТИ ЗАДАЧИ С КОЭФ. И БЕЗ НИХ
       УИА   -14(М14)
 РЖА 0
КЦ     МОДА  14(М14)  ВСЕГО 15 КОЭФ.
       СЧ    ЦОЗУ
       ПО    ЦК
       ИЛИ   Л64
       ЗНАК  0    НОРМАЛИЗАЦИЯ
       МОДА  14(М14)   ** ВСЕГО 15 КОЭФИЦИЕНТОВ**
       УМН   С1(М4)
       ЗП    ЦВ
       МОДА  14(М14)   НА СООТВЕТСТВУЮЩИЙ КОЭФ.
       УМН   L1(М4)
ЦК     СЛ    ЦО  ЗАПИСЬ ЦЕНЫ
       ЗП    ЦО  ОБЩАЯ ЦЕНА
       МОДА  КЦ
       ЦИКЛ  (М14)
       УМН   С19(М4)
       РЖА   3
 СЛ Л641 ДЕНОРМАЛИЗАЦИЯ
       И     Е32П1
       ПВ    ПЕРЕВ(М15)  ПЕРЕВ В 10 СИСТ.
       ЗП    СТОИМ
 СЧ СТАТ(М12)
       СБР   МАСКО(М4)
 АIF (NОТ ∧АС).М
 ЗП R2
 НТЖ =М40Х’41’
 ПО ОТД2
 СЧ R2
 И =М44В’17’
 ПО ОТД2
 НТЖ =М44Х’9’
 ПО ОТД2
 НТЖ =М44Х’9’
 ПБ ОТД1
ОТД2 СЧ R2
ОТД1 СДА 64+40
 АGО .М1
.М АNОР
       СДА   64+40
.М1 АNОР
       ПВ    ИЗ10В8(М15)
       СДА   64-2    УМН.НА 4
 УИ М13  АДРЕС ЯЧЕЕК ОТДЕЛА
 СЛИ М13(М4)
 СЧ СМНАОТ(М13)
 И Е24П1
 СЛЦ МЕТР1
 ИЛИ Л64
 ДЕЛ СТО
 СЛ Л64
 И Е24П1
       ПВ    ПЕРЕВ(М15)  В СИМВОЛЫ УПП
       ИЛИ   К4   К4: 00000
 ЗП КЗ4+41 ОСТАТОК БУМАГИ НЕ БОЛЕЕ 10000
 МОДА (М10)
 СЧ ДЕЖ(М13)
       СЛ    ВР1
       МОДА  (М10)  ОТДЕЛУ
 ЗП ДЕЖ(М13)
 УИА -2(М11)
 УИА (М4)
ОЛИМО  УИА   (М17)  МИНУТ И СЕКУНД
 МОДА 2(М11)
 СЧ    ДЕЖ(М13)  ПО СООТВ.КАТЕГОРИИ
       И     ШТУБ
КЗТ    ЗП    R2
       ВЧ    КЗ108
       ПЕ    КЗУ
       СЛИА  1(М17)  ДОБАВ.ЕЩЕ ОДИН ЧАС
       ПБ    КЗТ
КЗУ    СЧИ   М17
       ПВ    ПЕРЕВ(М15)
       ИЛИ   К4  К4: 00000
 МОДА (М4)
 ЗП КЗ4+39 ЧАСЫ
       УИА   (М17)
       УИА   -1(М14)
 СЧИ М13
 ЗП РЯ1
       СЧ    R2
       МОДА  КЗ97А  ПЕРЕВОД МИН И СЕК
       ПВ    (М13)
       СДА   64-16
       РЗБ   КЗ111
       ИЛИ   К2   К2: .00.00
 МОДА (М4)
 ЗП КЗ4+40 МИН.И СЕК.
 СЛИА -2(М4)
 СЧ РЯ1
 УИ М13
       МОДА  ОЛИМО
 ЦИКЛ (М11)
       СЧ    ЦО
 ЗП СТАТ+13(М12)  СТОИМОСТЬ
 СЧ КУСЛМ
 ПВ ТБУФ(М15)
 ПБ ПЧКОН ЗДЕСЬ ВЫХОД НА КОНЕЦ
ПРОБНМ СЧ 23(М1)
 СДА 64+22
 И П7777
 ЗП ЦМ
 ПО ОБНМ
 СЧ ШИФРМ(М1)
 НТЖ К812
 И П7777
 ПО ОБНМ
 СЧ ЗОНЫ(М1)
 И Е43
 ПЕ ОБНМ      ЕСТЬ БАЦ И БПЛ
 СЧ СТАТ+8(М12)   ЕСТЬ БАЦ,НЕТ БПЛ
 И Е48П17
 ИЛИ ЦМ
 ЗП СТАТ+8(М12)
 ПБ ОБХОД
НЕТБАЦ СЧ 23(М1)
 СДА 64+22
 И П7777
 ЗП РЯ2
 ПО ОБХОД
 СЧ ШИФРМ(М1)
 НТЖ К812
 И П7777
 ПО ОБХОД
 СЧ ЗОНЫ(М1)
 И Е43
 ПО ОБХОД    НЕТ БАЦ,НЕТ БПЛ
 РЖА 3       НЕТ БАЦ,ЕСТЬ БПЛ
 СЧ СТАТ+8(М12)
 И Е16П1    ИСП МЕТРЫ ОБЩИЕ
 ВЧ РЯ2           МЕТРЫ ГП-ПЛ
 ЗП РЯ1
 СЧ СТАТ+8(М12)
 И Е48П17     ЗАК.МЕТРЫ
 ИЛИ РЯ1      ИСП.АЦПУ
 ЗП СТАТ+8(М12)
 ПБ ОБХОД
РАЯ ПАМ 1
РАЯ1 ПАМ 1
П14 КОНД В’14’
П16 КОНД В’16’
ПЧОБЛ КК 0,ПОЛЕПЧ-ПЧКЗ+2048
 КК 0,ПОЛЕПЧ-ПЧКЗ+2048
 КОНД М23В’1’
*
ЧТСИМВ СЧ (М12)
 СДА 64(М13)
 И П377
 МОДА ЧТСИМ
 ПИНО (М13)
 СЛИА 1(М12)
 УИА 40(М13)
 ПБ (М16)
ЧТСИМ СЛИА -8(М13)
 ПБ (М16)
*
ЗПСИМВ СДА 64(М15)
 ЗП РАЯ1
 СЧ П377
 СДА 64(М15)
 И (М14)
 НТЖ (М14)
 ИЛИ РАЯ1
 ЗП (М14)
 МОДА ЗИП
 ПИНО (М15)
 СЛИА 1(М14)
 УИА -48(М15)
ЗИП СЛИА 8(М15)
 ПБ (М16)
НОМЭ КОНД М24В’7’М8В’1’
НОМИ КОНД П’  0-0 ’
ДЕЖУРН КОНД П’ ДЕЖ. ’
 КОНД П’ ЛИМ. ’
 КОНД П’ ЭКС. ’
ГРАНЬ КОНД Е’.16666666’
Е30П25 КОНД М24В’77’
Е21П1 КОНД М21В’-1’В’-1’
Е32Е30 КОНД М29В’5’
Е32П1 КОНД М32В’-1’В’-1’
Е48П30 КОНД М29В’-1’
СТО КОНД Е’100’
КЗ101 КОНД М47В’1’В’360’
КЗ102 КОНД Е’2.4’
КУСБК КОНД М24В’446’М17В’1’
Л641 КОНД М39В’641’
Н36П25 КОНД М24В’7777’В’-1’
ШТУБ КОНД М30В’-1’В’-1’
К812  КОНД Х’812’
КСБОЙ КОНД Х’080400800040’ ПРИЧИНЫ СБ.АРХ,ОШ.МЛ,ОШ.МД,СБ.ВС
ПРСБО ПАМ 1               ПРИЧИНА СБОЯ
РКТ   ПАМ 1
П1770 КОНД В’1770’
ЗМ14 ПАМ 1
КУС15З ПАМ 1
РЯ1 ПАМ 1
РЯ2 ПАМ 1
РЯ3 ПАМ 1
СЛОВО6 ПАМ 1
ЦО ПАМ 1
R2 ПАМ 1
ТКОМ ПАМ 1
БУФТРВ ПАМ 6
СТАТЗ ПАМ 16
 Е
 М
 ВХОД Н,ОРГ1,ПОРНЗ,ДВЕРС
ВЗУ ВНЕШ ФИЗЗП
В3В4 ВНЕШ ШКАНУ,ИМЕНА
АРФА ВНЕШ СДВЧУР,LЗГЛВВ
БОЭК1 ВНЕШ Л,АРХМЛ,СЧЕТРВ
ДИСКИ ВНЕШ НОМЛД,Е16П1,КТОМ
АВРАСП ВНЕШ АВРАСП,ВПЧКЗ,ДЛЯПЧ,Р1,Р
КАЧКА ВНЕШ Е48П17,ШКРЗ
КИТ ВНЕШ Е1П25
МОТТ ВНЕШ ПРОБ6
ВИСП ВНЕШ КОЛПОД
ДИСП70 ВНЕШ УКАЗД,ТЗНМЛ,ОБЩТОМ,П3,П77
ДИСП70 ВНЕШ АВОС,ВРЕМЯ,(ГОД1,ГОД),(ДАТА,ГОД),ДЛЯЗП
ДИСП70 ВНЕШ Е15П11,Е16П32,Е17П1,Е1П23,Е24П1
ДИСП70 ВНЕШ Е46П48,Е47П1,ЖДЕТ,ЖИЗОС,ИЗ10В8
ДИСП70 ВНЕШ (Л64,ЭК9),МГРП,НОММБ,НОММЛ,П60
ДИСП70 ВНЕШ П177,П17,П3777,П377,П37,П77777,П7777
ДИСП70 ВНЕШ П777,П7,ПЕРЕВ,ПРЕДЕЛ,ПРМЗОН,Т,ТБУФ
ДИСП70 ВНЕШ ТРВ,ТУС,УЧЕТРВ,ФИЗОБМ,ШЗНМБ,ТРВР
ДИСП70 ВНЕШ АХ,НОММЛ1,НОММЛ2,ШИФРМ,Е48П43,П1777
ДИСП70 ВНЕШ Е48П25,ЯЧОБМЛ,ЯЧСЧМБ
ДИСП70 ВНЕШ ЗОНЫ,МДР,НАПРМГ,НПЗ,МЧПЗ,ЧПЗ
ДИСП70 ВНЕШ МАЛКАТ,ЯЧЛИС,ТУСП,ЯЧВР,ЕКТ
СОСТАВ ВНЕШ ЯЧЧТР,ЯЧТР,СДТ,ЛИСОСТ
 ЭКВИВ (ССЫЛКА,ПЧКЗ),(КЗ4,ССЫЛКА+1),(ДЛИНВ,20)
*
 ЭКВИВ (КЗ1,НОВ),(ЦОЗУ,КЗ1),(ЦТР,КЗ1+1),(ЦД,КЗ1+2)
 ЭКВИВ (ЦЛ,КЗ1+3),(ЦЭ,КЗ1+4),(ЦМ,КЗ1+5)
 ЭКВИВ (ЦВУ,КЗ1+6),(ЦFS,КЗ1+7),(ЦПИ,КЗ1+8)
 ЭКВИВ (ЦПЛ,КЗ1+9),(ЦМБ,КЗ1+10),(ЦМД,КЗ1+11)
 ЭКВИВ (ЦМЛ,КЗ1+12),(ЦПР,КЗ1+13),(ЦТЕР,КЗ1+14)
 ЭКВИВ (ВНСЧЕТ,КЗ1+15),(ВКСЧЕТ,КЗ1+16)
 ЭКВИВ (ЦВ,КЗ1+17),(СТОИМ,КЗ1+18)
 ЭКВИВ (ПОРНЗ,5),(СТАТ,СТАТЗ-ПЧКЗ)
ПЛАН2 ВНЕШ ФИО,ФИО1,ТЕР,СТПМК,ИН
 ЭКВИВ (СМНАОТ,12),(ДЕЖ,13),(МАСКО,425)
 ЭКВИВ (С1,430),(С19,448),(ЗГТ,449),(ЗГТ1,450)
 ЭКВИВ (ЗГТ2,451),(L1,454)
 ЭКВИВ (ПОЛЕПЧ,КЗ4)
АВРАСП ВНЕШС РАСПЕЧ
ДИСП70 ВНЕШ ИНФ,ШПЭГ,Е42П1,П37777
*КОНЕЦ ЧАСТИ

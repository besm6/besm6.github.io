* ОТЛАДЧИК ОС ДИСПАК. БЛОК ПОДКЛЮЧЕНИЯ ЗАДАЧИ И РЕАКЦИИ НА
* ЗАВЕРШЕНИЕ ПРИЕМА И НА ОБРЫВ СВЯЗИ С ТЕРМИНАЛОМ (НЕРЕЗИДЕНТ)
 Б
НРОТ СТАРТ ’10000’
 УПОТР НРОТ(М7)
ИПЗ КОНД В’0’
 КОНД М41В’3’
 КОНД (3)В’0’
 КОНД В’3’
 КОНД (16)В’0’
 КОНД В’0’
 КОНД В’0’     НЕ ЗАНИМАТЬ
АП КОНД П’0  АП ’
 КОНД В’0’
НТ КОНД В’0’
НЗ КОНД В’0’
КУСОВД КОНД М2В’27’М17В’3’
ПРОВД КОНД В’0’
РАД КОНД М8П’77777’
Э64 КОНД В’0’
 КОНД М15В’1’       ДЛЯ Э64 !!!!!
 КОНД М47В’1’
КТУС КОНД В’0’
ОШ1 КОНД М8П’0 ОШ.’В’377’
 КОНД В’0’
КГТСЛ КОНД М40В’377’М26В’1’М35В’1’В’777777’
БУВ КОНД (2)В’0’
НАЧАЛО СЧ ШКОТ          * В Е G I N *
 УИА (М2)
 ПО АШТО          НЕТ ВНОВЬ ВКЛЮЧАЕМЫХ ЗАДАЧ
 НЕД 0             N ЗАДАЧИ В ТСЛ ЗАНОСИТ ’БОП’ !
 УИ М14
 ЗП Р
 СЧ ТУСП-1(М14)
 УИ М3
 СЧ АВОС(М3)
 СДА 64+33
 УИ М2
 МОДА МАТПР
 ПВ (М15)
 МОДА ИС1
 УИА (М15)
 СЧИ М15
 ПВ ЗАПВЫХ(М15)
РИПО1 СЧ 0             СМ:= 0
 УИА -7-ДИП(М17)
РИПО УИА ’2000’        РОСПИСЬ ПЕРВЫХ 8 СЛОВ ИПО
 МОДА (М17)
 ЗП 7+ДИП(М2)
 УИА 3
 МОДА РИПО
 ЦИКЛ (М17)
 СЧ Е41           ВЫДАВАТЬ СИТУАЦИЮ НА ЭКРАН
РР ИЛИ Е1            Э70 ИГНОРИРОВАТЬ ПРИ ТРАССИРРОВКЕ
 УИА ’2000’
 ЗП 6+ДИП(М2)
 УИА ’2003’
ИС1 СЧ ШКОТ
 И Е48-1(М14)
 ПО ОТКПР         БЫЛА ДИРЕКТИВА  И П О
 НТЖ ШКОТ
 ЗП ШКОТ
 СЧ ШКРДО
 ИЛИ Е48-1(М14)    РАЗРЕШЕНИЕ НАБОРА ДИРЕКТИВ
 ЗП ШКРДО
 СЧ ТЕРЕЗА(М3)
 НЕД П77777
 УИ М16
 СДА 64-40
 ЗП НТ
 СЧ Е48(М16)
 ИЛИ ШКОТЛ
 ЗП ШКОТЛ         ЗАПИСЬ В ШКАЛУ ОТЛАДОЧНЫХ ТТ
 УИА 3
 СЧ НАТЕ
 И Е40П1
 ИЛИ НТ
 ЗП НАТЕ
 МОДА НАТЕ
 УИА (М16)
 МОДА ОТКПР
 УИА (М15)
 ПБ ВТ1А          ВЫДАЧА НАЧ.ТЕКСТА,РАЗРЕШЕНИЕ ПРИЕМА
АШТО СЧ ШТО           АНАЛИЗ ШКАЛЫ ОТЛ. ТТ-ОВ
 ПО ЗР
 НЕД
 УИ М16
 СЛИА -1(М16)       М16:= N ТЕРМИНАЛА.’0’ -> ШТО
 СЧ Е48(М16)
 УИА ’2003’
 НТЖ ШТО
 ЗП ШТО
 УИА 3
 СЧИ М16
 СДА 64-40
 ЗП НТ
 СЧ ТСЛ(М16)
 СДА 64+20
 И П77          ПОД 48 З-Ч
 УИ М15
 СЧ ТСЛ(М16)
 И КОТ
 НТЖ КОТ
 ПЕ ПРИЕМ         ПРИНЯЛИ ДИРЕКТИВУ
 УИА 72(М13)       ОБРЫВ СВЯЗИ
 УИА -1(М2)
 УИА ’2003’
 СЧ ШКРДО
 И Е48-1(М15)
 ПО ИСКМ          ЗАДАЧА В РЕШЕНИИ
 НТЖ ШКРДО         ГАШЕНИЕ Е ЗАДАЧИ В ШКРДО
 ЗП ШКРДО
 СЧ ДСТОП
 И Е48-1(М15)
 ПО ОБР           ДИРЕКТИВЫ <СТОП> НЕ БЫЛО
 НТЖ ДСТОП         ГАШЕНИЕ Е ЗАДАЧИ В ДСТОП
 ЗП ДСТОП         Е ЗАДАЧИ -> МШГ
 СЧ Е48-1(М15)
 ИЛИ МШГП
 ЗП МШГП
ОБР СЧ Е48-1(М15)    Е ЗАДАЧИ В ШГ
 УИИ М3(М15)
 ПВ ВШГП(М15)
 УИИ М15(М3)
ИСКМ МОДА ИСК
 ПИНО (М2)          ЗАДАЧУ СНИМАЕМ
 СЧ ТСЛ(М16)      ГАШЕНИЕ ТСЛ
 И КГТСЛ
 ЗП ТСЛ(М16)
ИСК СЧ ТУСП-1(М15)
 УИ М3
 СЧ АВОС(М3)
 СДА 64-15
 СДА 64+15
 ЗП АВОС(М3)
 СЧ ШКОТЛ
 НТЖ Е48(М16)
 ЗП ШКОТЛ
 СЧ ШКИВ
 ИЛИ ЕНРОТ
 ЗП ШКИВ
 СЧИ М15
 ЗП ИЗАХВ+ИПЗ
 ПБ ЗР            ОБСЛУЖИВАЕМ ТОЛЬКО ОДНУ ЗАДАЧУ
ПРИЕМ СЧ Е48-1(М15)    ПОДГОТОВКА К РАБОТЕ БЛОКА ОВД
 ЗП НЗ
 НЕД
 ЗП ИЗАХВ(М7)
 СЧ ТУСП-1(М15)
 ЗП КТУС
 УИ М3
 СЧИ М15
 ЗП Р
 МОД ТСЛ(М16)
 УИА Н(М1)         М3:= АИПЗ. М1:= А БУФЕРА ПРИЕМА
 СЧ ШКРДО
 И Е48-1(М15)
 ПО ЗАРЕШ         ЗАДАЧА РЕШАЕТСЯ
 СЧ АВОС(М3)
 СДА 64+33
 УИ М2            М2:= АИПО
 СЧ ПРОВД
 ПЕ ВОВД          БЛОК ОВД В ПАМЯТИ
 ПВ ТБУФ(М15)     ДАЙ ЛИСТ
 СДА 64-8
 УИ М6
 СДА 64-2
 ИЛИ КУСОВД
 ИЛИ НОММБ1
 ЗП ПРОВД
 ПВ ФИЗОБМ(М16)   ЧТЕНИЕ ОВД
ВОВД УИА 3
 СЛИА 2+ДИП(М2)
 МОДА РОП1-ОВД(М6)
 УИА (М15)
 УИА 48(М13)
 СЧ П377
 ИЛИ 11(М1)
 ЗП 11(М1)
МАТПР МОД Р
 УИА (М17)
 ПБ ЗАНПРП
ЗАРЕШ СЧ (М1)          ВО ВРЕМЯ РЕШЕНИЯ ЗАДАЧИ РАЗРЕШЕНА
 НТЖ СТОП          ТОЛЬКО ДИРЕКТИВА <СТОП>
 СДА 64+8
 ПЕ ОШ            НЕ <СТОП>
 УИА ’2003’        УСТ. БЛПР
 СЧ 5(М3)
 И П7
 ПЕ NОSТОР        ЗАДАЧА В ОБМЕНЕ
 СЧ МШГП
 НТЖ Е48-1(М15)    РА ДИРЕКТИВ,УСТАНОВКА ПРИЗНАКА’СТОП’
 ЗП МШГП
 СЧ ШКРДО
 ИЛИ Е48-1(М15)
 ЗП ШКРДО
 СЧ ДСТОП
 ИЛИ Е48-1(М15)
 ЗП ДСТОП
ГДЕ УИА 3
 СЧ 3(М3)
 СДА 64-33
 РЗБ РАД
 ИЛИ П21
 ЗП БУВ+1
 СЧ АП
 ИЛИ НТ
ВС1 ЗП БУВ
 МОДА ОТКПР
 УИА (М15)
 ПБ ВТ1           ВЫДАЧА СООБЩЕНИЯ
ВТ СЧ Е23
 ПБ ТВ40          НА ВЫДАЧУ
ОТК2 УИА 1-ДИП(М15)    СУММИРОВАНИЕ ИНФОРМАЦИИ НА ИПО
 СЧ АВОС(М3)
 СДА 64+33
 УИ М2
 УИА (М14)
 СЧ 0
 УИА ’2000’        М2:= АИПО; НУЛЕВОЕ СЛОВО:≡ 0
 ЗП (М2)
КСИ УИА ’2000’        ЦИКЛИЧЕСКОЕ СЛОЖЕНИЕ
 СЛЦ (М2)
 ЗП (М2)
 МОДА (М14)
 СЧ 1(М2)
 УИА 3
 СЛИА 1(М14)
 МОДА КСИ
 ЦИКЛ (М15)         СУММИРОВАНИЕ ОСНОВНОГО МАССИВА
 МОДА КСИ1
 ПИО (М3)          КОМАНДЫ ПЕРЕСКОЧИЛИ - ОСН.ПОЛЕ RЕАDУ
 УИА (М3)
 УИА -4(М15)       ПЕРЕСКОК
 СЧ
 СЛИА 1(М14)
 ПБ КСИ
КСИ1 ПЕ КСИ           ИНФОРМАЦИЯ ОБ УЗЛАХ
 ПБ ОТК1          ’-’НЕ ВЫДАВАТЬ - < ИДИ > ИЛИ < ВЫП >
ОТКПР МОДА МИС           РАЗРЕШЕНИЕ НАБОРА ОЧЕРЕДНОЙ ОПЕРАЦИИ
 УИА (М16)         ( - )
 СЧ 33(М7)        ВРЕМЯ РАБОТЫ -> ПОЛЬЗОВАТЕЛЮ
 НТЖ Е48
 СЛЦ 33(М3)
 ЗП 33(М3)
 СЧ Е48
 ЗП 33(М7)
НАЧ МОДА НАЧАЛО        ПОСЛЕ ТВ40 (Е23) -> НАЧАЛО
 УИА (М15)
ОТК1 СЧ НТ            ГАШЕНИЕ ’ШЗПРИМ’
 СДА 64+40
 УИ М14
 УИА ’2003’
 СЧ ШЗПРИМ
 ИЛИ Е48(М14)
 НТЖ Е48(М14)
 ЗП ШЗПРИМ
 УИА 3
 МОДА НАЧАЛО
 ПИО (М15)         ’-’ НЕ ВЫДАЕМ
 СЧ (М16)
 ИЛИ НТ
 ЗП БУВ
ВТ1 МОДА БУВ
 УИА (М16)
ВТ1А УИИ ’32’(М15)     М32:= ВОЗВРАТ
 ПБ ВТ
NОSТОР МОДА ОБМЕН         ОПЕРАЦИЯ < С Т О П > НЕВОЗМОЖНА -
 УИА (М16)         - ВЫДАЧА СООБЩЕНИЯ ’ОБМЕН’
 СЧ Е48П41
 ЗП БУВ+1
 ПБ НАЧ
ЗР СЧ ПРОВД         ЗАВЕРШЕНИЕ РАБОТЫ
 ПО ЗР1           ОВД НЕТ В ОП
 ПВ ТБУФ(М15)     ВОЗЬМИ ЛИСТ
 СЧ 0
 ЗП ПРОВД
ЗР1 МОДА ЗР2
 ПИО (М2)          ЗАДАЧУ ПОЛЬЗОВАТЕЛЯ НЕ СНИМАЕМ
 УИИ М17(М13)      М17:=ПРИЧИНА СНЯТИЯ;М15:=АИПЗ
 УИИ М15(М3)
 ПВ АВОСТА(М16)
ЗР2 СЧ ЕНРОТ
 ПБ БЛИСКВ
ОШ СЧ ОШ1           ВЫДАЧА СООБЩЕНИЯ ОБ ОШИБКЕ
 ИЛИ НТ
 ПБ ВС1
ВЫЗКОД СЧ КУСКОД
 НТЖ ПРОВД
 ЗП ПРОВД
 СЛИА 8(М13)
 УИИ М4(М13)
 ПВ ФИЗОБМ(М16)
 УИИ М13(М4)
 ПБ НАЧН-НРКОД(М6)
*
КУСКОД КОНД М2В’16’М2В’27’ КУС НРКОДА
Р ПАМ 1
МИС КОНД М32П’0-’М24В’172’
НАТЕ КОНД П’ОТЛАД’
 ТЕКСТ П’ЧИК ОС↑’
СТОП КОНД М16П’СТОП’М8В’377’
ОБМЕН КОНД П’ОБМЕН’
 Е
 М
ДИСП70 ВНЕШ П77777,П377,П17,П7,П3
Е48П17 ВНЕШН КАЧКА.Е48П17
ДИСП70 ВНЕШ ЗАНПРП,ШКОТ,НОММБ1,ТБУФ,ТВ40,АВОСТА
ВЗУ ВНЕШ ФИЗОБМ
БОЭК1 ВНЕШ ЗАПВЫХ
МОТТ ВНЕШ Н,ШКОТЛ,ШКВИД
ОВД ВНЕШ ОВД,РОП1
 ВХОД ОТК2,ОШ,ОТКПР,ИСКМ,НЗ,НТ,КТУС,РИПО1,Э64,NОSТОР
 ВХОД ГДЕ,ВЫЗКОД
ДИСП70 ВНЕШ Е40П1,ВСЕЕД,БЛИСКВ,ТСЛ,АВОС,ШКИВ,ЗАНОСМ
ДИСП70 ВНЕШ ТУСП,МШГП,ТЕРЕЗА,П77
ВИСП ВНЕШ ВШГП
КИТ ВНЕШ Е48П41,ШКРДО,ДСТОП
МОТТ ВНЕШ КОТ,ДИП,Е48П45,ШТО,ШЗПРИМ
СОСТАВ ВНЕШ ЕНРОТ,ИЗАХВ
НРКОД ВНЕШ НАЧН,НРКОД
П21 КОНД В’21’
*ЧП
         ФИНИШ ,             В ВАШЕМ ФИНИШЕ ОШИБКА ИЛИ ЕГО НЕТ

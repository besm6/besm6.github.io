 МАСRО
∧М УВВ ∧А
 АIF ((∧ВS NЕ 0)).К1
∧М КК 27,∧А
 МЕХIТ
.К1 АIF (∧А ЕQ ’’’147’’’).К2
∧М ДК 27,∧А  ОСТАНОВ
 МЕХIТ
.К2 АNОР
∧М КК 0,0
 МЕХIТ
 МЕND
 МАСRО
∧М ИСКБВ ∧ТИП,∧АБВ
∧М УИА ’2003’
 СЧ ТГОЧДР+∧ТИП
 УИ ∧АБВ
 СЧ (∧АБВ)
 ЗП ТГОЧДР+∧ТИП
ТГОЧДР ВНЕШН ДИСП70.ТГОЧДР
 МЕND
ДИСКИ СТАРТ ’72321’
 Б
 Е
 М
∧С SЕТВ 1
ИПЗМД КОНД В’0’
ПВВМД ВНЕШН ПВВ.ПВВМД
 КОНД М41В’3’
 КОНД (2)В’0’
 МОДА ПРЛСЧБ
 ЗП 0
 КОНД В’201302007’
 КОНД (2)В’0’
ЯМ16Д КОНД В’0’
РЯМ КОНД В’0’
 КОНД (6)В’0’
РЯВ ПАМ 7
 КОНД М47В’1’   КОМ.СВЕРТКА
ПРР КОНД М39В’13’
НГД ПАМ 2
РС КОНД В’0’
РАЗЛ ПАМ 1
ИНФСД КОНД В’0’
УСМД КОНД В’0’
 КОНД (2)В’0’
 КОНД М47В’1’
ЗАВ ПАМ 1
СЧЗАКД КОНД В’0’
 КОНД В’0’
ЗАНКМД КОНД (2)М47В’1’
РЯД3 ПАМ 1
РЯД4 ПАМ 1
СОД ПАМ 2
ДАЙГОТ ПАМ 2
ЗАНМД ПАМ 2
МДНЕИС ПАМ 2
ЕNQМДС ПАМ 2
ШЗМД ПАМ 2
ЗУС ПАМ 2
ОСУ ПАМ 2
РЯД КОНД В’0’
РЯД1 КОНД В’0’
РЯД2 КОНД В’0’
РЯОД КОНД В’0’
РАБМД КОНД В’0’
НОМСВ КОНД В’0’
НОМЛД КОНД В’0’
Е16П1 КОНД В’177777’ НЕ РАЗДЕЛ С НОМЛД
Е32П17 КОНД М16В’177777’      С Е16П1
БУФКДЗ ПАМ 1
КУСКИ КОНД В’0’
ОТВАВ  ПАМ 1            ОТВЕТ ОБР.АВАРИИ МД
ПРЗАПС ПАМ 2            ПРИЗНАК СЕКТ.САПИСИ
ММБ КОНД В’-1’М42В’37’
МСТАР КОНД В’-1’М40В’177’
МНОВ КОНД В’-1’М44В’7’М35В’3’
МТЕСТ КОНД В’-1’М44В’7’
Е36П1 КОНД В’777777777777’
Е11Е9 КОНД М10В’1’М8В’1’
П11 КОНД В’11’
ЧЕЙБУС ПАМ 1
ЕСТЬБУ ПАМ 1
КОМОМД ПАМ 1             КОМАНДА ОПРОСА МД
*  НЕ РАЗДЕЛЯТЬ СПУД  ОТ БУФСТ (ДЛЯ СБОЙВУ)
СПУД ПАМ 1
ДРД ПАМ 1
ДРУД ПАМ 1
БУФОПР ПАМ 8
СПУСТ ПАМ 1
ДРСТ ПАМ 1
ДРУСТ ПАМ 1
БУФ99 ПАМ 8
ЕСТЬСТ ПАМ 1
БУФСТ ПАМ 8
Е34П25 КОНД М24В’1777’
       КОНД М24В’7777’
       КОНД М24В’37777’
КОНЗОН НОП ,
 КОНД М24В’01744’   7,25 МГБ
 КОНД М24В’07634’   29   МГБ
 КОНД М24В’36000’   100  МГБ
МД100М ПАМ 2
КТОМ КОНД М41В’140’
 КОНД М41В’120’
 КОНД М41В’100’
КТОМЛ КОНД М41В’40’
 КОНД М41В’60’
 КОНД М41В’100’
НОМРМД КОНД (2)В’0’
БУДАВ КОНД (2)В’0’
МСТР КОНД М36В’7’М19В’5’
П3700 КОНД В’3700’
П7600 КОНД В’7600’ НЕ РАЗД С П3700
НЗ ПАМ 2
ПРОСВ ПАМ 2
РИМ КОНД (2)В’0’
АПРО КОНД А(ПРО)
РАОБЩ КОНД В’0’
ВЫК КОНД (2)М32В’-1’В’-1’
ОСЧК КОНД В’-1’М23В’1’М11В’37’
ПРД ПАМ 1
О40 КОНД В’-1’М39В’1’
NКОБ КОНД В’0’
П167 КОНД В’167’
ПРСКМД ПАМ 2
ЗАНЛ ПАМ 2
КСБС КОНД М22В’17’
 КОНД М24В’3’М6В’3’
СТАРП КОНД В’0’
П2013 КОНД В’2013’
Е10П8 КОНД М7В’7’
ОШДР КОНД М32В’160001’
ПВКЛМД КОНД М44В’1’
ЯМГРП ПАМ 1
КСБЭ57 КОНД М46В’3’М24В’777’М18В’77’В’7777’
КРЭ70 КОНД М47В’1’М39В’1’М30В’37’М22В’17’М12В’77’В’7777’
С15 КОНД В’0’
УХВАТ ПАМ 1
ШЖЗАХС ПАМ 1  СИСТЕМН
ШЗЖЗАХ КОНД В’0’
ФЗ15 СЧ МДНЕИС-3(М16)
 ИЛИ Е32(М4)
 НТЖ Е32(М4)
 ЗП МДНЕИС-3(М16)
 СЧ ЗАНКМД-3(М16)
 И Е48
 ПЕ Д             НАПРАВЛ.ЗАНЯТО ДРУГОЙ МАШИНОЙ
 ПБ Д3
Д СЧ ОЖОСВ-3(М16)
 ПЕ Д3            ЗАПРОС К НАПРАВЛЕНИЮ ВЫДАН
 СЧ ОЧКУСД-1(М14)
ДС ЗП ОЖОСВ-3(М16)   ЗХ
 ПВ ЗАПРОС(М11)
ДА СЧ МГРП
 ИЛИ Е32(М16)
 ЗП МГРП
 ПБ Д3
Д11 СЧ ШОЧМД(М1)
 И ШОЧНМД-3(М16)
 ПО ДЗС
 ПВ ОПРЗАК(М2)
 СЧ 0
 ЗП ОЖОСВ-3(М16)
Д1 СЧ ШОЧНМД-3(М16)
 И Е48-1(М14)
 ПО ДИ
 СЧ ОЧКУСД-1(М14)
 ПВ МДНАП(М2)     ИНВ.N МД->М15; N НАПР,N МД->М1
 СЧ ЗАНМД-3(М16)
 И Е32(М4)
 ПЕ Д3            УСТР-ВО ЗАНЯТО
 ПВ ПОДОБМ(М11)   ВЫДАТЬ ЗАКАЗ В КМД
Д2 СЧ ЯМГРП
 ПВ ТВ67(М15)     ГАШ.29(28)Р ГРП, ОТКРЫТИЕ МАСКИ
 СЧ 0
 ЗП ЯМГРП
Д3 НОП ,
 УИА 3
 СЧ Е40
Д3А1 УИА 1027
 НТЖ ШЗПОМД
 ЗП ШЗПОМД
Д33 И Е48Е47
 ПО Д4А           ПРЕРЫВ.НЕ ПО ЗАПРОСУ
 НЕД Е2
 УИ М16           НОМ.НАПР.ПРЕРЫВ.->М16
 ПБ ПРЗ
Д5 СЧ СОД-3(М16)
 И Е24П17
 ПО Д5Д           НАПРАВЛЕНИЕ НЕ ЗАНЯТО ОБМЕНОМ
 СДА 64+16
 И ЯПРМД-3(М16)
 ПО Д8А
Д5Д СЧ ЯПРМД-3(М16)
 ПЕ ОБРПР         ЕСТЬ ОКОНЧИВШИЙСЯ ОБМЕН
Д7 СЧ СОД-3(М16)
 ПЕ Д8            ЕЩЕ НЕ ВСЕ ВЫДАННЫЕ ЗАКАЗЫ ОБРАБОТАНЫ
 СЧ ДАЙГОТ-3(М16)
 ПО Д7В
ОПРГД СЧ ЗАНКМД-3(М16)
 СДА 64+3
 И П3
 УИ М3
 СЧ СОД-3(М16)
 И П377
 ПО НПР
 СЧ 0
 ЗП ДАЙГОТ-3(М16)
НПР НОП 0
 СЧ ДАЙГОТ-3(М16)
 И Е32П25(М3)
 ПО Д7В
 СЧ Е32П25(М3)
 И НОМРМД-3(М16)
 ИЛИ РМД-3(М16)
 ЗП РМД-3(М16)
 СЧИ М3
 СДА 64-3
 УИ М6
 УИА 3
 УИА -7(М13)
 СЧИ М16
 И Е3
 СДА 64-45
 ЗП РЯД2
 СЧ КОМ99
 ЗП КОМОМД
 СЧ МД100М-3(М16)
 И Е32(М6)
 ПО ОНЕ100
 СЧ КОМ99         ВРЕМЕННО;НАДО СЧ КОМ97
 ЗП КОМОМД
ОНЕ100 НОП ,
 УИА БУФОПР(М11)
 СЧИ М6
 СДА 64-42
 ИЛИ РЯД2
ОПРМД ЗП РЯД2
 РЗБ НАПРМГ
 НТЖ Е10П8
 ПВ ОПГД(М5) ОПРОС ГОТОВНОСТИ
 ПО ПОПРМД НЕ ГОТОВ
 СЧ Е32(М6)
 НТЖ РМД-3(М16)
 ЗП РМД-3(М16) МД ГТ
ПОПРМД СЛИА 1(М6)
 СЧ РЯД2
 СЛЦ Е43
 ЦИКЛ ОПРМД(М13)
ОПГД ВНЕШН ПВВ.ОПРГД
 СЧ ДАЙГОТ-3(М16)
 ИЛИ Е32П25(М3)
 НТЖ Е32П25(М3)
 ЗП ДАЙГОТ-3(М16)
 СЧ РМД-3(М16)
 НТЖ РМД1-3(М16)
 ИЛИ ВЫК-3(М16)
 И Е32П25(М3)
 И НОМРМД-3(М16)
 ПЕ ОПРДИС
 СЧ ПВКЛМД      ПРИЗНАК ПРОВЕРКИ НА ’ВКЛЮЧИ МД’
 СЛЦ Е43
 ЗП ПВКЛМД     СЧЕТЧИК ДО Е48
 ПО ОПРДИС
 СЧ ЗАВ
 УИ М7            М7 := БАЗА  АВМД
 ПЕ НОВД-А(М7)
 СЧИ М16
 И Е3
 ПЕ ОПИ
 СЧ НОМРМД+1
 ЗП ДАЙГОТ+1
ОПИ ПАМ 0
Д7В СЧ ЗАНКМД-3(М16)
 СДА 64+3
 УИ М3
 ПВ ОСВМД(М6)     ОСВОБОЖДЕНИЕ НАПРАВЛЕНИЯ
 СЧ ШОЧНМД-3(М16)
 ПО Д9            НЕТ ЗАКАЗОВ К НАПРАВЛЕНИЮ
 УИА 3
 НЕД 0
 УИ М14           ВЗЯТЬ СТАРШИЙ ЗАКАЗ
 СЧ ОЧКУСД-1(М14)
 ЗП ОЖОСВ-3(М16)
 СДА 64+39
 И Е3П1
 УИ М3
 ПВ ЗАПРОС(М11)   ЗАПРОС К ПРИОР.ЛИНИИ НАПРАВЛ.
 УИА ДА(М2)
 ПБ УСТШЗ
Д8 ПАМ 0
Д8А СЧ Е32(М16)      ОТКРЫТЬ МАСКУ 29(28)Р В МГРП
Д9 ПАМ 0
 ИЛИ ЯМГРП
 ЗП ЯМГРП
ДВ УИА Д2(М2)
 ПБ УСТШЗ         ГАШ.44(43)Р,УСТ.40Р В ШЗПОМД
ПРО СЧ Е46-3(М16)
 ПБ ПРОХ1
ПРОХ СЧ НОМРМД-3(М16)
 ЗП ПРОСВ-3(М16)
 СЧ Е26-3(М16)
ПРОХ1 УИА 3
 ЗП РАЗЛ
 УИА -3(М3)
ПРОС1 СЧ ПРОСВ-3(М16)
 ПО ПРОС11
 И Е32П25+3(М3)
 ПЕ ПРОС11
 ЦИКЛ ПРОС1(М3)
ПРОС11 СЧ ЗУС-3(М16)
 ИЛИ ОСУ-3(М16)
 И Е32П25+3(М3)
 ПЕ ПРОС2
 СЧ ГОД
 И Е48П46
 ПЕ ПРОС2 ОБЩ МЛ
 ЦИКЛ ПРОС1(М3)
 СЧ 0
 ЗП ПРОСВ-3(М16)
ПС1 ПАМ 0
 СЧ РАЗЛ
 ПБ Д3А1
ПРОС2 НОП ,
АВХЗМД НОП ,
 УИА 1027
 СЧ Е26
 НТЖ ШЗПОМД
 ИЛИ Е40
 ЗП ШЗПОМД
 ПБ Д3
Д102 НОП
 УИА 1027
 СЧ Е32П25(М17)
 ИЛИ ПРОСВ-3(М16)
 НТЖ Е32П25(М17)
 ЗП ПРОСВ-3(М16)
 ПБ (М10)
Д4А СЧ ШЗПОМД
 И Е44Е43
 ПО ДОС
ПРПО НОП ------  ВХОД В ДРАЙВЕР ПО КОНЦУ ОБМЕНА
 ИСКБВ 0,М15   ИСКЛЮЧЕНИЕ ЗАЯВКИ ИЗ ОЧ-ДИ К ДРАЙВЕРУ МД
 ПИНО ПРПОЯ(М15) ОЧЕРЕДЬ НЕ ПУСТА
 СЧ ШЗПОМД
 И Е44Е43
 ПБ Д3А1         НА ДИСПЕТЧЕР
ПРПОЯ НОП ------  НАЧАЛО ОБР-КИ ВЫПОЛНЕННОЙ ЗАЯВКИ
 СЧ 7(М15)     7-Е СЛОВО БВВ
 СДА 64+47
 СЛЦ П3
 УИ М16
 СЧ ШКПРЕ
 ИЛИ Е32(М16)
 НТЖ Е32(М16)
 ЗП ШКПРЕ
 СЧ Е5
 РЕГ ’44’
 УИА -2(М2)
ПЕРДР НОП ,
 МОДА 4(М15)
 КК ’33’,2(М2)
 КК ’32’,СПУД+2(М2)
 ЦИКЛ ПЕРДР(М2)
*                            М2 = 0 ДЛЯ ПРУ12
 СЧ ЛИС2-1(М16)
 ПВ ПРУ12(М10)
 СЧ ИПЗМД+4
 ЗП СТАРП         УПРЯТАЛ ТЕКУЩ АДР ПЕРЕХВАТА КЧ
 УИА КЧВОСЛ(М10)
 СЧИ М10
 СДА 64-24
 ЗП ИПЗМД+4       НОВЫЙ АДРЕС РЕАК.НА КЧ
 УИА 3
 СЧ КУС2-1(М16)
 ЗП (М15)
 И Е18
 ПО ЗАПМД  РАСПАКУЮ ПРИ ЗАПИСИ
 КК ’33’,ДРД
 СЧМР
 И Е33
 ПО ЗАПМД  РАСПАКУЮ ПРИ ЧТЕНИ, ЕСЛИ НЕТ ОШИБКИ
 СЧИ М15
 ПВ ВОСЛИС(М15)    ВОЗВРАТ БВВ
 ПБ ПРИКЧ   ОБОШЕЛ РАСПАКОВКУ
*
* ПОДПР-МА РЕАКЦИИ НА КЧ ПРИ РАСПОКОВКЕ
*
КЧВОСЛ НОП ------  ОБРАБОТКА КЧ ПРИ ВОССТАНОВЛЕНИИ ЛИСТА
 СЧ И16          ВОССТ РЕГ ИСПОРЧЕННЫХ ОБР-КОЙ ПРЕРЫВАНИЯ
 УИ М16
 СЧ И17
 УИ М17
 ПВ ВОСРКЧ(М15)   ВОЗВРАТ БВВ В ПУЛ
 СЧ КУС2-1(М16)
 И Е18
 ПЕ ПРИКЧ     НА ЧТЕНИИ НЕТ ОСТАНОВА
 СТОП *
 ПБ ПРИКЧ
*
ЗАПМД СЧИ М15
 ПВ ВОСЛМД(М15)   ВОССТ ЛИСТА,ВОЗВРАТ БВВ В ПУЛ
 ПАМ 3
ПРИКЧ НОП ------
 СЧ П2013
 ПВ ПОК(М13)
 СЧ СТАРП
 ЗП ИПЗМД+4
 УИА 3
 СЧ КУС2-1(М16)
 И Е18
 ПЕ НЕТКНЧ      БЫЛО ЧТ/КОНТР ЧТ
 КК ’33’,ДРД
 СЧМР
 И Е33
 ПЕ НЕТКНЧ     ОШ ПРИ ЗАПИСИ
 МОД КНАПР-1(М16)
 СЧ 3          ЗАПОМИНАЮ КОНТР СУММУ
 ЗП КСКНЧ-3(М16)
 СЧ ЛИС2-1(М16)
 ЗП СТРОБМ-3(М16)  УПРЯТАЛ СТРН. ОБМЕНА
 СЧ ЛКЧТМД-3(М16) ЛИСТ ДЛЯ КОНТРОЛЬНОГО ЧТЕНИЯ
 ЗП ЛИС2-1(М16)
 СДА 64+36
 УИ М10
 СЧ КУС2-1(М16)
 ИЛИ Е18
 ЗП КУС2-1(М16)   ПОДСУНУЛ ЧТЕНИЕ
 ПВ ПВВМД(М15)  КОНТР ЧТЕНИЕ
 ПАМ 3
 ПБ ПРПО       НА ОБРАБ СЛЕД ПРЕРЫВАНИЯ
СТРОБМ ПАМ 2
КСКНЧ ПАМ 2
НЕТКНЧ НОП ------- ЧТ/КОНТР ЧТ
ПВВ ВНЕШ ВОСЛМД,ВОСЛИС
 ВХОДН ПРПО
 СЧ СОД-3(М16)
 СДА 64+16
 И Е8П1
 ЗП ЯПРМД-3(М16)
 ПБ Д5
Д10 УИА 3(М16)
 УИА -1(М15)
 СЧ ЗАВ
 МОД ЗАВ
 ПЕ НОВ-А
 СЧ ШЗПОМД
 И Е24
 ПО ДКП
ДКК СЧ НОМРМД-3(М16)
 ЗП ДАЙГОТ-3(М16)
 ПЕ ДИП
 УИА 4(М16)
 ЦИКЛ ДКК(М15)
 ПБ ДИП
ДГ2 СЧ ШЗПОМД
 ИЛИ Е40
 ЗП ШЗПОМД
 УИА 3
 СЛИА 3(М3)
 СЧИ М16
 И Е3
 УИ М10
 СЛИ М3(М10)
 СЧ ОЖОСВ-3(М16)
 ПЕ ЗАН
 СЧИ М3
 СДА 64-39
 ИЛИ П377
 ПБ ДС
ДИП СЧ ШЗПОМД
 ИЛИ Е24
 НТЖ Е24
 ЗП ШЗПОМД
ДКО УИА -3(М3)
 СЧ СОД-3(М16)
 И Е8П1
 ПЕ ЗАН
ДГ СЧ ДАЙГОТ-3(М16)
 И Е32П25+3(М3)
 ПЕ ДГ2
 ЦИКЛ ДГ(М3)
ЗАН УИА 1027
 СЧ ШЗПОМД
 И О40
 ЗП ШЗПОМД
 ПЕ Д33
 ПБ ВХМД
ДКП СЧ ДАЙГОТ+1(М15)
 ПЕ ДКО
 СЧ ПРОСВ-3(М16)
 ПО ППР
ПП СЧ НЗ-3(М16)
 ПО ППИ
 СЧ СОД-3(М16)
 И Е24П17
 ИЛИ ОЖОСВ-3(М16)
 ПЕ ППИ
 СЧ ШЗПОМД
 ИЛИ Е46-3(М16)
 ЗП ШЗПОМД
 ПБ ПРО
ППР СЧ ОСУ-3(М16)
 ПЕ ПП
ППИ УИА 4(М16)
 ЦИКЛ ДКП(М15)
 ПБ ВХМД
ПРЗАН СЧ ШЗПОМД
 ПЕ Д33
ДК СЧ ИВ25
 ЗП ИПЗМД+5
 СЧ Е44
 ПВ ИЗШГ(М15)
 СЧ ШКОБМ
 ПВ ВШГ(М15)
 СЧ 0
 ЗП ШКОБМ
 ПБ БВЗ
ОСВКМД УИА ’2003’
 ИЛИ ОСУ-3(М16)
 ЗП ОСУ-3(М16)
 СЧ НЗ-3(М16)
 ПЕ ОСВУ
 СЧ Е48-3(М16)
 ИЛИ Е47(М16)
 ИЛИ Е40
 И ШЗПОМД
 ПЕ ОСВ1          КМД ОСВ.НЕЛЬЗЯ
ОСВМД НОП 0
 СЧ СОД-3(М16)
 ПЕ ОСВ1          КМД ОСВ.НЕЛЬЗЯ
 СЧ ЗАНКМД-3(М16)
 ЗП ПРСКМД-3(М16)
 СЧ Е48
 ЗП ЗАНКМД-3(М16) 1Р->ЗАНКМД/Н
 ПБ ОС1
ОС4 ПАМ 0
 СЧИ М3
 И П3
 УИ М17
 ПБ О4
ОС3 СЧ РС
 СТОП *
ОС2 ПАМ 0
 УИА 1027
 СЧ ШКЗАКС
 ПВ ВШГ(М15)
 СЧ ШКЗАКЗ
 ПВ ВШГП(М15)
 СЧ 0
 ЗП ШКЗАКС
 ЗП ШКЗАКЗ
ОС1 НОП
ОСВ1 СЧ 0
 ПБ (М6)
*
*
**************************************************
****                                           ***
****           П О Д Г О Т О В К А             ***
****                                           ***
****          О Б М Е Н А   С   М  Д           ***
****                                           ***
**************************************************
*
*
ПОВОБМ НОП ,
 УИА Д5(М11)
ПОДОБМ НОП ,
 СЧ СОД-3(М16)
 УИА 3
 И П377
 ПЕ (М11)
 СЧ БУДАВ-3(М16)
 И Е8(М15)
 ПЕ (М11)
 ПВ ОПРЗАК(М2)
 СЧ СИСТ
 ИЛИ Е48-1(М14)
 ЗП СИСТ
 СЧ ОЧКУСД-1(М14)
 И Е7
 ПО ПОДОБ1         НЕ ТЕСТОВЫЙ ОБМЕН
 СЧ ОЧКУСД-1(М14)
 СДА 64+42
 УИ М10
 МОД ТУСП-1(М10)
 СЧ ИНФСЛП
 И Е24           ЧТ/ЗП ТОЛЬКО СЛУЖЕБНАХ СЛОВ
 СДА 64+3
 ИЛИ ОЧКУСД-1(М14)
 ЗП ОЧКУСД-1(М14)
ПОДОБ1 НОП ,
 СЧ ОЧЛИСД-1(М14)
 ЗП ЛИС2-1(М16)
 СЧ ОЧКУСД-1(М14)
ПОДОПР НОП ,             ИЗ АВМД ’ОПРОС ,ЧТЕН.ТАБ.ДЕФ.
 ЗП КУС2-1(М16)
 ПВ МДНАП(М2)
 СЧ ТЗНМД(М1)
 И Е40
 ПО ТОТМД
* НА УС-ВЕ СМЕНИЛСЯ ДИСК
 СЧ КУС2-1(М16)
 ИЛИ Е21Е18         ЧИТАЕМ СЛ.СЛОВА
 ЗП КУС2-1(М16)
 ПБ ПРОВДФ
ТОТМД НОП ,
 СЧ КУС2-1(М16)
 И Е6
 ПО ПРОВДФ        НЕ СЕКТОРНЫЙ ОБМЕН
 СЧ ПРЗАПС-3(М16)
 ПЕ ПРОВДФ        ЧТЕНИЕ ПЕРЕД ЗАПИСЬЮ УЖЕ БЫЛО
 СЧ Е18
 ИЛИ КУС2-1(М16)
 ЗП КУС2-1(М16)   ПЕРЕД ЗАПИСЬЮ ДЕЛАЕМ ЧТЕНИЕ
ПРОВДФ НОП ,             ПРОВЕРКА НА ДЕФ.ЗОНЫ ОБМЕНА
 СЧ 0
 ЗП ПРЗАПС-3(М16) УЖЕ НЕ НУЖЕН
 СЧ ТЗНМД(М1)
 И ЕДЕФ
 ПО НЕДЕФ
 СЧИ М1
 СДА 64-4
 УИ М13
 СЧ БУФКДЗ
 УИА (М2)
 ПВ ПРУ12(М10)
 СЧ КУС2-1(М16)
 И Е36П25
 СДА 64+8
 ЗП РЯД1
 СДА 64+16
 ИЛИ РЯД1
 ЗП РЯД1
*   ЗОНА(16Р)ЗОНА(16Р)
 УИА -14(М10)
ПДЕФЗ УИА -1(М2)
 УИА 0
 МОДА (М13)
 СЧ 1024+14(М10)
 УИА 3
 ПО НЕДЕФ
 ЗП РЯД
ПДЕФ НТЖ РЯД1
 И Е16П1+1(М2)
 ПО ДЕФЗ
 СЧ РЯД
 ЦИКЛ ПДЕФ(М2)
 ЦИКЛ ПДЕФЗ(М10)
НЕДЕФ СЧ КУС2-1(М16)
ОБМ И Е6
 ПО ОБ1В          НЕ СЕКТ.ОБМЕН
 СЧ ЛСЕК-3(М16)
 ЗП ЛИС2-1(М16)
 СЧ КУС2-1(М16)
 ПВ МДНАП(М2)     ИНВ.N МД->М15, N НАПР,N МД->М1
ОБ1В НОП 0
 СЧ ТЗНМД(М1)
 УИИ М2(М3)
 И П7777
 ПО ОБ3
 СЧ СТМГ+32(М1)
 ПО НЕТСТ
 СДА 64+6
 НТЖ ТЗНМД(М1)
 И П7777
 ПО БОБТ
ПЕРП СЧ СТМГ+32(М1)
 ПВ ДСЛ(М3)
 УИИ М3(М2)
 ПО ОБ3
НЕТСТ СЧИ М1
 ИЛИ Е48
 ЗП СТМГ+32(М1)
 СЧ ТЗНМД(М1)
 И П7777
 СДА 64-6
БОБТ ИЛИ СТМГ+32(М1)
 СЛЦ Е25
 ЗП СТМГ+32(М1)
 И Е45
 ПЕ ПЕРП
ОБ3 СЧ КУС2-1(М16)
 УИА 3
 И Е18
 ПЕ ОБ5           ЧТЕНИЕ (18Р КУС=1)
 СЧ ЛИС2-1(М16)
 УИА 0(М2)
 РЕГ 71   ЧИСЛ.СВЕРТКА
 ПВ ПРУ12(М10)
 СЧ ПР13
 ПВ ПОК(М13)      УСТ.ПОК
 СЧ 0
 УИА -1023(М7)
 УИА 0
КСЗ2 НОП
 СЛЦ 2047(М7)
 ЦИКЛ КСЗ2(М7)      ФОРМИРОВАНИЕ КОНТР.СУММЫ ПО АБЗ.
 УИА 3             УСТ.БЛПР,БЛЗ
 МОД КНАПР-1(М16)
 УИА (М10)
 ЗП 3(М10)
 ЗП 7(М10)
*          СТРУКТУРА СЛУЖЕБНЫХ СЛОВ ДОРОЖКИ:
*
* 0 СЛ :   48-37 Р.Р - 12-1 Р.Р НОМЕРА ЗОНЫ
*          36÷28РР-СООТВЕТСТВУЮТ 24÷16 РАЗРЯДАМ СЧЕТЧИКА ВРЕМЕН И
*                  (ЕДИНИЦА СЧЕТЧИКА ВРЕМЕНИ ЭКВ.0,02 СЕК);
*          27Р-НОМЕР НАПРАВЛЕНИЯ (’0’-3-Е НАПР,’1’-4-Е);
*          26,25РР-НОМЕР ЛИНИИ;
*          24÷22РР-НОМЕР УСТРОЙСТВА;
*          21÷7РР-ДАТА ЗАПИСИ (21÷16РР-ЧИСЛО,15÷11РР-МЕСЯЦ,
*                 10÷7РР-МЛАДШАЯ ЦИФРА ГОДА);
*          6÷4РР-НОМЕР БЭСМ;
*          2-1 Р.Р -  14-13 Р.Р НОМЕРА ЗОНЫ
* 3 СЛ- КОНТР СУММА
* 1-СС
* ’1’41Р-НОВ.К.СУММ,’1’43Р-КОНТР НОМ ДОР
*          ’1’ 40Р-СС РАСПИСАНЫ ПО-НОВОМУ (Т.Е.В СООТВЕТСТВИИ
*                  С ДАННОЙ СТРУКТУРОЙ);
*          39÷25РР-’КЛЮЧ’ СС (КОД ’70707’);
*          24÷13РР-НОМЕР ПАКЕТА;
*          12÷1РР-КОНТРОЛЬНАЯ СУММА ’СЕКТОРА’;
* 2-Е СС:  ШИФР ЗАДАЧИ,ОТ ИМЕНИ КОТОРОЙ ПРОИЗВЕДЕНА ЗАПИСЬ.
*
*
 СЧИ М1            РОСПИСЬ ЧЕТНЫХ СС:
 СДА 64-21
 ЗП РЯД           УСТ.НОМ.НАПР,ЛИНИИ,УСТР-ВА
 СЧ КУС2-1(М16)
 И Е7
 ПЕ ОБ4А          ТЕСТОВЫЙ РЕЖИМ
 СЧ ВРЕМЯ
 СДА 64+15
 СДА 64-27
 ИЛИ РЯД
 ЗП РЯД           УСТ.ВРЕМЯ
ОБ4А СЧ ГОД
 СБР КСБГОД
 СДА 64+27
 ИЛИ РЯД           УСТ.ДАТУ И НОМЕР БЭСМ-6
 ЗП (М10)
 СЧ КУС2-1(М16)
 И Е36П25
 СДА 64-12
 ИЛИ (М10)
 ЗП (М10)
 СЧ КУС2-1(М16)
 СДА 64+36
 И П3
 ИЛИ (М10)
 ЗП (М10)
 ЗП 4(М10)
 СЧ КУС2-1(М16)
 СДА 64+42
 УИ М12
 СЧ КУС2-1(М16)
 И ЕФ
 ПО СИС
 СЧ КУС2-1(М16)
 РЗБ П167
 ПБ СИС1
СИС МОД ТУСП-1(М12)
 СЧ ШИФРМ         УСТ.ШИФР ЗАДАЧИ
СИС1 ЗП 2(М10)
 ЗП 6(М10)
 СЧ ТЗНМД(М1)     РОСПИСЬ НЕЧЕТНЫХ СС:
 И Е12П1
 СДА 64-12
 ИЛИ КЛЮЧ          УСТ.НОМЕР ПАКЕТА,КЛЮЧ
 ИЛИ ПРР
 ЗП 1(М10)
 ЗП 5(М10)
 ПБ ОБ8
ОБ5 НОП ,
 УИА -7(М10)
 СЧ 0
ОБ6 МОД КНАПР-1(М16)
 ЗП 7(М10)        ОЧИСТИТЬ ПОЛЕ СЛУЖЕБНЫХ СЛОВ
 ЦИКЛ ОБ6(М10)
*
*   У Х О Д  В    А  Д  А  П
*
ОБ8 НОП ,
 СЧ СОД-3(М16)
 ИЛИ Е24(М15)
 ЗП СОД-3(М16)
 СЧИ М15
 ЗП РЯД1
 СЧ ЛИС2-1(М16)
 СДА 64+36
 УИ М10 НОМЕР ЛИСТА- ПАРАМ. ПВВМД
 СЧ КУС2-1(М16)
 ПВ ПВВМД(М15)
 ПАМ 3
 СЧ РЯД1
 УИ М15
 СЧ СОД-3(М16)
 ИЛИ Е8(М15)       УСТ.ПРИЗНАК ВЫДАННОГО ЗК.(В 8÷1Р.СОД)
 ЗП СОД-3(М16)
 СЧ ЗАНМД-3(М16)
 ИЛИ Е32(М4)
 ЗП ЗАНМД-3(М16)
 УИА 3             ГАШ.БЛПР
 СЧ БУДАВ-3(М16)
 И Е16П1
 ИЛИ Е8(М15)
 ЗП БУДАВ-3(М16)
 ПБ (М11)
*
*
ДЕФЗ НОП
 СЧ МД29М-3(М16)
 И Е32(М4)
 УИА 0(М5)
 ПО ДЕФ725
 УИА 1(М5)
ДЕФ725 НОП ,
 СЧ РЯД
 СДА 64+32
 ПИНО ДЕФ1(М2)
 СДА 64+8
ДЕФ1 И П177
 СДА 64-24
 СЛЦ КОНЗОН(М5)
 СЛЦ Е27    ДОР.ЗАМ
 НТЖ КУС2-1(М16)
 И Е36П25
 НТЖ КУС2-1(М16)
 ЗП КУС2-1(М16)
 ПБ ОБМ
 СТРН ,
*
**************************************************
****                                           ***
****            Н  А  Ч  А  Л  О               ***
****                                           ***
****    ОБРАБОТКИ ПРЕРЫВАНИЯ ПО КОНЦУ ОБМЕНА   ***
****                                           ***
**************************************************
*
*
ОБРПР НОП ,
 НЕД П7
 НТЖ П7
 ПВ МДНАПС(М2)    ПОДГОТОВКА М1,М3,М4,М15
 СЧ ВСЕЕД
 НТЖ Е8(М15)       ПОГАСИТЬ В СОД/Н ПРИЗНАК ОЖИДАНИЯ
 НТЖ Е24(М15)      ПРЕРЫВ.И ПРИЗН.ЗАНЯТОСТИ НАПРАВЛЕНИЯ
 НТЖ Е16(М15)      ПРИЗНАК ПОДВОДА
 И СОД-3(М16)
 ЗП СОД-3(М16)
 СЧ ВСЕЕД
 НТЖ Е8(М15)
 И ЯПРМД-3(М16)
 ЗП ЯПРМД-3(М16)
 СЧ БУДАВ-3(М16)
 ИЛИ Е8(М15)
 НТЖ Е8(М15)
 И Е18П1
 ЗП БУДАВ-3(М16)
 КК ’33’,ДРД
 СЧМР
 СДА 64+32
 И Е1
 НТЖ Е1
 УИ М10         М10 = 0  - БЫЛ БНС
 ПЕ ЧЕЙОБМ
*
********************************************************
*******     Б Ы Л           Б     Н     С        *******
********************************************************
*
  СЧ МД100М-3(М16)
  И Е32(М4)
  ПО ЧЕЙОБМ
 КК ’33’,ДРД
 СЧМР
 И Е45
 ПО ЧЕЙОБМ        НЕТ ПРИЗНАКА ДРУ
 СЧ КОМ99
 ЗП КОМОМД
 СЧ КУС2-1(М16)
 УИА БУФОПР(М11)
*
*   НА СУММАТОРЕ КУС ОБМЕНА С МД:
*                                 42-40 Р.Р - N НАПР.МД
*                                 38-25 Р.Р.- N ЗОНЫ
*                                 10- 8 Р.Р - N УСТР.МД
*
 ПВ ОПГД(М5)      99 КОМАНДА ПОСЛЕ ПЛОХОГО ОБМЕНА
*
*  В ЯЧЕЙКАХ ОТ ’БУФОПР’ ДО БУФОПР+7  НАХОДИТСЯ
*       БАЙТ УТОЧНЕННОГО СОСТОЯНИЯ.
*
 ПО ЧЕЙОБМ        СБРОС КАНАЛА ИЛИ БНС ПРИ 99 КОМ.
 СЧ ЕСТЬБУ
 ПЕ ЧЕЙОБМ        НЕ ЗАПОМНИЛИ СТАРЫЙ
 СЧ КУС2-1(М16)
 НТЖ  ЧЕЙБУС
 ПО ЧЕЙОБМ        ТАКОЙ УЖЕ БЫЛ
 УИА -10(М15)
ПЕРЕБУ НОП ,
 КК ’33’,СПУД+10(М15)
 КК ’32’,СПУСТ+10(М15)
 ЦИКЛ ПЕРЕБУ(М15)
*
 КК ’33’,БУФ99
 НТЖ НС100М
 И   ИС100М
 ПО НУЖНАС
 КК ’33’,ДРУД
 И Е19           ПЕРВОЕ ОБРАЩЕНИЕ
 ПО НЕСТАТ
НУЖНАС НОП ,
 СЧ КОМ97
 ЗП КОМОМД
 УИА БУФСТ(М11)
 СЧ КУС2-1(М16)
*
 ПВ ОПГД(М5)      ЧТЕНИЕ СТАТИСТИКИ МД 100 МГБ
*
 НТЖ Е48          48Р СМ=1 - ЕСТЬ СТАТИСТИКА
НЕСТАТ НОП ,
 И Е48
 ЗП ЕСТЬСТ
 СЧ ТЗНМД(М1)
 И П7777
 ИЛИ ЕСТЬСТ
 КК ’32’,ЕСТЬСТ
 СЧ КУС2-1(М16)
 ЗП ЕСТЬБУ
 ЗП ЧЕЙБУС
 СЧ ШКИВ
 ИЛИ ЕСБОЙВ
 ЗП ШКИВ
 УИА 3
*
*
ЧЕЙОБМ НОП ,
 СЧ КУС2-1(М16)
 И Е5
 ПЕ ПРОВДР
 ПВ ОПРЗАК(М2)
 СЧ ОЧКУСД-1(М14)
 СДА 64+42
 УИ М12
 СЧ ОЧКУСД-1(М14)
 И Е7
 ПО ПРОВДР
*
* Т Е С Т О В Ы Й   О Б М Е Н
*
 СЧИ М10
 СДА 64-8
*
 ЗП РС            48Р-НАПР,47,46Р-ЛИНИЯ,45÷43РР-УСТР-ВО
 СЧ ТЗНМД(М1)
 И Е12П1         36÷25РР-N ПАКЕТА,
 СДА 64-24
 ИЛИ РС
 МОД ТУСП-1(М12)
 ЗП 0
 УИА 0(М2)
 СЧ ОЧЛИСД-1(М14)
 ПВ ПРУ12(М10)
 СЧ ОЧКУСД-1(М14)
 И Е21
 ПО СЛСЗОН        СЛ.СЛОВА И ЗОНА
* ТОЛЬКО СЛ. СЛОВА
 УИА ХОРОШО(М6)
 УИА 0(М7)
ПРПСС УИИ М2(М16)
 УИА 8(М10)
ПРПСС1 СЛИА -1(М10)
 ПВ ПРЛСЧА(М5)
 УИА ’2000’
 МОДА (М7)
 ЗП 1024(М10)
 УИА 3
 ПИНО ПРПСС1(М10)
 УИА -1(М10)
БОЛЕЕ КК ’33’,ДРУД(М10)
 УИА 0
 МОДА (М7)
 ЗП 1033(М10)
 СЧМР 64
 МОДА (М7)
 ЗП 1034(М10)
 УИА 3
 СЛИА 1(М7)
 ЦИКЛ БОЛЕЕ(М10)
 ПБ (М6)
СЛСЗОН НОП ,
 УИА -1023(М2)
 УИА КЧТЕСТ(М5)
УСТП3 НОП ,
 СЧ П3
 ПВ ПОК(М13)
СЧТЭК НОП ,
 СЧ Е11
 УИ М21
 СЧ 2047(М2)
 УИА 3
 УИ
 ЦИКЛ СЧТЭК(М2)
 УИА 1027
 СЧ ПР13
 ПВ ПОК(М13)
 РЕГ ’101’
 УИИ М6(М15)
 УИИ М2(М17)
 УИИ М5(М16)
 УИИ М17(М12)
 ПВ ЗАНПРП(М15)
 УИИ М15(М6)
 УИИ М17(М2)
 УИИ М16(М5)
 УИА ’76010’(М7)
 ПВ ПРПСС(М6)
 СЧ ГУС
 ДК ’36’,Ц
Ц И КРОМЕ
 ЗП ГУС
 ПБ ХОРОШО
*
КЧТЕСТ НОП ,
 РЕГ ’101’
 УИА ’2000’
 ЗП 2047(М2)
 УИА ’2003’
 ПБ УСТП3
*
*
ПРОВДР НОП ,             ПРОВЕРКА  Д Р
 УИА АВДИС(М7)
 ПИО ПЛОХО(М10)    БЫЛ БНС
 УИИ М2(М16)
 УИА 0(М10)
 ПВ ПРЛСЧА(М5)
 ЗП РЯД
 ЗП ФКУСД
 СЧ Е21
 И КУС2-1(М16)
 ПЕ ЧТСЛСЛ        ЧТЕНИЕ СС ИЛИ ОПРОС УСТР-В
 СЧ СТРОБМ-3(М16)
 ПО БЫЛОЧТ     БЫЛО ЧТЕНИЕ
 СЧ КУС2-1(М16)  БЫЛО КОНТР ЧТ
 НТЖ Е18
 ЗП КУС2-1(М16)   ВОССТ. ПРИНАК ЗАПИСИ
 СЧ СТРОБМ-3(М16)
 ЗП ЛИС2-1(М16)  ВОССТ. СТРН. ОБМЕНА
 СЧ 0               УБИЛ
 ЗП СТРОБМ-3(М16)   ПРИЗНАК КОНТР ЧТ
 СЧ Е2Е1
 ПВ ПОК(М13)     ГАШ ПОК
 УИА ПЛОХО(М5)     ВЫХОД ПРИ КЧ
 УИА ОШКСЗП(М10)   М10 := *** ОШКСЗП ***
 УИА -1023(М13)
 СЧ 0
 УИА 0
КСМКНЧ НОП -------  КОНТР СУММ ПРИ КОНТР ЧТ
 СЛЦ ’3777’(М13)
 ЦИКЛ КСМКНЧ(М13)
 УИА 3
 НТЖ КСКНЧ-3(М16)
 ПО КОНЗП         КС ПРИ КОНТР.ЧТЕНИИ СОВПАЛА
 ПБ ПЛОХО
*
БЫЛОЧТ НОП -------
 СЧ ПР13
 ПВ ПОК(М13)    УСТ ПОК
 УИА 0(М2)
 СЧ ЛИС2-1(М16)
 ПВ ПРУ12(М10)
ЧТСЛСЛ НОП ,
 УИИ М2(М16)
 УИА 1(М10)
 ПВ ПРЛСЧА(М5)
 ЗП РЯД
 УИА 3             СНЯТЬ БЛПР
 И Е39П25
 НТЖ КЛЮЧ
 УИА ОШКЛЮЧ(М10)   М10 := *** ОШКЛЮЧ ***
 ПЕ ПЛОХО         НЕТ СОВПАДЕНИЯ КЛЮЧА
 СЧ РЯД
 СДА 64+12
 ЗП РЯД
 СЧ КУС2-1(М16)
 И Е5
 ПО НЕСЛУЖ
 СЧ ДОБЗ-3(М16)
 ПЕ ОП             ОПРОС МД
 ПБ ПРЗОНЫ         ЧТЕНИЕ ТАБ.ДЕФ.ЗОН
НЕСЛУЖ СЧ ТЗНМД(М1)
 НТЖ РЯД
 И Е12П1
 ПО ПРЗОНЫ
* НА УСТР-ВЕ ДРУГОЙ ПАКЕТ
 СЧ ТЗНМД(М1)
 ИЛИ Е40
 ЗП ТЗНМД(М1)
 УИА ОШNПАК(М10)   М10 := *** ОШNПАК ***
 ПБ ПЛОХО
*
ПРЗОНЫ НОП ,             ПРОВЕРКА НОМЕРА ЗОНЫ
 СЧ РЯД
 И Е31
 ПО НЕТЗОН
 СЧ ФКУСД
 СДА 64-36
 ЗП РЯД2
 СЧ ФКУСД
 СДА 64+12
 ИЛИ РЯД2          38-25 Р.Р СМ - N ЗЫНЫ ИЗ СЛУЖ.СЛОВ
 НТЖ КУС2-1(М16)
 И Е34П25+2
 УИА ОШЗОНЫ(М10)   М10 := *** ОШЗОНЫ ***
 ПЕ ПЛОХО         N ЗОНЫ НЕ СОВПАЛ
НЕТЗОН НОП ,
 СЧ КУС2-1(М16)
 И Е21
 ПО КСЧТЕН
* БЫЛО ЧТЕНИЕ СЛ.СЛОВ ИЗ-ЗА СМЕНЫ ПАКЕТА
 СЧ ТЗНМД(М1)
 ИЛИ Е40           СНЯЛИ ПРИЗНАК:НА УСТР-ВЕ ДРУГОЙ ПАКЕТ
 НТЖ Е40
 ЗП ТЗНМД(М1)
 ПБ ПОВОБМ        ВЫПОЛНИМ САМ ОБМЕН
*
КСЧТЕН  НОП ,
* БЛОК КОНТРОЛЬНОГО СУММИРОВАНИЯ  ПРИ ЧТЕНИИ
* -------------------------------------------
*
 СЧ Е2Е1
 ПВ ПОК(М13)      ГАШ.ПОК
 УИА ПЛОХО(М5)     ВЫХОД ПРИ КЧ
 УИА ОШКССЧ(М10)   М10 := *** ОШКССЧ ***
 УИА -1023(М6)
 СЧ 0
 УИ М21
НОВС1 НОП ,
 СЛЦ 2047(М6)
 ЦИКЛ НОВС1(М6)
 УИА 1027
 ЗП РЯД2
 УИИ М2(М16)
 УИА 3(М10)
 ПВ ПРЛСЧА(М5)
 НТЖ РЯД2
 УИА ОШКССЧ(М10)   М10 := *** ОШКССЧ ***
 ПЕ ПЛОХО         НЕ СОВПАЛА КОНТР.СУММА
*
 СЧ ПР13
 ПВ ПОК(М13)
 СЧ КУС2-1(М16)
 И Е6Е5
 ПО ХОРОШО        БЫЛ ОБМЕН СТРАНИЦЕЙ
 И Е5
 ПЕ ОП            ЧТ. КАТ ДЕФ ЗОН
* БЫЛО ЧТЕНИЕ ЗОНЫ ДЛЯ СЕКТОРНОГО ОБМЕНА
 УИА 1(М2)         1->М2
 СЧ ОЧЛИСД-1(М14)
 ПВ ПРУ12(М10)
 МОД ТУСП-1(М12)
 УИА (М10)          М10 := АДРЕС ИПЗ
 СЧ ОБЩМЛ(М10)
 ПЕ ПЕРДЛ
 ПВ ПЕРЕН(М10)   СЕКТОБМ
СПЕДЛ СЧ ОЧКУСД-1(М14)
 И Е18
 ПЕ ОТКРСЗ        В КУС ПРИЗНАК ЧТ, ЗАКАЗ ВЫПОЛНЕН
* ВЫПОЛНИМ СЕКТОРНУЮ ЗАПИСЬ
 СЧ Е1
 ЗП ПРЗАПС-3(М16)
 ПБ ПОВОБМ
*
*
* ЗАКОНЧИЛАСЬ ЗАПИСЬ НА МД
КОНЗП НОП ,
 СЧ КУС2-1(М16)
 И Е6
 ПО ХОРОШО        НЕ СЕКТОРНАЯ ЗАПИСЬ
ОТКРСЗ НОП ,             ОТКРЫТИЕ СЕКТОР.ЗАДАЧИ
 СЧ Е48-1(М12)
 ПВ ВШГП(М15)
ХОРОШО НОП ,             ОСТАЛОСЬ ТОЛЬКО ВЫБР.ЗАКАЗ
 УИА Д7(М11)        УХОД ПОСЛЕ ВЫБРЗК НА Д7
 ПБ ВЫБРЗК
*
*
*
ПЛОХО НОП ,             М10 - ПРИЧИНА ПЛОХОГО ОБМЕНА
* УХОД В АВМД
 СЧ 0
 ЗП СТРОБМ-3(М16)
 СЧ ПР13
 ПВ ПОК(М13)
 СЧ КУС2-1(М16)
 И Е5
 ПЕ ПОС           ОПРОС ИЛИ ЧТЕНИЕ ТАБ.ДЕФ.ЗОН
 УИИ М6(М16)       М6:= М16
 ПБ АВАРМД
*
* ВОЗВРАТ НА МЕТКУ ’КОНАВ’
* ПОСЛЕ ОБРАТОБКИ В АВМД ПЛОХОГО ОБМЕНА С МД
КОНАВ НОП ,
 ПЕ Д7            АВАРИЙНАЙ ЗАКАЗ ВЫКИНУЛИ
* ПЕРЕСТАНОВКА АВАРИЙНОГО ЗАКАЗА В КОНЕЦ ОЧЕРЕДИ
 ПВ ОПРЗАК(М2)
 УИА -74(М2)
 ПВ ДВОЧ(М10)
 СЧ СЧЗАКД
 УИ М14
 СЧ РЯД1
 ЗП ОЧЛИСД-1(М14)
 СЧ РЯД2
 ЗП ОЧКУСД-1(М14)
 УИА Д7(М10)       М10 :=Д7 ,ВОЗВРАТ ИЗ Д5ИИ ИЛИ ФЗ1С
 СЧ ОТВАВ         ОТВАВ=1 - МД ВЫКЛЮЧИЛСЯ
 ПО Д5ИИ          ЕСТЬ ГОТОВНОСТЬ
 СЧИ М1
 НТЖ П7
 И П37
 УИ М2
 УИА 1027
 СЧ Е32(М2)
 ИЛИ ВЫК-3(М16)
 ЗП ВЫК-3(М16)  ВЫКЛЮЧИЛСЯ НА ОБМЕНЕ
 ПБ ФЗ1С  НА ВКЛ В ОЧЕР К УСТР
*
*
**************************************************
****                                           ***
****            К  О  Н  Е  Ц                  ***
****                                           ***
****    ОБРАБОТКИ ПРЕРЫВАНИЯ ПО КОНЦУ ОБМЕНА   ***
****                                           ***
**************************************************
*
*
***********************************************
*
*  ПОДПР-МА ВЫБОРА СТАРШЕГО ЗАКАЗА
*
ОПРЗАК СЧ ШОЧМД(М1)
 И ШОЧНМД-3(М16)
 И СИСТ
 ПЕ ОПРЗА
 СЧ ШОЧМД(М1)
 И ШОЧНМД-3(М16)
ОПРЗА НЕД 0
 УИ М14
 ПБ (М2)
*
*
*
* ПОДПР-МА  ПОДГОТОВКИ МОДИФИКАТОРОВ  ПО НУ
*
МДНАП СБР НАПРМГ
 СДА 64+42
МДНАП1 УИ М1            <М1>: НАПР,ЛИНИЯ,УСТР-ВО
 И П37
 УИ М4
 И Е3П1
 НТЖ Е3П1
 УИ М15           <М15>: ИНВ.N УСТР-ВА
 СЧИ М1
 СДА 64+3
 УИ М3            <М3>: НАПР,ЛИНИЯ
 ПБ (М2)
*
***********************************************
*
* ПОДПР-МА ВЫБРОСА ЗАКАЗА
*
*
ВЫБРЗК СЧ ВСЕЕД
 НТЖ Е32(М4)
 И ЗАНМД-3(М16)
 ЗП ЗАНМД-3(М16)
 УИА -74(М2)
 ПВ ДВОЧД(М10)    ВЫБРОСИТЬ ЗАКАЗ И ПЕРЕДВИНУТЬ ОЧЕРЕДЬ
 СЧ ШКОМД
 ИЛИ ШКМД
 ЗП ШКМД
 СЧ ШКОДП
 ИЛИ ШКМДП
 ЗП ШКМДП
 СЧ 0
 ЗП ШКОДП
 ЗП ШКОМД
 СЧ РЯД2
 И ЕФ
 УИА ВШГ(М10)
 ПЕ ОБРФ
 СЧ ШЗЗ
 ИЛИ Е48-1(М12)
 НТЖ Е48-1(М12)
 ЗП ШЗЗ
 ПБ СНЗЩД
СНЯЛИЗ НОП ,
 СЧ ШЗНМБ(М10)
 И Е47П1
 ЗП ШЗНМБ(М10)
 СЧ ШМАРС
 И Е48-1(М12)
 ПЕ МАРС
 СЧИ М12
ВЫБИ УИИ М5(М16)
 ИЛИ Е30
 УИА ВШГП(М10)
 ПВ ЗАПВЕТ(М16)
 УИИ М16(М5)
 СЧ РЯД2
 И Е7
 ПЕ ОБР8          ОТКРЫТЬ ТЕСТ-ЗАДАЧУ
 ПИО ОБР9(М2)    ОКОН.ОБМ.
 СЧ ТУСП-1(М12)
 И Е36
 ПО ОБР9          ЗАДАЧА НЕ ЖДЕТ СТРАНИЦУ
 СЧИ М13
 СДА 64-23
 ЗП РЯД
 НТЖ ТУСП-1(М12)
 И Е35П24
 ПЕ ОБР9          ЗАДАЧА ЖДЕТ ДРУГУЮ СТР.
 СЧ Е36           ПОГАСИТЬ В УС ПРИЗНАК ОЖИДАНИЯ СТР.
 ИЛИ РЯД
 НТЖ ТУСП-1(М12)
 ЗП ТУСП-1(М12)
 ПБ ОБР8
ОБРФ МОД ТУС-1(М12)
 УИА (М2)
 СЧ НАЛЕВО
 ИЛИ Е1
 НТЖ Е1
 ЗП НАЛЕВО
ОБР8 УИИ М5(М15)
 СЧ Е48-1(М12)
 МОДА (М10)
 ПВ (М15)
 УИИ М15(М5)
ОБР9 СЧ ИПЗМД+33
 НТЖ Е48
 СЛЦ 33(М2)
 ЗП 33(М2)
 СЧ Е48
 ЗП ИПЗМД+33
 УИА 3
 ПБ (М11)
МАРС ИЛИ Ш1М
 ЗП Ш1М
 СЧ Е48-1(М12)
 НТЖ ШМАРС
 ЗП ШМАРС
 СЧ НОМАРС+1
 ПБ ВЫБИ
* Е N Q
* -----
*
ЕNQ УИА 3
 УИ 0
 УИА ЕNQ1(М16)
 УИА 1027
Q НОП 0
 ЗП И20
 ЗП СМ
 СБР НАПРМГ
 ЗП РЯОД
 СЧИ М16
 УИА РК(М17)
 СЧИМ М1
 СЧИМ М2
 СЧИМ М3
 ЗП (М17)
 УИА РК+4(М17)
 ПВ ПРКА(М3)
 СЧ (М17)
 УИМ М3
 УИМ М2
 УИМ М1
 СЧИ М16
 И Е5П1
 УИ М17
 СЧ РК
 УИ М16
 СЧ СМ
 И Е42
 СДА 64+41
 СЛЦ Е2Е1
 ПБ (М16)
ЕNQ1 УИ М16           N НАПР--М16
 СЧ ШЗМД-3(М16)
 И Е32(М17)
 ПО ЕNQ2          МД НЕ ЗАНЯТ ОС
 МОД НЗАД
 СЧ Е48-1
 ИЛИ ШКЗАКС
 ЗП ШКЗАКС
ЖДИЗАХ НОП ,
 УИА ЕNQ(М16)
 СЧ И20
 ЗП СМ
 ПБ ТБУФ13
ЕNQ2 НОП ,
 СЧ ЕNQМДС-3(М16)
 И Е32(М17)
 ПЕ ЕС            МД УЖЕ ЗАХВАЧЕН МАТЕМАТИКОМ
 СЧ ПРИПАД
 ДК ’36’,ВТЕ
ВТЕ НОП ,
 РЕГ ’63’          ПРИПИСКА АДАП-А
 УИ 0
 СЧИ М15
 ЗП С15
 СЧ И20
 ПВ ЗАХМД(М15)
 ЗП СМ
 ДК ’36’,ВТЕ1
ВТЕ1 НОП ,
 СЧ ОС1417
 РЕГ ’63’          ТЕКУЩАЯ ПРИПИСКА
 УИ 0
 МОД С15
 УИА (М15)
 СЧ СМ
 ПО ЕС            МД НЕ ЗАНЯТ ДРУГИМ  С В С
 МОД НЗАД
 СЧ Е48-1
 ИЛИ ШЖЗАХС
 ЗП ШЖЗАХС
 ПБ ЖДИЗАХ
ЕС НОП ,
 СЧ ШЗМД-3(М16)
 ИЛИ Е32(М17)
 ЗП ШЗМД-3(М16)
 СЧ ГУС
 ЗП УХВАТ
Q1 УИА 3
 ПБ (М15)
*
*
* D Е Q
* -----
*
DЕQ УИА 1027
 ПВ Q(М16)
 УИ М16
 СЧ ШЗМД-3(М16)
 ИЛИ Е32(М17)
 НТЖ Е32(М17)      ОСВОБОДИТЬ МД
 ЗП ШЗМД-3(М16)
 СЧ ШКЗАКС
 ИЛИ ШГ            <ШЗАКЗ>∨->ШГ
 ЗП ШГ
 СЧ ШКЗАКЗ
 ИЛИ ШГП
 ЗП ШГП
 СЧ 0
 ЗП УХВАТ
 ЗП ШКЗАКЗ
 ЗП ШКЗАКС
 СЧ ЕNQМДС-3(М16)
 И Е32(М17)
 ПЕ Q1  ЕСТЬ ЗАХВАТ ОТ ПОЛЬЗ
 СЧ ПРИПАД
 ДК ’36’,ВТD
ВТD НОП ,
 РЕГ ’63’          ПРИПИСКА АДАП-А
 УИ 0
 СЧИ М15
 ЗП С15
 СЧ И20
 ПВ ОССМД(М15)    В АДАП
 СЧ ОС1417
 ДК ’36’,ВТD1
ВТD1 НОП ,
 РЕГ ’63’          ТЕКУЩАЯ ПРИПИСКА
 УИ 0
 МОД С15
 УИА (М15)
 ПБ Q1
*
*
ВХОДМД ЗП ИНФСД         ИНФ.СЛОВО->ИНФСД
 СЧ УСМД
 СДА 64+НО
 И П77
 УИ М12           СИСТЕМНЫЙ НОМЕР ЗАДАЧИ->М12
 СЧ ИНФСД
 СЛЦ Е16
 И Е18
 ПО ФЗ3           МБ-ДИСК
 СЧ ИНФСД
 СДА 64+12
 И Е6П1
 УИ М6
 СДА 64+1
 УИ М2
 СЧ ИНФСД
 И Е13
 СДА 64+10
 ПВ ДАЙНУД(М10)
 УИА 0(М5)         М5:= 0 - МД
 ПИНО ЗОН2(М11)
 СЧ ИНФСД
 И П3777
 НТЖ П3777
 ПЕ ЗОН2
 СЧ Е34П25
 ПБ ОБХПР
ФЗ3 НОП ,
 УИА 1(М5)         М5:= 1 - МБ
 СЧ АВОС(М15)
 СДА 64+15
 И Е11П1
 ЗП РЯД1
 И Е6П1
 УИ М2
 ПО НЕТМЛ
 СЧ УКАЗД(М15)
 И Е32-24(М2)
 ПО НЕТМЛ
 СЧИ М2
 СДА 64+1
 УИ М2
 СЧ РЯД1
 И Е1
 СДА 64-2
 ПВ ДАЙНУД(М10)   ДАЙ ОТН.НОМЕР УСТР-ВА И ДЕЛЬТУ
 СЧ РЯД1
 СДА 64+6          ВЫДЕЛИТЬ МБ НАЧАЛЬНЫЙ
 ЗП РЯД1
 СЧ ИНФСД
 СДА 64+12
 И Е5П1
 ВЧ РЯД1          <N НАПР,N МБ (МАТ)-МБ НАЧ>->СМ
 СДА 64-29
 СЛЦ ФКУСД         ФОРМИРОВАНИЕ N ЗОНЫ
 ЗП ФКУСД
 СЧ ИНФСД
 И Е5П1
ФИ5 СДА 64-24
 СЛЦ ФКУСД
 И Е34П25(М11)
ОБХПР ЗП ФКУСД
 ПБ ХЛУМ
ХЛОМ НОП 0
 И Е36
 СДА 64+29         УСТ.В ФКУСД ПРИЗНАК ТЕСТОВОГО РЕЖИМА
 ИЛИ ФКУСД
 ЗП ФКУСД
 РЖА 3
 И Е7
 ПЕ ФЗ6           ТЕСТОВЫЙ РЕЖИМ
 СЧ ЯЧЧП(М15)
 И ЕЧП
 ПЕ ВЫБРАВ    СН ЗАД
 СЧ ФКУСД
 ПИНО ФЗЗ(М11)
 СДА 64+24
 НТЖ Е10П1
 ПО ФЗ5А          ОБРАЩЕНИЕ К ПРЕДЗОНЕ
 СЧ ФКУСД
ФЗЗ НОП ,
 ВЧ КОНЗОН(М11)
 ПО ЗВНЕПР        НОМЕР ЗОНЫ > ДОПУСТ.(>’1743’)
ФЗ5А СЧ ИНФСД
 СЛЦ 0
 ПЕ ФЗ6           ОБМЕН СТРАНИЦЕЙ
 ЗП РЯД
 СДА 64+12
 И П77
 УИ М10
 ВЧ П30
 ПЕ ПЕН
 СЧ АРХМЛ(М15)
 И Е48-24(М10)
 ПЕ САР
ПЕН СЧ РЯД
 СБР КСБС(М5)
 СДА 64+44         И ПРИЗНАК СЕКТОРНОГО ОБМЕНА (6Р)
 ИЛИ Е6
ФЗ5В ИЛИ ФКУСД
 ЗП ФКУСД
ФЗ6 СЧ 35(М15)
 СЛЦ Е1
 ЗП РЯД
 И Е15
 ПЕ ФЗ7           ЧИСЛО ОБРАЩЕНИЙ К МД > 16384
 СЧ РЯД
 ЗП 35(М15)       +1 К СЧ-КУ ОБРАЩ.(В ИПЗ)
 И Е3П1
 ПЕ ФЗ7           СЧ-К ОБРАЩ.≠N∧8
 СЧ ЖДЕТ(М15)
 СЛЦ П12           +10 К СЧ-КУ ПОТЕРЬ В ИПЗ ПОЛЬЗОВ.
 ЗП ЖДЕТ(М15)
ФЗ7 УИИ М10(М16)
 ПБ ДБИР          В ХЛАМ, ВОЗВРАТ НА МЯ
*
МЯ НОП ,
 СЧ УСМД
 СДА 64-П
 И Е48П43
 ИЛИ ФКУСД
 ЗП ФКУСД
 СЧИ М16
 СДА 64-42
 РЗБ НАПРМГ
 ИЛИ ФКУСД
 ЗП ФКУСД
 И Е7
 ПЕ ФЗ7Б
 ПИНО КОБРА(М11)
 СЧ ФКУСД
 И Е34П25
 НТЖ Е34П25
 ПЕ КОБРА
 СЧ ВСЕЕД
 НТЖ Е34П25
 И ФКУСД
 СЛЦ Е26Е25
 ПБ КОБР
КОБРА НОП ,
 СЧ ФКУСД
 СЛЦ Е27
КОБР ИЛИ Е19
 ЗП ФКУСД
ФЗ7Б СЧИ М14
 СДА 64-36
 ПБ ФЗ7В          В БОЭК1
* ФЗ7В - ПОСТАНОВКА ЗЫКАЗА В ОЧЕРЕДЬ
*  ЕСЛИ  УС-ВО  НЕ ГОТОВО  ВЫХОД НА    Н У Р
*  ПРИ НОРМАЛЬНЫХ УСЛОВИЯХ ВЫХОД НА    Ф З 1 Е
ФЗ1Е НОП ,
 УИА ФЗ15(М10)
 ПБ ФЗ1И
НУР УИА ФЗ15(М10)
 ПБ ФЗ1С
*
САР СЧ ИНФСД
 СДА 64+24
 И П17
 ИЛИ Е6
 ПБ ФЗ5В
ЗОН2 НОП ,
 СЧ ИНФСД
 СБР МАСКАН
 СДА 64+32
 ПБ ФИ5
Д5ИИ СЧ ОЧКУСД-1(М14)
 ПВ МДНАП(М2)
ФЗ1И СЧ ШОЧЛ(М3)
 ИЛИ Е48-1(М14)
 ЗП ШОЧЛ(М3)
 СЧ ШОЧНМД-3(М16)
 ИЛИ Е48-1(М14)
 ЗП ШОЧНМД-3(М16) ПОСТ.ЗАКАЗ В ОЧЕРЕДЬ К НАПР.
ФЗ1С СЧ ШОЧМД(М1)
 ИЛИ Е48-1(М14)
 ЗП ШОЧМД(М1)
 ПБ (М10)
БМ УИА 1(М1)
 СЧ 27(М15)
 И Е28
 ПЕ ЗПМД
 СЧ ИНФСД
 И Е40
 ПЕ НУ2 ЧТЕНИЕ
 УИА (М6)
 СЧ ИНФСД
 И Е13
 ПЕ НУ1
 УИА 1(М6)
НУ1 МОДА (М15)
 СЧ ШЗНМБ-12(М2)
 И Е8(М6)
 УИА 50(М6)   НЕТ ЗП
 ПО МДВЫБР
 УИА (М1)
НУ2 СЧ ШЗНМБ(М15)
 И Е48
 ПО ОК
 СЧ ТЗНМД(М16)
 И Е47
 ПО ОК1
ЗПМД НОП ,
 СЧИ М16
 И Е6
 СДА 64+5
 УИ М2
 СЧИ М16
 И П37
 УИ М4
 СЧ МД29М(М2)
 И Е32(М4)
 УИА 0(М7)
 УИА 0(М11)        7,25 МГБ
 ПО НЕ100М
 СЧ МД100М(М2)
 И Е32(М4)
 УИА -5(М7)
 УИА 1(М11)        29   МГБ
 ПО НЕ100М
 УИА 2(М11)        100  МГБ
НЕ100М НОП ,
 СЧ ШЗНМБ(М15)
 И Е48
 НТЖ Е48
 ПО ЗАПСДВ
 СЧ РЯД2
 СДА 64+6
 И П1777
 СДА 64-24(М7)
ЗАПСДВ НОП ,
 ЗП ФКУСД
 ПБ (М10)
ВЫБРАВ УИА 42(М6)
 ПБ МДВЫБР        42->М6
ЗАКР2Д УИА 36(М6)        36->М6
 ПБ МДВЫБР
ЗВНЕПР УИА 34(М6)        34->М6
 ПБ МДВЫБР
НЕТМЛ УИА 33(М6)        33->М6
 ПБ МДВЫБР
ОК СЧ РЯД
 И Е2(М1)
ОК1 УИА 49(М6)  НЕТ ПАРОЛЯ
 ПЕ ЗПМД
МДВЫБР УИИ М17(М12)
 СЛИА ПЗ(М17)
 УИИ М16(М6)
 УИА Д3(М1)
 СЧ ИНФСД
 ЗП (М15)
 ПБ ВЫ
*
ЗАПРОС НОП
 УИА 1027
 СЧ ШЗПОМД
 ИЛИ Е48-3(М16)
 ЗП ШЗПОМД
 ПБ (М11)
 ВХОД КСБЭ57,КРЭ70,БМ,НЕТМЛ
 ВХОД ИПЗМД,КТОМ,КТОМЛ,П11,РЯД,РЯД1,РЯД2,СОД
 ВХОД МДНЕИС,ВЫБРЗК,МДНАП,ОСВКМД,ЕNQ,DЕQ,ШЗМД,
 ВХОД КОНАВ,ОТВАВ,ЕNQМДС,НОМРМД,РАБМД,Е36П1
 ВХОД ПРОХ,НЗ,Д11,Д7В,Д102,НГД,АПРО,ОСУ,ОС4,РИМ
 ВХОД Д10,ОСВ1,МДНАП1,ШЗЖЗАХ,РАОБЩ,ОШДР
 ВХОД ОСВМД,ПРО,Д3А1,ПРОСВ,ОС3,ПС1
 ВХОД ШЖЗАХС,ЧЕЙБУС,ЕСТЬБУ
 ВХОД АВХЗМД,ДРД,ДРУД,ПВКЛМД
ПВВ ВНЕШ ЗАХМД,(ОССМД,ОСВМД)
 ВХОД СПЕДЛ,РЯД3,РЯД4
 ВХОД УСМД,ФЗ1Е,ФЗ15,ФЗ1С,Д3,СНЯЛИЗ,ПОДОПР,РС
 ВХОД БУФКДЗ
 ВХОД ДВ,ДК,NКОБ,ДАЙГОТ,ДКО,ОПРГД,ДКК,ДГ2,МД100М
 ВХОД Е16П1,КУСКИ,НОМЛД,Д1,ЗАНКМД,П3700,ЗУС
 ВХОД ЗВНЕПР,ПРЗАН,ММБ,МСТАР,МНОВ,МТЕСТ
 ВХОД ЯМ16Д,РЯМ,РЯВ,О40,ИНФСД,ВЫК,ПРД
 ВХОД Е34П25,СПУСТ,ЕСТЬСТ,КОМОМД
 ВХОД ЗАН,ХЛОМ,ОС2,ФЗ7Б,НУР,МЯ,БУФ99,ПРСКМД,ЗАНЛ
 ВХОД Е11Е9,СЧЗАКД,ВХОДМД,БУДАВ,РЯОД
 ВХОД ЗАНМД,ОСЧК,ЗАКР2Д,МСТР,ЗАПРОС,НОМСВ,ЗАВ,КОНЗОН
АВМД ВНЕШ АВДИС,(А,АВМД),НОВД,ПОС,ОП,НОВ
БОЭК1 ВНЕШ ФЗ7В,ДАЙНУД,АРХМЛ,ОПР
ВЗУ ВНЕШ П,ЕФ,ВХМД,ПОК,ДСЛ,ПРКА,ВЫ
ВИСП ВНЕШ ВШГ,ВШГП,ИЗШГ
БОЭК1 ВНЕШ МДНАПС,УСТШЗ
ДИСП70 ВНЕШ ШКЗАКС,ТУСП,ШКОДП,ЗАНПРП,ШКМДП,НО
ДИСП70 ВНЕШ П377,П3777,П3,П1777,П17,УКАЗД,Е44Е43,РМД1,Е47П1
ДИСП70 ВНЕШ ШКОБМ,П37,ЛСЕК,ШЗПОМД,ЖДЕТ,ВРЕМЯ,ШИФРМ,ИНФСЛП,АВОС
ДИСП70 ВНЕШ ОЧКУСД,П7777,СТМГ,КРОМЕ,П7,ТБУФ13,ТВ67,БВЗ,ГУС,ТУС
ДИСП70 ВНЕШ НЗАД,СМ,ВИСП16,ШГ,КУС2,ТЗНМД,ШКЗАКЗ,РМД,ГОД,И16
ДИСП70 ВНЕШ И17,МД29М,П77,П177
ДИСП70 ВНЕШ ВСЕЕД,Е48Е47,Е21Е18,(Е12П1,П7777),(Е11П1,П3777)
ДИСП70 ВНЕШ (Е10П1,П1777),(Е8П1,П377),(Е6П1,П77),КЛЮЧ,ИВ25,ШЗЗ
ДИСП70 ВНЕШ (Е5П1,П37),(Е3П1,П7),(Е2Е1,П3),(Е6Е5,П60),И20,МГРП
ДИСП70 ВНЕШ ПРИПАД,ОС1417,Е48П43,Е48П37,Е39П25,Е36П25
ДИСП70 ВНЕШ Е26Е25,РК,Е12Е11,ПР13,П12,П6,Е35П24
ДИСП70 ВНЕШ (Е48П46,Е46П48),ШКМД,ШКОМД,ФКУСД,Е18П1,ОЧЛИСД
ДИСП70 ВНЕШ ЛИС2,ШГП,ПЗ,ЛКЧТМД,ОБЩМЛ,НАПРМГ,ШКПРЕ,КНАПР
ДИСП70 ВНЕШ П30,ШМАРС,Ш1М,ЗАПВЕТ,НОМАРС
ДИСП70 ВНЕШ НОММЛ1,ДОБЗ,ОБМОТВ
ДИСП70 ВНЕШ НАЛЕВО
КИТ ВНЕШ ЯПРМД,Е26П13,Е32П25,Е24П17,ШОЧНМД,ШОЧЛ,ШОЧМД,КСБГОД
КИТ ВНЕШ ОЖОСВ,СИСТ
ДМЛМБ ВНЕШ ПРЛСЧА,ПРЛСЧБ
СОСТАВ ВНЕШ ЕЧП,ЯЧЧП,ШЗНМБ,ЕДЕФ
ДИСП70 ВНЕШ ШКИВ,КОМ99,КОМ97,НС100М,ИС100М
СОСТАВ ВНЕШ ЕСБОЙВ
ХЛАМ ВНЕШ ДБИР,ДВОЧД,ПРУ12,ДИ,О4,ДОС,ПРЗ,ОСВУ,ПЕРЕН
ПВВ ВНЕШ ВОСРКЧ
АВМД ВНЕШ ОПРДИС
АВМД ВНЕШ АВАРМД,ОШБНС,ОШКСЗП,ОШКССЧ,ОШNПАК,ОШЗОНЫ,ОШКЛЮЧ
ХЛАМ ВНЕШ ПЕРДЛ,ДВОЧ,СНЗЩД,ДЗС,ХЛУМ
РЕЗАРХ ВНЕШ МАСКАН
 ЭКВИВ (ЯМ16,ЯМ16Д),(ВРП,Е12)
         ФИНИШ ,             В ВАШЕМ ФИНИШЕ ОШИБКА ИЛИ ЕГО НЕТ

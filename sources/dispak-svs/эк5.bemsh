ЭК5 СТАРТ ’10000’
 УПОТР ЭК5(М1)
 Е
 М
Э176 УИА 3
 СЧ (М3)
 СДА 64+12
 И П17
 ЗП РАБ
 ВЧ П3
 ПО АВО42         НЕВЕРНЫЙ НОМЕР РАБОТЫ
 СЧ
 ЗП 2(М3)         ОБНУЛИЛ РМР МАТЕМАТИКА
 ПВ УПРИ(М10)
 СЧ Е5
 ПВ ЗАХЗАН(М15)
 СЧ (М3)
 И Е48П17
 ПЕ ШКАЛА
 СЧ (М3)
 И П7777
 УИА 0(М10)        ПРИЗНАК ОТКАЗА ОТ ОДНОГО ЛИСТА
ИСКЛИС НОП *            ИСКЛЮЧЕНИЕ ВИРТ.ЛИСТА
 УИИ М13(М3)
 СДА 64-ТБДТР
 УИИ М16(М1)
 ПВ ИСКВЛ(М17)
 УИИ М11(М1)
 УИИ М1(М16)
 ПО НЕТВЛ         НЕТ ВИРТУАЛЬНОГО ЛИСТА
 СЧ РАБ
 ПЕ НЕНОЛЬ
* N РАБ = 0   ЗАПРОС СВЕДЕНИЙ О ВИРТ.ЛИСТАХ
ВКЛЛИС УИА 1027
 СЧ СТАРОЕ
 УИА 1026
 СДА 64+ТБДТР
 СДА 64-ТБДТР
 ИЛИ ТОП-1(М11)
 ЗП ТОП-1(М11)
 УИА 1027
 МОДА ВССЫ
 ПИО (М10)
 СЧ Е48(М15)
 ИЛИ 2(М3)
ВССЫ ЗП 2(М3)
 ПВ ССЫ(М17)
 МОДА КОТ0
 ПИО (М10)
*
*  НЕТ ВИРТУАЛЬНОГО ЛИСТА
*
НЕТВЛ НОП ,
 МОДА СЛЛИСТ
 ПИНО (М10)
 УИА 1(М14)         СМ:=1
 ПБ ГАШЕ5
*
НЕНОЛЬ НОП ,
 СЧ СТАРОЕ
 И ЕОБМ
 ПЕ ВКЛЛИС        ЛИСТ В ОБМЕНЕ
 СЧ СТАРОЕ
 СДА 64+СДВМЛ
 И П37
 ЗП РАБ1          РАБ1 - МАТ НОМЕР ЛИСТА
 И П3
 УИ М4            М4 - СТОЛБЕЦ ПРИП
 СЧ РАБ1
 НТЖ П37
 УИ М16
 СДА 64+2
 УИ М5
 СЛИ М5(М3)        М5 - СТРОКА ИПЗ ПРИП
 СЧИ М11
 ВЧОБ АКТОП
 ПЕ НАМБ          ЛИСТ ОТКАЧАН
 СЧ СТАРОЕ
 И ЕОТКЛ
 ПЕ НЕПРИП        С ПРИПИСКИ НЕ СНИМАТЬ
 СЧ ПРИП(М4)
 И 24(М5)
 НТЖ 24(М5)
 ЗП 24(М5)
 СЧ Е48(М16)
 ИЛИ ЛИСТМБ(М3)
 ЗП ЛИСТМБ(М3)    ЛИСТА НЕТ
 СЧ Е48(М16)
 ИЛИ 22(М3)
 ЗП 22(М3)
 СЧ Е48(М16)
 ИЛИ 32(М3)
 ЗП 32(М3)
НЕПРИП СЛИА -1(М11)
 СЧИ М11
 СДА 64+5
 УИ М4
 СЧИ М11
 И П37
 УИ М5
 СЧ ТЗФСТР(М4)
 НТЖ Е48(М5)
 ЗП ТЗФСТР(М4)    ОСВОБОДИЛИ ЛИСТ
*
ИЗМИПЗ СЧ РАБ
 НТЖ Е1
 ПЕ РЕСЗАД        N РАБ = 2; ЛИСТ В РЕСУРС ЗАДАЧИ
* N РАБ = 1; ОТКАЗ ОТ ДОП ЛИСТА В ПОЛЬЗУ ОС
 СЧ П7777
 И ЯЧЛИС(М3)
 ЗП РАБ1
 ПО ГАШЕ5
 НТЖ ЯЧЛИС(М3)
 ЗП ЯЧЛИС(М3)
 СЧ РАБ1
 ВЧ Е1
 ИЛИ ЯЧЛИС(М3)
 ЗП ЯЧЛИС(М3)     ОЗУ В ПАСП. := ОЗУ В ПАСП. -1
 СЧ ОЗУ
 СЛЦ Е1
 ЗП ОЗУ
 СЧ ТОБ
 СЛЦ Е1
 ЗП ТОБ
 ПБ КОНОЧ
* N РАБ = 2; ОТКАЗ ОТ ДОП.ЛИСТА В ПОЛЬЗУ ЗАДАЧИ
РЕСЗАД СЧ ЛИСОСТ(М3)
 И П17777
 ВЧ Е1
 НТЖ ЛИСОСТ(М3)
 И П17777
 НТЖ ЛИСОСТ(М3)
 ЗП ЛИСОСТ(М3)
 ПБ КОНОЧ
ШКАЛА ЗП РАБ600
 УИА 1(М10)           ПРИЗНАК ИСКЛЮЧ.ПО ШКАЛЕ
 СЧ (М3)
 И П177
ЗАПСМ ЗП (М3)
СЛЛИСТ СЧ РАБ600
 ПО ГАШЕ5            ВСЕ ЛИСТЫ ИСКЛЮЧИЛИ
 НЕД П77777
 УИ М15
 СЧМР
 СДА 64+1(М15)
 ЗП РАБ600
 СЧИ М15
 НТЖ П37
 ЗП РАБ1
 СЧ (М3)
 И П177          ВЫРЕЗАЛ МОДИФИКАЧИЮ
 СДА 64-5
 ИЛИ РАБ1
 ПБ ИСКЛИС
КОНОЧ МОДА КОТ0
 ПИО (М10)
 СЧ Е48(М15)
 ИЛИ (М3)
 ПБ ЗАПСМ
КОТ0 УИА 0(М14)
 ПБ ГАШЕ5
*
НАМБ СЧ СТАРОЕ
 И ЕОТКЛ
 ПЕ ОЛНМБ
 СЧ Е48(М16)
 ИЛИ ЛИСТМБ(М3)
 ЗП ЛИСТМБ(М3)
ОЛНМБ НОП
 СЛИА ТОП-ТБД-1(М11)
 СЧИ М11
 СДА 64+5
 УИ М4
 СЧИ М11
 И П37
 УИ М5
 СЧ ШЗНМБО(М4)
 ИЛИ Е48(М5)
 ЗП ШЗНМБО(М4)
 СЧ ТМБ
 СЛЦ Е1
 ЗП ТМБ
 ПБ ИЗМИПЗ
*
ГАШЕ5 НОП
 СЧ Е5
 ПВ ГАШЗАН(М15)
 УИА 3
 МОДА КОТМ14
 ПИО (М10)
 СЧ (М3)
 ПБ Э105Г-Э(М7)
*
*  ЭК-КОД ДОЗАКАЗА РАБОЧЕЙ ПАМЯТИ
Э177 УИА 3
 СЧ (М3)
 СДА 64+18
 УИ М4            М4 := ТИП ПАМЯТИ
 ВЧ П3
 ПО АВО42         ОШ. В ИНФ.СЛОВЕ ЭК-КОДА
 СЧ (М3)
 И Е18П1
 МОДА (М4)
 ПО ДАЙРЕС        ЗАПРОС РЕСУРСА
 ЗП РАБ           РАБ := 18-1 Р.Р. СМ МАТ.
 СЧИ М4
 НТЖ Е2
 ПО ДОЗСЕГ
 СЧ Е9
 ПВ ЗАХЗАН(М15)   ЗАНЯТИЕ ОЗУ,ТОБ,КУСКИ
 УИА 3
 СЧ ПРЕДЕЛ
 МОДА ДОЗТРА
 ПИНО (М4)
* ДОЗАКАЗ   О З У
 СЧ МАХОЗУ
 ЗП 2(М3)
 СЧ ЯЧЛИС(М3)
 И П7777
 СЛЦ РАБ
 ВЧОБ МАХОЗУ
 УИА 2(М14)        КОТ= 2 - СУММ. ЗАКАЗ ОЗУ > МАХ
*                          =--------------------------------=
 ПЕ ГАШЕ9
 УИА 1027
 СЧ ОЗУ
 ЗП 2(М3)         РМР МАТ. := РЕСУРС ОЗУ  О С
 ВЧ РАБ
 УИА 1(М14)        КОТ= 1 - В ОС НЕТ РЕСУРСА ОЗУ
*                          =--------------------------------=
 ПЕ ГАШЕ9
 СЧ РАБ
 СЛЦ ЯЧЛИС(М3)
 ЗП ЯЧЛИС(М3)     УВЕЛИЧЕЛИ ЗАКАЗ В ПАСП.
 СЧ ЛИСОСТ(М3)
 И П17777
 ЗП (М3)
 СЧ РАБ
 ВЧОБ (М3)
 НТЖ ЛИСОСТ(М3)
 И П17777
 НТЖ ЛИСОСТ(М3)
 ЗП ЛИСОСТ(М3)
 СЧ ОЗУ
 ВЧ РАБ
 ЗП ОЗУ
ИЗМТОБ СЧ ТОБ
 ВЧ РАБ
 ЗП ТОБ
 ПБ НОРМА9
* ДОЗАКАЗ   Т Р А К Т О В
ДОЗТРА НОП
 И П777
 ЗП 2(М3)         РМР МАТ. := МАХ ТРАКТЫ
 СЧ ЯЧТР(М3)
 СДА 64+СДТ
 И П777
 СЛЦ РАБ
 ВЧОБ 2(М3)
 УИА 2(М14)        КОТ= 2 - СУММ.ЗАКАЗ ТРАКТОВ > МАХ
*                          =--------------------------------=
 ПЕ ГАШЕ9
 УИА 1027
 СЧ ТОБ
 ЗП 2(М3)         РМР МАТ. := РЕСУРС ТРАКТОВ  О С
 ВЧ РАБ
 УИА 1(М14)        КОТ= 1 - В ОС НЕТ РЕСУРСА ТРАКТОВ
*                          =--------------------------------=
 ПЕ ГАШЕ9
 СЧ РАБ
 СДА 64-СДТ
 СЛЦ ЯЧТР(М3)
 ЗП ЯЧТР(М3)      УВЕЛИЧЕЛИ ЗАКАЗ В ПАСП.
 СЧ П1777
 СДА 64-СДТ
 И ЯЧЧТР(М3)
 ЗП (М3)
 НТЖ ЯЧЧТР(М3)
 ЗП ЯЧЧТР(М3)
 СЧ (М3)
 СДА 64+СДТ
 ВЧ РАБ
 СДА 64-СДТ
 ИЛИ ЯЧЧТР(М3)
 ЗП ЯЧЧТР(М3)     УВЕЛИЧЕЛИ РЕСУРС ТРАКТОВ
 ПБ ИЗМТОБ
* ДОЗАКАЗ   С Е Г М Е Н Т А
ДОЗСЕГ СЧ РАБ
 СДА 64+12
 УИ М4            М4 := ВН
 ВЧ П30
 ПЕ АВО57
 ВЧОБ П37
 ПЕ АВО57
  СЧ АРХМЛ(М3)
 И Е48-’30’(М4)
 УИА 4(М14)        КОТ= 4 - ВН ЗАНЯТ
*                          =--------------------------------=
 ПЕ КОТМ14
 СЧ П30
 ЗП (М3)          СММ := 30(ВН)
 УИА -31(М5)
 СЧ КОНКВА
 ЗП R1-Э(М7)      R1 := ЕД. СВОБОДНЫХ КВАНТОВ ’ОБЩТОМ’
ПСВКВ СЧ (М3)
 СДА 64+1
 УИ М6
 СЧМР
 СДА 64+45
 УИ М10
 МОДА (М3)
 СЧ ШЗНМБ-12(М6)
 СДА 64+8(М10)
 И П17
 ПО ВНСВОБ        ВН СВОБОДЕН
 УИ М12           М12 := ССЫЛКА НА ’ОБЩТОМ’
 СЧИ М4
 НТЖ (М3)
 ПО КОТМ14        ВН ЗАНЯТ
 СЧ R1-Э(М7)
 НТЖ Е48-1(М12)
 ЗП R1-Э(М7)
ВНСВОБ СЧ (М3)
 СЛЦ Е1
 ЗП (М3)
 МОДА ПСВКВ
 ЦИКЛ (М5)
 СЧ R1-Э(М7)
 УИА 3(М14)        КОТ= 3 - 12 ЗАКАЗОВ ВН
*                          =--------------------------------=
 ПО КОТМ14
 НЕД
 ЗП R1-Э(М7)      R1 := N СВОБ.КВАНТА ’ОБЩТОМ’
*
 СЧ РАБ
 И П7777
 ПО АВО42
 СЛЦ П37
 СДА 64+5
 УИ М11           М11 := РАЗМЕР КУСКА В СЕГМЕНТАХ
 СДА 64+4
 УИА 2(М14)        КОТ= 2 - КУСОК > 17(8) СЕГМЕНТОВ
*                          =--------------------------------=
 ПЕ КОТМ14
* ПОИСК СВОБОДНОГО КУСКА В  О С
 СЧ Е9
 ПВ ЗАХЗАН(М15)   ЗАНЯТИЕ ОЗУ,ТОБ,КУСКИ
 УИА 3
 СЧ КУСКИ
 ЗП 2(М3)         РМР МАТ.:= КУСКИ
 УИА 1(М14)        КОТ= 1 - НЕТ РЕСУРСА СЕГМ. В  О С
*                          =--------------------------------=
 СЧ ВСЕЕД
 СДА 64-48(М11)
 УИА 0(М12)        М12 - НАЧАЛО КУСКА
 УИИ М10(М11)
 СЛИА -48(М10)
ПОИСКК ЗП РАБ
 НТЖ КУСКИ
 И РАБ
 ПО НАШЛИК        НАШЛИ КУСОК
 СЧ РАБ
 СДА 64+1
 СЛИА 1(М12)
 МОДА ПОИСКК
 ЦИКЛ (М10)
 ПБ ГАШЕ9
* ЗАНЯТИЕ КУСКА
НАШЛИК УИА 1027
 СЧ КУСКИ
 НТЖ РАБ
 ЗП КУСКИ
 СЧИ М4
 СДА 64+1
 УИ М5            М5:= N СЛОВА ШЗНМБ+12
 СЛИ М5(М3)
 СЧИ М4
 И Е1
 НТЖ Е1
 СДА 64-2
 УИ М6            М6 := N КВАНТА ШЗНМБ
* ЗАНЕСЕНИЕ В  И П З
 СЧ R1-Э(М7)
 СДА 64-12(М6)
 ИЛИ ШЗНМБ-12(М5)
 ЗП ШЗНМБ-12(М5)
 СЧ R1-Э(М7)
 И П3
 УИ М6            М6 := N КВАНТА ’ОБЩТОМ’
 СЧ R1-Э(М7)
 СДА 64+2
 УИ М5            М5 := N СЛОВА ’ОБЩТОМ’
 СЛИ М5(М3)
 СЧИ М11
 СДА 64-12
 ЗП РАБ
 СЧИ М12
 ИЛИ РАБ
 СД КТОМЛ-1(М6)
 ИЛИ ОБЩТОМ(М5)
 ЗП ОБЩТОМ(М5)
 СЧ МДР(М3)
 ИЛИ Е48-8(М4)
 ЗП МДР(М3)
 СЧ УКАЗД(М3)
 ИЛИ Е48-8(М4)
 ЗП УКАЗД(М3)
НОРМА9 УИА 3
 УИА 0(М14)        КОТ= 0 - НОРМА
*                          =--------------------------------=
ГАШЕ9 СЧ Е9
 ПВ ГАШЗАН(М15)
 УИА 3
 ПБ КОТМ14
*
ДАЙРЕС СЧ ОЗУ
 ПБ ДАЙОЗУ
 СЧ ТОБ
 ПБ ДАЙТРА
 СЧ КУСКИ
 ПБ Э105Г-Э(М7)   СМ МАТ.:= КУСКИ
ДАЙОЗУ СДА 64-24
 ЗП (М3)
 СЧ ЛИСОСТ(М3)
 И П17777
 ВЧОБ Е14
 ИЛИ (М3)
 ПБ Э105Г-Э(М7)   СММ:= РЕС ОС(28-25),РЕС.ЗАДАЧИ(24-1)
ДАЙТРА СДА 64-24
 ЗП (М3)
 СЧ ЯЧЧТР(М3)
 СДА 64+СДТ
 И П1777
 ВЧОБ Е10
 ИЛИ (М3)
 ПБ Э105Г-Э(М7)   СММ:= РЕС.ОС(28-25),РЕС.ЗАДАЧИ(24-1)
*
КОТМ14 СЧИ М14
 ПБ Э105Г-Э(М7)
*
АВО42 НОП
 УИА ’42’(М6)
 ПБ АВОС1-Э(М7)
АВО57 НОП
 УИА ’57’(М6)
 ПБ АВОС1-Э(М7)
Э140 СЧ (М3)
 УИ М11
 И П77
 ВЧ МИНЗ
 ПЕ КОН
 ВЧ МАКЗ
 ПО КОН
 СЧ Е48-1(М11)
 НТЖ ЕЗАР
 ПО КОН
 СЧ (М3)
 СДА 64+45
 ПО ЭТ
 НТЖ ГОД
 И П7
 ПЕ ДРЭВМ
ЭТ СЧ ШКРЗ
 И Е48-1(М11)
 ПО Э105Г-Э(М7)
 МОД ТУСП-1(М11)
 СЧ ШИФРМ
 ПБ Э105Г-Э(М7)
КОН СЧ Е1
 ПБ Э105Г-Э(М7)
ДРЭВМ СЧ 0
 ЗП РАБ
 ПВ ТБУФ(М15)
 СДА 64-8
 УИ М12
 СДА 64-2
 ИЛИ КУСКБ-Э(М7)   546 З
 СЛЦ НОММЛ1
 МОДА ПР30-Э(М7)
 ПВ (М6)
 УИА 8(М4)
 УИА -126(М14)
 СЛИ М4(М12)
ГДЕ СЧ 7(М4)
 НТЖ (М3)
 И Е46П48
 ПО ГДЕ1
ГД СЛИА 8(М4)
 МОДА ГДЕ
 ЦИКЛ (М14)
ГД1 СЧ КАТ-Э(М7)
 ПВ ТБУФ(М15)
 СЧ РАБ
 ПО Э105Г-Э(М7)
 ПВ ТБУФ(М15)
 СЧ 0
 ПБ Э105Г-Э(М7)
ГДЕ1 СЧ 6(М4)
 И Е31
 ПО ГД  НЕ В РЕШ
 СЧ 7(М4)
 СДА 64+17  КАНАЛ
 НТЖ (М3)
 И П77
 ПЕ ГД
 СЧ РАБ
 ПЕ ГД2
 ПВ ТБУФ(М15)
 СДА 64-8
 УИ М10
 СДА 64-2
 ИЛИ КУСБК-Э(М7)
 СЛЦ НОММЛ1
 ЗП РАБ
 ПВ ФИЗОБМ(М16)
 УИА 3
ГД2 МОДА (М10)
 СЧ 127(М14)
 ПО ГД
 ЗП (М3)
 СЧ РАБ
 ПО КОНЕЦ-Э(М7)
 ПВ ТБУФ(М15)
 ПБ КОНЕЦ-Э(М7)
* Р А Б О Ч И Е
РАБ1 ПАМ 1
РАБ ПАМ 1
* К О Н С Т А Н Т Ы
Е28П17 КОНД М16В’7777’
КОНКВА КОНД М33В’73567’
* В Х О Д Н Ы Е
 ВХОД Э140,Э176,Э177
* В Н Е Ш Н И Е
БОЭК1 ВНЕШ АРХМЛ,Л,ИСКВЛ,ССЫ,ЛИСТМБ
ГЕНС1 ВНЕШ РАБ1М,РАБ2М
ЭКВВОД ВНЕШ (Э,ЭКВВОД),Э105Г,R1,АВОС1,КУСКБ,ПР30,КАТ
ЭКВВОД ВНЕШ КУСБК,КОНЕЦ
ДИСП70 ВНЕШ П3,П7,П17,П37,П77,П777,П1777,П7777,П77777
ДИСП70 ВНЕШ ЗАХЗАН,ГАШЗАН,ТУСП,ЛТБД,Е18П1
ДИСП70 ВНЕШ П30,ТМБ,ТОБ,ОЗУ,ШЗНМБО,КДВ,П60,ГОД
ДИСП70 ВНЕШ ШИФРМ,ТБУФ,НОММЛ1,Е46П48
ДИСП70 ВНЕШ МИНЗ,МАКЗ,НПЗ,ЕЗАР,ПРИП,РАБ600
КАЧКА ВНЕШ Е48П17,ШКРЗ,СТАРОЕ,РАБТОП
ВЗУ ВНЕШ ФИЗОБМ
ДИСП70 ВНЕШ Е48П7,ТЗФСТР,П3777,ОБЩТОМ,УКАЗД,МДР,ВСЕЕД
ДИСП70 ВНЕШ П177,ПРЕДЕЛ,П17777,МАХОЗУ
ДИСКИ ВНЕШ П11,КУСКИ,КТОМЛ
КИТ ВНЕШ ТОПЛИС,АКТОП
СОСТАВ ВНЕШ ТБД,ТБДТР,ТОП,ВР,ЯЧТОП,ШЗНМБ,ЯЧЛИС
СОСТАВ ВНЕШ ЯЧЧТР,СДТ,ЯЧТР,ЛИСОСТ,СДВМЛ
СОСТАВ ВНЕШ ЕОТКЛ,ЕОБМ,МОДЛИ
ХЛАМ ВНЕШ УПРИ
*КОНЕЦ ЧАСТИ

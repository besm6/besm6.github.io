ПЕРЕХ СТАРТ ’2000’
 М
      УПОТР ПЕРЕХ(М10)
********  0-Я ЯЧЕЙКА ЛИСТА ******
 ПБ РЭ        ВХОД ИЗ БОЭКА
********  1-Я ЯЧЕЙКА ЛИСТА *******
* ЭКСТРАКОД ДЛЯ ЧТЕНИЯ НЕСТАНДАРТНОГО БЛОКА ПЕРЕХВАТА
К СЧ СПЕЦ(М5)
 Э62 ’124’
********
РЭ МОДА МЭ-ПЕРЕХ(М10)  - 4(8)   - РАСПРЕДЕЛИТЕЛЬ
 ПИНО (М14)
 УИА ’2003’
 УИИ М17(М6)
 СЛИА 1(М17)
 ПВ ЗАНПРП(М15)
 СЧ П77777
 НТЖ ВСЕЕД
 И АВОС(М1)
 ЗП АВОС(М1)   - УСТРАН.ПЕРЕХВ.Э74
 СЧ УКАЗД(М1)   - ЕПЕР:=0
 ИЛИ ЕПЕР
 НТЖ ЕПЕР
 ЗП УКАЗД(М1)
 СЧ 33(М1)    - ПРИЗНАК РЕАКЦИИ НА ЭК
 СДА 64+23
 СДА 64-23
 ЗП 33(М1)
 УИА 3
 СЧ П77777
 И 5(М1)
 НТЖ 5(М1)
 ЗП 5(М1)      - М27:=0
 СЧ П77777
 СДА 64-1
 ИЛИ Е1
 СДА 64-21
 УИА ’2003’
 И 36(М1)
 НТЖ 36(М1)
 ЗП 36(М1)    - УНИЧТОЖЕНИЕ СПО
 СЧ 22(М1)
 И П77777
 НТЖ 22(М1)
 ЗП 22(М1)   - КРА:=0
 СЧ 23(М1)
 И П77777
 НТЖ 23(М1)
 ЗП 23(М1)   - ЗПСЧ:=0
 СЧ ЛИСОСТ(М1)  РАЗР.НА ЗАКАЗ ЛИСТОВ
 ИЛИ Е13
 НТЖ Е13
 ЗП ЛИСОСТ(М1)
 СЧ ’44’(М1)    ОБНУЛЕНИЕ ПРИЧИНЫ СНЯТИЯ
 И Е40П1
 ЗП ’44’(М1)
 СЧ ЯЧНИА(М1)
 ИЛИ ЕНИИАС
 ЗП ЯЧНИА(М1)
 И ЕПК
 ЗП ПОЛЕ+3   - ЗАКАЗ НА ВЫДАЧУ СООБЩ.ОПЕРАТОРУ
 СЧ 32(М1)
 И А41
 НТЖ А41     - ЕСТЬ ЛИ МЕТРЫ АЦПУ
 ПЕ А42
 СЧ 32(М1)
 ИЛИ А41    - ДОБАВИМ
 И Б1
 ЗП 32(М1)
А42 УИА ЗАДАЧА(М15)
 СЧИ М15
 ЗП 34(М1)   -  ИНФ.ДЛЯ АВОСТА
 ЗП 4(М1)
 УИА ’3’
 СЧ 23(М1)
 СДА 64+15
 И L177
 УИ М15  - ПРИЧИНА СНЯТИЯ (ПЕРВАЯ)
 ПО А19   - ’КОНЕЦ ЗАДАЧИ’
 СЛИА -13(М15)
 УИИ М16(М15)
 СЛИ М15(М15)
 СЛИ М15(М16)   - УМН. НА 3
А19 МОДА РАСПЕЧ-АВРАСП(М7)
 СЛИА (М15)
 СЧ (М15)
 ЗП ПОЛЕ       - ПЕРЕПИСЬ ПРИЧИНЫ СНЯТИЯ
 СЧ 1(М15)       ДЛЯ ДАЛЬНЕЙШЕГО ИСП.
 ЗП ПОЛЕ+1
 СЧ 2(М15)
 ЗП ПОЛЕ+2
 УИА ’2000’
 СЧ 1024          ЗАПРОС ЛИСТА ДЛЯ ПЕРЕХА
 УИА ’2003’
 СЧ 39(М1)
 ПО А24   - НЕ БЫЛО Э63 ВРЕМЯ
 СДА 64-25
 СЛЦ 39(М1)
 ПЕ А24    - НЕВЕРН.ИНФ. К Э63
 СДА 64+23
 СДА 64-23
 ЗП А38     - КОМПЕНСАЦИЯ ВРЕМЕНИ Э63 ’3’
 СЧ ’41’(М1)
 СЛЦ А38
 ЗП ’41’(М1)
А24 СЧ 0     - ГАШЕНИЕ Э63
 ЗП 39(М1)
 СЧ А25
 НТЖ ВСЕЕД
 СЛЦ ’41’(М1)
 УИА -1023(М15)
 ПО А26     - ЕСТЬ ОСТАТОК ВРЕМЕНИ БОЛЬШЕ 4 МИН.
 СЧ А25
 СЛЦ ’41’(М1)
 ЗП ’41’(М1)
 СЧ А27
 ЗП 39(М1)  - ДОБАВИЛИ 4 МИН ЦП НА ПЕРЕХВАТ
А26 СЧ Е28
 ИЛИ 72(М1)
 ЗП 72(М1)   - УНИЧТОЖЕНИЕ ПРИЗНАКА ’ВЫВОД ЗАПРЕЩЕН’
 СЧ АВОС(М1)
 ИЛИ Е27
 ЗП АВОС(М1)
 СЧ 23(М7)
 ИЛИ Е48
 ЗП 23(М7)
 РЕГ 65
ПЕРЕП МОДА (М15)
 СЧ 1023(М10)
 УИА 0
 ЗП 2047(М15)
 УИА 3
 МОДА ПЕРЕП
 ЦИКЛ (М15)
 УИА ВХД(М15)
 СЧИ М10
 СДА 64-2
 ИЛИ Е19
 ПБ ТБУФ
*
А25 КОНД М24В’77764400’
А41 КОНД В’177700’
Б2 КОНД М24К’173777767’К’160177777’
Б1 КОНД В’7777777777777600’
А27 КОНД В’7777777740005600’
*******
 ОТМЕН (М10)
 УПОТР ПЕРЕХ(М14)
МЭ УИИ М14(М10)
 МОД ТУСП-1(М6)
 УИА (М10)    - ВОССТАНАВЛИВАЕМ НА ИР 10 АДРЕС ИПЗ
 МОД ’17’(М10)
 УИА (М16)  - ИР 7 ИЗ ИПЗ -> ИР 16
 УИА ’2000’
 СЧ 1(М16)   - ИМБИБ
 УИА ’2003’
 ЗП А32
 УИА ’2000’
 СЧ 3(М16)       - ’*’ => ВЫДАЧА НА ОП.ЕLSЕ.НЕТ
 УИА ’2003’
 НТЖ А38      - ЗАКАЗ НА ВЫДАЧУ ОПЕРАТОРУ
 ПЕ ПРИЗНП       - НЕ ВЫДАВАТЬ СООБЩ. ОП
 СЧ ЕПК       - Е25 => ЗАКАЗ ОТ ЭК-ДА
 ИЛИ ЯЧНИА(М10)
 ЗП ЯЧНИА(М10)        ПК <> 0
ПРИЗНП СЧ ЕПЕР
  ИЛИ УКАЗД(М10)
  ЗП УКАЗД(М10)  - УСТАНОВКА ПРИЗНАКА ЭК-ДА
  УИА ’2000’
  СЧ 2(М16)    - ИМФАЙЛ
  УИА 3
  ЗП А34
  СЧИ М14
  СДА 64+5
  ЗП А38
  СЧ Е48П37
  И 31(М7)
  ИЛИ А38
  ЗП 31(М7)
  РЕГ ’20’
  СЧ 25(М7)
  ИЛИ Е48
  ЗП 25(М7)
  Э64 ИНФСЛП-ПЕРЕХ+’2000’
  СЧ 25(М7)
  ИЛИ Е48
  НТЖ Е48
  ЗП 25(М7)
А39 СЧИ М14
  СДА 64-2
  ИЛИ Е19
  МОДА ВХД1-КАНАЛ(М5)
  УИА (М15)
  ПБ ТБУФ
ИНФСЛП ЗП КЛЮЧП-ПЕРЕХ+’2000’
      ЗП КЛЮЧП-ПЕРЕХ+’2007’
      КОНД М23В’1’
КЛЮЧП КОНД П’<=><=>’
      КОНД П’<=><=>’
      КОНД П’/*/*/*’
      КОНД П’ДИМОН.’
А32 КОНД В’0’
А34 КОНД В’0’
      КОНД П’/:/:/:’
      КОНД М40В’172’
А38 КОНД П’*     ’
      ОТМЕН (М14)
      ОТМЕН (М10)
*
*        ВХОД В РЕЖИМЕ ЗАДАЧИ ПОЛЬЗОВАТЕЛЯ
*
ЗАДАЧА РЖА 7
 Э50 ’124’
 УИА СТПР(М17)    ВЫХОД ИЗ НЕТСТС
 Э50 ’151’
 ЗП СКВ          ОПРЕДЕЛЕНИЕ СКВ
 СДА 64-24
 ЗП УТИЛИТ
 ИЛИ LЭ62
 ЗП LЭ62
 ПВ ЗАКЛ(М7)      ПОДГОТОВКА СТРОКИ В СТАТИСТИКУ
 СЧ КЛЮЧ
 СДА 64
 Э63 ИНФСТ
 ПИНО НЕТСТС(М16)
 ЗП Р
 И L77777
 ЗП ДЛНУ
 СЧ Р
 СДА 64+22
 И L3
 УИА ’31’(М16)   - !!!!!!
 ПЕ НЕТСТС    - ФАЙЛ СТАТИСТИКИ ИМЕЕТ ПАР.
 ЗП LF
СТПР НОП 0
 СЧ КЛЮЧ
 Э63
*
*           ПРИЗНАКИ ПЕРЕХВАТА ПО ТИПАМ У-В
*           Е48 - БАЦ В КОДАХ АЦПУ
*           47Р - ПРИЗН.БПЛ (46-41 Р-НУ)
*           39Р - ПРИЗН.БГП (38-33 Р-НУ)
*           31Р - ПРИЗН.БПИ (30-25 Р-НУ)
*           23Р - ПРИЗН.БАЦ (22-17 Р-НУ)
*
 СДА 64+8
 ЗП Р
 УИА -3(М5)
 ПЕ ОБС     ЭТО НЕ ДИМОН
 ЗП УТИЛИТ
ОБС СЧ Р
 СДА 64+8
 ЗП Р
 И L377
 ИЛИ КШКАУС+3(М5)
 ЗП КШКАУС+3(М5)
 ЦИКЛ ОБС(М5)
 СДА 64+7
 СДА 64-39
 НТЖ L40
 НТЖ КШКАУС
 ЗП КШКАУС      ПРИЗНАК СС.СТРУКТУРЫ
*******
 УИА АВ(М16)    - РЕАК.НА АВОСТ
 СЧИ М16
 Э50 ’103’
 СЧ L12L
 ЗП ИНФСТ
 СЧ 0
 ЗП НО       - ВЫХОД ИЗ АВОСТА->НЕТПЕР
 СЧ L3
 Э50 ’102’
* ОБРАБОТКА ВСЕХ БУФЕРОВ
НАЧАЛО СЧ СДВГ     - НОМЕР ТЕКУЩЕГО БУФЕРА
 УИ М3
 СЧ КШКАУС(М3)
 И ЕП7
 ПО НЕТПЕР     - НЕ ЗАКАЗАНО
 УИА СОS(М7)
НСS СЧИ М3
 Э50 ’214’
 ЗП ОБЬЕМ      - РАЗМЕР (В ЗОНАХ)
 ЗП ДЛИНА
 УИ М4
 СДА 64-36
 РЗБ LП7
 ИЛИ L2ПРОБ
 ЗП МАСКА+8
 СЧ МАСКА+7
 ИЛИ Е16П1П
 НТЖ Е16П1П
 ЗП МАСКА+7
 СЧ ТАБЛТ(М3)
 И Е16П1П
 ИЛИ МАСКА+7
 ЗП МАСКА+7
 УИА БПУСТ(М16)      - НЕТ СОДЕРЖИМОГО БУФЕРА
 ПИО АВОСТ(М4)   -    М4=0
 ПБ (М7)
СОS СЧ ТАБЛТ(М3)
 И Е24П19
 УИА (М6)
 Э62 ’44’     - ПРО
 СЧ КШКАУС(М3)
 И L40
 ПЕ ДОПБУФ
 УИА L1(М6)
ДОПБУФ СЧ КШКАУС(М3)
 И L77
 ПО НЕТИМ         ИМЯ НЕ ЗАКАЗАНО
 СДА 64-36
 ИЛИ ЭК07
 ЗП Р
 СЧ КЛЮЧ
 Э63 Р     - СВЕДЕНИЯ ОБ ОБЛАСТИ
 ЗП Р
 И L177
 ЗП ДЛИНА
 ЗП ЧИСЗОН
 ПИНО АРФА(М16)    - ОШ. АРФЫ ПО ЭК.ЗАПРОСА ДЛ.ОБЛ.
 СЧ КШКАУС(М3)
 И L77
 СДА 64-39
 РЗБ LП7
 ИЛИ LМИНС
 ЗП МАСКА+’12’
 СЧ НУСМ
 ЗП МАСКА+’11’
 СЧ Р
 СДА 64+22
 И L3
 УИА ПАРОБ(М16)   - ОБЛ.,К СОЖАЛЕНИЮ, ИМЕЕТ ПАРОЛЬ
 ПЕ АВОСТ
ПЕ СЧ КШКАУС(М3)
 И L77
 ЗП НУ
 ПВ ЗАХВ(М7)
 СЧ ДЛИНА
 ЗП ЧИСЗОН
 СДА 64-39
 РЗБ LП7
 ИЛИ L8СК
 ЗП МАСКА+’13’
 ПИНО ЭЛЬФ(М6) - Е40=0 - ЗАПИСАТЬ В ССЫЛОЧОЙ СТРУКТУРЕ
 СЧИ М4
 ВЧОБ ДЛИНА      - ЧТО БОЛЬШЕ
 ПО НАЧЛ
 СЧ ДЛИНА
 УИ М4       - ЧИСЛО ЗАПИСЕЙ В ОБЛ
НАЧЛ СЧ КШКАУС(М3)
 И L77
 СДА 64-12
 ИЛИ БУФЛИС
 ЗП ИНФСЛ
 СЧ (0)
 ЗП ЧИСЗОН
НАЧАЛИ ПИО КОНЧИЛ(М4)
 СЧ ТАБЛТ(М3)
 И Е24П19
 ИЛИ ЧИСЗОН
 ИЛИ LЭ62
 Э62 ’41’
 НТЖ L77777
 УИА ОШ62(М16)   - ОШИБКА Э62’41’
 ПЕ АВОСТ
 Э70 ИНФСЛ
 СЧ ’4002’
 СЧ ИНФСЛ
 СЛЦ L1
 ЗП ИНФСЛ
 СЧ ЧИСЗОН
 СЛЦ L1
 ЗП ЧИСЗОН
 СЛИА -1(М4)
 ПБ НАЧАЛИ
КОНЧИЛ НОП
 УИА МАСКА(М12)
 ПВ СОNSUL(М17)
 ПВ СТАТИС(М15)   - ЗАПИСЬ СТАТИСТИКИ
НЕТПЕР СЧ СДВГ
 УИ М3
 СЛЦ L1
 ЗП СДВГ
 СДА 64+2
 ПО НАЧАЛО
 СЧ 0
 Э50 ’103’
 СЧ УТИЛИТ
 ПЕ ТЕЛЕ
 ЗП НО
 ПВ НСS(М7)
 СЧ ДИМОНН
 ЗП МАСКА+’13’
 СЧ LПРОБ6
 ЗП МАСКА+’11’
 ЗП МАСКА+’12’
 ПВ СТАТИС(М15)
 УИА ’2000’(М7)
 УИА -3(М16)
СНЯ СЧ ПОЛЕ+3(М16)
 ЗП ’1703’(М16)
 ЦИКЛ СНЯ(М16)
 СЧ NСИСТ
 УИА (М4)
 Э50 ’131’    - ЗАКАЗ 2053
 УИ М5
 ПО К
КОН Э62 (0)
*******
НЕТИМ НОП
 Э50 ’100’
 ИЛИ Е24П1
 НТЖ Е24П1
 ИЛИ АБУФ
 НТЖ (0)
 СЧ КЛЮЧ
 Э63 ИНФБЮД
 СЧ 0
 ПИНО НБЮДС(М16)
 СЧ ШКБЮД
 СДА 64+24
НБЮДС ЗП ШИФБЮД
 СЧ МАСКА+2
 СБР ОППОПП
 РЗБ LПРОБ6
 СДА 64+8
 ЗП Р
 СЧ L377
 СДА 64-40
 И КШКАУС(М3)
 ИЛИ Р
 ЗП Р
 СЧ МАСКА+3
 СДА 64+8
 ЗП Р3
 И L377
 ИЛИ Р
 ЗП ИМЯ
 СЧ МАСКА+3
 СДА 64-40
 ЗП Р
 СЧ МАСКА+4
 СДА 64+24
 И Е16П1П
 СДА 64-24
 ИЛИ Р
 ЗП ИМЯ+1
 Э50 64
 И =М32В’-1’
 СДА 64+24
 ИЛИ ИМЯ+1
 ЗП ИМЯ+1
 СЧ ШКБЮД-1
 И L377
 ИЛИ ИМЯ+1
 ЗП ИМЯ+1
 СЧ КШКАУС(М3)
 И L377L
 ПО НОН
 ЗП Р3
S1 НОП ,
 СЧ ИМЯ+1
 ИЛИ L377L
 НТЖ L377L
 ИЛИ Р3
 ЗП ИМЯ+1
 СЧ КШКАУС(М3)
 СЛЦ L25
 ЗП КШКАУС(М3)
 УИА 6(М16)   - НЕВОЗМОЖНО СОЗДАНИЕ ОБЛАСТИ
 СДА 64+5
 И L25
 ПЕ АРФА
НОН СЧ L01L
 ЗП ИНФ01
 СЧ ИМЯ
 ЗП МАСКА+’11’
 УИА ЕS(М16)
 СЧ ИМЯ+1
 ИЛИ Е24П1
 НТЖ Е24П1
 ИЛИ ЕМИН
 ИЛИ (М16)
 ЗП МАСКА+’12’
 СЧ ОБЬЕМ
 УИА -1(М13)
 ПИО ВЦ(М6)
 РЖА ’22’
 ИЛИ С0
 УМН С
 РЖА 3
 СЛ С1
 И L7777
 ЗП ДЛИНА
ВЦ ЗП Р
 СЧ ШКБЮД+3(М13)
 И Е24П1
 ЗП ШКБЮД+3(М13)
 ВЧ ДЛИНА
 ПО НОМ
 ЦИКЛ ВЦ(М13)
 ВЧ Р
 ПО ВЦ1
 УИА -1(М13)
ВЦ1 СЧ ШКБЮД+3(М13)
 ЗП ДЛИНА
 УИА ’7’(М16)   - НЕТ БЮДЖЕТА
 ПО АРФА
НОМ УИА (М14)
 СЧ ШКБЮД
 СДА 64+20
 СДА 64-44
 И ШКБЮД-1
 СЛИА 1(М13)
 УИА ’25’(М16)
ПРБЮД ЗП Р4
 ПО АРФА
 И L48
 ПЕ ЕСТЬБ
НУЛЬБЮ СЛИА 1(М14)
 СЧ Р4
 СДА 64-1
 ПБ ПРБЮД
ЕСТЬБ СЧИ М14
 СДА 64-20
 ЗП Р
 СЧИ М13
 СДА 64-18
 ИЛИ Р
 ИЛИ ДЛИНА
 НТЖ (0)
 СЧ КЛЮЧ
 Э63 ИНФ01
 ПИО ХОР(М16)
 УИИ М12(М16)
 СЛИА -6(М12)
 ПИНО АРФА1(М12)  - ОШ.ПРИ СОЗДАНИИ ОБЛ.
 СЧ L25               КРОМЕ ОШ.N%6
 СДА 64-5
 ИЛИ КШКАУС(М3)
 И L377L
 ЗП Р3
 ПБ S1    - ПОДСТАВИМ СИМВОЛ ’А’ ВМЕСТО ВРЕМЕНИ
АРФА1 СЛИА -12(М12)  - АВОСТ ’22’
 ПИО НУЛЬБЮ(М12)
 СЧ ДЛНУ
 ПЕ АРФА
 СЧ ОБЬЕМ
 ПО АРФА
 СЧ 0
 ЗП ОБЬЕМ
 ПБ НОН
ХОР СЧ L04L
 ЗП ИНФ01
 СЧ ТАБЛТ(М3)
 СДА 64+24
 И L3
 ЗП Р
 СЧ ШКБЮД
 И =М24В’-1’
 ИЛИ Р
 НТЖ (0)
 СЧ КЛЮЧ
 Э63 ИНФ01
 УИА 1(М5)
 ПИНО АРФА(М16)    - ОШ.ПО ЭК.ЗАДАНИЯ ПРАВ
 УИА ПS(М7)
 УИА ИНФ01(М10)
 УИА 1(М5)    - СПОСОБ ЗАДАНИЯ ИМЕНИ
*
ЗАКАЗ СЧИ М5   - ЗАКАЗ ОБЛАСТИ
 ИЛИ L12L
 ЗП (М10)
 СЧ Е48П32
 НТЖ (0)
 СЧ КЛЮЧ
 Э63 (М10)
 ПБ (М7)
ЗАХВ ЗП LР2
 СДА 64-12
 Э50 ’105’
 ЗП Р4
 НТЖ LЗАК
 ПО ВЫХЗАХ
 СЧ LЗАК
 ПО НЕТЗАХ
 Э50 ’116’
 СЧ 0
 ЗП LЗАК
НЕТЗАХ СЧ Р4
 Э50 ’115’
 СЧ Р4
 ЗП LЗАК
 УИА -4(М16)
 СЧ
ОБЛИС НОП
 ЗП ЛИСТЫ+4(М16)
 ЦИКЛ ОБЛИС(М16)
ВЫХЗАХ СЧ LР2
 ПБ (М7)
ПS ПИНО АРФА(М16)    - ОШ.ПРИ ЗАКАЗЕ ОБЛ.
 УИА ’24’(М14)   - ОБЛ-КАТАЛОГ
 ПО АРФА
 НТЖ КШКАУС(М3)
 И L77
 НТЖ КШКАУС(М3)
 ИЛИ L39
 ЗП КШКАУС(М3)
 ПБ ПЕ
*
СТАТИС СЧ ИНФСТА
 УИА ’10’(М5)
 ПО ЛД
 НТЖ L48
 ПО (М15)    - УЖЕ ПРОБОВАЛИ ЗАКАЗЫВАТЬ
 ПБ ТИС
ЛД УИА ИНФСТ(М10)
 ПВ ЗАКАЗ(М7)
 ПИНО НЕЗАХ(М16)   - НЕ ЗАКАЗЫВАЕТСЯ- И НЕ НАДО
 ЗП ИНФСТА
ТИС И L77
 ПВ ЗАХВ(М7)
 ЗП НУ
 СЧ ДЛНУ
 ЗП ЧИСЗОН
 СЧ (0)
 ЗП СТРОКА
 ЗП ЧСВВ
 ЗП LПОЗЗ
 УИА ’110’(М1)    - ДЛИНА СТРОКИ В БАЙТАХ
 СЧИ М1
 ЗП LПОЗЗ
 УИА -’13’(М1)
ПЕС СЧ МАСКА+’13’(М1)
 ЗП БУФЕР+’13’(М1)   - ПЕРЕПИСЬ
 ЦИКЛ ПЕС(М1)
 УИИ М10(М15)
 ПВ ЗАПСТР(М4)
 УИИ М11(М10)
ПОЗА СЧ ВСЕД
 ПБ ЧТСЛ
НЕЗАХ СЧ Е48
 ЗП ИНФСТА
 УИИ М17(М15)
 ПБ НЕТСТС
*
*******
*    ПОДГОТОВКА ЗАКЛЮЧИТЕЛЬНОГО СООБЩЕНИЯ
ЗАКЛ УИА МАСКА+1(М14)     - БУФЕР ЗАПИСИ
      УИА -8(М13)
 УИА -4(М16)
 СЧ (0)
АПОЛ ЗП ЛИСТЫ+4(М16)
 ЦИКЛ АПОЛ(М16)
 ЗП LL
      Э50 ’100’
      ЗП Р2
      РЗБ LПРОБ6
      ЗП МАСКА
      СЧ Р2
      СДА 64-24
      РЗБ LПРОБ6
      ЗП (М14)
      УИА 3(М2)
      Э50 ’114’
 ЗП Р4
      СДА 64-13
      СДА 64+6
      РЗБ LПРОБ6
 ИЛИ L2ПРОБ
      ПВ ЗАП6(М6)
      УИА (М2)
      ПВ ЗАП6(М6)
      УИА 2(М2)
      СЧ Р4
      СДА 64-19
      СДА 64+3
      РЗБ LПРОБ6
      ПВ ЗАП6(М6)   - МЕСЯЦ
      УИА (М2)
      СЧ LТОЧКА
      ПВ ЗАП6(М6)
      СЧ Р4
      СДА 64-24
      РЗБ LПРОБ6
      ЗП Р2
      СДА 64-8
      ПВ ЗАП6(М6)   - 1-Я ЦИФРА ГОДА
      СЧ Р2
      ПВ ЗАП6(М6)   - 2-Я ЦИФРА ГОДА
      Э53 ’10’     - АСТРОНОМИЧЕСКОЕ ВРЕМЯ
      ПВ ЗАПВРЕ(М6)
      УИА 3(М2)
 СЧ СКВ
 СДА 64-39
      РЗБ LП7
 ПВ ЗАП6(М6)
 ПВ ЗАП6(М6)
 СЧ Р4
 ПВ DЗАПСИ(М15)
 УИИ М6(М7)
      Э63 ’4’      - ВРЕМЯ  ЦП
      ПБ ЗАПВРЕ
*******
СОNSUL УИА 53(М1)
  УИА БУФЕР-1(М14)
  УИА -8(М13)
  УИА ’50’(М11)
  СЧ МАРШР
МСКК СЛИА -1(М1)
 ПВ DЗАПСИ(М15)
 УИА МСКК(М15)
 ПИНО DВБСИР(М1)
 СЧ L377
 ПВ DЗАПСИ(М15)
 УИА БУФЕР(М1)
 Э71 ВЫДОП
 ПБ (М17)
****
ТЕЛЕ УИА (М16)
      УИА ПОЛЕ-1(М12)   - АДРЕС РАПЕЧАТКИ О ПРИЧИНЕ СНЯТИЯ
      УИА (М10)
      УИА (М11)
      УИА МАСКА-1(М14)
      УИА -8(М13)
      УИА -18(М4)
МПВ ПВ DВБСИР(М15)
      ЗП Р1
      НТЖ LПРОБ6
      И L377
      ПО МПВ1
      УИА (М16)
      ПБ МПВ3
МПВ1 ПИНО МПВ2(М16)
      УИА 1(М16)
МПВ3 СЧ Р1
      ПВ DЗАПСИ(М15)
МПВ2 ЦИКЛ МПВ(М4)
      СЧ L377
      УИА -48(М6)
      ПВ DЗАПСИ(М15)
      УИА МАСКА(М11)
      Э71 ВСЕД
МЛР ЗП Р4
      ПО КОН
      НЕД (0)
      УИ М15
      СЛЦ L1777
      ИЛИ L200
      СДА 64-40
      ЗП Р1
      СЧИ М15
      Э62 ’102’
      СЧ МАСКА
      СДА 64-8
      СДА 64+8
      ИЛИ Р1
      ЗП МАСКА
      Э71 LВЫВ
      РЖА ’7’
      Э71 ВСЕД
      ПО МСС
      Э71 ОТКАЗ
МСС СЧ L48
      СДА 64-1(М15)
      И Р4
      НТЖ Р4
      ЦИКЛ МЛР(М6)
      ПБ КОН
*******
АВОСТ УИА -2(М14)
СА МОДА 2(М14)
 СЧ (М16)
 ЗП МАСКА+’13’(М14)
 ЦИКЛ СА(М14)
 ПВ СТАТИС(М15)   - ЗАПИСЬ СТАТИСТИКИ
РОЛ СЧ НО
 ПО НЕТПЕР
 УИА МАСКА(М12)
 ПВ СОNSUL(М17)
 Э62 (0)
АРФА СЧИ М16
 ИЛИ L48
ХЬЮ УИА МАСКА+’11’(М15)
 Э50 ’202’
 УИА РОЛ(М15)
 СЧ 0
 ПВ СЫ(М11)
 ПБ СТАТИС
АВ СЧ ИНФСТА
 ПО РТ   - АВОСТ НЕ НА ЗАПИСИ СТАТИС
 НТЖ НУ
 ПЕ РТ    - IDЕМ
 ЗП НУ
 ПБ РОЛ
РТ Э50 ’101’
 ПБ ХЬЮ
*******
ЗАПВРЕ СЛЦ П24
 УИА -5(М4)
 УИА -2(М3)
 ИЛИ С0
 РЖА ’22’
 СЛ (0)
 ЗП Р2
 СЧ LПРОБ6
 ПВ DЗАПСИ(М15)
ДЮ1 СЧ Р2
 РЖА ’22’
 ДЕЛ Д10Ч+5(М4)
 Э57 (0)
 ЗП А38
 УМН Д10Ч+5(М4)
 ВЧОБ Р2
 ЗП Р2
 СЧ А38
 РЖА 7
 СЛ С0
 ПВ DЗАПСИ(М15)
 СЧ LПРОБ6
 ПИО ДЮШЕС(М4)
 СЛИА 1(М3)
 ПИНО ДЮ(М3)
 СЧ LТОЧКА
 ПВ DЗАПСИ(М15)
 УИА -2(М3)
ДЮ СЛИА 1(М4)
 ПБ ДЮ1
ДЮШЕС УИИ М15(М6)
 ПБ DЗАПСИ
*******
ЗАП6 ЗП Р1
      УИИ М16(М2)
      УИА 1(М2)
      ПИО МЮZ1(М16)
МЮZ СДА 64+40
      ПВ DЗАПСИ(М15)
      СЧ Р1
      СДА 64+40
      СЧМР
      ЗП Р1
      СЛИА -1(М16)
      ПИНО МЮZ(М16)
      СЧ LПРОБ6
      ПБ (М6)
МЮZ1 СЧ LТОЧКА
 ПБ ЗАП6
DВБСИР СЧ (М12)  - ПОСИМВ.ЧТЕНИЕ
      СДА ’100’(М11)
      И L377
      ПИО М2D(М11)
      СЛИА -8(М11)
      ПБ (М15)
М2D УИА 40(М11)
      СЛИА 1(М12)
      ПБ (М15)
DЗАПСИ СЛИА 8(М13)   - ПОСИМВ.ЗАПИСЬ
      И L377
      ПИНО М1D(М13)
      СЛИА 1(М14)
      УИА -48(М13)
 ЗП ИР13
      СЧ (0)
      ЗП (М14)
 СЧ ИР13
М1D СДА ’110’(М13)
      ИЛИ (М14)
      ЗП (М14)
      ПБ (М15)
*******
ЭЛЬФ СЧ (0)     ***** ПЕРЕКОДИРОВКА ИЗ  АЦПУ  В  УПП
      ЗП LПОЗЗ
      ЗП ЧСВВ    - ЧИСЛО ПОЗИЦИЙ  СВЕРНУТЫХ В ПРОБЕЛЫ
 ЗП LF
 ЗП СТРОКА
 СЧ О41
 И МАСКА+8   - ИЗ ПРОБЕЛА ДЕЛАЕМ ТОЧКУ
 ЗП МАСКА+8
 МОД СДВГ
 СЧ КШКАУС
 И L39
 ПО НЕОБН    - ФАЙЛ УЖЕ БЫЛ ЗАВЕДЕН
 УИА ’24’(М16)
 СЧ ДЛИНА
 ПО АРФА
 УИ М4
 УИА (М5)
 УИА L1(М1)
ЗСЛ1 СЧИ М5
 ПВ ЧТСЛ(М11)
 УИА 1(М2)
 ПВ ЗПСЛ(М11)
 СЛИА ’2000’(М5)
 СЛИА -1(М4)
 ПИНО ЗСЛ1(М4)
 СЧ ВСЕД
 ПВ ЧТСЛ(М11)
НЕОБН УИА (М17)
      РЖА ’7’
 ПВ ЭКСТ(М2)    - ЗА 0-Й ЗОНОЙ АЦПУ
 ПВ УСТ(М15)    - УСТАНОВКА ЧИСЛА ЧИТ.СЛОВ НА ИР 6
 УИА ’22’(М1)
 НТЖ L1
 ПЕ ОШБ
      УИА ’77402’(М12)
      УИА ’50’(М11)
 УИА ’4400’(М10)
МЭЛ УИА МЭЛ(М4)      ********* ДЕКОДИРОВКА  СИМВОЛОВ  АЦПУ: ***
      УИА ’0’(М5)         СИМВОЛЫ С 001 ПО 140(8) ПЕРЕКОД.ВЫЧИТ
*                                175 - КОНЕЦ БУФЕРА АЦПУ;
*                                141 - ПЕРЕХОД НА СЛЕД.АБЗАЦ;
*                                142 - ЗАПИСЬ СФОРМ.СТРОКИ;
*                          143 - 173 -  IDЕМ+N ПЕРЕВОДОВ СТОРОК
      УИА БУФЕР-1(М14)                 (ЗАПИСЬ 1 ПРОБ.) N=СИМВ-
      УИА ’77770’(М13)           177 - БУДЕТ ЗАДАНО ЧИСЛО ПОВТО
LЦ РЖА ’7’                 200 - 376 - ЗАДАНА ПОЗИЦ.СЛЕД.СИМВОЛ
     ПИО КОНАБЗ(М12)      ЕСЛИ ЗАДАНА ПОЗИЦИЯ МЕНЬШЕ ТЕКУЩЕЙ -
      ПВ DВБС(М15)        СЛЕД.СТРОКА НАЧИНАЕТСЯ С ЗАДАННОЙ ПО
LЦЦ ЗП Р1              СИМВОЛЫ 000 И 377 - ПРОПУСКАЮТСЯ
*
      ПО LЦ        - 000 (8)
      ПИНО ЗВ(М5)
      НТЖ L177
      ПО ЗПОВТ     - ЗАДАНО ЧИСЛО ПОВТОРОВ
      И L200
      ПЕ NПОЗ      - ЗАДАН НОМЕР ПОЗИЦИИ
      СЧ Р1
      НТЖ L175
      ПО КОНЧИЛ    - КОНЕЦ ПОТОКА
      СЧ Р1
      ЗП Р2
      НТЖ L141
      ПО LЦ3       - НАЙДЕН СИМВОЛ ’141’
      И L140
      ПЕ LЦ1       ->  НЕ СЛУЖ.СИМВОЛ
   СЧ Р1
   НТЖ L174        - НАЙДЕН КОНЕЦ АБЗАЦА
   ПО КОНАБЗ
      СЧ Р1
      ВЧ L141
      ВЧ L1
      ПЕ LЦ1      - СИМВОЛ  ’140’
      РЖА ’7’
      ПО ЗАПСТР    - НАЙДЕН КОНЕЦ СТРОКИ  142
      НТЖ ВСЕД
      УИ М5
      СЛИА 2(М5)
      ПВ ЗАПСТР(М4)
      СЧ L77          - В СВОДКЕ ЗАПРЕЩЕНЫ ПЕРЕВОДЫ СТРОК,
      СДА 64+2        - ЗДЕСЬ ОСТАВИЛА ПО-ПРЕЖНЕМУ.
      СДА ’30’
      ЗП БУФЕР
DЗ СЧ 0
      ЗП LПОЗЗ
      ПВ ЗАПСТР(М4)
      ЦИКЛ DЗ(М5)
      ПБ МЭЛ
LЦ3 СЧ L141
 НТЖ L174
 ПО КОНАБЗ        - ’141’ - КОНЕЦ АБЗАЦА (ДЛЯ СТАРЫХ ВЕРСИЙ)
 ПБ LЦ
LЦ1 СЧ LПОЗЗ
      СЛ L1
      ЗП LПОЗЗ
      СЧ Р1
      СЛЦ L377
      И L377
      ПВ DЗАПСИ(М15)
      СЧ LПОЗЗ
      СЛ ЧСВВ
      ВЧ L177
      ПО ЗАПСТР     - ИСЧЕРПАН БУФЕР ЗАПИСИ
      РЖА ’7’
      ЦИКЛ LЦ1(М5)
      ПБ LЦ
ЗПОВТ УИА ’77777’(М5)
      ПБ LЦ
ЗВ НТЖ ВСЕД
      УИ М5
      СЛИА 2(М5)
      СЧ Р2
      ЗП Р1
      ПБ LЦ1
NПОЗ СЧ Р1
      НТЖ L377
      ПО LЦ
      СЧ LПОЗЗ
      СЛ ЧСВВ
      ВЧОБ Р1
 УИА (М5)
 ЗП Р1
 НТЖ L200
 ПО LЦ
 СЧ Р1
      СЛ L1
      ЗП Р1        - СВЕРТКА ПРОБЕЛОВ
      И L200
      ПО СИТУАЦ   - НАЙДЕН  НЕПЕРЕКОДИРУЕМЫЙ СИМВОЛ - N ПОЗ ОТР
      СЧ Р1
      И L177
      СЛ ЧСВВ
      ВЧ L1
      ВЧ L1
      ЗП ЧСВВ
 ПЕ ЗАПСТР
      ПБ LЦ1
СИТУАЦ СЧ ВОПРОС
 ПБ LЦЦ
КОНАБЗ СЛИ М6(М12)
      СЛИА ’77776’(М6)
      СЧИ М6
      СДА 64+12
      ПЕ ЗАКАБЗ
      УИА ’77402’(М12)
 УИИ М11(М10)
 СЛИА -’6000’(М11)
 ПИО ЗАКАБЗ(М11)
 УИА ’50’(М11)
      СЛИА ’400’(М10)
      ПБ LЦ
ЗАКАБЗ ПВ КЭ6Э(М2)
 УИА ’77402’(М12)
      УИА ’50’(М11)
      УИА ’4400’(М10)
      ПВ УСТ(М15)
 НТЖ L1
      ПО LЦ
 УИА СОNТ(М4)   - В ПОСЛЕДНИЙ РАЗ
      ПБ ЗАПСТР
УСТ ЗП LLЛЛLL
    СЧ ’4000’
    УИ М6
    СДА 64+15
    ЗП LLД
    ПИНО УСТТ(М6)
    УИА ’6001’(М6)
УСТТ СЛИА ’77775’(М6)
     СЧ LLЛЛLL
     ПБ (М15)
******
ЭКСТ СЧ (0)     ***** ОБСЛУЖИВАНИЕ  Э62 ’41’ *****
 ЗП ЗОН
КЭ6Э СЧ ОБЬЕМ
 ПО (М2)
 НТЖ ЗОН
 ПО (М2)
 МОД СДВГ
 СЧ ТАБЛТ
 И Е24П19
 ИЛИ LЭ62
      СЛЦ ЗОН
      Э62 ’41’
      НТЖ L77777
 УИА КОНЧИЛ(М11)
 ПЕ ПОЗА   КОНЕЦ ВЫХОДНОГО ПОТОКА
 СЧ L1
 СЛЦ ЗОН
 ЗП ЗОН
 СЧ L1
      ПБ (М2)   !!!!!
СОNТ СЧ ВСЕД
 ПВ ЧТСЛ(М11)
 ПБ КОНЧИЛ
DВБС ПИО DВБ(М17)
 СЧ СИМ
 УИА (М17)
 ПБ (М15)
DВБ МОДА (М10)
      СЧ (М12)
      СДА 64(М11)
      И L377
      ПИО МИТТ(М11)
      СЛИА ’77770’(М11)
      ПБ (М15)
ЗАКА УИА 1(М17)
 НТЖ L174
 ПЕ ЗАКАБЗ
 УИА (М17)
 ПБ ЗАКАБЗ
МИТТ УИА ’50’(М11)
      СЛИА 1(М12)
 ЗП СИМ
 ПИО ЗАКА(М6)
 СЛИА ’77777’(М6)
 МОДА (М15)
 ПИНО (М6)
 СЧ LLД
 СДА ’75’
 УИ М6
 СЛИА 64(М6)
 МОДА (М10)
 СЧ (М12)
 СДА (М6)
 МОДА (М10)
 ЗП (М12)
 УИА (М6)
 СЧ СИМ
      ПБ (М15)
******
ЗАПСТР НОП       ЗАПИСЬ СТРОКИ В ФАЙЛ
 УИИ М7(М4)
 СЧИ М11
 ЗП ИР11
 СЧИ М5
 ЗП ИР5
 СЧ СТРОКА  - НОМЕР ПОСЛ.ЗАПИСАННОЙ СТРОКИ
 ПЕ НЕПЕРВ
 СЧ (0)     - ПЕРВОЕ ОБРАЩЕНИЕ
 ПВ ЧТСЛ(М11)   - ЧТЕНИЕ 0-ГО СЛ.0-Й ЗОНЫ
 ПВ ПРОВ0(М11)  - ПРОВЕРКА КОРРЕКТН.0-ГО СЛ
 СЧ Р
 НТЖ L1
 ПЕ ЛОК
 СЛЦ L1    - ПУСТОЙ ФАЙЛ
 СЛЦ L1
 ЗП (М15)
 СЧ ПЕРВСЛ
 ЗП 1(М15)
ЛОК СЧ 1(М15)   - 1-Е СЛОВО 0-Й ЗОНЫ
 И L177
 НТЖ L1
 УИА ’10’(М16)  - 1-Е СЛ.СЛ.0-Й ЗОНЫ -> ДЛ.NЕ.1
 ПЕ ОШБ
 СЧ 1(М15)
 СДА 64-17
 СЧМР
 ЗП ХВОСТ   - АДРЕС ПОСЛ.СТРОКИ ФАЙЛА
 УИА СN(М4)
NСТР ЗП Р2  - ОПРЕДЕЛЕНИЕ N СТРОКИ
 ПВ ЧТСЛ(М11)
 ЗП Р
 НТЖ L177
 СДА 64+5
 СЛЦ L1
 И L3
 ПО (М4)    - НЕТ НОМЕРА
 ВЧ L1
 СДА 64-3
 УИ М3
 СЧ Р2
 СЛЦ L1
 ПВ ЧТСЛ(М11)
 СДА 64+24(М3)
 ПБ (М4)
******
СN ЗП СТРОКА
 СЧ НУ
 НТЖ ИНФСТА
 ПЕ СNN     - НЕ ЗАПИСЬ СТАТИСТИКИ
 СЧ СТРОКА
 СЛЦ L1
 ПБ СN1
СNN СЧ Р
 СДА 64+14
 И Е17П1
 НТЖ L1
 УИА ’11’(М16)  - НЕВЕРН.ССЫЛКА НАЗАД
 ПЕ ОШБ
 СЧ L144
 ИЛИ С0   - СОЗДАЕМ НОМЕР, НАЧИНАЮЩИЙ СЛЕД.СОТНЮ
 РЖА ’22’
 СЛ (0)
 ЗП Р
 СЧ СТРОКА
 ИЛИ С0
 СЛ (0)
 ДЕЛ Р
 РЖА ’7’
 СЛ С0
 СЛЦ L1
 РЖА ’22’
 СЛ (0)
 УМН Р
 РЖА ’7’
 СЛ С0
СN1 И Е24П1
 ЗП СТРОКА
******
НЕПЕРВ УИА БУФЕР-1(М1)
 УИА (М5)
МZ2 СЧ СТРОКА
 ПО МZК2
 И Е24П1
 НТЖ СТРОКА
 ПО МZК1    - НОМЕР СТРОКИ НЕ ПРЕВЗОШЕЛ 24Р.
МZК2 СЧ L144
 ЗП СТРОКА
МZК1 СЧ СТРОКА
 УИА (М4)
МZК ПО МZ1
 СЛИА 8(М5)
 СЛИА 1(М4)
 СДА 64+8
 ПБ МZК
МZ1 СЧ СТРОКА
 СДА 64(М5)
 СЧМР
 ЗП (М1)   - ПЕРВОЕ СЛОВО
 СЧ L3
 СЛЦ L3
 ИЛИ С0   - ДЕЛИТЕЛЬ НА 6.
 РЖА ’22’
 СЛ (0)
 ЗП Р
 РЖА ’7’
 СЧИ М4
 СЛ LПОЗЗ
 ВЧ L1
 ИЛИ С0
 РЖА ’22’
 СЛ (0)    - ЧИСЛО ЗАПИСЫВАЕМЫХ СИМВОЛОВ.
 ДЕЛ Р
 РЖА ’7’
 СЛ С0
 И L37
 СЛ L1
 УИ М2     - ЧИСЛО ЗАПИСЫВАЕМЫХ СЛОВ
 УИ М3
 СЛЦ L1
 ЗП ЗПСЛОВ
 СЧИ М4
 ЗП ЧБАЙТ
 СЛИА 1(М2)
МП СЧ 1(М1)   - ПЕРЕПИСЬ ОСТАЛЬНОЙ ИНФ.ЗА НОМЕРОМ СТР.
 СДА 64(М5)
 ЗП Р
 СЧМР
 ЗП 1(М1)
 СЧ Р
 ИЛИ (М1)
 ЗП (М1)
 СЛИА 1(М1)
 СЛИА -1(М3)
 ПИНО МП(М3)
 СЧ ХВОСТ
 ПВ ЧТСЛ(М11)
 ПВ НАЙДИ(М11)  - НАЙДЕМ МЕСТО В ЗОНЕ,ГДЕ ПОСЛ.СТРОКА
 ПЕ НАШЛИ   - СМ=АДРЕС
 СЧ L1
 ЗП LL   - ’ФЛАГ’
 СЧ ХВОСТ
 ЗП Р3   - ТЕКУЩИЙ АДР.ПОИСКА
 И Е17П11
 ПО N1   - ХВОСТ В 0-Й ЗОНЕ
 СЧ (0)
 ПВ НАЙДИ(М11)  -ПОИСК В 0-Й ЗОНЕ
 ПЕ НАШЛИ
 СЧ Р3
N1 УИА -4(М14)
 СДА 64+10
 ЗП Р
N2 СЧ ЛИСТЫ+4(М14)
 ПО NN
 И L177
 ПО NN  - В 0-Й ЗОНЕ УЖЕ ИСКАЛИ
 НТЖ Р3
 ПО NN  - В ХВОСТОВОЙ ЗОНЕ ИСКАЛИ
 СЧ LL
 ПО N3
 СЧ ЛИСТЫ+4(М14)
 И L48
 ПО NN
 ПБ N4
N3 СЧ ЛИСТЫ+4(М14)
 И L48
 ПЕ NN
N4 СЧ ЛИСТЫ+4(М14)
 И L177
 СДА 64-10
 ПВ НАЙДИ(М11)
 ПЕ НАШЛИ
NN ЦИКЛ N2(М14)
 СЧ LL
 ПО НК
 СЧ 0
 ЗП LL
 ПБ N1
НК СЧ LF
 ПЕ ИСКЛ    - УЖЕ ИСКЛЮЧАЛИ СТРОКУ
 СЧ L1
 СДА 64-10   - ПОПЫТКА ИСКАТЬ В N+1 ЗОНЕ
 СЛЦ Р3
 ЗП Р3
 СДА 64+10
 ВЧ ЧИСЗОН
 ПЕ NК1
 СЧ (0)
 ЗП Р3   - НЕТ БОЛЬШЕ ЗОН
 СЧ LL      - ПРОСМОТР НАЧАЛЬНЫХ ЗОН
 ПЕ ИСКЛ    - УЖЕ БЫЛ -> ИСКЛЮЧ.СТРОКИ
 СЧ L1
 ЗП LL
 ПБ НК
NК1 СЧ Р3
 ПВ ЧТСЛ(М11)  - ЧТЕНИЕ ОЧЕРЕДНОЙ ВЫБРАННОЙ ЗОНЫ
 СЧ Р3
 ПВ НАЙДИ(М11)
 ПО НК
НАШЛИ УИА БУФЕР-2(М1)
 ЗП Р3
 СЧ ХВОСТ
 СДА 64+17
 СЧМР
 ИЛИ ЕЕ15  - ССЫЛКА ВПЕРЕД НА 1
 ЗП (М1)   - НАЗАД - НА ХВОСТ
 СЧ LПОЗЗ
 СДА 64-7
 ИЛИ (М1)
 ЗП (М1)
 СЧ ЧБАЙТ
 СДА 64-5
 ЗП Р
 СЧ ЗПСЛОВ
 УИ М2
 ИЛИ Р
 ИЛИ (М1)
 ЗП (М1)   - ПОДГОТОВЛЕННОЕ СЛ.СЛ.НОВОЙ СТРОКИ
 ПВ ЗПСЛ(М11)    ЗАПИСАТЬ СЛОВО ПО АДР.Р3
 СЧ ХВОСТ
 ПВ ЧТСЛ(М11)   - РАБОТАЕМ С СЛ.СЛ.ПОСЛ.СТРОКИ
 ИЛИ Е31П15
 НТЖ Е31П15   - 0 В ССЫЛКУ ВПЕРЕД
 ЗП Р
 СЧ Р3
 СДА 64-14
 ИЛИ Р
 ЗП Р
 УИА 1(М2)
 УИА Р(М1)
 ПВ ЗПСЛ(М11)
 СЧ Р3
 ЗП ХВОСТ
 СЧ L1
 ПВ ЧТСЛ(М11)
 ИЛИ Е48П32
 НТЖ Е48П32
 ЗП Р
 СЧ ХВОСТ
 СДА 64+17
 СЧМР
 ИЛИ Р
 ЗП Р
 УИА 1(М2)
 УИА Р(М1)
 ПВ ЗПСЛ(М11)
 СЧ ИР11
 УИ М11
 СЧ ИР5
 УИ М5
 СЧ (0)
 ЗП LПОЗЗ
 ЗП ЧСВВ
 СЧ СТРОКА
 СЛЦ L1
 ЗП СТРОКА
 ПБ (М7)   **** ВЫХОД ****
******
ИСКЛ НОП
 СЧ L1
 ЗП Р3
 ЗП LF
 ПВ ЧТСЛ(М11)
ИСК1 СДА 64+14
 И Е17П1
 УИА ’12’(М16)
 ЗП Р2
 НТЖ L1
 ПО ОШБ
 СЧ Р2
 ПВ NСТР(М4)
 ПО ИСК
 НТЖ L1
 ПЕ ИСК
 СЧ Р2   - СТРОКУ С НОМЕРОМ 1 НЕ ИСКЛЮЧАЕМ
 ЗП Р3
 СЧ Р
 ПБ ИСК1
ИСК СЧ Р
 СДА 64+14
 И Е17П1
 ЗП LLЛЛLL
 ПВ ЧТСЛ(М11)
 ЗП БУФЕР-2
 СДА 64-17
 СЧМР
 НТЖ Р2
 УИА ’13’(М16)
 ПЕ ОШБ
 УИА БУФЕР-2(М1)
 СЧ (М1)
 ИЛИ Е48П32
 НТЖ Е48П32
 ЗП (М1)
 СЧ Р3
 СДА 64+17
 СЧМР
 ИЛИ (М1)
 ЗП (М1)
 УИА 1(М2)
 ПВ ЗПСЛ(М11)
 СЧ Р
 СДА 64-17
 СЧМР
 НТЖ Р3
 УИА ’13’(М16)
 ПЕ ОШБ
 СЧ Р3
 ПВ ЧТСЛ(М11)
 ИЛИ Е31П15
 НТЖ Е31П15
 ЗП Р
 СЧ LLЛЛLL
 СДА 64-14
 ИЛИ Р
 ЗП (М1)
 ПВ ЗПСЛ(М11)
 УИА Р(М1)
 СЧ Р2
 ПВ ЧТСЛ(М11)
 ИЛИ СВОБ
 ЗП Р
 ПВ ЗПСЛ(М11)
 СЧ Р
 ПВ НАЙДИ(М11)
 ПО ИСКЛ
 ПБ НАШЛИ
******
ЧТСЛ ЗП LР
 СДА 64+10
 ЗП LР1
 ЗП АБС
 СЧ НУ
 СДА 64-12
 ЗП А38
 СЧ LР
 НТЖ ВСЕД
 ПО ТЧ10   - СМ:=ВСЕЕД -> КОНЕЦ:ЗАПИСАТЬ ВСЕ ЗОНЫ
 УИА -4(М14)
ТЧ СЧ ЛИСТЫ+4(М14)   - ПОИСК ЗАДАННОГО АДРЕСА В ОЗУ
 ПО ТЧ1
 НТЖ LР1
 НТЖ А38
 И L177
 ПО ТЧ8   - НАЙДЕН ЛИСТ
ТЧ1 ЦИКЛ ТЧ(М14)
 УИА -4(М14)
 УИА (М15)
 УИА (М13)
ТЧ2 СЧ ЛИСТЫ+4(М14)   - ПОИСК НЕЗАДЕЙСТВОВАННЫХ ЛИСТОВ
 ПО ТЧ3
 И L48
 ПЕ ТЧ4
 УИИ М13(М14)
 СЛИА -1(М13)
ТЧ4 ЦИКЛ ТЧ2(М14)
 УИИ М14(М13)
 СЛИА 1(М14)
 ПИНО ТЧ3(М13)
ТЧ10 УИА -4(М13)
ТЧ5 СЧ ЛИСТЫ+4(М13)
 И L48
 ПО ТЧ6
 ИЛИ ЛИСТЫ+4(М13)
 ИЛИ L40
 НТЖ L40
 НТЖ L48
 ЗП LР2
 ПВ ЧТЕНИЕ(М15)
 СЧ L48
 НТЖ ЛИСТЫ+4(М13)
 ЗП ЛИСТЫ+4(М13)
ТЧ6 ЦИКЛ ТЧ5(М13)
 УИА (М14)
 СЧ LР
 НТЖ ВСЕД
СЫ ПЕ ТЧ3
 УИА -4(М13)
СЫР СЧ (0)
 ЗП ЛИСТЫ+4(М13)
 ЦИКЛ СЫР(М13)
 ЗП СТРОКА
 ЗП LF
 ПБ (М11)
ТЧ3 СЛИА 4(М14)
 СЧ LР1
 РЖА ’7’
 СЛ L1
 ВЧОБ ЧИСЗОН
 УИА ’10’(М16)   - АДРЕС ВЫХОДИТ ЗА ПРЕДЕЛЫ ФАЙЛА
 ПЕ ОШБ
 СЧИ М14
 СЛЦ БЛИСТ
 СДА 64-30
 ИЛИ L40
 ИЛИ LР1
 ЗП LР2
 СЧ НУ
 СДА 64-12
 ИЛИ LР2
 ЗП LР2
 ПВ ЧТЕНИЕ(М15)
 СЧ LР2
 ЗП ЛИСТЫ(М14)
 ПБ ТЧ9
ТЧ8 СЛИА 4(М14)
ТЧ9 СЧ LР
 И L1777
 УИ М15
 СЧИ М14
 СЛЦ БЛИСТ
 СДА 64-10
 ЗП LР
 МОД LР
 СЛИА (М15)
 СЧ (М15)
 ПБ (М11)
ЧТЕНИЕ Э70 LР2
 ПБ (М15)
ЗПСЛ СЧ ЛИСТЫ(М14)
 ИЛИ L48
 ЗП ЛИСТЫ(М14)
 УИИ М14(М2)
 УИИ М16(М1)
 УИИ М13(М15)
ЗПЧ СЧ (М16)
 ЗП (М13)
 СЛИА 1(М16)
 СЛИА 1(М13)
 СЛИА -1(М14)
 ПИНО ЗПЧ(М14)
 ПБ (М11)
******
НАЙДИ СЧИ М15  - М15:= ПРОИЗВОЛЬНЫЙ АДРЕС В ЛИСТЕ,
 ЗП LР       - ГДЕ ПРОИЗВОДИТСЯ ПОИСК МЕСТА
 И L1777
 НТЖ LР
 УИ М5  - НАЧАЛО ЛИСТА
 УИ М13
 СЧ (М5)   - 0-Е СЛОВО В ЛИСТЕ
 И Е24П1
 ЗП LР1
 И L1777
 ПО НРР
 НТЖ LР1
 ПО НОР
 ВЧ L1
 НТЖ L1777
 ПО НОР
НРР СЧ L1
 ЗП (М5)
 ЗП LР1
НОР СЧ 0
 ЗП ПОЛДЛ
 ЗП АДР
 СЛИА 1(М5)
 СЧИ М5
 ПБ ОЛD
ДР СЧ (М5)
 И СВОБ
 НТЖ СВОБ
 ПО ДЫРА   - НАЙДЕН ПУСТОЙ ЭЛЕМЕНТ
 СЧ 0
 ЗП ПОЛДЛ
 ЗП АДР
ЭЛЕМ СЧ (М5)
 И L37   - ДЛИНА ЭЛЕМЕНТА
 ЗП LР
 СЧИ М5
 СЛЦ LР
 УИ М5
ОЛD И L1777
 ВЧ LР1
 ПЕ ДР
 СЧ АДР
 ПЕ АД1
 СЧ LР1
 ЗП АДР
АД1 СЧ ЗПСЛОВ        ЧИСЛО ВСТАВЛЯЕМЫХ СЛОВ
 ВЧОБ L1777
 ВЧ АДР
 ПО КД
 СЧ 0
 ПБ (М11)
КД СЧ ЗПСЛОВ
 СЛ АДР
 ЗП LР
 НТЖ (М13)
 И Е14П1
 НТЖ (М13)
 ЗП (М13)
 СЧИ М13
 СЛЦ LР1
ВЫХВ УИ М5
 УИ М15
 СЧ АБС
 СДА 64-10
 ЗП АДР
 СЧИ М15
 И L1777
 ИЛИ АДР
 ПБ (М11)
ДЫРА СЧ (М5)
 И L37
 СЛЦ ПОЛДЛ
 ЗП ПОЛДЛ
 СЧ АДР
 ПЕ АД
 СЧИ М5
 ЗП АДР
АД СЧ ЗПСЛОВ
 ВЧОБ ПОЛДЛ
 ПЕ ЭЛЕМ
 МОД АДР
 УИА (М15)
 И L1777
 ПО НТ
 ИЛИ СВОБ
 МОД ЗПСЛОВ
 ЗП (М15)
НТ СЧ СВОБ
 ИЛИ ЗПСЛОВ
 ЗП (М15)
 СЧ АДР
 ПБ ВЫХВ
ПРОВ0 И Е24П1
 ЗП Р
 И L1777
 НТЖ Р
 УИА ’21’(М16)
 ПО (М11)
 ВЧ L1
 НТЖ L1777
 ПО (М11)
ОШБ СЧ ОШР+2
 СДА 64+16
 СДА 64-16
 ЗП ОШР+2
 СЧИ М16
 СДА 64-30
 РЗБ LП7
 ИЛИ ОШР+2
 ЗП ОШР+2
 СЧ ИНФСТА
 НТЖ НУ
 УИА ОШР(М16)
 ПЕ АВОСТ
 УИИ М17(М7)
 СЧ LПРОБ6
 ЗП ИНФСТ
 ПБ ОШРС
НЕТСТС УИА ОШР+1(М15)
 СЧИ М16
 ИЛИ L48
 Э50 ’202’
ОШРС УИА ОШР(М12)
 СЧ (0)
 ЗП ДЛНУ
 СЧ L48
 ЗП ИНФСТА
 ПБ СОNSUL
* КОНСТАНТЫ *
С КОНД Е’1.41’
С1 КОНД В’6400000000000001’
L174 КОНД В’174’
LF ЭКВ ’1000’
ВОПРОС КОНД В’137’
МАРШР КОНД В’2’   2 ЛОГ.МАРШРУТ ОП.ТЕРМ.
ЕП7 КОНД М6Ю’1’
ВЫДОП КОНД М24К’012200000’К’011000020’
ИР11 ЭКВ ’1001’
ИР5 ЭКВ ’1002’
L144 КОНД В’144’
ЗПСЛОВ ЭКВ ’1003’
ЧБАЙТ ЭКВ ’1004’
LР2 ЭКВ ’1005’
L141 КОНД В’141’
L177 КОНД В’177’
ВСЕД КОНД В’-1’
LПОЗЗ ЭКВ ’1006’
СИМ ЭКВ ’1007’
L200 КОНД В’200’
L175 КОНД В’175’
L140 КОНД В’140’
L77 КОНД В’77’
L37 КОНД В’37’
L17 КОНД В’17’
LLЛЛLL ЭКВ ’153’
ИР13 ЭКВ ’1033’
СТРОКА ЭКВ ’1010’
ХВОСТ ЭКВ ’1011’
ПОЛДЛ ЭКВ ’1012’
Е31П15 КОНД В’17777740000’
НУ ЭКВ ’1013’
ЕЕ15 КОНД М14В’1’
АДР ЭКВ ’1014’
LР1 ЭКВ ’1015’
LР ЭКВ ’1016’
LLД ЭКВ ’1017’
Р3 ЭКВ ’1020’
Р ЭКВ ’1021’
Е17П11 КОНД М12В’177’
С0 КОНД М42В’64’
ЧИСЗОН ЭКВ ’1022’
БЛИСТ КОНД В’4’
L40 КОНД М39В’1’
ЛИСТЫ ЭКВ ’1023’   ’1023’-’1027’
КШКАУС НОП
 КОНД М40П’А’М39Ю’1’
 КОНД М40П’В’М39Ю’1’
 КОНД М40П’С’М39Ю’1’
 КОНД М40П’D’М39Ю’1’
НО КОНД В’0006060606060600’     -- ’’ --
Е48П32 КОНД В’7777760000000000’
NСИСТ КОНД В’5077000000220123’
ПОЛЕ КОНД (4)В’0’
LЭ62 КОНД В’0000000000020000’   - ЛИСТ ДЛЯ ЧТЕНИЯ Э62 ’41’
ТЧК КОНД В’356’
Е16П1П КОНД М8В’377’В’377’
СПЕЦ КОНД В’0010010000501741’    - СПЕЦ 00 - ВЫЗОВ ДИМОНА
ТАБЛТ НОП
 КОНД В’101000000’П’АЦ’
 КОНД В’102000000’П’ПИ’
 КОНД В’103000000’П’ГП’
 КОНД В’104000000’П’ПЛ’
LL ЭКВ ’1066’
ШИФБЮД ЭКВ ’1046’ - 20 ЯЧЕЕК
АБУФ КОНК В’00000000’
 КОНК А(ШКБЮД)
ИНФБЮД КОНД М42В’24’М41Ю’1’М24В’20’
ШКБЮД ЭКВ ’1045’  - 20 ЯЧЕЕК (1045-1065)
Р1 ЭКВ ’1030’
ОБЬЕМ ЭКВ ’1034’
БПУСТ КОНД П’ БУФЕР’
 КОНД П’ ПУСТ ’
 КОНД П’      ’
ПАРОБ КОНД П’ЕСТЬ П’
 КОНД П’АРОЛЬ ’
 КОНД П’!     ’
ОШ62 КОНД П’ОШИБКА’
 КОНД П’ Э62 4’
 КОНД П’1     ’
Р2 ЭКВ ’1031’
СКВ ЭКВ ’1037’
СДВГ КОНД В’0’
БУФЛИС КОНД В’0000020000000000’
ЧСВВ ЭКВ ’201’
МАСКА ЭКВ ’300’
БУФЕР ЭКВ ’111’
LПОДГ ЭКВ ’212’
LП7 КОНД П’777777’
L77777 КОНД В’77777’
Е14П1 КОНД В’37777’
СВОБ ЭКВ Е48П32
L2ПРОБ КОНД П’ 000  ’
ЭК07 КОНД В’0700000000000000’
Е24П19 КОНД М18В’77’
ОППОПП КОНД П’0  0  ’
L377L КОНД М24В’377’
L25 КОНД М24В’1’
L48 КОНД М47В’1’
LЗАК КОНД В’0’
ИНФ01 КОНД В’0’
ИМЯ КОНД (4)В’0’
L01L КОНД В’0100000000000001’
L04L КОНД В’0400000000000001’
L12L КОНД В’1200000000000000’
Р4 ЭКВ ’1040’
LПРОБ6 КОНД П’      ’
L7777 КОНД В’7777’
L1777 КОНД В’1777’
ИНФСТА КОНД В’0’    - НУ ОБЛ.СТАТИСТИКИ
ОШР КОНД П’ОШИБКА’
 КОНД П’ ФАЙЛА’
 КОНД П’ КО=00’
ИНФСТ КОНД В’0700000000000010’
 КОНД П’АRFА.П’
 КОНД П’ЕРЕХВА’
 КОНД П’Т00000’М32В’377’
L3 КОНД В’3’
АБС ЭКВ ’1032’
ДЛИНА ЭКВ ’1035’
ИНФСЛ ЭКВ ’1036’
ДИМОНН КОНД П’ДИМОН ’
ЕS КОНД П’0000∧0’
ЕТ КОНД П’0000Т0’
О41 КОНД В’-1’М40В’1’
УТИЛИТ ЭКВ ’1067’
КЗОН ЭКВ ’1041’
ЗОН ЭКВ ’1042’
LМИНС КОНД П’-00   ’
НУСМ КОНД П’    НУ’
L8СК КОНД П’000<8>’
ЕМИН КОНД П’000 0 ’
ДЛНУ ЭКВ ’1043’
ПЕРВСЛ КОНД В’0000020000040001’
КЛЮЧ КОНД П’КЛЮЧАР’
Е17П1 КОНД В’377777’
Е24П1 КОНД В’77777777’
*******
Д10Ч   КОНД В’5255567200000000’     ДЕЛИТЕЛЬ НА 10 ЧАС.
       КОНД В’5112771000000000’     1 ЧАС
       КОНД В’4756514000000000’     10 МИН.
       КОНД В’4613560000000000’     1 МИН.
       КОНД В’4457500000000000’     10 СЕК.
       КОНД В’4314400000000000’     1 СЕК.
П24 КОНД В’24’
LТОЧКА КОНД П’.0000.’
LВЫВ КОНД М24К’112300000’К’111000000’
L39 КОНД М38В’1’
ОТКАЗ КОНД В’0120000200000000’
L377 КОНД В’377’
L1 КОНД В’1’
*
*        СВЯЗИ БЛОКОВ ОС      *
*
АВРАСП ВНЕШ АВРАСП,РАСПЕЧ,ВЫХОД
БОЭК1 ВНЕШ ВХД
В3В4 ВНЕШ ШКАНУ
ДИСП70 ВНЕШ ТБУФ,ТЗФСТР,ШЗНМБ,АВОС,П77777,ЗАНПРП
ДИСП70 ВНЕШ КРОМЕ,ГУС
ДИСП70 ВНЕШ Е1,Е2,Е6,Е14,Е18,Е19,Е26,Е28,(ЕПЕР,Е40),Е48
ДИСП70 ВНЕШ ТУСП,Е40П1,Е48П37,Е48П7,Е27
ДИСП70 ВНЕШ ВСЕЕД,УКАЗД,ЯЧНИА
ДИСП70 ВНЕШ (ЕПК,Е25),(ЕНИИАС,Е26),Е13
ДИСП70 ВНЕШ НЛОС
КАНАЛ ВНЕШ (ВХД1,ВХД),КАНАЛ
СОСТАВ ВНЕШ ЛИСОСТ
*КОНЕЦ ЧАСТИ

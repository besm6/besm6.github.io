 Б
ПРОЦЕС СТАРТ ’10000’
 УПОТР ПРОЦЕС(М7)
ИПЗ КОНД В’0’
 КОНД М41В’3’ РК
 КОНД (21)В’0’
 КОНД В’0’ НЕ ЗАНИМАТЬ
 КОНД (6)В’0’
 КОНД  В’714244’
 КОНД В’0’
 КОНД М15В’1’
 КОНД М47В’1’
НЗАДА ПАМ 1
R2 КОНД В’0’
 КОНД В’0’
СМ КОНД В’0’
РАБ ПАМ 1
R1 КОНД В’0’
ВХОД УИА 3
 ПО НОВЗК
 И П77
 УИ М1 НОМЕР ЗАДАЧИ МАТЕМАТИКА
 ЗП НЗАДА
 СЧ Е48-1(М1)
 ЗП СМ
  СЧ  Е48
  ЗП  33(М7)
 МОД ТУСП-1(М1)
 УИА (М2) АДРЕС ИПЗ МАТЕМАТИКА
 МОД 36(М2)
 УИА (М16)
 МОДА ЧЛИ
 УИА (М15)
 СЧИ М15
 ПВ ЗАПВЫХ(М15)
****************************************
*                                      *
*         ЭКСТРАКОДЫ ВЕТВЛЕНИЯ         *
*         Э53(12,15,16,20,21,)         *
*                                      *
****************************************
Э5312 СЧИ М16
 ВЧОБ ЧЭК
  ПЕ  ОШВЕТВ
 СЧ ШКП
 И Е48(М16)
 ЗП РАБ
 ПО ДШ
 СЧ 9(М2)
 И Е15
 УИ М10
 МОДА ДПОДЧ
 ПВ (М17)
 СЧИ М13
 ЗП LS
ДШ ЗП ОК
 СЧ (М2)
 МОДА ЭКСТР-1
 ПБ (М16)
ЭКСТР  УИА АВОС(М4)
 ПБ Э1122
 СДА 64-24
 ПБ АД12
 УИИ М4(М2)
  ПБ  Э5313 Э53 13
 УИА ’2003’
  ПБ  Э5314 Э53 14
 УИИ М17(М2)
 ПБ Э53     15
  УИА  ’2003’
  ПБ Э5316
 СЧ СОБЫТ(М2)
 ПБ Э5318   17
 УИИ М4(М2)
  ПБ  Э5320 Э53 20
 ЗП РАБ
  ПБ  Э5321
 УИА АТПОРТ(М4)
 ПБ Э1122
 ЗП ПОРТО
 ПБ Э23     23
  ЗП  0
 ПБ Э5320     24
Э25 УИА -МЧПЗ(М13)
 ПБ НАН   Э53  25
 УИА -МЧПЗ(М16)
  ПБ  Э5326 Э53 26
Э27 СЧ ШКОК
  ПБ  Э5327
Э5330 СЧ Е48-1(М13)
 ПБ А30    30
 СЧ Е48-1(М1)
 ПБ А31    31
 СЧ 9(М2)
  ПБ  Э5332  Э53 32
 СЧ ШГП
 ПБ А33    33
 СЧ 9(М2)
  ПБ  Э5334 Э53 34
 УИА -3(М5)
 ПБ Э5335   ПЕРЕДАЧА  ОЗУ
 УИА 1027    ДАЙ СОБЫТ.ПОДЧИН
  ПБ  Э5336
 УИА (М14)
  ПБ  Э5337  Э53 37
 УИА 1027    РАЗР СОБ
       ПБ    Э5340
       И     П7777
       ПБ    Э5341
 ЗП 0    АДР  42
 ПБ Э5313
 СЧ 9(М2)    АДР  43
 ПБ Э5343
 СЧ ШКАП     44
 ПБ Э5344
 ЗП R1     45
 ПБ Э5345  ОТ 3-Й ЗАДАЧИ
 ЗП РАБ
 ПБ Э25
 УИА 1(М14)
 ПБ Э5337
 УИА 2(М14)
 ПБ Э5337  А50
 УИА 3(М14)
 ПБ Э5337  А51
 СЧ Е48-1(М1)
 ПБ Э5352
*   ЧЭК   ПОСЛЕДНИЙ АДРЕС УМЕНЬ.НА 10,Т.Е.А-10
ЧЭК КОНД В’42’
Э5343 УИИ М17(М13)
 ПВ ЗАНПРП(М15)
 СЧ 9(М2)
 СЛИА АВОС(М4)
Э11223 И П77777
 ЗП СМ
 УИ М16
 УИА 1024
 СЧ (М16)
 УИА 1027
  СЧ (М4)
 И Е48П16
 ИЛИ СМ
 ЗП (М4)
НОР СЧ
  ПБ ЭКВЫХ
Э1122 СЛИ М4(М2)
 ПБ Э11223
Э5314 УИИ М4(М2)
 УИИ М13(М1)
Э5340 СЧ ЯЧК(М4)
 ИЛИ ЕКА
 ЗП ЯЧК(М4)
 ПБ ЗЗ
Э5318 И Е25
 УИА 2(М16)
 ПО ВЫЙ
 УИА ’2003’
 СЧ 33(М2)
 И Е22
 ПО ПП
 СЧ 0
 ЗП СОБЫТ(М2)
 СЧ АВОС(М2)
 И Е48П16
 ЗП АВОС(М2)
 УИА 3(М16)
 ПБ ВЫЙ
ПП НОП 0
 СЧ ШЗБУДП
 И Е48-1(М1)
 ПЕ ОТЛ
 УИИ М16(М1)
 УИА ППП(М17)
 СЧ Е6
 ПВ ТАЙМ(М15)
ОТЛ НОП 0
 СЧ ЯЧК(М2)
 ИЛИ ЕКА
 ЗП ЯЧК(М2)
 СЧ МШГП
 ИЛИ Е48-1(М1)
 НТЖ Е48-1(М1)
 ЗП МШГП
 УИА (М16)
 СЧ СОБЫТ(М2)
 СДА 64+24
 И СОБЫТ(М2)
  ПО ВЫЙ
 СЧ ШЗАВЕТ
 ИЛИ Е48-1(М1)
 ЗП ШЗАВЕТ
ВЫЙ СЧИ М16
 ПБ ЭКВЫХ
А33 ПАМ 0
 И Е48-1(М13)
 ПО НВ1
 УИА ’2003’
 СЧ ОСПОД
 И Е48-1(М13)
 ПО ЧЛИ
 ИЛИ МШГП
 ЗП МШГП
 СЧ Е48-1(М13)
 НТЖ ВСЕЕД
 ЗП РМР
 И ПОДЧ(М2)
 ЗП ПОДЧ(М2)
 СЧ ОСПОД
 НТЖ Е48-1(М13)
 ЗП ОСПОД
 УИА ЭК74(М16)
 СЧИ М16
 ЗП 3(М15)
 СЧ 5(М15)
 И Е48П16
 ИЛИ П2007
 ЗП 5(М15)
 УИА ’2003’
 СЧ ШКОК
 ИЛИ Е48-1(М13)
 ЗП ШКОК
 СЧ 0
 ЗП 34(М15)
 ЗП СОБЫТ(М15)
 ЗП МАСГЛ(М15)
 СЧ ШПЭГ(М15)
 И Е48П27
 ЗП ШПЭГ(М15)
 СЧ ШЗАВЕТ
 И РМР
 ЗП ШЗАВЕТ
 СЧ ШЗБУДП
 И РМР
 ЗП ШЗБУДП
 СЧ Е33
 ИЛИ Е16П32
 И АВОС(М15)
 ЗП АВОС(М15)
 СЧ 36(М15)
 И Е41П1
 ИЛИ Л177
 ЗП 36(М15)
 ПБ НОР
НВ1 СЧ Е2
 ПБ ЭКВЫХ
Э5332 УИ М16
 УИА 0
 СЧ (М16)
 УИА 3
 ЗП РАБ
 ПБ ЛИК
АД12 УИИ М4(М2)
 УИИ М13(М1)
АДЫ ЗП РАБ
 СЧ Е48П25
Э5312А УИА ’2003’
 И СОБЫТ(М4)
 НТЖ СОБЫТ(М4)
 ИЛИ РАБ
ЗАН ЗП СОБЫТ(М4)
  УИА 3
ЗЗ СЧИ М13
 МОДА НОР
 УИА (М16)
 ПБ ЗАПВЕТ
Э5334 УИ М16
 УИА 0
 СЧ (М16)
 УИА 3
 СДА 64-24
 ПБ АДЫ
Э53 И П77777
Э5315 ПЕ Э535А ВОЗВРАТ ИЗ ВЕТВИ В НОВ МЕСТО
 СЧ АВОС(М2)
Э535А УИ М16 АДРЕС ВОЗВРАТА
 СЧ 5(М2)
 И Е48П16
 ЗП Р1
 МОДА НЕТЛИС
 УИА (М15)
 СЧИ М15
 ПВ ЗАПВЫХ(М15)
     УИА -10(М4)
 УИА ’2000’
 СЧ -6(М16)
 УИА ’2003’
 И П2007
 УИА 36(М17)
 ПО НЕЭКС
 УИА 1024(М17)
 СЧ ЛПРОЦ
 ПВ УПРИ1(М10)
 УИА 3
 СЧИ М1
 ИЛИ Е48
 УМН П12
 СЧМР 64
 УИ М16
 СЛИ М16(М17)
 УИА -2(М4)
 УИА 2
 СЧ 1022(М17)
 УИА 3
 УИИ М17(М2)
 И Е48-1(М1)
 ПО ОШВЕТВ
ЕРЕ УИА 2
 МОДА 2(М4)
 СЧ (М16)
 УИА 3
 ЗП (М17)
 МОДА ЕРЕ
 ЦИКЛ (М4)
 УИА 2
 СЧ 3(М16)
 УИА 3
 И Е24П1
 ЗП (М17)
 УИА 2
 СЧ 3(М16)
 СДА 64+24
 УИА 3
 ЗП (М17)
 УИА -4(М4)
ПЕР2 УИА 2
 МОДА 4(М4)
 СЧ 4(М16)
 УИА 3
 ЗП (М17)
 МОДА ПЕР2
 ЦИКЛ (М4)
 УИА 2
 СЧ 9(М16)
 УИА 3
 ЗП 37(М2)
 ПБ НОВБЛ
НЕЭКС УИИ М17(М2)
 УИА -9(М4)
ПЕРСУМ УИА ’2000’
 МОДА -2(М16)
 СЧ (М4)
 УИА ’2003’
 ЗП (М17)
 МОДА ПЕРСУМ
 ЦИКЛ (М4)
 УИА 0
 СЧ -1(М16)
 УИА 3
 ЗП 37(М2)
 ПБ М
НОВБЛ УИИ М17(М1)
 ПВ ЗАНПРП(М15)
М СЧ 5(М2)
 И П77777
 ИЛИ Р1
 ЗП 5(М2)
 СЧ 36(М2)
 СДА 64+21
 СДА 64-21
 ЗП 36(М2)
 СЧ 6(М2)
 СДА 64+27
 ИЛИ 36(М2)
 ЗП 36(М2)
 УИА 1027
 СЧ ЯЧК(М2)
 ИЛИ ЕКА
 ЗП ЯЧК(М2)
 СЧИ М1
 ПВ ЗАПВЕТ(М16)
 СЧ 9(М2)
 И Е48П25
 ЗП ИНФСЛП(М2)
 СЧ 7(М2)
 СДА 64+24
 ИЛИ ИНФСЛП(М2)
 ЗП ИНФСЛП(М2)
 СЧ 5(М2)
 И П460
 ПО ВЫХ1
 МОД 3(М2)
 УИА (М16)
 УИА 0
 СЧ (М16)
 УИА 3
 ПБ ВЫХ1
Э5316 УИИ М4(М2)
Э5336 СЧ СОБЫТ(М4)
 И Е24П1
 ЗП РАБ
 НТЖ СОБЫТ(М4)
 ЗП СОБЫТ(М4)
 СЧ РАБ
Э5316А СДА 64-24
 ЗП РАБ
 СЧ АВОС(М4)
 И П77777
 ИЛИ РАБ
 ЗП РАБ
 СЧ ЯЧК(М4)
 И ЕКА
 СДА 64+СДКА
 ИЛИ РАБ
 ИЛИ Е16
 ПБ ЭКВЫХ
Э5327 И Е48-1(М1)
 ПЕ НЕТГЛ
 СЧ ИЗАХВ(М2)
 И Е27
 ПЕ НЕТГЛ
 УИА ’2003’
 СЧИ М1
 ИЛИ Е41   АВАРИЯ
ЖДУ МОДА Э5330
 УИА (М16)
 УИИ М13(М1)
 УИИ М4(М2)
 ПБ ЗАПВЕТ
А31 И ШКГР
 ПЕ ВХ31
МШ СЧ Е48-1(М13)
 УИА 1027
 И ОСПОД
 ПО ЧЛИ
 НТЖ ОСПОД
 ЗП ОСПОД
 СЧ МШГП
 ИЛИ Е48-1(М13)
 ЗП МШГП
 СЧ 36(М15)
 И Е41П1
 ЗП 36(М15)
 ПБ ЗЗ
Э5320 СЧ СОБЫТ(М4)
 СДА 64+24
 ПБ Э5316А
Э5344 ЗП Р1
 МОДА ПРАВА
 ПВ (М17)
 УИА 1027
 СЧ СОБЫТ(М4)
 ИЛИ Е21
 ПБ ЗАН
Э5321 УИИ М4(М2)
 УИИ М13(М1)
ЛИК УИА 1027
 И Е48
 ПО ИМ
 СЧ РАБ
 И Е24П1
 ЗП РАБ
 И МАСГЛ(М4)
 НТЖ МАСГЛ(М4)
 ЗП МАСГЛ(М4)
 СЧ РАБ
 И СОБЫТ(М4)
 НТЖ СОБЫТ(М4)
 ПБ ЗАН
НАН НОП 0
 СЧ ИЗАХВ(М2)
 И Е27
 ПЕ НЕТГЛ
 СЧ ШКРЗ
 И Е48+МЧПЗ(М13)
 ПО НАН1
 МОД ТУСП+МЧПЗ(М13)
 УИА (М12)
 СЧ ШИФРМ(М12)
 НТЖ (М2)
 ПО ЕСШИ
НАН1 МОДА НАН
 ЦИКЛ (М13)
НЕТГЛ СЧ Е1
 ПБ ЭКВЫХ
ЕСШИ СЛИА ЧПЗ(М13)
 СЧИ М13
 ЗП Р1
 СЧИ М1
 НТЖ Р1
 ПО НЕТГЛ
 СЧ (М2)
 ПО НЕТГЛ   НЕВЕРНО ЗАДАН ШИФР
 СЧ ПОДЧ(М2)
 И Е48-1(М13)
 ПЕ НЕТГЛ
 СЧ 36(М12)
 НТЖ СБОЙ
 И Л177
 ПО НЕТГЛ
 СЧ ШККЗ
 И Е48-1(М13)
 ПЕ НЕТГЛ
 СЧ Е48-1(М1)
 НТЖ ВСЕЕД
 ЗП Р1
 УИА -МЧПЗ(М16)
НАСЛ СЧ ШКРЗ
 И Е48+МЧПЗ(М16)
 ПО НАСЛ1
 МОД ТУСП+МЧПЗ(М16)
 СЧ ПОДЧ
 И Р1
 МОД ТУСП+МЧПЗ(М16)
 ЗП ПОДЧ
НАСЛ1 МОДА НАСЛ
 ЦИКЛ (М16)
 СЧ Е48-1(М1)
 ИЛИ ПОДЧ(М12)
 ЗП ПОДЧ(М12)
 СЧ ШКОК
 ИЛИ Е48-1(М1)
 НТЖ Е48-1(М1)
 ЗП ШКОК
 СЧИ М13
 ИЛИ Е35  НОВАЯ ПОДЧИН
 ПВ ЗАПВЕТ(М16)
 СЧ И(М2)
 И Н22П27
 ЗП И(М2)
 СЧИ М13
 СДА 64-21
 ИЛИ И(М2)
 ЗП И(М2)
 СЧ РАБ
 ПО НОР
 УИИ М13(М1)
 УИИ М4(М2)
 ПБ Э5330
Э23 И Е48
 ПО СР
ЭШ МОД (М2)
 УИА (М16)
 МОДА В2
 УИА (М15)
 СЧИ М15
 ПВ ЗАПВЫХ(М15)
 УИА 42(М15)
 СЛИА 1(М16)
 УИА
     СЧ (М16)
 СЧ -1(М16)
 УИА 3
 ЗП ПОРТО
СР СЧ ПОРТО
 СДА 64+24
 И П7777
 ЗП АН+2
 ЗП РАБ
 СЧ 0
 ЗП R1
 СЧИ М1
 ЗП R2
 СЧ ПОРТО
Э53230 И П377
 ЗП ПОРТО
    УИА 1(М12)  РАБОТА  С  ТП  ОТПРАВИТЕЛЯ
Э53231 И П377
    УИА 1(М15)   НЕТ НОМ ПОРТА
  ПО ОШ
    МОДА  АТП
    ПВ  (М13)
    УИИ  М5(М16)
    ПЕ  ОШ
*
  УИА ’2000’
  СЧ  (М5)
  УИА 3
 МОДА (М12)
 ЗП Н
    УИ  М6      АБУФ
  СДА 64+15
    И  П7777
    ЗП  LМ        МАХ   ДЛИНА  СООБЩЕНИЙ
*
    УИА  ’2000’
    СЧ  (М4)
    УИА 3
    И  Е48-1(М10)
    УИА 2(М15)   НЕТ ПО ИЛИ ПП
    ПО  ОШ
*
 МОД АТПОРТ(М2)
 УИА (М15)
    УИА  ’2000’
    СЧ  1(М15)
    УИА  3
    И  Е16
   УИА 8(М15)
  ПЕ АОШ        ВСЕ ПОРТЫ ЗАКРЫТЫ
*
    УИА  ’2000’
    МОДА  (М12)
   СЧ 1(М4)
    УИА  3
    И  Е48-1(М10)
    УИА 9(М15)
  ПЕ АОШ  ПОРТ  ЗАКРЫТ
*
    УИИ  М16(М6)
 МОДА ОШ
 УИА (М15)
 СЧИ М15
 ПВ ЗАПВЫХ(М15)
 УИА 7(М15)
 УИА 0
 СЧ (М6)
 УИА 3
 МОДА (М12)
 ЗП Н+2
  СЛИА 1(М16)
 МОДА (М12)
 СЧ Н
 И Е28
 ПО Х
 СЛИА 1(М16)
 СЛИА 1(М6)
*
Х СЧ LS
  МОДА Э232
  ПИО (М12)
*
  УИА ’2000’
  СЧ  1(М6)
  УИА 3
  ЗП Р3
  СДА 64+36
  ПЕ Э232
 СЧ LМ
Э232 ЗП LS
 СЛ РАБ
  ВЧОБ LМ
 ПО Э233
 УИА 6(М15)
 ПБ ОШ
Э233 МОДА Э239
  ПИО (М12)
 СЧ Р3
 СДА 64+24
 И П7777
 ЗП РАБ
    МОДА ШИФ
    УИА (М17)
    СЧ  ШИФРМ(М2)
    СЧИМ М2
    СЧИМ  М5
    СЧИМ  М6
    СЧМ
 УИА -МЧПЗ(М10)
 СЧ Р3
 СДА 64+16
 И П77  НОМ КАН
 ПО Э236
 УИ М10
 СЧ ШКРЗ
 И Е48-1(М10)
 ПО Э236
 МОД ТУСП-1(М10)
 УИА (М15)
 УИА 1024
 СЧ (М6)  ШИФР ПОЛУЧ
 УИА 3
 ПО Э237
 НТЖ ШИФРМ(М15)
 ПЕ НЕТШ
*
Э237 УИА ’2003’
 МОД ТУСП-1(М10)
    УИА  (М2)
  СЧИ М2
  НТЖ ММ2
  УИА 5(М15)
  ПО ОШ    ПЕРЕДАЧА ВНУТРИ ОДНОЙ ЗАДАЧИ
 СЧ СМШГП
 И Е48-1(М10)
 ЗП ОС1
 НТЖ СМШГП
 ЗП СМШГП
    СЧИ М10
    ЗП  Р1    НОМ.  ЗАДАЧИ  ПОЛУЧАТЕЛЯ
 УИИ М17(М10)
 ПВ ЗАНПРП(М15)
  УИА (М12)
 СЧ ОС
 ИЛИ ОС1
 ЗП ОС
    СЧ Р3
    ПБ  Э53231     РАБОТА  ТП  ПОЛУЧАТЕЛЯ
Э236 СЧ ШКРЗ
 И Е48+МЧПЗ(М10)
 ПО Э236П
 МОД ТУСП+МЧПЗ(М10)
 СЧ ШИФРМ
 УИА 1024
 НТЖ (М6)
 УИА 3
 ПО Э236А
Э236П МОДА Э236
 ЦИКЛ (М10)
НЕТШ УИА 17(М15)   ЗАД НЕТ
 ПБ ОШ
Э236А СЛИА НПЗ+ЧПЗ(М10)
 ПБ Э237
Э239 УИА ’2000’
    СЧ  (М6)
    УИА  3
    ПО  Э2313
    НТЖ  ШИФ
    УИА  18(М15)
  ПЕ АОШ1  НЕ  ОТ ТОЙ  ЗАДАЧИ
Э2313 УИА ’2000’
    СЧ  1(М6)
    УИА  3
 ЗП Р2
    И  П377
 УИА 19(М15)  НЕ ТОТ ПОРТ
    ПО  Э2314
    НТЖ  ПОРТО
  ПЕ АОШ1  НЕ  ИЗ  ТОГО  ПОРТА
Э2314 СЧ Р2
 СДА 64+16
 И П77   НОМ КАН ОТПР
 ПО Э23140
 ЗП СМ
 СЧИ М1
 НТЖ СМ
 ПЕ АОШ1  НЕ ТОТ КАН
Э23140 СЧ Е48-1(М10)
 ЗП АН+1
 СЧ Р3
 И Е24
 ПЕ НЕ
 СЧ АН+1
 УИИ М14(М4)
    УИА  ’2000’
    ИЛИ  1(М4)
    ЗП  1(М4)    ЗАКРЫЛИ  ПРИЕМ
НЕ УИА 3
  СЧ  ШИФ
  УИА ’2000’
  ЗП (М6)
  УИА 3
  СЧ  LS
  СДА 64-36
 ЗП Р2
 СЧ R2 НОМ КАНАЛА ОТПРАВ
 СДА 64-16
 ИЛИ ПОРТО
 ИЛИ Р2
  УИА ’2000’
  ЗП 1(М6)
  УИА 3
*
  СЧ  LS
  ПО Э23В   ПЕРЕПИСЫВАТЬ НЕЧЕГО
  НТЖ П77777
  УИ М5  С-К ЦИКЛА
  СЛИА 1(М5)
 СЧ Н+1
 И Е28
 ПО У
 СЧ Н+3
 И П77777
 ПБ Й
У СЧ ММ6
 СЛЦ Е2
Й СЛЦ АН+2
 УИ М11
 СЧ Н
 И Е28
 ПО Z
 МОД Н+2
 УИА (М10)
 ПБ ZZ
Z УИИ М10(М6)
  СЛИА 2(М10)
ZZ МОД РАБ   А ОТН БП
 УИА (М4)
 СЛИ М10(М4)
  СЧИ М11
  СДА 64+10
  УИ М4
  СЧИ М10
  СДА 64+10
  УИ М3
  СЧ
  ЗП Р2
    МОДА СМЕНА
    ПВ (М13)
    МОДА СМЕН
    ПВ  (М13)
  СЧ Е1
  ЗП Р2
ПЕСО УИА 1024
 РЕГ 65
  СЧ (М11)
  ЗП (М10)
    УИА  3
*
    СЛИА  1(М10)
    СЧИ  М10
    И  П1777
    ПЕ  Э2315
  СЛИА 1(М3)
    МОДА СМЕН
    ПВ  (М13)
Э2315 СЛИА 1(М11)
    СЧИ М11
    И  П1777
  СЛИА 1(М5)
   ПЕ  Э2316
  СЛИА 1(М4)
    МОДА  СМЕНА
    ПВ  (М13)
Э2316 МОДА ПЕСО
  ПИНО (М5)
  МОДА Э23В
  УИА (М5)
ОСВОБ1 УИИ М17(М2)   ОСВОБОЖДЕНИЕ ЛИСТОВ
  СЧИ М3
    МОДА ОСВОБ
    ПВ  (М13)
  УИИ М13(М5)
       МОД   ММ2
       УИА   (М17)
  СЧИ М4
ОСВОБ НТЖ П37  ОСВОБОЖДЕНИЕ  ЛИСТОВ
    УИ  М16
       СЛИА  -1(М16)
    ПБ  ОСВОБЛ
Э23В УИА (М15)
 ПБ В2
АОШ МОДА ВЫХОШ1
  ПИНО (М12)
АОШ1 СЧ Е48-1(М1)
  СДА 64-31
  УИА ’2000’
  ИЛИ (М5)
  ЗП (М5) БЫЛ ЗАПРОС НА ПЕРЕДАЧУ
  УИА 3
ОШ МОДА В2
  ПИНО (М12)
   СЛИА ’100’(М15)
В2 МОДА В3И
  ПИНО (М15)
 СЧ Р3
 И Е24
 ПЕ В3
   СЧ Р1
  ИЛИ Е29
  ПВ ЗАПВЕТ(М16)
В3И СЧИ М15
 ПО В3
 И Е7
 ПО ВЫХОШ1
В3  МОД ММ2
  УИА (М2)
ВЫХОШ1 СЧ R1
 ПО ВЫХОШ2
 МОД R2
 УИА (М1)
 МОД АН
 УИА (М2)
ВЫХОШ2 СЧИ М15
 ЗП АН
 ИЛИ ОК
 ЗП ОК
 СЧ (М2)
 И Е48
 ПО ХХ
 УИИ М17(М1)
 ПВ ЗАНПРП(М15)
 МОД (М2)
 УИА (М15)
 СЧ АН
 УИА 0
 ЗП (М15)
 СЧ 1(М15)
 УИА 3
 ПО ХХ
 СЧ (М2)
 СЛЦ Е1
 ЗП (М2)
 СЧ R1
 ПО ЭШ
 ПБ П
ХХ СЧ ОК
  ПБ ЭКВЫХ
СМЕНА УИА 1(М12)
  МОД ММ2
 УИА (М6)
 МОД R2
 УИА (М17)
  ПБ СМЕНН
СМЕН УИА (М12)
 УИИ М6(М2)
 МОД Р1
 УИА (М17)
СМЕНН НОП 0
 ПВ ЗАНПРП(М15)
  УИА 3
  СЧИ М3(М12)
  СДА 64-10
  УИИ М17(М6)
  УИ М16
  МОДА СМНОШ
 УИА (М15)
 СЧИ М15
 ПВ ЗАПВЫХ(М15)
 УИА 1024
  СЧ 13(М16)    ВЫЗОВ ЛИСТА
  УИА ’2003’
*
  СЧИ М3(М12)
  НТЖ П37
  УИ М16
  СЧ  КОПРЗ(М17)
  ИЛИ Е48(М16)
  ЗП КОПРЗ(М17)
*
  СЧИ М3(М12)
  СДА 64+2
  НТЖ П7
  УИ М16
 МОДА ЯЧПРИ(М17)
  СЧ (М16)
  МОДА (М12)
 ЗП ЯЧПРИ(М7)    ПРИП
 МОД НЗАД
 УИА (М17)
 ПВ ЗАНПР(М15)
  УИИ М17(М6)
*
  СЧИ М10(М12)
  И П7777
  МОДА (М12)
  ИЛИ К1
  УИ М10(М12)   НОВЫЙ  МАТ  АДР
*
СМЕН4 СЧ Р2
  ПО (М13)
  СЧИ М3(М12)
  НТЖ П37
  УИ М16
  НТЖ П37
       ПЕ    ОСВОБЛ
       УИА   -1(М16)    ПЕРЕХОД  ЧЕРЕЗ  НОЛЬ
ОСВОБЛ СЧ Е48+1(М16)
  НТЖ ВСЕЕД
  УИА ’2003’
  И КОПРЗ(М17)
    ЗП  КОПРЗ(М17)  ОСВОБОДИЛИ  ПРЕДЫДУЩИЙ ЛИСТ
    ПБ  (М13)
СМНОШ СЧИ М3(М12)
 ВЧ Е1
 И П37
 УИ М3(М12)
 МОДА ОШ
 УИА (М5)
   УИА 7(М15)
 ПБ ОСВОБ1
К1  КОНД В’70000’  ПР. ТОГО ЧТО МШГ ПРИ ВЫХ. НЕ МЕНЯТЬ
  КОНД В’60000’
Э5345 И Е48
 ПО Э5
П МОД (М2)
 УИА (М16)
 МОДА ВЫХОШ2
 УИА (М15)
 СЧИ М15
 ПВ ЗАПВЫХ(М15)
 УИА 42(М15)
 СЛИА 1(М16)
 УИА
 СЧ (М16)
 СЧ -1(М16)
 УИА 3
 ЗП R1
Э5 СЧ R1
 СДА 64+40
 И П77 НОМ КАНАЛА ОТПРАВИТЕЛЯ
 УИА 40(М15)
 ПО ВЫХОШ2
 УИ М16
 СЧИ М1
 ЗП R2
 СЧИ М2
 ЗП АН
 УИА 1027
 СЧ ШКРЗ
 И Е48-1(М16)
 ПО ВЫХОШ2 НЕТ ЗАДАЧИ
 СЧ R1
 СДА 64+16
 И П77
 УИ М17
 УИА 104(М15)
 ПО ВЫХОШ2    НЕТ ПОЛУЧ
 СЧ ШКРЗ
 И Е48-1(М17)
 ПО ВЫХОШ2
 СЧ СМШГП
 И Е48-1(М17)
 ЗП ОС1
 СЧ СМШГП
 И Е48-1(М16)
 ИЛИ ОС1
 ЗП ОС1
 НТЖ СМШГП
 ЗП СМШГП
 СЧ ОС
 ИЛИ ОС1
 ЗП ОС
 УИИ М1(М16)
 МОД ТУСП-1(М16)
 УИА (М2)
 УИИ М17(М16)
 ПВ ЗАНПРП(М15)
 СЧ R1
 СДА 64+24
 И П377
 УИА 1(М12)
 МОДА АТП
 ПВ (М13)
 ПЕ ВЫХОШ1
 СЧ R1
 И Е24П1
 ЗП РАБ
 УИА 0
 СЧ (М16)
 УИА 3
 ЗП Н
 УИ М16
 МОДА ВЫХОШ1
 УИА (М15)
 СЧИ М15
 ПВ ЗАПВЫХ(М15)
  УИА 41(М15)
  СЧ Н
 И Е28
  ПО УХ
 СЛИА 1(М16)
 УИА
 СЧ -1(М16)
УХ УИА 1024
 СЧ (М16)
 УИА 3
 ПЕ ВЫХОШ1
 УИА 1024
 СЧ 1(М16)
 УИА 3
 СДА 64-24
 ПЕ ВЫХОШ1
 СЧ РАБ
 УИА 1024
 ИЛИ 1(М16)
 ЗП 1(М16)
 УИА 3
 СЧ 0
 ЗП АН+2
 ЗП РАБ
 СЧ R1
 СДА 64+24
 ПБ Э53230
*                 ПОИСК  АТП  ПО  НОМЕРУ  ПОРТА (НА  СМ)
*           ВХ: М2 - АИПЗ
*           ВЫХ: М16 - АП
*                М4 - АТП
*                 М10 - ОТНОСИТ.НОМ ПОРТА В ЭЛЕМЕНТЕ  ТП
*          ВХ:  М12 - 1-РАБОТА С ОТПРАВИТЕЛЕМ, 0-РАБ С ПОЛУЧАТЕЕЛ
*         ВЫХ: М15 - НОМЕР  ОШИБКИ
*              М14,М17 - РАБ
*            <СМ> = 0  -  ОШИБОК  НЕ  БЫЛО
АТП УИ М10
 МОДА АТПАВ
 УИА (М15)
 СЧИ М15
 ПВ ЗАПВЫХ(М15)
    СЧ  АТПОРТ(М2)
    УИА  3(М15)    АТП = 0
АТП3 И  П77777
    ПЕ  АТП1
АТПВ СЧ Е1
    ПБ  (М13)
АТП1  УИ  М16
 УИА 1024
  СЧ (М16)
 УИА 3
 УИ М17
    УИИ  М4(М16)
    СЧИ М10
    ВЧ  Е6     МИНУС  32
    ПЕ  АТП2
    НТЖ
    ПО АТП2
    УИ  М10
 СЧИ М17
    УИА  2(М15)    НЕТ ПО ИЛИ  ПП
    ПБ  АТП3
АТП2 СЛИ М16(М10)
 УИА 3
    СЛИ М16(М10)    УМН  НА  2
    СЛИ  М16(М12)    ПРИЕМ  ИЛИ  ПЕРЕДАЧА
    СЛИА 1(М16)
 УИА 1024
 СЧ (М16)
 УИА 3
 СЧ
 ПБ (М13)
АТПАВ УИА 4(М15)
 ПБ АТПВ
*                      У П Р А В Л Е Н И Е   П О Р Т А М И
*
*                           Э 5 3   0 0 4 1
*
*             РР. 10-12  НА  <СМ>:
*              0 - ПОРТ НА  ПРИЕМ  ОТКРЫТ  ( 1 - ЗАКРЫТ )
*              2 - ПОРТ  НА  ВЫДАЧУ  ОТКРЫТ ( 3 - ЗАКРЫТ )
*              4 - ВСЕ  ПОРТЫ  ОТКРЫТЫ  ( 5 - ЗАКРЫТЫ )
*
Э5341 УИ М10
    СДА  64+9
    УИ  М6
       И     Е3
  ПО Э41А
  УИА 1(М10)
Э41А СЧИ М10
    И  П377
  УИА (М12)
    МОДА АТП
    ПВ  (М13)
 ПЕ ВЫХОШ2
  УИА (М15)
  СЧИ М6
  И Е3
  ПО Э410
  УИА 33(М10)
  СЛИА -4(М6)
Э410 МОДА БРА
    ПБ  (М6)
БРА СЛИА 1(М4)
    ПБ  Э41ОТК
    СЛИА  1(М4)
    ПБ  Э41ЗАК
    СЛИА 2(М4)
    ПБ  Э41ОТК
    СЛИА  2(М4)
    ПБ  Э41ЗАК
Э41ОТК СЧ ВСЕЕД
    НТЖ Е48-1(М10)
    УИА  ’2000’
    И  (М4)
    ЗП  (М4)
  УИА 3
  СЧИ М6
  И Е2
 ПЕ ВЫХОШ2
  УИА ’2000’
  СЧ (М16)
  УИА 3
  И Е48П33
  ЗП Р1
  УИА ’2000’
  НТЖ (М16)
  ЗП (М16)
  УИА 3
  СЧ Р1
  СДА 64+31
ЕЩЕ ЗП Р1
  ПО ЭКВЫХ
  НЕД
  УИ М4
   ИЛИ Е44
  ПВ ЗАПВЕТ(М16)  ОТКРЫЛСЯ ПОРТ НА ПРИЕМ
  СЧ  Р1
 НТЖ Е48-1(М4)
  ПБ ЕЩЕ
Э41ЗАК СЧ  Е48-1(М10)
    УИА ’2000’
    ИЛИ (М4)
       ЗП    (М4)
  УИА 3
 ПЕ ВЫХОШ2
 ПБ ЭКВЫХ
ШУФ ПАМ 4
ПОРТО ПАМ 1
ШИФ ПАМ 1
ММ2  ПАМ 1
ММ5 ПАМ 1
ММ6 ПАМ 1
Е48П33 КОНД М32В’-1’
LS  ПАМ 1
LМ  ПАМ 1
ИМ СЧ РАБ
 И Е24П1
 И Н19
 ЗП РАБ
 ИЛИ МАСГЛ(М4)
 ЗП МАСГЛ(М4)
 СЧ РАБ
 ИЛИ СОБЫТ(М4)
 ПБ ЗАН
МСП СЧИ М13
 ПО НЕТГЛ
 ВЧ П60
 ПО НЕТГЛ
 СЧ ШКРЗ
 И Е48-1(М13)
 ПО НЕТГЛ
 СЧ ЕЗАР
 ИЛИ ШКГР
 ИЛИ НОМАРС
 И Е48-1(М1)
 ПЕ МСПАРХ
 СЧ ПОДЧ(М2)
 И Е48-1(М13)
 ПО НЕТГЛ
МСПАРХ МОД ТУСП-1(М13)
 УИА (М4)
 УИИ М15(М4)
 ПБ (М17)
ДПОДЧ СЧ (М2)
 И П77
 УИ М13
 СЧ ШКГР
 И Е48-1(М1)
 ПО ОБЫЧН
 УИА -15(М14)
СЛЗАДА СЧ ГЛГРУП+15(М14)
 НТЖ Е48-1(М1)
 ПЕ НЕТО
 СЧ ЗАГРУП+15(М14)
 И Е48-1(М13)
 ПЕ МСП
НЕТО МОДА СЛЗАДА
 ЦИКЛ (М14)
 ПБ НЕТГЛ
ОБЫЧН СЧ (М2)
 СДА 64+6
 ПО МСП
 СЧ ПОДЧ(М2)
ПРАВА И ШКАП
 ЗП Р1
 ПО НЕТГЛ  НЕТ ПОДЧ
 НЕД 0
 УИ М13
 СЧ ШКРЗ
 И Е48-1(М13)
 ПО ПРВ
 СЧ ТУСП-1(М13)
 УИ М4
 УИИ М15(М4)
 СЧ ШИФРМ(М4)
 НТЖ (М2)
 ПО (М17)  ЕСТЬ ПОДЧ
ПРВ СЧ Р1
 НТЖ Е48-1(М13)
 ПБ ПРАВА
А30 ПАМ 0
 УИА ’2003’
 ИЛИ МШГП
 НТЖ Е48-1(М13)
 ЗП МШГП
 СЧ Е48-1(М13)
 ИЛИ ОСПОД
 ЗП ОСПОД
 СЧ Л177
 ИЛИ 36(М4)
 ЗП 36(М4)
 СЧ ШКГР
 И Е48-1(М1)
 ПО НОР
 СЧ ШКЗАПР+15(М14)
 ИЛИ Е48-1(М13)
 ЗП ШКЗАПР+15(М14)
 ПБ НОР
НОВЗК СЧ ШЗАВЕТ
 ЗП СМ
НОВЗАК СЧ СМ
 И ШКАП
 ПО КОНОБР ОБРАБОТАЛИ КОГО СМОГЛИ
 НЕД
 УИ М1 НОМЕР ЗАКАЗЧИКА
 СЧ 0
 ЗП РАБ
 ЗП Р3
 СЧ СМ
 НТЖ Е48-1(М1)
 ЗП СМ
 СЧ ШГП
 И Е48-1(М1)
 ПО НОВЗАК
 МОД ТУСП-1(М1)
 УИА (М2) АДРЕС ИПЗ
 СЧ ЯЧК(М2)
 И ЕКА
 ЗП Р1
 ПО ЗПОЛ
 СЧ СОБЫТ(М2)
 СДА 64+24
 И СОБЫТ(М2)
 ПО ЗПОЛ
 МОД АВОС(М2)
 УИА (М3)
 УИИ М17(М1)
 ЗП Р3             Р3 ≠ 0 - БЫЛО СОБЫТИЕ ДЛЯ САМОЙ ЗАДАЧИ
 ПБ ПОХГЛ
ЗИГЛ СЧ МАСГЛ(М2)
 СДА 64-24
 И МАСГЛ(М2)
 ПО СЕС
 СЧ И(М2)
 СДА 64+21
 И П77
 УИ М4
 ИЛИ Е33
 ЗП РАБ            РАБ ≠ 0 - СОБЫТИЕ ДЛЯ ГЛАВНОЙ
 ПВ ЗАПВЕТ(М16)
 СЧ 36(М2)
 СДА 64+41
 ПЕ З
 СЧ 36(М2)
 ИЛИ Л175
 ЗП 36(М2)
З СЧ ОСПОД
 ИЛИ Е48-1(М1)
 ЗП ОСПОД
 МОД ШПЭГ(М2)
 УИА (М3)
 УИИ М17(М4)
ПОХГЛ УИИ М16(М3)
 МОДА НЕТЛИС
 УИА (М15)
 СЧИ М15
 ПВ ЗАПВЫХ(М15)
 СЛИА -11(М16)
 ПВ ЗАНПРП(М15)
   УИИ М17(М3)
 УИА 1027
  СЛИА  -11(М17)
 УИА -9(М4)
 СЧ ВСЕЕД
 НТЖ Р1
 И ЯЧК(М2)
 ЗП ЯЧК(М2)
 СЧ 3(М2)
 И Е24П1
 ЗП 3(М2)
 УИА -3(М16)
ОБ МОДА 3(М16)
 СЧ 6(М2)
 И Е24П1
 МОДА 3(М16)
 ЗП 6(М2)
 МОДА ОБ
 ЦИКЛ (М16)
 СЧ ИНФСЛП(М2)
 И Е48П25
 ИЛИ 9(М2)
 ЗП 9(М2)
 СЧ МАСГЛ(М2)
 СДА 64-24
 ИЛИ 8(М2)
 ЗП 8(М2)
 СЧ СОБЫТ(М2)
 ИЛИ МАСГЛ(М2)
 И Е19
 СДА 64-8
 ИЛИ 6(М2)
 ЗП 6(М2)
 СЧ РАБ
 ПО ЕЩЕОСН
*
*
 СЧ МАСГЛ(М2)
 И Е48П25
 ЗП МАСГЛ(М2)
ЕЩЕОСН СЧ ИНФСЛП(М2)
 СДА 64-24
 ИЛИ 7(М2)
 ЗП 7(М2)
 СЧ 36(М2)
 СДА 64-27
 ИЛИ 6(М2)
 ЗП 6(М2)
 СЧ Р1
 СДА 64+СДКА-2
 ИЛИ 6(М2)
 ЗП 6(М2)
ЦПЕРЕП МОДА (М2)
 СЧ 9(М4)
 УИА 0
 ЗП (М17)
 УИА 3
 МОДА ЦПЕРЕП
 ЦИКЛ (М4)
 СЧ 37(М2)
 УИА 0
 ЗП (М17)
 СЧ -5(М3)
 УИА 3
 И Е27
 ПЕ ААА
 СЧ П2007
 УИА 0
 И -6(М3)
 ПО МИМ
ААА УИА 1027
 СЧ ЛПРОЦ
 ПВ УПРИ1(М10)
 УИА 1024(М4)
 СЧИ М1
 ИЛИ Е48
 УМН П12
 СЧМР 64
 УИ М17
 СЛИ М17(М4)
 СЧИ М3
 НТЖ АВОС(М2)
 И П77777
 ПО ВСЕ           БЫЛО СОБЫТИЕ ДЛЯ САМОЙ ЗАДАЧИ
* СОБЫТИЕ ДЛЯ ГЛАВНОЙ ЗАДАЧИ
 СЛИА 480(М17)
 СЛИА 1(М4)
ВСЕ СЧ Е48-1(М1)
 УИА 2
 ИЛИ 1022(М4)
 ЗП 1022(М4)
 УИА -2(М4)
ПЕРЕ УИА 3
 МОДА (М2)
 СЧ 2(М4)
 УИА 2
 ЗП (М17)
 МОДА ПЕРЕ
 ЦИКЛ (М4)
 УИА 3
 СЧ 3(М2)
 И Е24П1
 ЗП 3(М2)
 СЧ 4(М2)
 СДА 64-24
 ИЛИ 3(М2)
 УИА 2
 ЗП (М17)
 УИА -4(М4)
 УИА 3
ПЕР1 МОДА 4(М2)
 СЧ 5(М4)
 УИА 2
 ЗП (М17)
 УИА 3
 МОДА ПЕР1
 ЦИКЛ (М4)
 СЧ 37(М2)
 УИА 2
 ЗП (М17)
МИМ НОП 0
 УИА 1027
 СЧ РАБ
 ПО ЗПОЛ          БЫЛО СОБЫТИЕ ДЛЯ САМОЙ ЗАДАЧИ
 ПБ СЕ
Э5326 СЧ Е48-1(М1)
 НТЖ ВСЕЕД
 ЗП Р1
 УИА 1027
ОЧИСТ СЧ ШКРЗ
 И Е48+МЧПЗ(М16)
 ПО ОЧИ
 МОД ТУСП+МЧПЗ(М16)
 СЧ ПОДЧ
 И Р1
 МОД ТУСП+МЧПЗ(М16)
 ЗП ПОДЧ
ОЧИ МОДА ОЧИСТ
 ЦИКЛ (М16)
 СЧ И(М2)
 И Н22П27
 НТЖ И(М2)
 ЗП РМР
 НТЖ И(М2)
 ЗП И(М2)
 СЧ 0
 ЗП МАСГЛ(М2)
 СЧ ШПЭГ(М2)
 И Е48П27
 ЗП ШПЭГ(М2)
 СЧ 36(М2)
 И Е41П1
 ЗП 36(М2)
 СЧ Е48-1(М1)
 ИЛИ ШКОК
 ЗП ШКОК
 СЧ Е48-1(М1)
 ИЛИ МШГП
 ЗП МШГП
 СЧ РМР
 СДА 64+21
 И П77
 ПО ЭКВЫХ
 ИЛИ Е35
 ПВ ЗАПВЕТ(М16)
ЭКВЫХ ЗП (М2)
 СЧ Е48-1(М1)
 ПВ ВШГП(М15)
ЖДИ СЧ 33(М7)
 НТЖ Е48
 СЛЦ 33(М2)
 ЗП 33(М2)
ВЫХ СЧ ОС
 ИЛИ СМШГП
 ЗП СМШГП
 СЧ 0
 ЗП ОС
 СЧ Е35
 ПБ ОТКАЗН
Э5313 УИА 1027
 СЧ ЯЧК(М4)
 ИЛИ ЕКА
 НТЖ ЕКА
 ЗП ЯЧК(М4)
 ПБ ЭКВЫХ
ЗПОЛ СЧ ШКОК
 И Е48-1(М1)
 ПО ЗИГЛ
 ПБ СЕС
СЕС СЧ Е48-1(М1)
 УИА 1027
 ИЛИ МШГП
 ЗП МШГП
СЕ СЧ Р3
 ПО НЕТВЕТ    НЕТ СОБЫТ.СОБСТ.
 СЧ АВОС(М2)
 ЗП 3(М2)
 ЗП 8(М2)
 СЧ 5(М2)
 И Е48П16
 ЗП 5(М2)   М27
 СЧ СОБЫТ(М2)
 ЗП (М2)
 И Е48П25
 ЗП СОБЫТ(М2)
НЕТВЕТ СЧ Е48-1(М1)
 НТЖ ШЗАВЕТ
 ЗП  ШЗАВЕТ
 ПБ НОВЗАК
КОНОБР УИА 1027
 ЗП SSЕСТЬ
 ПБ ВЫХ
ОШВЕТВ УИА 36(М17)
 МОДА ВЫХ1
 УИА (М16)
 УИА ’2003’
 ПБ АВВЫХ
НЕТЛИС МОДА НОВЗАК
 УИА (М16)
 УИА 1027
 СЧ ШЗАВЕТ
 ИЛИ Е48-1(М1)
 НТЖ Е48-1(М1)
 ЗП ШЗАВЕТ
 СЧ ЯЧК(М2)
 ИЛИ ЕКА
 НТЖ ЕКА
 ЗП ЯЧК(М2)
 УИА 58(М17)
АВВЫХ СЧ МШГП
 ИЛИ Е48-1(М1)
 ЗП МШГП
 СЧ Е48-1(М1)
 ПВ ВШГП(М15)
АВАС УИИ М15(М2)
 ПБ АВОСТА
ЧЛИ СЧ Е3
 ПБ ЭКВЫХ
Э5335 СЧ 0
 ЗП (М2)
 ЗП ММ10
 СЧИ М1
 ЗП ММ1
 МОД 9(М2)
 УИА (М10)
 УИА 0(М16)        Ч.Л. -> ЧЛИ
 МОДА ЕСТЬЧЛ
 УИА (М15)
 СЧИ М15
 ПВ ЗАПВЫХ(М15)
ПЛИ УИА 1024
 МОДА 3(М5)
 СЧ (М10)
 УИА 3
 МОДА 3(М5)
 ЗП ШУФ
 МОДА ПЛИ
 ЦИКЛ (М5)
 УИА -3(М5)
ПЛИ1 МОДА 3(М5)
 СЧ ШУФ
 ПО ПОП1
 СДА 64+6
 МОДА 3(М5)
 ЗП ШУФ
 СЧМР
 ПО ПОП1
 СДА 64+42
 ЗП РАБ
 И Е6
 ПО ПОП1
 НТЖ РАБ
 ПВ УСТВЛ(М15)
 ЗП Н+1
 СЧ 0
 ЗП ММ2
 ЗП R1            R1 := 0 - ЛИСТ ВО 2-ОЙ ЗАДАЧИ ЕСТЬ
 СЧ ММ10
 ЗП ММ6
 СЧ РАБ
 И П37
*            1-АЯ ЗАДАЧА -Г.З. , 2-АЯ ЗАДАЧА - П.З.
ОБОРОТ НОП ,
 ЗП ММ5           ММ5 := N МАТ ЛИСТА 2-ОЙ ЗАДАЧИ
 НТЖ П37
 УИ М10
 СЧ ММ10
 НТЖ П37
 УИ М13
 УИА 1027
 СЧ 32(М2)
 И Е48(М13)
 ПО ПЛИ3          ЛИСТ В ОЗУ
 МОД ММ1
 УИА (М17)
 ПВ ЗАНПРП(М15)
 УИА 1(М16)         Ч.Л. -> НЕТЛГ
 СЧ ММ10
 СДА 64-10
 УИ М6
 УИА 1024
 СЧ 1(М6) ⊃¬DПОДКАЧКА
 УИА ’2003’
ПЛИ3 НОП
 СЧ ММ10
 И П3
 УИ М14 НОМ СТОЛБЦА ИПЗ
 СЧ ММ10
  СДА 64+2
 НТЖ П7
 УИ М6
 СЛИ М6(М2)
 СЧ 24(М6)
 И ПРИП(М14)
 ЗП Р1
 СБР ПРИП(М14)
 ЗП Р2
 СЧ 24(М6)
 НТЖ Р1
 ЗП 24(М6)
 УИИ М17(М10)
 ПВ УПРИ(М10)
 УИИ М10(М17)
 СЧ Р2
 СДА 64+38
 УИ М3
 СЧ Е41П1
 УИА 1026
 И  ТОП(М3)
 ЗП ТОП(М3)       УБРАЛИ N ЗАДАЧИ
 И ВИОЛПМ
 УИА 3
 ЗП Н             Н := ИНФОРМАЦИЮ О ЛИСТЕ 1-ОЙ ЗАДАЧИ
 СЧ ММ5
 И П3
 УИ М1
 СЧ Р2
 РЗБ ПРИП(М1)
 ЗП Р1
 СЧ ММ5
 СДА 64+2
 НТЖ П7
 УИ М12
 СЛИ М12(М4)
 УИА 1027
 СЧ 32(М4)
 И Е48(М10)
 ПО ПОЗУ4         ЛИСТ В ОЗУ
 СЧ ММ5
 СДА 64-10
 УИ М11
 МОД LS
 УИА (М17)
 ПВ ЗАНПРП(М15)
 УИА 3(М16)        Ч.Л -> НЛИСТ
 УИА ’2000’
 СЧ 1(М11)
 УИА ’2003’
ПОЗУ4 НОП
 СЧ 24(М12)
 СБР ПРИП(М1)
 ЗП СМ    ПР-КА ПОДЧ.ЗАД
 РЗБ ПРИП(М14)
 ИЛИ 24(М6)
 ЗП 24(М6)
 СЧ СМ
 СДА 64+38
 УИ М11
 ПВ УПРИ(М10)
 СЧ Е41П1
 УИА 1026
 И ТОП(М11)
 ЗП ТОП(М11)      УБРАЛИ N ЗАДАЧИ
 И ВИОЛПМ
 УИА 3
 ЗП Н+1           Н+1 := ИНФОРМАЦИЯ О ЛИСТЕ 2-ОЙ ЗАДАЧИ
НЛ2З НОП ,             НЕТ ЛИСТА 2-ОЙ ЗАДАЧИ
 СЧ Е5
 ПВ ЗАХЗАН(М15)
 УИИ М6(М4)        ССЫ ПОРТИТ М4
 УИИ М16(М1)       ИСКЛИС ПОРТИТ М1
 УИИ М15(М5)       ИСКЛИС ПОРТИТ М5
 УИИ М13(М2)       М13 := АДР.ИПЗ 1-ОЙ ЗАДАЧИ
 СЧ Н
 ПВ ИСКЛИС(М17)    ИСКЛЮЧЕНИЕ ЛИСТА ИЗ ЦЕП.1-ОЙ ЗАДАЧИ
 СЛИА 1(М11)
 УИИ М13(М6)       М13 := АДР.ИПЗ 2-ОЙ ЗАДАЧИ
 СЧ Н+1
 ПВ ИСКВЛ(М17)    ИСКЛЮЧЕНИЕ ЛИСТА ИЗ ЦЕП.2-Й ЗАДАЧИ
 И R1
 ПЕ СТ440         ЛИСТ ИСКЛЮЧИЛИ, А ЕГО БЫТЬ НЕ ДОЛЖНО
 СЧ R1
 ПЕ НЕТЛ2З
   УИИ М13(М2)       М13 := АДР.ИПЗ 1-ОЙ ЗАДАЧИ
 ПВ ССЫ(М17)      ВКЛЮЧЕНИЕ ЛИСТА В ЦЕП. 1-ОЙ ЗАДАЧИ
 СЧ ММ1
 ИЛИ ЕЗП
 СДА 64-ТОПНЗ
 ИЛИ Н
 УИА 2
 ЗП ТОП-1(М11)    СФОРМИРОВАЛИ ТОП 1-ОЙ ЗАДАЧИ
 УИА 3
НЕТЛ2З НОП ,
 УИИ М11(М3)
 СЛИА 1(М11)
 УИИ М13(М6)       М13 := АДР.ИПЗ 2-ОЙ ЗАДАЧИ
 ПВ ССЫ(М17)      ВКЛЮЧЕНИЕ ЛИСТА В ЦЕП. 2-ОЙ ЗАДАЧИ
 СЧ LS
 ИЛИ ЕЗП
 СДА 64-ТОПНЗ
 ИЛИ Н+1
 УИА 2
 ЗП ТОП-1(М11)
 УИА 3
 УИИ М1(М16)
 УИИ М5(М15)
 УИИ М4(М6)        ВОССТАНОВИЛИ ИР-Ы
 СЧ Е5
 ПВ ГАШЗАН(М15)
 СЧ ЯЧПРИ(М12)
 ИЛИ ПРИП(М1)
 НТЖ ПРИП(М1)
 ИЛИ Р1
 ЗП 24(М12)
 УИА 3
 СЧ ММ2
 ПЕ ОТНЛ
ПОП1 СЧ ММ10
 СЛЦ Е1
 ЗП ММ10
 И П7
 ПЕ ПЛИ1
 МОДА ПЛИ1
 ЦИКЛ (М5)
 МОД ММ1
 УИА (М1)
 СЧ LS
 ИЛИ Е36
 ПВ ЗАПВЕТ(М16)
ВЫХ1 СЧ (М2)
 ПБ ЭКВЫХ
НЕЛПД СЧ Е48(М10)
 СДА 64+16
 ИЛИ Е48Е47
 ИЛИ (М2)
 ЗП (М2)
 ПБ ПОП1
ЕСТЬЧЛ МОДА ДЕШЧЛ
 ПБ (М16)
ДЕШЧЛ МОДА 0              М16=0
 ПБ ЧЛИ
 УИА 1027           М16=1
 ПБ НЕТЛГ
 МОДА 0             М16=2
 ПБ НЕЛПД
НЛИСТ УИА 1027          М16=3
 УИИ М16(М2)
 СЧ Е1
 ЗП R1            R1 := 1 - НЕТ ЛИСТА У 2-ОЙ ЗАДАЧИ
 МОДА ПОЗУ2
 ПВ (М17)
 УИИ М13(М10)
 УИИ М16(М4)
 ПВ УПРИ(М10)
 МОДА НЛ2З
 УИА (М17)
ПОЗУ2 СЧ Е48(М13)
 НТЖ 22(М16)
 ЗП 22(М16)
 СЧ Е48(М13)
 НТЖ 32(М16)
 ЗП 32(М16)
 СЧ Е48(М13)
 НТЖ ЛИСТМБ(М16)
 ЗП ЛИСТМБ(М16)
 ПБ (М17)
* ОТНЯТИЕ ЛИСТА У ПОДЧ ЗАДАЧИ
НЕТЛГ СЧ 32(М4)
 И Е48(М10)
 ПО НЕТЛГ1        ЛИСТ В ОЗУ
 МОД LS
 УИА (М17)
 ПВ ЗАНПРП(М15)
 УИА 2(М16)        Ч.Л. -> НЕЛПД
 СЧ ММ5
 СДА 64-10
 УИ М15
 УИА 0
 СЧ 20(М15)
НЕТЛГ1 УИА 3
 УИИ М15(М2)
 УИИ М2(М4)
 УИИ М4(М15)
 СЧ ММ10
 ПВ УСТВЛ(М15)
 ЗП Н+1
 СЧ Е1
 ЗП ММ2
 СЧ LS
 ЗП LМ
 СЧ ММ1
 ЗП LS
 СЧ LМ
 ЗП ММ1
 СЧ ММ5
 ЗП ММ10
 СЧ ММ6
*            1-АЯ ЗАДАЧА -П.З. , 2-АЯ ЗАДАЧА - Г.З.
 ПБ  ОБОРОТ
ОТНЛ СЧ ММ6
 ЗП ММ10
 СЧ LS
 ЗП ММ1
 СЧ LМ
 ЗП LS
 УИИ М15(М2)
 УИИ М2(М4)
 УИИ М4(М15)
 ПБ ПОП1
Э5337 СЧ Е1
 ПВ ТБУФ(М15)
 СДА 64-8
 УИ М3
 СДА 64-2
 СЛЦ ВРОК
 СЛЦ НОММБ1
 ЗП ММ2
 УИИ М16(М3)
 ПБ ФИЗОБМ
Э5352 НОП
 И ЕЗАР
 ПО НЕТГЛ         КОЗ = 1,НЕ 20-Й КАНАЛ
 СЧ 15(М2)
 СДА 64-6
 ИЛИ ЛИСТУП
 ПВ ТБУФ(М15)
 УИА 3
 СДА 64-8
 УИ М4
 СДА 64-2
 ИЛИ Е19
 ЗП Р1
 УИИ М17(М1)
 ПВ ЗАНПРП(М15)
 МОДА ЧЛЭ52
 УИА (М15)
 СЧИ М15
 ПВ ЗАПВЫХ(М15)
 МОД 9(М2)
 УИА (М15)
 СЛИА ИНФАРХ-ДНЕПР(М4)
 УИА 0(М3)         КОЗ = 0, НОРМА
ПЕРЕ52 СЧ 0(М2)
 ПО ЗАПВЛУ
 СЧ (М4)
 УИА 1024
 ЗП (М15)
 УИА 3
Э52А СЛИА 1(М15)
 СЛИА 1(М4)
 НТЖ ВСЕЕД
 ПО ВЫХ52
 СЧИ М4
 И П1777
 ПЕ ПЕРЕ52
 УИА 2(М3)         КОЗ = 2, ИНФ.>ОТВЕД.МАССИВА
ВЫХ52 СЧ 15(М2)
 СДА 64-42
 ИЛИ Р1
 ПВ ТБУФ(М15)
 СЧИ М3
 ПБ ЭКВЫХ
ЧЛЭ52 УИА 3(М3)         КОЗ = 3, ИНФ.АРХИВА В Ч.Л.
 ПБ ВЫХ52
ЗАПВЛУ УИА 1024
 СЧ (М15)
 УИА 3
 ЗП (М4)
 ПБ Э52А
Е48П27 КОНД М26В’-1’
СБОЙ КОНД М41В’101’
ВРОК КОНД М17В’1’В’23’    4 ТР.3СЕКТ
Н22П27 КОНД В’-1’М21В’77’
Р1 КОНД В’0’
Р2 КОНД В’0’
Р3 КОНД В’0’
ВОЗ СЧ ММ2
 ПВ ТБУФ(М15)
 СЧ Р1
 ПБ (М13)
ММ10 КОНД В’0’
ММ1 КОНД В’0’
ОК КОНД В’0’
Е48П7 КОНД М6В’-1’
*     Е36-А24,Е35-А25..........
ШКП КОНД М18В’437754’
Н КОНД (4)В’0’
АН ПАМ 3
ОС ПАМ 1
ОС1 ПАМ 1
 М
 Е
БОЭК1 ВНЕШ ЗАПВЫХ,ССЫ,ИСКЛИС,ИСКВЛ,УСТВЛ
АВОСТ ВНЕШ СТ440
ГРУППА ВНЕШ ВХ31,ШКГР,ГЛГРУП,ЗАГРУП,ШКЗАПР
ДИСП70 ВНЕШ П1777,П7777,Н19,П37,ОСПОД,ЭК74
ДИСП70 ВНЕШ ТБУФ,П6,НОММБ1,ЗАПВЕТ,ТУС,Е24П1,SSЕСТЬ
ДИСП70 ВНЕШ П3,П77777,П377,П17,МДР,ШЗАВЕТ,МШГ,ШГ,П7,ВСЕЕД
ДИСП70 ВНЕШ ГУС,КРОМЕ,ЭК46,Е48П16,АВОСТА,АВОС,ПОДЧ,Л175
ДИСП70 ВНЕШ СОБЫТ,ШКСВН,ШИФРМ,Е41П1,Е47П43,ПРИП,Е48Е47
ДИСП70 ВНЕШ ЗАНПРП,П2007,ШКОК,П177,Е48П37,И,П77,ШГП
ДИСП70 ВНЕШ СМШГП,МШГП,ШКАП,МИНЗ,МАКЗ,ШЗБУДП,ЧПЗ,ЕЗАР
ДИСП70 ВНЕШ НОММБ,ШККЗ,НЗАД,Е48П25,МЧПЗ,ТУСП,НПЗ
ДИСП70 ВНЕШ БИНОТ2,ШПЭГ,МАСГЛ,К2003,ИНФСЛП,Е16П32,КНАПР,К2013
ДИСП70 ВНЕШ ЛПРОЦ,НЗАРХ,ЯЧК,ЕКА,СДКА,ТАЙМ,П12,П60
ДИСП70 ВНЕШ (ЛИСТУП,П50),ШКЗЗК,ОТКАЗН,РМР
ДИСП70 ВНЕШ ЗАНПР,ЗАХЗАН,ГАШЗАН,НОМАРС,ЕЗП
ДНЕПР ВНЕШ ДНЕПР,ИНФАРХ
ВЗУ ВНЕШ ФИЗОБМ,ФИЗЗП,(ППП,П)
ВИСП ВНЕШ ВШГП
ДИСКИ ВНЕШ КТОМ,КТОМЛ
КИТ ВНЕШ (Л177,Е48П42),ТОПЛИС
 ВХОД Р1,Р2,Р3,ВОЗ,LS,ЖДИ,НЗАДА,ЭКВЫХ
 ВХОД ЖДУ,НВ1,НЕТГЛ,МШ,ЧЛИ
КАЧКА ВНЕШ Е48П17,ШКРЗ,П460,(R,R2)
СОСТАВ ВНЕШ ИЗАХВ,ТОП,ТБДТР,ТОПНЗ
СОСТАВ ВНЕШ АТПОРТ,КОПРЗ,ЯЧПРИ,ЛИСТМБ
ХЛАМ ВНЕШ УПРИ,УПРИ1
ГЕНС1 ВНЕШ ВИОЛПМ
СОСТАВ ВНЕШ МОДЛИ
         ФИНИШ ,    В ВАШЕМ ФИНИШЕ ОШИБКА ИЛИ ЕГО НЕТ

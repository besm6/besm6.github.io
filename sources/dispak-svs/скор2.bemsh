СКОР2 СТАРТ ’10000’
 Б
 Е
 М
 УПОТР СКОР2(М5)
 УИА 3
 СЧ 0
 ЗП ПРСК1-П(М7)  П У С К А Ч ---->С К О Р 2.
 ЗП КП
 НОП      *      *       *        *         *
 НОП      *
 НОП      *
 НОП    МОДУЛЬ ВЫЗОВА З-ЧИ ПО КЛЮЧУ. ЗАПИСЬ КЛЮЧЕЙ(ЗАП).
 НОП    И ПО ШИФРУ ЗАДАЧИ.
 НОП      *
 НОП      *
 НОП      *
 МОДА БУФ-П(М7)
 УИА (М11)
 СЧИ М11
 ЗП АН
 УИА -6(М14)      ДЛЯ  В С.
 УИА -5(М15)     ДЛЯ ШИФРА
 УИА (М10)
 СЧ БУФ-П(М7)
 ЗП Р-П(М7)
 НТЖ ШИФ-П(М7)
 СДА 64+24
 ПЕ АШ
 СЧ Ш1-П(М7)
 ЗП Ш-П(М7)
 СЧ КСП
 ЗП Р1-П(М7)
 ПБ ФПЗ-П(М7)
АШ СЧ БУФ-П(М7)
 СДА 64+24
 НТЖ ШИФ-П(М7)
 И Е24П1
 ПО ШИФР         ВЫЗОВ ПО ШИФРУ (ШИФР ХХХХХХ)
 СЧ БУФ+1-П(М7)
 ИЛИ БУФ+2-П(М7)
 ИЛИ БУФ+4-П(М7)
 ПЕ ДС
 СЧ ЗАДА-П(М7)
 ПЕ ВП-П(М7)
ДС ПАМ 0
 НОП    *
 НОП    *  *  *  ВЫЗОВ ЗАДАЧИ ПО КЛЮЧУ(КЛЮЧ)  *   *
 НОП    *
 МОДА ПС
 УИА (М13)
ПС МОДА ВС-П(М7)
 ПВ (М16)
 НТЖ П17
 ПО КС              КОНЕЦ КЛЮЧА
 НТЖ П17
 НТЖ П377
 ПЕ ПСА
 УИА 1(М4)
КС СЛИА ПСА-ПС(М13)
 УИА 1(М10)
 СЧ П17
 ЗП СИ-П(М7)  ПРОБЕЛ
ПСА СЧ Р1-П(М7)
 СДА 64-8
 ИЛИ СИ-П(М7)
 ЗП Р1-П(М7)
 МОДА (М13)
 ЦИКЛ (М15)
 МОДА ПСБ
 ПИНО (М10)       БЫЛО ДОПОЛН.ПРОБЕЛОМ
 МОДА ВС-П(М7)
 ПВ (М16)
 НТЖ П17
 УИ М4
ПСБ МОДА ПС1        НЕТ ШИФРА (ОДИН КЛЮЧ)
 ПИНО (М4)
 УИА -4(М15)
 МОДА ВШ            ЗА ШИФРОМ
 ПВ (М13)
ПС1 СЧ ДЕЛ-П(М7)
 ПВ ФИЗОБМ(М16)    ЧТ.КЛЮЧЕЙ---->ЗОНА’675’
 УИА 3
 СЧ (М1)
 НТЖ ШИФ-П(М7)
 ПЕ НДС-П(М7)
 УИА -126(М11)
 УИА ТКЛЮ-П(М16)
 УИИ М12(М1)
ПДС СЧ 2(М12)
 НТЖ Р1-П(М7)
 ПЕ НЕТ
 СЧ ВР-П(М7)
 НТЖ ВСЕЕД
 ПО ДА
 МОД АРК-П(М7)
 СЧ 128
 НТЖ КЛЮСЭИ-П(М7)
 ПЕ ДА
 МОД АРК-П(М7)
 СЧ 128+127(М11)
 И КВАНТ-П(М7)
 ПО МШ
ДА СЛИА 127(М11)
 СЧ Р1-П(М7)     КЛЮЧ ЗАДАЧИ
 ЗП КЛЮЧП-П(М7)
*     ПРОВЕРКА
 СЧ 3(М12)
 И Е31
 ПО ДАДА   МОЖНО
 СЧ ШКОПТТ
 И Е48-1(М2)
 ПО ВЫД-П(М7)
*
ДАДА СЧ 3(М12)
ОТКРЫТ ПО ВЫД-П(М7)
 ЗП Р1-П(М7)
 СДА 64+24
 И П77
 ПЕ ЕСТЕРМ
 СЧ Р1-П(М7)
 СДА 64+8
 ПБ ОТКРЫТ
*
ЕСТЕРМ ЗП СИ-П(М7)
 НТЖ Е1
 ПО ОДИН-П(М7)     --> ОДНОСТАНОЧНАЯ СИСТЕМА
 УИА 1(М4)
 СЧ ВР-П(М7)
 СДА 64+24
 ПЕ Я3-П(М7)       --> МНОГОСТАНОЧНАЯ СИСТЕМА
** ОТКАЗ ПО РАСПИСАНИЮ **
 УИА ТРЖМ-П(М16)
МШ МОДА ВЫДАЧА-П(М7)
 ПВ (М13)
 МОДА ОСВ-П(М7)
 ПВ (М13)
 СЧ ПРСК1-П(М7)
 СДА 64-8
 ЗП ПРСК1-П(М7)
 ПО МШ1-П(М7)     --> НА ВЫХОД
 РЗБ ЛУ377-П(М7)
 СДА 64+16
 ИЛИ РТ
 ЗП БУФ-П(М7)
 УИА БУФ-П(М16)
 ПБ ВЫДАЧА-П(М7)
НЕТ СЛИА 2(М12)
 МОДА ПДС
 ЦИКЛ (М11)
 ПБ ВЫД-П(М7)
ШИФР СЧ Е1
 ЗП НЕВЫД
 МОДА ВС-П(М7)
 ПВ (М16)
 НТЖ П377
 УИА 6(М15)
 ПО ВЫД-П(М7)        ВЫДАЧА :’ОШИБ’
 МОДА ФЗП
 УИА (М13)
 СЧ СИ-П(М7)
 ВЧ П12
 ПО ШИФР              НЕ ЦИФРА
 УИА -4(М15)
ВШ1 ПАМ 0
 СЧ К-П(М7)
 ПЕ ФОРШИФ
 СЧ КП              <- ПАРОЛЬ
 СДА 64-8
 ИЛИ СИ-П(М7)
 ЗП КП
ВШ МОДА ВС-П(М7)    --> ВЫБРАТЬ ШИФР
 ПВ (М16)
 НТЖ П377
 ПО ВШ2             -->  КОНЕЦ ШИФРА
 СЧ СИ-П(М7)
 НТЖ ПР14
 ПО НП              -->  НА ПАРОЛЬ
 СЧ СИ-П(М7)
 ВЧ П12
 ПЕ ВШ1
 НТЖ 0
 ПО ДОПРАЗ-П(М7)  ДОП.РАЗДЕЛЫ ПАСПОРТА
ВШ2 НОП ,
 СЧИ М15
 СДА 64+2
 НТЖ П3
 ПО БТР       -->   В ШИФРЕ 4 ЦИФРЫ
 СДА 64+2
 ПО НЕОГЧЕ    -->   В ШИФРЕ < 4 ЦИФР
 НТЖ П3
 ПО НЕОГЧЕ
 СДА 64+2
 ПО БТР       ->    13 > Ч.Ц.Ш. > 4
НЕОГЧЕ СЧ К-П(М7)
 УИА НЕВШ-П(М16)
 УИА 2(М15)
 ПЕ НДС1-П(М7)
** БЛОК ТЕРМИНАЛЬНОГО РАСПИСАНИЯ **
* ПРОВЕРКА ВНЕ РАСПИСАНИЯ *
БТР СЧ ВР-П(М7)
 НТЖ ВСЕЕД
 ПО ТЕК-П(М7)  -> ВНЕ РАСПИСАНИЯ
 СЧ Ш-П(М7)
 СДА 64+40
 ПВ ИЗ10В8(М15)
 ЗП ОГ
 ИЛИ NТ-П(М7)
 ЗП NТ-П(М7)
 УИА -3+1(М16)
* ПРОВЕРКА ПРИОРИТЕТНЫХ ШИФРОВ *
ПРШ СЧ МАСПШ
 НТЖ Ш-П(М7)
 МОДА ТПШ-П(М7)
 НТЖ 3-1(М16)
 ПО ШИП
 МОДА ПРШ
 ЦИКЛ (М16)
* ПРОВЕРКА РАСПИСАНИЯ ТЕРМИНАЛА *
 СЧ ВР-П(М7)
 И Е8
 УИА ТРЖО-П(М16)
 ПО МШ
* ПРОВЕРКА РАСПИСАНИЯ ОТДЕЛА *
 СЛЦ ОГ
 МОДА IО-П(М7)
 ПВ (М15)
 МОДА ШКО-П(М7)
 И (М16)
 УИА ШИ-П(М16)
 ИЛИ ПУСТИ-П(М7)
 ПО МШ
* ПРОВЕРКА РАБОЧИХ МЕСТ *
 УИА -5+1(М15)
ПРМ МОДА ТРМ-П(М7)
 СЧ 5-1(М15)
РМ ЗП Р-П(М7)
 ПО ЦР0
 НТЖ NТ-П(М7)
 УИА ВАШ-П(М16)
 И П17777
 ПЕ НЕТО
 ЗП ПРСК1-П(М7)
 ПБ ТЕК-П(М7)
НЕТО УИ М17
 И П177
 ПЕ ЦР
 СЧИ М17
 НТЖ NТ-П(М7)
 СДА 64-33
 И Е48П41
 ИЛИ ПРСК1-П(М7)
 СДА 64+8
 ЗП ПРСК1-П(М7)
ЦР СЧ Р-П(М7)
 СДА 64+16
ЦР0 ПЕ РМ
 МОДА ПРМ
 ЦИКЛ (М15)
 СЧ ПРСК1-П(М7)
 ПО ТЕК-П(М7)
 ПБ МШ
ШИП СЧ ВСЕЕД
 ЗП ВР-П(М7)
 МОДА ТПШ-П(М7)
 СЧ 3-1(М16)
 ЗП Ш-П(М7)
 ПБ ТЕК-П(М7)
ПР ИЛИ Е36
 ЗП ШКИВ
 СЧ Р-П(М7)
 ПВ ФИЗЗП(М16)
 МОДА ВЫХ2-П(М7)
 УИА (М13)
 СЧ ЗАДА-П(М7)
 ПО ОСВ-П(М7)
 МОДА МОЛ-П(М7)
 УИА (М13)
 СЧ ВСЕЕД
 ЗП (М2)
 СЧ RR-П(М7)
 ПО ПИМ
 СЧ 0
 ЗП RR-П(М7)
 УИА 1027
 МОД ЗАДА-П(М7)
 УИА (М17)
 СЧ ШКОК
 И Е48-1(М17)
 ПО ОСВ-П(М7)
 МОД ТУСП-1(М17)
 УИА (М15)
 СЧ ПАК
 ИЛИ Е48-1(М17)
 ЗП ПАК
 СЧ 36(М15)
 И Е41П1
 ИЛИ Л173-П(М7)
 ЗП 36(М15)
 СЧ Е48-1(М17)
 НТЖ ВСЕЕД
 И МШГП
 ЗП МШГП
 ПБ ОСВ-П(М7)
ПИМ ПАМ 0
 СЧ Ф-П(М7)
 ПО ОСВ-П(М7)
 СЧИ М1
 СДА 64-2
 ИЛИ НОММЛ2
 ЗП Р-П(М7)
 СЧ 5(М12)
 И П1777
 СДА 64-24
 СЛЦ Р-П(М7)
 ЗП Р-П(М7)
 ПВ ФИЗЗП(М16)
 СЧ Ф-П(М7)
 ЗП ШИФРГЗ(М1)
 СЧ Р-П(М7)
 ПБ ЗПК0-П(М7)
НП ЗП К-П(М7)
 ПБ ВШ1
ПОДКЛ НТЖ Е31
 ПЕ БЗ
 СЧ ЗАДА-П(М7)
 ПЕ ОТЗ
ОТЗ МОДА ПОДКЛ
 УИА (М13)
 СЧ СИ-П(М7)
 НТЖ Е1
 ПО НЧТ-П(М7)
 СЧ ЯЧКАТ+7(М12)
 СДА 64+45
 НТЖ ГОД
 И П7
 ПЕ НЧТ-П(М7)
 СЧ ЯЧКАТ+7(М12)
 СДА 64+17
 И П77
 УИ М15
 СЧ ШКРЗ
 И Е48-1(М15)
 ПО ЗК            ЗАДАЧА НЕ ПРОШЛА БОП
 УИА 1027
 СЧ ТУСП-1(М15)
 УИ М6
 МОДА ОСВ-П(М7)
 УИА (М14)
 СЧ ТЕРЕЗА(М6)
 ЧЕД 0
 ВЧ СИ-П(М7)
 ПО ЗК
 СЧ ЗАДА-П(М7)
 ПЕ МЛ
 СЧ 36(М6)
 СДА 64+41
 НТЖ Е1
 НТЖ Е7
 ПО КГ
 СЧ ШККЗ
 И Е48-1(М15)
 ПЕ КГ
 СЧ ТЕРЕЗА(М6)
 ИЛИ Е48-1(М2)
 ЗП ТЕРЕЗА(М6)
 СЧ ШЗНКТ
 ИЛИ Е48-1(М2)
 ЗП ШЗНКТ
 СЧ ЧПТ(М6)
 И Е48П7
 ИЛИ СИ-П(М7)
 ЗП ЧПТ(М6)
 СЧИ М15
 СДА 64-20
 ИЛИ ТСЛ-1(М2)
 ЗП ТСЛ-1(М2)
 СЧИ М15
 ИЛИ Е36
 ПВ ЗАПВЕТ(М16)
МЛ УИИ М3(М15)
 СЧ ЯЧОК(М6)
 И ЕОК
 ПБ КИГ            БЫЛО : ПО КИГ 11.11.87 ШЕБАНОВ
 НТЖ ЯЧОК(М6)
 ЗП ЯЧОК(М6)
 СЧ 0
 ЗП (М6)
 СЧ Е48-1(М15)
 ПВ ВШГП(М15)
КИГ СЧ RR-П(М7)
 ПО КГ
 МОД ЗАДА-П(М7)
 УИА (М17)
 СЧ Е48-1(М17)
 НТЖ ВСЕЕД
 И МШГП
 ЗП МШГП
 СЧ Е48-1(М17)
 ПВ ВШГП(М15)
 УИИ М15(М3)
 МОД ТУСП-1(М17)
 УИА (М13)
 СЧ 36(М13)
 И Е41П1
 ИЛИ Л173-П(М7)
 ЗП 36(М13)
 СЧ ШКОК
 И Е48-1(М17)
 ПЕ ПУК
 СЧИ М17
 УИА -6(М16)
СР ИЛИ Е35(М16)
 ПВ ЗАПВЕТ(М16)
КГ УИА 3
 МОДА ВЫХ2-П(М7)
 УИА (М13)
 ПБ (М14)
ПУК УИА (М16)
 НТЖ ШКОК
 ЗП ШКОК
 СЧИ М15
 СДА 64-21
 ИЛИ И(М13)
 ЗП И(М13)
 СЧ Е48-1(М17)
 ИЛИ ПОДЧ(М6)
 ЗП ПОДЧ(М6)
 СЧ ОСПОД
 ИЛИ Е48-1(М17)
 ЗП ОСПОД
 СЧИ М15
 ПБ СР
ЗК СЧ ЗАДА-П(М7)
 ПЕ НЧТ-П(М7)
 МОДА ОСВ-П(М7)
 ПВ (М13)
 УИА ЖДИ1-П(М16)
 ПБ НДС0-П(М7)
БЗ СЧ ИНФ-П(М7)
 ПО ФЗ
 СЛИА -127(М11)
 УИИ М6(М11)
 СЧ ИНФ-П(М7)
 ЗП Р-П(М7)
 УИА (М4)
 ПБ ЧТ1-П(М7)
ФЗ МОДА ОСВ-П(М7)        ОСВОБОЖДЕНИЕ КАТАЛОГА
 ПВ (М13)
 ПБ ФПЗ-П(М7)
*
* ЭКСТРАКОД ФОРМИРОВАНИЯ ЗАДАЧИ ПО КЛЮЧЮ
*
ЭФЗ СЧИ М15
 Э50 ’7701’
 НТЖ 0
 ПО НЕВВД-П(М7)
 НТЖ П77777
 ПО НЕВВД-П(М7)
 НТЖ Е15П13
 ПО НЕВВД-П(М7)
ФЗП СЧ ЗАДА-П(М7)
 ПО ПРОВЕР
 СЧ СИН-П(М7)
 ПО ФЗ1   З-ЧА НЕ ПОДЧИНЕННАЯ
 СЧ Ш-П(М7)
 НТЖ БУФ+4-П(М7)
 ПО ФЗ1
 СЧ БУФ+4-П(М7)
 ЗП Ф-П(М7)
 ПБ ФЗ1
ПРОВЕР СЧ НЕВЫД
 ПО ФЗП1-П(М7)
 СЧ 0
 ЗП НЕВЫД
*
*
ФЗ1 МОДА ПОДКЛ1
 УИА (М13)
 УИА (М4)         ПОДКЛЮЧЕНИЕ ПО ШИФРУ
 АIF (NОТ ∧АС).АС7
 СЧ ВР1
 ПЕ МШ
.АС7 АNОР
 ПБ РСК-П(М7)
ПОДКЛ1 НТЖ Е31
 ПО НЧТ-П(М7)     З-ЧА В РЕШЕНИЕ.
 МОДА НЕВВД-П(М7)
 УИА (М13)
 ПБ ОСВ-П(М7)
ФОРШИФ СЛИА 4(М15)     <--- ШИФР
 СЧ СИ-П(М7)
 СДА 64-44(М15)
 ИЛИ Ш-П(М7)
 ЗП Ш-П(М7)
 ПБ ВШ
О45П40 КОНД М39В’77’В’-1’
Е15П13 КОНД М12В’7’
П17777 КОНД В’17777’
РТ КОНД П’0Т-00↑’
КСП КОНД П’СП’
МАСПШ КОНД Х’000088776655’
НЕВЫД КОНД В’0’
 АIF (NОТ ∧АС).АС5
ДИСП70 ВНЕШ ВР1,ВР2
ПРИОР КОНД Х’010700110000’
 КОНД Х’011100000001’
 КОНД В’0’
КТЛГ МОД РБ
 СЧ ПРИОР
 ПО (М6)
 НТЖ Ш-П(М7)
 ПО ОБН ШИФР СОВПАЛ
 СЧ РБ
 СЛЦ Е1  +1 К СЧЕТЧИКУ
 ЗП РБ
 ПБ КТЛГ НА ПРОСМОТР КАТАЛОГА
РБ КОНД В’0’
ОБН СЧ 0
 ЗП ВР-П(М7)
 ЗП ВР1
 ЗП ВР2
 ПБ (М6)
РАСП МОДА КТЛГ
 ПВ (М6)
РАСП1 СЧ ВР-П(М7)
 И Е48
 ПЕ МШ
 СЧ Ш-П(М7)
 НТЖ СЕРП
 ПО УЖЕ
 СЧ Ш-П(М7)
 НТЖ ДИА
 СДА 64+32
 ПО НШ НЕЛЬЗЯ ВЫЗЫВАТЬ С 4199
УЖЕ СЧ ШПЕР
 ИЛИ Е48+1(М2)
 ЗП ШПЕР
 ПБ ДА1-П(М7)
СЕРП КОНД Х’419988776655’
ШПЕР ВНЕШН ДИСП70.ШПЕР
РАС ТЕКСТ П’0РАСПИ’
 КОНД М8П’САНИЕ’В’021’
ДИА КОНД Х’419999000005’
.АС5 АNОР
ДИСП70 ВНЕШ ЗАНЯТА,НОММЛ1,НОММЛ2,ТУСП,ГОД,ТЕРЕЗА,П3
ДИСП70 ВНЕШ ТСЛ,ШГ,ЗАПВЕТ,ШКНТ,ШКИВ,НБТ,Е24П1,Е15П11
ДИСП70 ВНЕШ ВСЕЕД,П77777,П1777,П377,П77,П17,П12,П7
ДИСП70 ВНЕШ Е41П1,МШГП,ШКОК,ОСПОД,ПОДЧ,ШККЗ
ВИСП ВНЕШ ВШГП
ДИСП70 ВНЕШ ЯЧОК,ЕОК,И,ПАК,Е48П7
МОТТ ВНЕШ ШЗНКТ,ШКАС
ДИСКИ ВНЕШ ЕNQ,DЕQ
ПУСКАЧ ВНЕШ ФЗП1,ВП
ПУСКАЧ ВНЕШ ЗПК,ОСВ,Ф,Ш,СТОП,ПЛАН,ЖДИ1,БУФ,ВР,НДС1
ПУСКАЧ ВНЕШ (П,ПУСКАЧ),ОБЩ,Р,Р1,RR,ЗАДА,НДС,К,ПРСК1
ПУСКАЧ ВНЕШ СИ,ШИФ,СИН,РСК,ВС,ИНФКАТ,ВЫД,НЧТ,ВЫХ2
ПУСКАЧ ВНЕШ НЕВВД,НЕВШ,МОЛ,ИНФ,ЧТ1,ДА1
ВЗУ ВНЕШ ФИЗОБМ,ФИЗЗП
В1К ВНЕШ ШИФРГЗ
КАЧКА ВНЕШ ШКРЗ
 ЭКВИВ (СК2,СКОР2),(ЯЧКАТ,0)
 ВХОД АН,ПР,ПОДКЛ
 ВХОД ФЗ1,КП,ДС,КИГ
ПУСКАЧ ВНЕШ КЛЮЧП,ФПЗ,ДОПРАЗ
 ВХОД ВШ2,ЭФЗ
ПУСКАЧ ВНЕШ ТПШ,ТРМ,ШКО,I,IО,АРК,NТ,КВАНТ,ДЕЛ,ЛУ377,ЗПК0,ВАШ
ПУСКАЧ ВНЕШ КЛЮСЭИ,ВЫДАЧА,(Ш1,ШС),ТРЖО,ТРЖМ,ТКЛЮ,ПУСТИ,ШИ
ПУСКАЧ ВНЕШ МШ1,НДС0,ОДИН,ТЕК,Я3,Л173
ПУСКАЧ ВНЕШ З
КИТ ВНЕШ Е48П41
МОТТ ВНЕШ (ВСЕПР,ПРОБ6),ШКОПТТ
ДИСП70 ВНЕШ ПР14,П177,ИЗ10В8
СОСТАВ ВНЕШ ЧПТ
 ЭКВИВ (КП,СК2),(АН,СК2+1),(ОГ,СК2+2)
 ФИНИШ
*КОНЕЦ ЧАСТИ

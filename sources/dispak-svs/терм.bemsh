ТЕРМ СТАРТ ’7344’
 Е
 М
*
*
*** Б Л О К   П О Д Г О Т О В К И   М А С С И В А ***
**** Д Л Я   В Ы Д А Ч И   Н А   Т Е Р М И Н А Л ****
*
*
*      БЛОК ЯВЛЯЕТСЯ ОБЩИМ ДЛЯ   Б Н Б О Т   И   Э 7 1 Т К,
* ПРИЧЕМ ДЛЯ ПОСДЛЕДНЕГО ОБЕСПЕЧИВАЕТСЯ ПОВТОРНАЯ ВХОДИМОСТЬ,
* КОГДА ВЫДЕЛЕННОГО БУФЕРА НЕ ХВАТАЕТ ДЛЯ РАЗМЕЩЕНИЯ МАССИВА.
*      В КАЧЕСТВЕ РАБОЧИХ ИСПОЛЬЗУЮТСЯ ЯЧЕЙКИ 41
* МОДУЛЕЙ ( М7 ) .
*                    ВХОДНАЯ ИНФОРМАЦИЯ
*
* М2     - НОМЕР ТЕМИНАЛА
* М13    - АДРЕС НАЧАЛА ВЫДАВАЕМОГО МАССИВА / ТЕКУЩИЙ АДРЕС /
* М16    - АДРЕС ВЫХОДА
* 41(М7) - АДРЕС КОНЦА ВЫДАВАЕМОГО МАССИВА + 1
* 42(М7) - ДЛЯ ПЕРВОГО ВХОДА <42>
*          - АДРЕС ПРОДОЛЖЕНИЯ РАБОТЫ ( М17 )
* ТБЛ    - АДРЕС ЗАПОЛНЯЕМОГО РЕКОРДА
*
* КРОМЕ ЭТОГО ИСПООЛЬЗУЮТСЯ:
* М1  - АБСОЛЮТНЫЙ АДРЕС ЗАПИСИ В РЕКОРД
* М5  - СЧЕТЧИК СИМВОЛОВ В СТРОКЕ
* М6  - СДВИГ ПО  Т А Б К О Д
* М10 - СЧЕТЧИК СИМВОЛОВ В СЛОВЕ ИСХОДНОГО МАССИВА
* М12 - СЧЕТЧИК СИМВОЛОВ В СЛОВЕ РЕКОРДА
* М14 - РАБОЧИЙ
* М15 - ИСПОЛЬЗУЕТ ПРОЦЕДУРА ’ПОИСК’
* М17 - РАБОЧИЙ
*
*
*
*
*
****** НАСТРОЙКА ПО ТИПУ ТЕРМИНАЛА ******
*
ДЛЯЭ71 НОП
 УИА -5(М12)      М12 - НОМЕР СИМВОЛА В РЕКОРДЕ
 УИА ’200’(М6)     М6:= 200 - СДВИГ В ТАБКОД-Е ДЛЯ ЕС
 СЧ ШКАС
 И Е48(М2)
 ПО ЕС7920
 УИА 0(М6)
ЕС7920 НОП
 СЧ 42(М7)
 УИ М17
 ПЕ (М17)         НЕ ПЕРВЫЙ ВХОД
 УИА  0(М10)
 УИА -80(М5)       М5 - ЧИСЛО СИМВОЛОВ В СТРОКЕ
 ПИНО ОЧСИМВ(М6)    ЕС-7920
 СЧ ТСЛ(М2)
 И Е37
 ПЕ ОЧСИМВ        ТЕСТОВЫЙ РЕЖИМ
 УИА -4(М12)
 СЧ П377
 ЗП (М1)
 СЧ ШНР
 И Е48(М2)
 ПО НЕТВК
 СЛИА -1(М12)
 СЧ Е1
НЕТВК НОП
 ЗП 46(М7)        N ЯЧЕЙКИ С СИМВ.ДЛЯ ПЕРЕВОДА СТРОКИ
*
****** КОНЕЦ НАСТРОЙКИ ПО ТИПУ ТЕРМИНАЛА ******
*
*
ОЧСИМВ УИА ОЧСИМВ(М17)   М17:= ОЧСИМВ
 ЦИКЛ ОЧСИМ(М10)    СЛОВО НЕ ПЕРЕКОДИРОВАЛИ
 СЧИ М13
 НТЖ 41(М7)
 ПО КОНМАС        КОНЕЦ МАССИВА
 СЛИА 1(М13)
 УИА 0
 СЧ -1(М13)       НОВОЕ СЛОВО
 УИА 3
 УИА -5(М10)
 ЗП 43(М7)
ОЧСИМ СЧ 43(М7)        ОЧЕРЕДНОЙ СИМВОЛ
 СДА 64-8
 ЗП 43(М7)
 СЧМР
 ЗП 42(М7)
 УИ М14
 СЧ ШКВТ
 И Е48(М2)
 ПЕ СИМВТ
 СЧ ТСЛ(М2)
 И Е37
 ПЕ ТЕСТР         БЕЗ ПЕРЕКОДИРОВКИ
 СЧ 42(М7)
*
****** А Н А Л И З   С И М В О Л А ******
*
 СЛИА -’21’(М14)
 ПИО СТРЕЛА(М14)   021
 ВЧ ПОСЛС
 ПЕ УППКУ      ПЕРЕК
 РЖА 7
 ПО ОЧСИМВ
 СЧ 42(М7)
 СЛИА -’123’(М14)
 ПИО КПКОП(М14)    144
 СЛИА -2(М14)
 ПИО СULЕFТ(М14)   146
 СЛИА -’24’(М14)
 ПИО КМ(М14)       172
 СЛИА -3(М14)
 ПИО АТ(М14)       175
 СЛИА -’17’(М14)
 ПИО КС(М14)   214
 СЛИА -’163’(М14)
 ПИО С376(М14)     377
 СЛИА 1(М14)
 ПИО С376(М14)     376
 СЛИА 1(М14)
 ПИО ЕТХ(М14)      375(ЕТХ)
 СЛИА 1(М14)
 ПИО СТRLО(М14)   374(СТRLО=’21’-ДЛЯ LРD-400)
 СЛИА ’212’(М14)
 ПИО УППКУ(М14)    162
 СЛИА -5(М14)
 ПИО УППКУ(М14)    167
 СЛИА -1(М14)
 ПИО УППКУ(М14)    170
 СЛИА -1(М14)
 ПИО УППКУ(М14)    171
 СЛИА -5(М14)
 ПИО УППКУ(М14)    176
 СЛИА -1(М14)
 ПИО УППКУ(М14)    177
НДС СЧ НЕДОПС        НЕДОПУСТИМЫЙ КОД
 ПИО УППКУ2(М6)    ТЕРМИНАЛ  М П Д
 СЧ НЕДСЕС
 ПБ УППКУ2
*
*
*
АТ СЧ АТС           СИМВОЛ ’АТ’ ДЛЯ VТ-340
 ПИО УППКУ2(М6)    ТЕРМИНАЛ  М П Д
 СЧ АТСЕС
 ПБ УППКУ2
*
****** ПЕРЕХОД НА НОВУЮ СТРОКУ ******
*
КС СЧ 0            В РЕКОРДЕ ЕС-7920 БУДЕТ ’377’
 ПИНО ПРНОЛЬ(М6)    ЕС-7920
НОВСТР НОП
 ПИНО ОЧСИМВ(М6)    ЕС-7920
 УИА NL3(М17)
 МОД 46(М7)
 СЧ NLС
 ПИНО NL2(М5)       ПЕРЕХОД ПО < LF >
 СДА 64-8          ПЕРЕХОД ПО ЗАПОЛНЕНИЮ СТРОКИ БЕЗ LF
NL2 ЗП 44(М7)        М5:= -80
 УИА -80(М5)
*
NL3 СЧ 44(М7)        ЗАПИСЫВАЕМ NL
 ПО ОЧСИМВ        NL ЗАПИСАНА
NL3А СДА 64-8          ЕЩЕ НЕ ВСЕ
 ЗП 44(М7)
 СЧМР
 УИ М14
 МОДА (М14)
 КК ’046’,ТАБКОД(М5) ТОЛЬКО ДЛЯ ТЕР. М П Д
 СЧМР ,
 ПБ УППСЛУ
*
*
СТRLО УИА ’21’(М14)
 ПБ ОБЩ   СТRLS НА LRD-400
ЕТХ УИА 3(М14)
 ПБ ОБЩ     ЕТХ
КПКОП УИА 5(М14)
 ПБ ОБЩ     СТRL,Е
СULЕFТ СЛИА -2(М5)
 УИА ’210’(М14)
ОБЩ ПИНО ОЧСИМВ(М6)    М5=М5-2+1
 СЛИА 1(М5)
 СЧИ М14
 ПБ ПРНОЛЬ
*
ТЕСТР НОП ,            ТЕСТОВЫЙ РЕЖИМ
 ПИНО ЗС(М14)       СИМВОЛ НЕ НУЛЕВОЙ
 ПИО КМ(М6)        ТЕРМИНАЛ МПД
 СЧ ТСЛ(М2)
 И Е40
 ПЕ КМ            ВЫДАЧА ПРИГ.НА ЕС-7920
 ПБ ЗС            ВЫДАЧА НА ЕС-7920 (ОКОНЧАНИЕ ПО L)
*
****** ПЕРЕВОД ИЗ КОДА УПП В КОД ТЕРМИНАЛА ******
*
УППКУ МОД 42(М7)
 МОДА (М6)
 КК ’046’,ТАБКОД(М5)
 СЧМР ,
УППКУ2 СЛИА 1(М5)         М5:= <М5> + 1 ( СЧЕТНЫЙ ВХОД )
 УИА ОЧСИМВ(М17)   М17:= ОЧСИМВ
УППСЛУ НОП ,
 ЗП 45(М7)
 СДА 64+40
 ЗП 42(М7)
 УИ М14
 СЧ ШНР
 И Е48(М2)
 ПО СЧ42
 СЧ 45(М7)
 И Е40
 ПЕ ПЕРЕКЛ        ЕСТЬ 7 Р. В ТАБКОД-Е
*
 СЧ ШVD130
 И Е48(М2)
 ПО НЕVDТ       ЭЛЕКТРОНИКА
СЧ42 НОП
 СЧ 42(М7)
ПРНОЛЬ НОП
  ПЕ СГ
 НТЖ П377
***************************
СГ ЗП 42(М7)        СИМВОЛ ГОТОВ
*
****** ЗАПИСЬ СИМВОЛА НА БУФЕР ******
*
ЗС НОП
 СЧ (М1)
 СДА 64-8
 ИЛИ 42(М7)
 ЗП (М1)
 МОДА (М17)
 ЦИКЛ (М12)         СЛОВО НЕ ЗАПОЛНЕНО
 СЛИА 1(М1)         М1:= <М1> + 1
 УИА -5(М12)       М12:= -5
 МОД ТСЛ(М2)
 СЧ ТСВ
 СЛЦ Е10           +ЕЩЕ ОДНО СЛОВО ВЫДАЧИ
 МОД ТСЛ(М2)
 ЗП ТСВ
 СЧИ М1
 И П37
 ПЕ (М17)         РЕКОРД НЕ ЗАПОЛНЕН
 СЧИ М17
 ЗП 42(М7)
 ПБ (М16)         ЗА СЛЕДУЮЩИМ РЕКОРДОМ(ЭТК71)
*
СТРЕЛА СЧ ГУС
 НТЖ ТУС+38
 И Е48П42
 ПО УППКУ         ДЛЯ Э71 ↑ НЕ КОНЕЦ
*
*
С376 СЧ ШКОПТТ
 И ШКАС
 И Е48(М2)
 ПО КМ            НЕ ОПЕРАТОРСКИЙ АС-ТЕРМИНАЛ
 И ШЗНКТ
 ПЕ КМ
*
* ОПЕРАТОРСКИЕ ТЕРМИНАЛЫ, ПОДКЛЮЧЕННЫЕ ЧЕРЕЗ АС
*
 МОД 42(М7)
 УИА -’376’(М14)
 ПИНО ОТ76(М14)     НЕ ОСВОБОЖДАТЬ ЛИНИЮ ПОСЛЕ ВЫДАЧИ
 СЧ ТСЛ(М2)
 ИЛИ ЕОСВ          ЕОСВ -> ТСЛ
 ЗП ТСЛ(М2)
ОТ76 НОП
 МОД НОМАС
 КК ’046’,ТАБКОД(М5)
 СЧМР
 СДА 64+8
 ИЛИ СNБЭСМ
 УИА ОТ76А(М17)    ПРИФОРМИРОВАНИЕ N БЭСМ
 УИА -3(М14)
 ЗП 44(М7)
ОТ76А СЧ 44(М7)        ТРИ СИМВОЛА
 СДА 64-8
 ЗП 44(М7)
 СЧМР
 ЦИКЛ СГ(М14)
КМ НОП ,
 УИА Е45(М10)
 СЧ ТСЛ(М2)
 И ЕСЛВ
 ПЕ КМ2
 УИА (М10)
 ПИНО КМ2А(М6)      ЕС-7920
 СЧ ТСЛ(М2)
 И Е37
 ПЕ КМ2
 МОД 46(М7)
 СЧ NLС
 И Е48П25
 ПВ NL3А(М17)     ПОСЛЕДНИЕ СИМВОЛЫ
КМ1 СЧ 44(М7)
 ПЕ NL3А          ЗАПИСЬ КОНЕЧНОЙ КОМБИНАЦИИ
КМ2 НОП
 СЧ ШVD130
 И Е48(М2)
 ПЕ КМ2А
*ККККККККККККККККККККККККККККККККККККККККККККККК*
*                  ИЗ БОЭК1
*    НА ПРИЕМЕ РЕГИСТР ДОЛЖЕН
*    БЫТЬ РУССКИМ.
*
 СЧ ШНР
 И Е48(М2)
 ПО КМ2А
 УИА КМ2А(М17)
 СЧ ШЛАТР
 И Е48(М2)
 УИА SО(М14)
 ПО РУСРЕГ
 УИА П17(М14)
 СЧ ТСЛ(М2)
 ИЛИ ЕЛАТР
 ЗП ТСЛ(М2)
РЕГГ СЧ (М14)
 ПБ ПРНОЛЬ
РУСРЕГ СЧ ВСЕЕД
 НТЖ ЕЛАТР
 И ТСЛ(М2)
 ЗП ТСЛ(М2)
 ПБ РЕГГ
*                  ИЗ БОЭК1
*ККККККККККККККККККККККККККККККККККККККККККККККК*
*
КОНМАС НОП ,
 СЧ ШКВТ
 И Е48(М2)
 ПО КМ
 ПБ КМВТ
*
КМ2А НОП
 СЧ (М1)
 УИА 1027
КМ3 СДА 64-8          ЦИКЛ ДОДВИЖКИ ПОСЛЕДНЕГО СЛОВА
 ЦИКЛ КМ3(М12)
 ЗП (М1)
 УИИ М17(М2)
 СЧ ШЗПРИМ
 ИЛИ Е48(М17)
 НТЖ Е48(М17)
 ЗП ШЗПРИМ
 СЧ ШКОПТТ
 И ШКАС
 И Е48(М17)
 ИЛИ ШКОЛА
 ЗП ШКОЛА
 СЧ ТСЛ(М17)
 ЗП ТБЛ(М17)
 СЧ ШСТRLО
 И Е48(М17)
 УИА 0(М13)        М13 := 0 - ЗАКОНЧИЛИ ВЫДАЧУ
 ПЕ УСТВД2
 СЧ (М10)
 ЗП ВППС(М17)
 ПБ УСТВД2
*
*
НЕVDТ НОП
*
 СЧ 42(М7)
 ПО СЧ42        ПРОП."NUL"
*
 НТЖ П12          ПРОПУСТ."LF"
 ПО СЧ42
*
 НТЖ ПЕОТ
 ПО СЧ42        ПРОПУСТ.СИМВ."СR"
*
*
*------ БЛОК ПЕРЕКЛЮЧЕНИЯ РЕГИСТРОВ (ЭЛ-КА И НР2640С)
*
 СЧ 42(М7)
 ЗП 45(М7)
 И П140
 НТЖ П140
 ПО РУС
*       ПЕРЕКЛЮЧЕНИЕ НА ЛАТ.РЕГ.
 СЧ ТСЛ(М2)
 И ЕЛАТР
 ПЕ СЛВНР1      РЕГ.УЖЕ БЫЛ ЛАТИНСКИМ
 УИА П17(М14)
 СЧ ТСЛ(М2)
 ИЛИ ЕЛАТР
ЛАТ УИА 1027
 ЗП ТСЛ(М2)
 УИА 3
 УИА СЛВНР1(М17)
 МОДА НАДОLF
 ПИО (М5)
ЛАТ1 СЧ (М14)
 ПБ ПРНОЛЬ
*      ПЕРЕКЛЮЧЕНИЕ НА РУС.РЕГ.
РУС УИА SО(М14)
 СЧ ТСЛ(М2)
 И ЕЛАТР
 ПО СЛВНР1       РЕГ.УЖЕ БЫЛ РУССКИМ
 НТЖ ТСЛ(М2)
 ПБ ЛАТ
НАДОLF УИА СЛВНР2(М17)
 СЛИА 1(М5)
 ПБ ЛАТ1
*
*------  Е N D  ------*
*
*
ПЕРЕКЛ НОП
 УИА 0(М14)
 СЧ ШVD130
 И Е48(М2)
 ПО ЭЛЕКТР
 СЧ СИМЕSС
 ПВ СГ(М17)
 УИА 1(М14)
ЭЛЕКТР НОП
 СЧ 45(М7)
 СДА 64+33
 И П77
 УИ М17           М17 := 6-1 Р.Р. ТАБКОД-А
 СЧ СЛКОД(М14)
 СДА 64(М17)
 И П377
ВЫДСИМ УИА ОЧСИМВ(М17)
 ПБ ПРНОЛЬ
*
*      ПОСЛЕ ПЕРЕКЛЮЧ.РЕГИСТРА НАДО
*      НАДО ВЫДАТЬ И САМ СИМВОЛ
СЛВНР1 СЧ 45(М7)
 ПБ ВЫДСИМ
СЛВНР2 СЛИА -1(М5)
 ПБ СЛВНР1
*
* ЗАНЯТЫ СЛЕДУЮЩИЕ КОДЫ VIDЕОТОN-А, ГЕНЕРИРУЕМЫЕ С ПОМОЩЬЮ СТRL
*      005 - СТRL,Е
*      016 - СТRL,N
*      017 - СТRL,О
*      023 - СТRL,S
*      024 - СТRL,Т
*
*
*
***************************************************
*                                                 *
*        ’ П М В Ы Д ’ ДЛЯ ВИРТ.ТЕРМИНАЛОВ        *
*                                                 *
***************************************************
*
СИМВТ НОП ,
 СЧ ТСЛ(М2)
 И Е37
 ПЕ ТРВТ          ТЕСТОВЫЙ РЕЖИМ
* АНАЛИЗ СИМВОЛА ВИРТ.ТЕРМИНАЛА
 СЛИА -’21’(М14)
 ПИО СТРВТ(М14)
 СЛИА -’356’(М14)
 ПИО КМВТ(М14)     377
 СЛИА 1(М14)
 ПИО КМВТ(М14)     376
 СЛИА ’204’(М14)
 ПИО КМВТ(М14)     172
 ПБ ЗС            ЗАПИСЬ СИМВОЛА В РЕКОРД
*
ТРВТ НОП ,
 ПИНО ЗС(М14)       ЗАПИСЬ СИМВОЛА В РЕКОРД
 СЧ 0             КОНЦЕВОЙ БАЙТ ДЛЯ ТЕСТ.РЕЖ
 ПБ ЗСКОН
*
СТРВТ НОП ,
 СЧ ГУС
 НТЖ ТУС+38
 И Е48П42
 ПО ЗС            Э 71
КМВТ НОП ,
 СЧ П377          КОНЦЕВОЙ БАЙТ ДЛЯ УПП
ЗСКОН ЗП 42(М7)
 ПВ ЗС(М17)
 УИИ М17(М12)
 СЛИА 5(М17)
*
 УИА 1027
 СЧ (М1)
 ПАМ 0
 СДА 64-8
 ЦИКЛ *(М12)
 ЗП (М1)
 МОД ТСЛ(М2)
 УИА (М13)
 СЧИ М2
 СЛЦ Е1
 СДА 64-42         N ВИРТ.ТЕРМИНАЛА
 ИЛИ ТСВ(М13)
 ЗП ТСВ(М13)
 ПИО ЦЕЛОЕ(М17)    ЦЕЛОЕ ЧИСЛО СЛОВ
 СЧ ТСВ(М13)
 СЛЦ Е10           +ЕЩЕ ОДНО СЛОВО ВЫДАЧИ
 ЗП ТСВ(М13)
ЦЕЛОЕ НОП ,
*
 СЧ ШЗПРИМ
 ИЛИ Е48(М2)
 НТЖ Е48(М2)
 ЗП ШЗПРИМ
*
 СЧ 0
 ЗП ВППС(М2)
*
 СЧ ШКВЫД
 ИЛИ Е48(М2)
 ЗП ШКВЫД
*
 УИА 0(М13)
*
*
 СЧ ШРАБВТ
 ИЛИ Е48(М2)
 ЗП ШРАБВТ
*
 СЧ НЗТСВТ
 ПО (М16)         НЕТ Т.З.В.Т.
 ИЛИ ЕРАБВТ
 ПВ ЗАПВЕТ
 СТРН ,
*
* ТЕСТОВАЯ ВЫДАЧА НА ЕС-7920
*       (ИЗ АСПВВ)
*
ТЕСТВЫ НОП ,
 СЧ НАЧПОЗ
 ЗП NСТР(М15)
 СЧ 0
 ЗП КNСТР(М15)    ДЛЯ СЛЕДУЮЩИХ НЕ ТЕСТОВЫХ Э 71
 СЧ ТСЛ(М15)
 УИ М3            М3 - АДРЕС ПЕРВОГО РЕКОРДА
СЛРВЫД НОП ,
 ПО ВСЕВЫД
 УИ М16           М16 - АДРЕС ТЕКУЩЕГО РЕКОРДА
 УИИ М17(М16)
 СЧ ДЛ(М16)
 И П777
 ВЧОБ 0
 УИ М12           М12 - ( - ЧИСЛО СЛОВ В РЕКОРДЕ +1 )
СЛСЛВЫ НОП ,
 СЧ Н(М17)
 ЗП СЛОВОБ
 ПЕ НЕНОЛЬ
 СЧ ТСЛ(М15)
 И Е40
 ПЕ ВСЕВЫД        ВЫДАЧА ПРИГЛАШЕНИЯ
НЕНОЛЬ НОП ,
 ПВ ВЫТЗП(М14)
 СЛИА 1(М17)
 СЧИ М2
 НТЖ ТСВ(М3)       РАЗМЕР ВЫДЫЧИ Э 71
 И П777
 ПО ВСЕВЫД
 ЦИКЛ СЛСЛВЫ(М12)
 СЧ ВН(М16)
 ПБ СЛРВЫД        ПЕРЕХОД НА СЛЕДУЩИЙ РЕКОРД
ВСЕВЫД НОП ,
 УИА 0(М1)         ЧИСЛО БАЙТОВ В ПОСЛЕДНЕМ СЛОВЕ
 СЛИА 1(М2)         ’ППСПО’ УМЕНЬШИТ М2 НА 1
 ПБ ППСПО
 СТРН ,
*
* ТЕСТОВЫЙ ПРИЕМ С ЕС-7920
*       ( ИЗ МОТТ )
*
ТЕСТПР НОП ,
 СЧ ТСЛ(М17)
ЕСТЬРП НОП ,
 УИ М12           М12 - АДРЕС РЕКОРДА
 УИА 0(М14)        М14 - НОМЕР СЛОВА В РЕКОРДЕ
СЛСЛПР НОП
 ПВ ДАЙСЛБ(М15)
 МОДА (М12)
 ЗП Н(М14)        ЗАПИСЬ СЛОВА В РЕКОРД
 СЧИ М14
 НТЖ ДЛ(М12)
 И П777
 ПО КОНРЕК        РЕКОРД КОНЧИЛСЯ
 СЛИА 1(М2)        СЛЕДУЮЩИЕ СЛОВО В БУФЕРЕ ПРИЕМА
 СЛИА 1(М14)       СЛЕДУЮЩИЕ СЛОВО В РЕКОРДЕ
 СЧИ М2
 НТЖ П700
 ПЕ СЛСЛПР
 ПБ КПТ           КОНЕЦ ПРИЕМА С ЕС-7920
*
КОНРЕК НОП ,
 СЧ НУЖРЕК
 И Е48(М17)
 ПО КПТ           ПЕРЕПИСАЛИ СКОЛЬКО ХОТЕЛ ЭЛЬБЕРТ
 УИА 1027
 ИЛИ ДАЙРЕК
 ЗП ДАЙРЕК        ЗАПРОС РЕКОРДА ДЛЯ ЭТОГО ТЕРМИНАЛА
 СЧ РАБДИН
 ПЕ ЗАКМОТ        ДИНАМО УЖЕ РАБОТАЕТ
 СЧ ЕДИН
 ЗП РАБДИН
 ИЛИ ШГ
 ЗП ШГ
ЗАКМОТ СЧ Е45           ЕДИНИЦА МОТТ
 ИЛИ ЖДУРЕК
 ЗП ЖДУРЕК
 УИА СЛЕДР(М15)
 УИИ М33(М15)
 ПБ НЕЛЬ
СЛЕДР НОП ,
 СЧИ М14
 НТЖ ДЛ(М12)
 И П777
 ПЕ СЛСЛПР        ОБРАЗОВАЛСЯ МАКРО РЕКОРД
 СЛИА 1(М2)
 СЧ ВН(М12)
 ПЕ ЕСТЬРП        ЕСТЬ НОВЫЙ РЕКОРД
 ПБ КПТ           КОНЕЦ ПРИЕМА
*
****************************************
 ВХОД ТЕСТВЫ,ТЕСТПР
АСПВВ ВНЕШ ППСПО,СЛОВОБ,ВЫТЗП,NСТР,КNСТР
МОТТ ВНЕШ ДЛ,ВН,П700,ДАЙСЛБ,РАБДИН,ЖДУРЕК,ДАЙРЕК,НУЖРЕК
МОТТ ВНЕШ КПТ,ЕТАВ
ДИСП70 ВНЕШ П777,ШГ
ВЗУ ВНЕШ НЕЛЬ
СОСТАВ  ВНЕШ ЕДИН
СИМЕSС КОНД В’33’
АТС    КОНД М40В’134’     СИМВОЛ АТ ДЛЯ ТЕРМИНАЛОВ  М П Д
АТСЕС  КОНД М40Х’Е0’      СИМВОЛ АТ ДЛЯ ТЕРМИНАЛОВ  Е С
НЕДОПС КОНД М40В’077’     НЕДОП.СИМ. ДЛЯ ТЕРМИНАЛОВ  М П Д
НЕДСЕС КОНД М40Х’40’      НЕДОП.СИМ. ДЛЯ ТЕРМИНАЛОВ  Е С
NLС   КОНД М40В’141’М32В’142’М24В’142’М16В’142’М8В’142’В’000’
      КОНД М40В’174’М32В’141’
СЛКОД КОНД М40В’232’М32В’210’М24В’231’М16В’234’М8В’035’В’014’
      КОНД М40В’104’М32В’110’М24В’303’М16В’101’М8В’102’В’305’
ПОСЛС КОНД В’143’
КОМВРЕ КОНД В’0’
ТКОМУЗ ПАМ 1
МОТТ ВНЕШ ШНР,ШVD130,ЕЛАТР,SО,ШЛАТР
 ВХОД КОМВРЕ,ТКОМУЗ
 ЭКВИВ (ЕСNS,Е17),(ЕСЛВ,Е36),(ЕОСВ,Е34)
КОТПВВ КОНД М18В’-1’В’7’
 ВХОДН КОТПВВ
ШУПАК КОНД В’0’
 ВХОДН ШУПАК
НМБЫТЬ КОНД М20В’1’
 ВХОД НМБЫТЬ
ВКЛЮМЛ КОНД В’0’
ВЫКНТР КОНД В’0’
 ВХОД ВКЛЮМЛ,ВЫКНТР
Л57600 ПАМ 1
ЛИСА ПАМ 1
ШАС01 КОНД В’0’          ВЫДАТЬ’01’В АС
ШКВТ ПАМ 1
ШРАБВТ ПАМ 1
НЗТСВТ ПАМ 1
ШКМБФ КОНД В’0’          ШКАЛА РАБОТУЮЩИХ МБ ;48Р- 0-ОЙ МБ
НАЧПОЗ КОНД  Ф’1840’       НАЧАЛО 24-ОЙ СТРОКИ
 ВХОД ШКМБФ
 ВХОД ШКВТ,ШРАБВТ,НЗТСВТ,ЗС,ДЛЯЭ71,Л57600
СЕТЬ ВНЕШ ЕРАБВТ
МОТТ ВНЕШ ШКВЫД
ДИСП70 ВНЕШ ЗАПВЕТ
МОТТ ВНЕШ ТСВ
 ВХОД ШАС01,ПОСЛС,ЛИСА
ДИСП70 ВНЕШ П77
ДИСП70 ВНЕШ ТБЛ,ТСЛ,IСОN,ВППС,П30,П37,П140,П377
МОТТ ВНЕШ ШКОРАБ,ШСТRLО
ДИСП70 ВНЕШ ГОД,ЗАНЯТА,ГУС,ТУС,Е48П25,П5,Е48П43
МОТТ ВНЕШ ШКАС,ШКОПТТ,ШЗПРИМ,ШКОЛА
МОТТ ВНЕШ ШЗНКТ,ПЕОТ,НОМАС
ВЗУ ВНЕШ ТАБКОД
МОТТ ВНЕШ Н,СNБЭСМ,УСТВД2
ДИСП70 ВНЕШ П3,Е6П4,КСД1,П17,П177,П12,ВСЕЕД
КИТ ВНЕШ Е40П33
КИТ ВНЕШ Е48П42
*КОНЕЦ ЧАСТИ

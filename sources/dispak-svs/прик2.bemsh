ПРИК2 СТАРТ ’20000’
 УПОТР ПРИК2(М1)
 Б
 Е
 М
*
*      Д Е Ш И Ф Р А Т О Р      *
*
 СЛИА -1(М2)
 МОДА ПР1
 ПИО (М2)        ВВД
*
 СЛИА -’10’(М2)
 МОДА ПР11
 ПИО (М2)        СБВ
*
 СЛИА -’10’(М2)
 МОДА ПР21
 ПИО (М2)        ВЫБ
*
*       Д И Р Е К Т И В А    25
*
 УИА 9(М3)
 УИА 1(М4)
 УИА ТМИН-П2(М16)
РЕЖ УИА -48(М2)
 СЧ 0
 МОДА ТМИН
 ЗП (М4)
РЕЖ1 СЧ КМИН
 СЛИА 16(М2)
 И Е10(М3)
 ПО РЕЖ3
 СЧИ М3
 ВЧОБ П11
РЕЖ2 СДА 64(М2)
 ИЛИ ТNКЛАС
 МОДА ТМИН
 ИЛИ (М4)
 МОДА ТМИН
 ЗП (М4)
 СЛИА -1(М3)
 МОДА РЕЖ4
 ПИО (М3)
 МОДА РЕЖ1
 ПИНО (М2)
 СЛИА 1(М4)
 ПБ РЕЖ
РЕЖ3 СЧ ПУСТО1
 ПБ РЕЖ2
РЕЖ4 МОДА ВЫДАЧА-П(М7)
 ПВ (М17)
 УИА ТОТЛ-П2(М16)
 СЧ МГРП
 И Е16
 ПО РЕЖ5
 МОДА ВЫДАЧА-П(М7)
 ПВ (М17)   ОТЛ
РЕЖ5 СЧ МГРП
 И Е15
 ПО РЕЖ6
 УИА ТСТК-П2(М16)
 МОДА ВЫДАЧА-П(М7)
 ПВ (М17)   С:СТК
РЕЖ6 СЧ ПРЕДЕЛ
 И Е20
 ПО РЕЖ6АА
 УИА ТАРХ-П2(М16)
 МОДА ВЫДАЧА-П(М7)
 ПВ (М17)
РЕЖ6АА СЧ ПРЕДЕЛ
 И Е19
 ПО РЕЖ6АВ
 УИА ТКПИ-П2(М16) С: ПИ БЕЗ КОНТРОЛЯ
 МОДА ВЫДАЧА-П(М7)
 ПВ (М17)
РЕЖ6АВ СЧ МГРП
 И ЕСТОП
 МОДА ВРСТОП-П(М7)
 УИА (М13)
 ПО РЕЖ6СП
 МОДА ЛNТЕР-П(М7)
 УИА (М14)
 СЧ БСТОП-П(М7)
 ИЛИ П17
 МОДА ЗППОЛЕ
 ПВ (М12)
РЕЖ6СП СЧ ПРЕДЕЛ
 И Е17
 МОДА ВРСПЕЦ-П(М7)
 УИА (М13)
 ПО РЕЖ7
 МОДА ЛNТЕР1-П(М7)
 УИА (М14)
 СЧ СПЕЦСТ-П(М7)
 МОДА ЗППОЛЕ
 ПВ (М12)
РЕЖ7 СЧ ЗАНЯТА
 И Е43
 ПО ПР1А-П(М7)
 УИА ТДЗАК-П2(М16)
 ПБ КОНВЫД-П(М7)
*
*
*------- ЗАПИСЬ В ПОЛЕ -------*
*
*       1) ЗП. ВРЕМЕНИ В АДРЕС ПО М13
*        2) ЗП.ЛОГ.НОМЕРА ТЕРМ ПО М14
*
ЗППОЛЕ ЗП ПОЛЕ-П(М7)
 СЧ (М14)
 УИ М10
 МОДА NТЕР-П(М7)
 ПВ (М17)
 РЗБ =П’037700’
 ИЛИ =П’Т000 0’
 ЗП ПОЛЕ+1-П(М7)
 СЧ ЖИЗОС
 СЛЦ Т
 ВЧ (М13)
 МОДА ВР1А-П(М7)
 ПВ (М2)
 ЗП ПОЛЕ+2-П(М7)
 СЧ ТВР1+1-П(М7)
 ЗП ПОЛЕ+3-П(М7)
 УИА ПОЛЕ-П(М16)
 МОДА ЗВР2-П(М7)
 ПВ (М17)
 ПБ (М12)
*
*-------  Е N D  -------*
*
*
*
*      Д И Р Е К Т И В А  N 1
*      ----------------------
*
*           ВВД
*
ПР1 УИА ’2003’
 СЧ ЗАНЯТА
 И Е4
 ПЕ ПНЕТ-П(М7)
ПР1Н СЧ Е29
 ИЛИ ШКИВ
 ЗП ШКИВ
 ПБ ТВЫП-П(М7)
ПР1К СЧ ПРЕДЕЛ
 И Е31
 ПО ПР11
 АIF (∧ВS ЕQ 1).ВУ2
 МОД ТУС+24
 УИА (М3)
 СЧ ЕСАЦ1
 СДА 64+32
 И П377
 УИА ’2003’
 ЗП ЯЧВСС-ОСА(М3)
 ПЕ ПР1М
 МОДА НПК
 УИА (М15)
 СЧ Е20
 МОД ТУС+24
 ПВ СОПКА-ОСА(М16)
 НТЖ 0
 ПО ПР11
 УИ М15
 СЧ 5(М15)
 СДА 64+24
 И П377
 МОДА ПР1К
 УИА (М17)
 ПЕ ПР1Л
 СЧ 5(М15)
 И П377
 ЗП ЯЧВСС-ОСА(М3)
 СДА 64-32
 УИА ’2003’
 ИЛИ ЕСАЦ1
 ЗП ЕСАЦ1
 МОДА ПР1М
 УИА (М17)
ПР1Л СЧИ М15
 ПБ НАКВ-ОСА(М3)
.ВУ2 АNОР
ПР1М УИА ’2003’
 ПБ ПР1Н
ПР11 УИА ’2003’
 СЧ ЗАНЯТА
 И Е4
 ПЕ ТВЫП-П(М7)
 СЧ ЯЧВ1
 ЧЕД 0
 И Е1
 ПО ТВЫП-П(М7)
 СЧ ЗАНЯТА
 ИЛИ Е4
 ЗП ЗАНЯТА
 СЧ ИНФЗАМ
 ЗП КУС-П(М7)
 УИИ М4(М1)
 МОДА ПРКАТ-П(М7)
 ПВ (М12)
 УИИ М2(М1)
 УИИ М1(М4)
 СЧ 10(М2)
 И П177
 СДА 64+2
 УИ М16
 СЧ Е48(М16)
 ИЛИ ЯЧВ1
 ЗП ЯЧВ1
 СЧ 18(М2)
 И П1777
 УИ М5
 СЧ КУС-П(М7)
 ПВ ТБУФ(М15)
 СЧ Е4
 ПВ ГАШЗАН(М15)
 МОДА ТВЫП-П(М7)
 ПИО (М5)         ЗОН НЕТ
 УИА (М4)
 СЧ КУС545
 ЗП КУС-П(М7)
 МОДА ПИК
 УИА (М12)
 СЧ 0
 ЗП РЯ2-П(М7)
ПР11С СЧ 0
 ПВ ТБУФ(М15)
 ЗП РЯ1-П(М7)
 УИА 3
 СЧ НОММЛ1
 ПВ ЕNQ(М15)
 УИА НОММЛ1(М13)
 СЧ РЯ1-П(М7)
 УИИ М10(М1)
 ПБ ЧКАТ-П(М7)
ПИК УИИ М2(М1)
 УИИ М1(М10)
ПР11А НОП
 СЧИ М5
 И П37
 УИ М15
 И Е1
 УИ М16
 СЧИ М5
 СДА 64+1
 УИ М12
 СДА 64+4
 УИ М17
 СЛИ М12(М2)
 СЛИ М17(М2)
 СЧ 512(М17)
 ИЛИ Е48(М15)
 НТЖ Е48(М15)
 ЗП 512(М17)
 СЧ 512(М12)
 СД КСО(М16)
 И П1777
 УИ М5
 СЧ 512(М12)
 ИЛИ Е48П25(М16)
 НТЖ Е48П25(М16)
 ЗП 512(М12)
 МОДА ПР11А
 ПИНО (М5)
 МОДА КОНЦ1А-П(М7)
 ПВ (М12)
 СЧ РЯ2-П(М7)
 И Е27
 ПЕ БЮДНРБ
 СЧ РЯ2-П(М7)
 СДА 64+41
 ПО БЮДНРБ        БЮДЖЕТ НЕ РАБОТАЛ
 СЧ РЯ2-П(М7)
 И Е26
 ПЕ БЮДНРБ        ЗАДАЧА ИНД.ПОЛЬЗ.(ВРЕМЕННО)
 СЧ КУС-П(М7)
 И Е18П11-П(М7)
 ИЛИ КУСЛИМ
 СЛЦ НОММЛ1
 УИИ М10(М1)
 МОДА ЧТК-П(М7)
 ПВ (М12)
 УИИ М1(М10)
 СЧ РЯ3-П(М7)
 СБР МАСКО(М2)
 АIF (NОТ ∧АС).АС4
 ЗП РЯ2-П(М7)
 НТЖ =М40Х41’
 ПО ОТД2
 СЧ РЯ2-П(М7)
 И =М44В’17’
 ПО ОТД2
 НТЖ =М44Х’9’
 ПО ОТД2
 НТЖ =М44Х’9’
 ПБ ОТД1
ОТД2 СЧ РЯ2-П(М7)
ОТД1 СДА 64+40
 АGО .АС3
.АС4 АNОР
 СДА 64+40
 ПВ ИЗ10В8(М15)
.АС3 АNОР
 СДА 64-2
 УИ М5            АДРЕС ЯЧЕЕК ОТДЕЛА
 СЛИ М5(М2)
 СЧ СМНАОТ(М5)
 И Е25
 ПО БЮДНРБ      ОТД ВНЕ БС
 СЧ РЯ2-П(М7)
 СДА 64-4
 И Е46П48
 ПО БЮДНРБ    З-ЧА ВНЕ БС
 НЕД П77777
 УИ М12
 СЧ РЯ1-П(М7)
 И Е1П25
 МОДА (М12)
 СЛЦ ДЕЖ(М5)
 МОДА (М12)
 ЗП ДЕЖ(М5)
 РЖА 3
 СЧ РЯ1-П(М7)
 СДА 64+40
 УМН К144
 СЧМР 64
 СЛЦ СМНАОТ(М5)
 ЗП СМНАОТ(М5)
 МОДА КОНЦ1А-П(М7)
 ПВ (М12)         ЗП. КАТАЛОГА ЛИМИТОВ
БЮДНРБ СЧ НОММЛ1
 ПВ DЕQ(М15)      ОСВ.МД
 УИА 3
 ПБ КОНЦ4-П(М7)
*
*
*      Д И Р Е К Т И В А  N 21
*      -----------------------
*        ВЫБ <N ЗАД.НА БУФЕРЕ>
*
*
ПР21 СЧ ТР5-П(М7)
 И П377
 УИ М4
 ЗП РЯ3-П(М7)
 СДА 64+7
 УИ М6
 СЧИ М4
 И П177
 ПО ПНЕТ-П(М7)
 УИ М4
 СЧ КУСБК(М6)
 ЗП КУС-П(М7)
 МОДА ПР11С
 ПВ (М12)         ЧТ.КАТАЛОГА ВВОДА
 УИИ М2(М1)
 УИИ М1(М10)
 СЧИ М4
 СДА 64-3
 УИ М16
 СЛИ М16(М2)
 ЗП РЯ2-П(М7)
*
*---- ВРЕМЕННО ----*
*
 СЧ 4
 НТЖ Е24
 ПО НЕВИТЯ
*
*-----------------*
*
 СЧ ЯЧКУПР(М16)
 И ЕКУПР
 ПЕ ПЕЧУПР
НЕВИТЯ СЧ 7(М16)
 И П1777
 ЗП РЯ1-П(М7)
 УИА (М15)
 СЧ 6(М16)
 И Е31
 ПО НЕРЕШ
* ЗАДАЧА В РЕШЕНИИ
 СЧ ЕПРИК
 ПВ ЗАНПАД(М15)
 МОДА ВСЕСВС
 СЧ 0
 ЗП РАБ
 СЧ ЕПРИК
 ПВ ЗАНПРТ(М15)
 СЧ 7(М16)
 СДА 64+45
 УИ М15          М15 := N ЭВМ РЕШЕНИЯ
 РЕГ ’254’
 ИЛИ РАБ
 И Е42(М15)
 ПЕ ПР21Б         ЗАДАЧА В РЕШЕНИИ НА РАБ. СВС
 СЧ 6(М16)
 НТЖ Е31
 ЗП 6(М16)
 ПБ ВДРУГА
*
НЕРЕШ НОП ,
*
*      * ПРОВЕРКА НА ИНД.ОБЛАСТЬ
*
*
ПР21А СЛИ М15(М2)
 УИИ М12(М15)
 СЧ 7(М15)
 И П1777
 ПО ПР21Б          ЗАДАЧИ НЕТ
 УИ М15
 НТЖ РЯ2-П(М7)
 ПЕ ПР21А
 СЧ 7(М12)
 СДА 64+10
 СДА 64-10
 НТЖ РЯ1-П(М7)
 ЗП 7(М12)        НОВАЯ ССЫЛКА
ВДРУГА НОП ,
 СЧ 5(М16)
 И П1777
 УИ М5
 СЧ (М16)
 СДА 64-5
 И Е1П25
 ЗП РЯ1-П(М7)
 СЧ 5(М16)        ИНФ. О МЕТРАХ
 СДА 64-24
 И Е48П41
 ИЛИ РЯ1-П(М7)
 ЗП РЯ1-П(М7)
 СЧ 6(М16)
 ЗП РЯ2-П(М7)
 МОДА КОНЦ1А-П(М7)
 ПВ (М12)        ЗП.КАТАЛОГА ВВОДА
 СЧ КУС-П(М7)
 И Е18П11-П(М7)
 ИЛИ КУС545
 СЛЦ НОММЛ1
 МОДА ЧТК-П(М7)
 ПВ (М12)         ЧТ. КАТАЛОГА ЗОН И ШИФРОВ
 СЛИ М4(М2)
 МОДА ПР21С
 ПИО (М6)
 СЛИА 256(М4)     ШИФРЫ ЗАД.СЧЕТНЫХ В 1 АБЗ.
ПР21С СЧ (М4)
 ЗП РЯ3-П(М7)
 РЗБ ПРОБ6
 ЗП ПОЛЕ+1-П(М7) <1 ПОЛ.ШИФРА>
 СЧ (М4)
 СДА 64-24
 РЗБ ПРОБ6
 ЗП ПОЛЕ+2-П(М7) <2 ПОЛ.ШИФРА>
 СЧ 0
 ЗП (М4)          ПОЧИСТ.ШИФР
 МОДА МИМО
 ПИНО (М6)        ЭТО 2-КАТАЛОГ
 ЗП 128(М4)       ПОЧИСТИТЬ ШИФР ОЖИДАЕМ.ЗАДАЧ
МИМО СЧ ТВЫБ
 ЗП ПОЛЕ-П(М7)
 СЧ НОМУС1
 УИ М10
 ПО НЕТЕР
 УИА ЕСТЬ-П(М16)
 МОДА ЗВР2-П(М7)
 ПВ (М17)
 УИА П’Т’(М15)
 СЧ 0
 ЗП НОМУС1
 МОДА NТЕР-П(М7)
 ПВ (М17)
Х РЗБ =П’037700’
 ИЛИ =П’0000 0’В’377’
 ЗП ПОЛЕ+3-П(М7)
 СЧ ПРИЗАС-П(М7)
 ПЕ ТЕРАС
У СЧИ М15
 СДА 64-40
 ИЛИ ПОЛЕ+3-П(М7)
 ЗП ПОЛЕ+3-П(М7)
 УИА ПОЛЕ-П(М4)
 ПБ ПР11А
НЕТЕР УИА ’377’(М15)
 СЧ ТР4-П(М7)
 НТЖ КЛЮЧРЗ
 И Е48П25
 ПЕ У    НЕ ОТ Э62 100
 ЗП ПРИЗАС-П(М7)
 УИА П’К’(М15)
 СЧ ТР4-П(М7)
 СДА 64-40
 ПБ Х
ТЕРАС УИА П’А’(М15)
 ПБ У
ПЕЧУПР СЧ ТУПР-П(М7)
 ЗП ПОЛЕ-П(М7)
 УИА ПОЛЕ-П(М4)
 ПБ КОНDЕQ-П(М7)
ПР21Б УИА ТЕК3-П(М4)
 ПБ КОНDЕQ-П(М7)
*
*
*      ПОЛЕ КОНСТАНТ
*
К144 КОНД М47В’1’В’144’
КУСЛИМ КОНД М24В’610’М17В’1’
ИНФЗАМ КОНД М17В’1’В’33’  6 ТР 3 С
НПК КОНД Х’5С’
ТNКЛАС КОНД П’ 0 0 0’
ТМИН КОНД М8П’2МИН:’В’143’
 ПАМ 3
 КОНД П’↑00000’
ТОТЛ КОНД П’2ОТЛ.↑’
ТСТК КОНД П’2СТК.↑’
ТДЗАК КОНД П’2ДИАЛГ’
 КОНД П’.ЗАКР↑’
ТАРХ КОНД П’2АРХ. ’
 КОНД П’ОТКЛ.↑’
ТКПИ КОНД П’2ПИ БЕ’
ДИСП70 ВНЕШ ТЕРЕЗА,ТСЛ,ЗАПВЕТ
 КОНД П’РОЛЯ↑ ’
ТВЫБ КОНД М8П’2ВЫБ ’В’143’
*
РАБ ПАМ 1
*
 ЭКВИВ (ДЕЖ,13),(МАСКО,425),(СМНАОТ,12)
 ЭКВИВ (П2,ПРИК2)
ПРИКАЗ ВНЕШ ПРИЗАС,ЕСТЬ,КОНЦ4А,КОНDЕQ
П21 КОНД В’21’
ПРИКАЗ ВНЕШ ПОЛЕ,ЛNТЕР,ПРОБ1,ТВР1,ВР1А,NТЕР,ВРСТОП
ПРИКАЗ ВНЕШ (П,ПРИКАЗ),ПР1А,КУС,ПРКАТ,ЧКАТ,РЯ1,РЯ2,РЯ3
ПРИКАЗ ВНЕШ КОНЦ1А,ЧТК,КОНЦ4,ПНЕТ,Е18П11
ПРИКАЗ ВНЕШ НАЧП,КОНЦГД,ТЕК3,КОПР,ТВЫП
ПРИКАЗ ВНЕШ СПЕЦСТ,ТР5,ТР4,ЗВРА,НН77НН,ТУПР,ЗВР2,БСТОП
ПРИКАЗ ВНЕШ ВЫДАЧА,КОНВЫД,ВРСПЕЦ,ЛNТЕР1
ДИСП70 ВНЕШ ЗАНЯТА,МГРП,РЯВВ1,ШКИВ,ЕСТОП,КУСБК,КУС545
ДИСП70 ВНЕШ ПРЕДЕЛ,ТУС,ЕСАЦ1,ЯЧВ1,ТБУФ,ГАШЗАН,НОММЛ1,КСО
ДИСП70 ВНЕШ ИЗ10В8,П17,П377,П37,П1777,Е48П25,П177
ДИСП70 ВНЕШ КМИН,ВРЕМЯ,ЯЧБКИО,ЕБКИО,ЕЕСТИО
ДИСП70 ВНЕШ Е46П48,П77777,ЯЧКУПР,ЕКУПР,НУСВУ
ДИСП70 ВНЕШ КЛЮЧРЗ,ТВ40,ЖИЗОС,Т
МОТТ ВНЕШ ПРОБ6,НОМУС1
СОСТАВ ВНЕШ ЧПТ,ЕПРИК
МОТТ ВНЕШ ЗАНПАД,ЗАНПРТ
ПВВ ВНЕШ ВСЕСВС
ТЕРМ ВНЕШС (ПУСТО1,ПОСЛС)
ДИСКИ ВНЕШ ЕNQ,DЕQ,П11
КИТ ВНЕШ Е1П25,Е48П41
ОСА ВНЕШ ЯЧВСС,СОПКА,ОСА,НАКВ
 ФИНИШ
*КОНЕЦ ЧАСТИ

 МАСRО
∧М УВВ ∧А
 АIF ((∧ВS NЕ 0)).К1
∧М КК 27,∧А
 МЕХIТ
.К1 АIF ((∧А ЕQ ’’’147’’’) ОR (∧А ЕQ ’’’4035’’’)).К2
∧М ДК 27,∧А  ОСТАНОВ
 МЕХIТ
.К2 АNОР
∧М КК 0,0
 МЕХIТ
 МЕND
 МАСRО
∧М ИСКБВ ∧ТИП,∧АБВ
∧М УИА ’2003’
 СЧ ТГОЧДР+∧ТИП
 УИ ∧АБВ
 СЧ (∧АБВ)
 ЗП ТГОЧДР+∧ТИП
 УИА 1(М17)
ТГОЧДР ВНЕШН ДИСП70.ТГОЧДР
 МЕND
ДМЛМБ СТАРТ ’54061’
 Б
 М
 Е
ТВМБ1 СЧ ЗОЧ1Б-1(М16)
 УИА 1(М17)
 ПО (М11)
 УИ М14
 СЧ МБЛИС-1(М14)
 ЗП ЛИС1Б-1(М16)
 СЧ МБКУС-1(М14)
 ЗП КУС1Б-1(М16)  КУС ЗАКАЗА
 СДА 64+42
 УИ М1
 СЧ КУС1Б-1(М16)
 И ЕФ
 ПЕ ВЗОМБ3
 МОД ТУСП-1(М1)
 УИА (М1)
 СЧ ЯЧЧП(М1)
 И ЕЧП
 ПО ВЗОМБ3        ЗАКАЗЫ ВЫПОЛНЯТЬ
 СЧ ШЗПОМБ
 И Е40
 ПО СНМБ          СНЯТЬ ЗАКАЗ
 УИА СНМБ(М10)
 ПБ СБОЙ2
ВЗОМБ3 СЧ КУС1Б-1(М16)
 УИА -7(М2)
 И Е29
 ПЕ ВЗОМБ4
 СЧ КУС1Б-1(М16)
 И Е18
 ПО ВЗОМБ1   ЗП
ВЗОМБ4 МОД КНАПР-1(М16)
 ЗП 7(М2)         РОСПИСЬ СЛУЖЕБНЫХ СЛОВ
 ЦИКЛ ВЗОМБ4(М2)
 ПБ ВЫЗ
ВЗОМБ1 СЧ П3
 ПВ ПОК(М13)
 СЧ ЛИС1Б-1(М16)
 ПВ КСМ(М12)      КОНТР. СУММИР-Е
 ЗП КСУМ1Б-1(М16)
 УИА 7(М1)
 УИА -8(М2)
 ЗП РЯОМБ
 СЧ КУС1Б-1(М16)
 СБР ММБН
ВЗОМБ6 МОД КНАПР-1(М16)
 ЗП 8(М2)
 СЛЦ Е36
 ЗП РЯО2
 СЧ РЯОМБ
 СДА 64-12
 ЗП РЯОМБ
 СЧМР 0
 МОД КНАПР-1(М16)
 ЗП 9(М2)         ЗАПИСЬ КОНТРОЛЬНОЙ СУММЫ СЕКТОРА
 СЛИА 2(М2)
 СЧ РЯО2
ВЫЗ ПИНО ВЗОМБ6(М2)
 СЧ ПР13
 УИА -7(М2)
 ПВ ПОК(М13)      УСТ.ПОК
ВЗОМБ7 ЗП 1             БЛОК ВЫТАЛКИВАНИЯ
 ЦИКЛ ВЗОМБ7(М2)
ПОДГРП НОП ----
 СЧ ЛИС1Б-1(М16)
 ПВ ПРИП1С(М10)
 СЧ ЛИС1Б-1(М16)
 СДА 64+36
 УИ М10       ПАРАМЕТР ПВВМБ
 СЧ КУС1Б-1(М16)
 ПВ ПВВМБ(М15)
 ПАМ 3
 УИА 1027
 ПБ (М11)
*БЛОК КОНТРОЛЬНОГО СУММИРОВАНИЯ ЛИСТА(АБЗАЦА) ОБМЕНА.
КСМ ПВ ПРИП1С(М10)
 УИА 1024(М6)
 СЧ 0
 ЗП РЯО1(М17)
 ПИО КСМ3(М17)     ОБМЕН С МЛ
 УИА (М2)          НОМЕР СЕКТОРА
 УИА -3(М1)        ЧИСЛО АБЗАЦЕВ ПРИ СУММИРОВАНИИ.
 СЧ КУС1Б-1(М16)
 И Е19
 ПЕ КСМ1          ОБМЕН ЛИСТОМ
 УИА (М1)
 СЧ КУС1Б-1(М16)
 И Е12Е11        НОМЕР АБЗАЦА
 СДА 64+2
 ИЛИ Е11
 УИ М6            АДРЕС ПЕРВОНАЧ.СУММИРОВАНИЯ.
 СЧ КУС1Б-1(М16)
 И Е2Е1
 УИ М2            НОМЕР СЕКТОРА
КСМ1 СЧ 0
 УИА -255(М4)
 ЗП РЯОМБ
 УИА 0
КСМ2 МОДА (М6)
 СЛЦ 255(М4)
 ЦИКЛ КСМ2(М4)
 УИА ’2003’
ШПИК ЗП РМР
 И Е12П1
 СЛЦ РЯОМБ
 ЗП РЯОМБ
 СЧ РМР
 СДА 64+12
 ПЕ ШПИК          *С ЕСТЬ ДОПОЛН. РАЗ-ДЫ ПРИ КС
 СЧ РЯОМБ
 ЗП РМР
 СДА 64+12
 ПЕ ШЛ
 УИА 3
 СЧ РЯОМБ
 СД КСД3(М2)
 ИЛИ РЯО1(М17)
 ЗП РЯО1(М17)
 СЛИА 256(М6)
 СЛИА 1(М2)
 ЦИКЛ КСМ1(М1)
 ПБ (М12)         ВЫХОД.КОНТРОЛЬНАЯ СУММА НА СМ.
ШЛ ЗП РЯОМБ
 СЧ РМР
 И Е12П1
 ПБ ШПИК
КСМ3 УИА -1023(М4)
 УИА КЧЗП(М5)
 УИА 0             ДЛЯ МЛ КОНТР.СУММИРОВАНИЕ
КСМ4 СЛЦ 2047(М4)
 ЦИКЛ КСМ4(М4)
 УИА 3
 ПБ (М12)
ОПРБ ПБ ОПРБ
КЧЗП НОП
 СЧ РЭ4
 СТОП ’2421’
 СЧ Е32-24(М16)
 МОДА ОШОС
 ИЛИ
 МОДА ОШОС
 ЗП
 ПБ (М12)
ДИСПМБ УИА 1027
 СЧ ШЗПОМБ
 НТЖ Е40
 ЗП ШЗПОМБ
ДИСМБ2 НОП ------
ОПРБ4 НОП ------  ВХОД В ДРАЙВЕР ПО КОНЦУ ОБМЕНА
 ИСКБВ 1,М15
 ПИНО ОПРБ4Я(М15)   ОЧЕРЕДЬ НЕ ПУСТА
 СЧ ШЗПОМБ
 ИЛИ Е48Е47
 НТЖ Е48Е47
 ЗП ШЗПОМБ
 ПБ ВХЗОБ
ОПРБ4Я НОП ------  НАЧАЛО ОБР-КИ ВЫПОЛНЕННОЙ ЗАЯВКИ
 СЧ 7(М15)     7-ОЕ СЛОВО БВВ
 СДА 64+45
 И Е1
 НТЖ Е1
 СЛЦ Е1
 УИ М16        НАПРАВЛЕНИЕ
 СЧ Е5
 РЕГ ’44’
 КК ’33’,4(М15)   СПУ
 КК ’32’,СПУБ
 КК ’33’,5(М15) ДР
 КК ’32’,ДРБ
 КК ’33’,6(М15) ДРУ
 КК ’32’,ДРУБ
 СЧ ЛИС1Б-1(М16)
 ПВ ПРИП1С(М10)
 УИА ’2003’
 СЧ ИПОБМ+4
 ЗП СТАРП      ТЕКУЩ ПЕРЕХВАТ КЧ
 СЧ АСЧББ3     НОВЫЙ ПЕРЕХВАТ
 ЗП ИПОБМ+4
 СЧ КУС1Б-1(М16)
 ЗП (М15)
 СЧИ М15
 ПВ ВОСЛМБ(М15)
 ПАМ 3
БЫЛОКЧ НОП ------
 СЧ СТАРП
 ЗП ИПОБМ+4    ВОССТАНОВИЛ СТАРЫЙ ПЕРЕХВАТ
 УИА 3
 КК ’33’,ДРБ
 СЧМР
 МОДА Е37П35
 И
 СДА 64+34
 УИ М15
 СЧ Е1
 СЛЦ ОБРМБ(М15)
 ЗП ОБРМБ(М15)   ЧИСЛО ОБРАЩЕНИЙ К НМБ
 СЧ КУС1Б-1(М16)
 УИА ИДИ2(М11)
 УИА (М15)
 И Е28
 УИА 1(М17)
 МОД ТУС+17
 ПЕ ТЕСТ-РЕСУРС
 СЧ КУС1Б-1(М16)
 И Е29
 ПЕ СНМБ
 КК ’33’,ДРБ
 СЧМР
 И ОШДР          М32В’160001’
*
**************************************************************
*                                                            *
*                                                            *
*                                                            *
 ПЕ ПОВКСМ        ПЛОХОЙ ОБМЕН    Д Р ,Д Р У        *
*                                                            *
*                                                            *
*                                                            *
**************************************************************
*
ИДИ2 СЧ КУС1Б-1(М16)
 УИА 3
 И Е18
 ПО СНМБ          ЗАПИСЬ НА МБ
*БЛОК КОНТРОЛЬНОГО СЧИТЫВАНИЯ С МБ.
 СЧ Е2Е1
 ПВ ПОК(М13)      ГАШ.ПОК
 СЧ ЛИС1Б-1(М16)
 ПВ КСМ(М12)      НА КОНТРОЛЬНОЕ СУММИРОВАНИЕ
 ЗП РЯОМБ         КОНТРОЛЬНАЯ СУММА
 СЧ КУС1Б-1(М16)
 СБР ММБН
 ЗП РЯО2
 УИА (М1)          НОМЕР СЕКТОРА
 УИА -3(М2)        ЧИСЛО СЕКТОРОВ
 СЧ КУС1Б-1(М16)
 И Е19
 ПЕ КСЧБ1         ОБМЕН ЛИСТОМ
 УИА (М2)          ОДИН СЕКТОР
 СЧ КУС1Б-1(М16)
 И Е2Е1
 СДА 64-1
 УИ М1            УДВОЕННЫЙ НОМЕР СЕКТОРА.
КСЧБ1 СЧИ М1
 СДА 64-34
 ИЛИ РЯО2
 МОД КНАПР-1(М16)
 НТЖ (М1)
*
**************************************************************
*                                                            *
*                                                            *
*                                                            *
 ПЕ ПСЧБЧ         СЛ. СЛОВА   Н Е В Е Р Н Ы Е       *
*                                                            *
*                                                            *
*                                                            *
**************************************************************
*
*
 СЧИ М1
 СДА 64+1
 УИ М3
 СЧ РЯОМБ
 СД КСД1(М3)
 И Е12П1
 МОД КНАПР-1(М16)
 НТЖ 1(М1)
*
**************************************************************
*                                                            *
*                                                            *
*                                                            *
 ПЕ ПСЧБМ         КОНТР.СУММА  Н Е В Е Р Н А        *
*                                                            *
*                                                            *
*                                                            *
**************************************************************
*
 СЛИА 2(М1)
 ЦИКЛ КСЧБ1(М2)
КСЧБ4 СЧ К2013
 ПВ ПОК(М13)      УСТ.ПОК
СНМБ МОД ЗОЧ1Б-1(М16)
 УИА (М14)
 УИА 1027
 СЧ СЧЗКМБ
 СЛЦ Е6П1
 И Е6П1
 ЗП СЧЗКМБ        УМЕНЬШЕНИЕ СЧЕТЧИКА ЗАКАЗОВ К МБ
 УИА 3
 СЧ ОЧМБ
 ИЛИ Е48-1(М14)
 ЗП ОЧМБ          ОСВ-Е МЕСТА В МБКУС
 СЧ МБЛИС-1(М14)
 ЗП ЯЛИСМБ
 СЧ МБКУС-1(М14)
 ЗП ЯКУСМБ
 И ССЫЛКА
 СДА 64+29
 ЗП ЗОЧ1Б-1(М16)  НОМЕР ОЧЕРЕДНОГО ЗАКАЗА
 УИА 7(М1)
 СЧИ М14
 НТЖ ШОЧНАП-1(М16)
 ПЕ СНМБ1         НЕ ПОСЛ.ЗАКАЗ ПОЛЬЗ
 ЗП ШОЧНАП-1(М16)
 ПБ ОПРБ6
СНМБ1 СЧИ М14
 НТЖ ФИЗ-1(М16)
 ПЕ ОПРБ6         НЕ ПОСЛ.СИСТ.ЗАКАЗ
 ЗП ФИЗ-1(М16)
ОПРБ6 УИА ТВМБ1(М5)
 ПВ ОПРБ6А(М11)
 УИА 1027
 СЧ ШЗПОМБ
 И Е40
 ПЕ ДИСПМБ        ВХОД ОТ ПОСТАНОВКИ ЗАКАЗА
ПСЧБ1 УИА -П(М11)
ПРНУ СЧ ШКОМБ+П(М11)
 ИЛИ ШКМБ+П(М11)
 ЗП ШКМБ+П(М11)
 СЧ 0
 ЗП ШКОМБ+П(М11)
 ЦИКЛ ПРНУ(М11)
 СЧ ШЗПОМБ
 НТЖ Е48-1(М16)
 ЗП ШЗПОМБ
 ПБ ДИСМБ2
*
*
*      БЛОК СНЯТИЯ ЗАКАЗА
ДВОЧ1 СЧ ОЧКУС-1(М14)
 ПИО СТ411(М14)
 ЗП ЯКУС          СОХР.КУС
 СЧ ОЧЛИС-1(М14)
 ЗП ЯЛИС
 УИА ’2003’
 СЧ СЧЗАК
 ВЧ Е1
 ЗП СЧЗАК         УМЕНЬШ.НА 1 ОБЩ.ЧИСЛА ЗАКАЗОВ
 УИА 3             ГАШ.БЛПР
 СЧ ВСЕЕД
 НТЖ Е48-1(М14)
 ЗП РЯО           0 В РАЗРЯД ВЫПОЛНЕННОГО ЗАКАЗА
 УИА -42(М12)
ДВОЧ2 СЧ ШОЧНАП+44(М12)
 СБР РЯО
 ЗП ШОЧНАП+44(М12)
НЕТЗК ЦИКЛ ДВОЧ2(М12)
 СЧИ М14
 НТЖ Е5
 ПО (М5)          ЗАКАЗ В ОЧЕРЕДИ ПОСЛЕДНИЙ
 УИИ М12(М14)
 СЛИА -15(М12)
ДВОЧ3 СЧ ОЧКУС+15(М12) ПЕРЕДВИЖКА ОЧЕРЕДИ КУС
 ЗП ОЧКУС+14(М12)
 СЧ ОЧЛИС+15(М12)
 ЗП ОЧЛИС+14(М12)
 СЧ ОЧКСУМ+15(М12) ПЕРЕДВИЖКА ОЧЕРЕДИ КСУМ
 ЗП ОЧКСУМ+14(М12)
 ЦИКЛ ДВОЧ3(М12)
 ПБ (М5)
*
*
ОПРБ6А СЧ ЯКУС(М17)
 СДА 64+42
 ПО СТ421         НЕТ НОМЕРА ЗАДАЧИ В КУС-Е
 УИ М12
 УИА (М14)
 СЧ ЯКУС(М17)
 И ЕФ
 ПЕ СНЗК1
 СЧ 0
 ЗП РЯО(М17)
 ПИНО ДЕС(М17)
 СЧ КОДИК
 ПО ДЕС
 НТЖ ЯКУС
 И Е48П43
 ПЕ ДЕС
 СЧ КОДИК
 И П77777
 МОД ТУСП-1(М12)
 ЗП 8
 СЧ 0
 ЗП КОДИК
ДЕС НОП 0
 ПИО СНЗК1(М1)     *С ПРИП-КУ И ЗАЩ-ТУ НЕ ТРОГАТЬ М1=0
СНЗЩ СЧ ЯЛИС(М17)
 ПВ ТОПСН(М7)
 ПИНО ЭТОМБ(М17)
 СЧ ШЗНМБ+ВР(М10)
 И Е47П1
 ЗП ШЗНМБ+ВР(М10)
ЭТОМБ СЧ РЯТОП
 СДА 64+СДВМЛ
 И П37
 УИ М10           <М10>: N СТР.ОБМЕНА (МАТ)
 НТЖ Е5П1
 УИ М7
 ПИО ЛИСТ57(М2)   ОКОН.ОБМ.
 СЧ 32(М2)
 НТЖ Е48(М7)       ГАШ.ЗАЩИТЫ В КОПРЗ ЗАДАЧИ МАТ-КА
 ЗП 32(М2)
 СЧИ М10
 И Е2Е1
 УИ М7            М7:N СТОЛБЦА КОПРП
 СЧИ М10
 СДА 64+2
 НТЖ Е3П1
 УИ М4            И4: N СТРОКИ КОПРП
 СЛИ М4(М2)
 СЧ ЯЛИС(М17)
 СДА 64-2
 РЗБ ПРИП(М7)
 ИЛИ 24(М4)
 ЗП 24(М4)
ЛИСТ57 ПИНО ДМБ(М17)
 УИА -П(М15)
ПРНС СЧ ШКОМЛ+П(М15)
 ИЛИ ШКМЛ+П(М15)
 ЗП ШКМЛ+П(М15)
 СЧ 0
 ЗП ШКОМЛ+П(М15)
 ЦИКЛ ПРНС(М15)
ДМБ СЧ ШКОБМ
 ИЛИ ШСЗПСЧ
 ПВ ВШГ(М15)
 СЧ ШЗЗПСЧ
 ПВ ВШГП(М15)
 СЧ 0
 ЗП ШСЗПСЧ
 ЗП ШЗЗПСЧ
 ЗП ШКОБМ
 УИИ М7(М16)
 УИИ М4(М17)
 СЧИ М12
 ИЛИ Е30
 ПВ ЗАПВЕТ(М16)
 УИИ М16(М7)
 УИИ М17(М4)
 УИА 3
 ПИО СНЗК3(М2)      ОКОН.ОБМ.
 СЧ ТУСП-1(М12)
 И Е36
 ПО СНЗК3         ЗАДАЧА НЕ ЗАКРЫТА ПО ОБМЕНУ
 СЧИ М10
 СДА 64-23
 ЗП РЯО(М17)
 НТЖ ТУСП-1(М12)
 И Е35П24
 ПЕ СНЗК3         ЗАДАЧА ЖДЕТ ДРУГУЮ СТРАНИЦУ
СНЗК1А СЧ Е36
 ИЛИ РЯО(М17)
 НТЖ ТУСП-1(М12)
 ЗП ТУСП-1(М12)
СНЗК1 УИА ’2003’        УСТ.БЛПР
 УИА ОТКРШГ(М10)                                                 °4
 ПИНО ГАШТАБ(М17)   МБ
 СЧ ЯКУС          МЛ
 И Е2
 ПЕ СНЗК2         ПОДВОД МЛ БЕЗ ОТВЕТА
ОТКРШГ СЧ ЯКУС(М17)
 И ЕФ
 УИА СНЗК2(М15)
 УИА ВШГ(М10)
 ПЕ СНЯТЬ
 УИА ВШГП(М10)
СНЯТЬ СЧ Е48-1(М12)
 ПБ (М10)
СНЗК3 СЧ ЯКУС(М17)
 ПИНО СНЗК4(М17)    МБ
 И Е3
 ПЕ СНЗК1
СНЗК2 СЧ ЯКУС(М17)
 ПБ (М5)
СНЗК4 УИА СНЗК2(М10)
ГАШТАБ СЧ ТАБОШ
 ПО (М10)         НЕТ СБОЕВ МБ
 СЧ ЯКУСМБ
 СБР НМБТР
 СДА 64+37
 НТЖ ТАБОШ
 И П3777
 ПЕ (М10)         ЗАКАЗ НА ДР. ТРАКТ
 ЗП ТАБОШ
 ПБ (М10)
*
*
ПСЧБЧ УИА 3(М15)        НЕВЕРНЫЕ СЛУЖ.СЛОВА
 ПБ ПСЧБО
ПСЧБМ УИА 2(М15)        КОН.СУММА
 ПБ ПСЧБО
*
* БЛОК ПОВТОРНОГО СЧИТЫВАНИЯ С МБ
*
ПСЧББ3 НОП ------  ПРОИЗОШЛО КЧ В АДАПТЕРЕ
 СЧ И17
 УИ М17
 СЧ И16
 УИ М16
 УИА БЫЛОКЧ(М15)
 ПБ ВОСРКЧ         ВОЗВРАТ БВВ В ПУЛ ПОСЛЕ КЧ
ПСЧББ ПО ПСЧББ1        НОЛЬ
 СЧ Е5
ПСЧББ1 ЗП РЯОМБ         ПРИЗНАК НЕ НУЛЯ В ЯЧЕЙКЕ С КЧ
 СЧ И16
 УИ М16
 УИА 1(М15)
 СЧ КУС1Б-1(М16)
 И Е18
 ПЕ ПСЧБО         КЧ НА ЧТ
 СЧИ М33
 ЗП СМ
 УИА МС(М10)
 СЧИ М10
 ВЧОБ СМ
 СДА 64+1
 ПО МСУ
 УИА -255(М4)
 УИА 0
МС МОДА (М6)
 СЧ 255(М4)
 УИ 0
 ЦИКЛ МС(М4)
 УИА 1027
 ПБ КСМ1
МСУ СЧ КУС1Б-1(М16)
 УИА 4(М15)
 И ЕФ
 ПЕ ЗАККАЧ  КЧ НА ФИЗОБМ
ПСЧББ2 СЛИ М6(М4)
 СЛИА 255-1024(М6)
 СЧИ М6
 СДА 64-28
 ИЛИ РЯОМБ
 ЗП ЯЧКЧМБ        АДРЕС В ЛИСТЕ
 СЧ ЛИС1Б-1(М16)
 И Е48П37
 СДА 64-2
 ИЛИ ЯЧКЧМБ
 ЗП ЯЧКЧМБ
 СЧ ЯЧКЧ
 И П17           ВИД КЧ И НОМЕР МОЗУ
 ИЛИ ЯЧКЧМБ
 ЗП ЯЧКЧМБ
 СЧ Е7
 ИЛИ ШКИВ
 ЗП ШКИВ          ВЫЗВАТЬ В РАБОТУ ’КЧ’
 УИА 3
 СЧ КУС1Б-1(М16)
 СДА 64+42
 УИ М12           НОМЕР ЗАДАЧИ
 СЧ ЛИС1Б-1(М16)
 ПВ ПРИП1С(М10)   ПРИПИСКА ПЕРВ. ЛИСТУ
 СЧ ТУСП-1(М12)
 УИ М12
 ЗП РЯОМБ
 ПБ ЛАК
ПСЧБЕ НОП ,
*..................................................
 УИИ М11(М15)
 СЛИА 1(М11)
 СЧИ М15
 НТЖ Е3
 ПЕ НЕ4
* КЧ ПРИ ЗАПИСИ ИЛИ БНС В ДР ПРИ ЗАПИСИ
 УИА 2(М11)        КЧ ПРИ ЗАПИСИ
НЕ4 НОП ,
 СЧ АДРБМБ
 УИ М13
 СЛЦ Е2
 И П17
 ЗП АДРБМБ
 СЧ ЛИС1Б-1(М16)
 СДА 64+36
 СДА 64-15-10        35-16 РР - АДРЕС ОП
 ЗП БУФМБ(М13)
 КК ’33’,СПУБ
 СЧМР
 СДА 64+32           15- 1 РР - АДРЕС ФАЙЛА
 ИЛИ БУФМБ(М13)
 ЗП БУФМБ(М13)
 КК ’33’,СПУБ
 СДА 64+38
 И П17             N МБ
 СДА 64-39
 ИЛИ БУФМБ(М13)
 ЗП БУФМБ(М13)
 СЧ КУС1Б-1(М16)
 И Е18
 СДА 64-27         ПРИЗНАК ЧТЕНИЯ (45 Р)
 ИЛИ БУФМБ(М13)
 ЗП БУФМБ(М13)
 КК ’33’,ДРБ
 КК ’32’,БУФМБ+1(М13)
 СЧМР
 И Е33
 ПО НЕТБНС
 УИА 1(М11)
 КК ’33’,БУФМБ+1(М13)
 СЧМР
 И Е45             ДРУ
 ПО НЕТБНС
 УИА 5(М11)
 КК ’33’,ДРУБ
 КК ’32’,БУФМБ+1(М13)
НЕТБНС НОП
 СЧИ М11
 СДА 64-45
 ИЛИ БУФМБ(М13)
 ЗП БУФМБ(М13)
*
* БУФМБ - ВОСЕМЬ КВАНТОВ ПО ДВЕ ЯЧЕЙКИ
*
*  0-АЯ ЯЧ. :
*            48-46 РР - ТИП СБОЯ =
*                                  1 - ДР
*                                  2 - КЧ
*                                  3 - КС
*                                  4 - СС
*                                  5 - ДРУ
*            45    Р  - ЧТЕНИЕ
*            43-40 РР - N МБ
*            35-16 РР - АДРЕС ОП
*            15- 1 РР - АДРЕС ФАЙЛА
*  1-АЯ ЯЧ. :
*            ПРИ ТИП СБОЯ = 1 - ДР
*            ПРИ ТИП СБОЯ = 5 - ДРУ
*..................................................
 СЛИА -4(М15)
 ПИО ПСЧБ(М15)     ОШ МУ->УВУ
 КК ’33’,ДРБ
 СЧМР
 МОДА Е37П35
 И
 СДА 64+31
 УИ М11
 СЛИА 4(М15)
 ПИНО ОШКС(М15)  ОШИБКИ КС, СС
 КК ’33’,ДРУБ
 МОДА АВУСТ
 И
 ПО АВБ1
 УИА 2(М15)  АППАРАТНЫЙ СБОЙ НМБ (АПСБ)
ПЕЧСБ СЧИ М15
 СЛИ М15(М11)
 И П3
 УИ М11
 СЧИ М15
 СДА 64+2
 УИ М15
 СЧ ЕСЧ(М11)
 СЛЦ СБОИМБ(М15)
 ЗП СБОИМБ(М15)    ЧИСЛО СБОЕВ МБ
 ПБ ПСЧБ
АВБ1 КК ’33’,ДРУБ
 МОДА ИНФСБ
 И
 ПО АВБ2
 УИА 1(М15)  ИНФОРМАЦИОННЫЙ СБОЙ МБ (ИНФСБ)
 ПБ ПЕЧСБ
АВБ2 КК ’33’,ДРУБ
 И Е44
 ПО АВБ3
 УИА (М15)   СЕКТОР НЕ НАЙДЕН (СНН)
 ПБ ПЕЧСБ
АВБ3 КК ’33’,ДРУБ
 И ОШМЕХП
 ПО АВБ4
 УИА 3(М15)  ОШИБКА АДРЕСА ТРАКТА (ОШАТБ)
 ПБ ПЕЧСБ
АВБ4 КК ’33’,ДРУБ
 И Е27
 ПО АВБ5
 УИА 4(М15)  ОШИБКА АДРЕСА СЕКТОРА (ОШАСБ)
 ПБ ПЕЧСБ
АВБ5 КК ’33’,ДРУБ
 И Е19
 ПО АВБ6
 УИА 5(М15) КОММУТАТОР МАГНИТНЫХ ГОЛОВОК НЕИСПРАВЕН (КГН)
 ПБ ПЕЧСБ
АВБ6 УИА 7(М15)  ПРОЧИЕ СБОИ
 ПБ ПЕЧСБ
ОШКС УИА 6(М15) ОШИБКИ КС, СС
 ПБ ПЕЧСБ
ПСЧБ СЧ К2013
 ПВ ПОК(М13)      УСТ.ПОК
 ПВ ПОДГРП(М11)
 СЧ ШЗПОМБ
 ПБ ДИСМБ2
СБОЙ2 СЧ АСБОЙБ
 ЗП 3(М1)         М33
 СЧ ИВ25
 ЗП 5(М1)         М27,М21
СБОЙ3 СЧ ЯЧЧП(М1)
 ИЛИ ЕЧП
 ЗП ЯЧЧП(М1)
 ПБ (М10)
ЗАККАЧ СЧ КУС1Б-1(М16)
 ЗП РЭ2
 СДА 64+42
 НТЖ ПР13
 УИА (М15)
 ПО ПСЧББ2
 СЧ ЛИС1Б-1(М16)
 ПБ СТ410
*
*
* ДИСПЕТЧЕР ПРОГРАММЫ ОБСЛУЖИВАНИЯ МЛ
ДИСЛ ПВ ДЛЯДИС(М10)
 СЧ ШОЧМГ-24(М1)
 ПВ ПОДОБМ(М11)   ВЫДАТЬ ЗАКАЗ НА ПОДВОД/ОБМЕН
ДИСП1 НТЖ Е1
 ПО ДИСП1А        ТРЕБУЕТСЯ ВЫБРОС ЗАКАЗА
 НТЖ Е2Е1
 ПЕ ДИСП1Б        ЗАКАЗ ВЫДАН ИЛИ ЗАНЯТО НАПРАВЛЕНИЕ
 УИА ДИСП1(М13)
 ПБ АВД           РАСП:ВКЛ.МГ,ВКЛ.ЗП; ПОВТ.ВЫП.ЗАКАЗА
ДИСП1Б УИА ’2003’
 СЧ МГРП
Д1 ИЛИ МПРЛ
 ЗП МГРП          ОТКРЫТЬ МАСКУ ПРЕРЫВ.ПО ПРОХ.ЗОНЫ
 РЕГ ’36’
 ПБ ВХОДМЛ
*
Д76 СЧ СОЛ-3(М16)
 И Е32(М15)
 ПО ДИ
 СЧ СОЛ-3(М16)
 ИЛИ Е40(М15)      УСТ.ПРИЗНАК: ОКОНЧИТЬ ПОДВОД
 ЗП СОЛ-3(М16)
ДИ СЧ ШОЧМГ-24(М1)
 ЧЕД 0
 СДА 64+1
 ПО Н533
 ПВ ДЛЯДИС(М10)
 СЧ ОЧКУС-1(М14)
 И Е5
 ПО ВХЛ
 СЧ ШОЧНАП-3(М2)
 ПО ДВИ1     НАПРАВЛЕНИЕ НЕ АКТИВНО
 НЕД
 УИ М10
 СЧ ОЧКУС-1(М10)    АКТИВНЫЙ ЗАКАЗ
 НТЖ ОЧКУС-1(М14)   ЗАКАЗ НОМБОБА
 И НАПРМГ
 ПЕ ДВИ1    АКТИВНО НЕ УСТР-ВО НОМБОБА
*-------  ОФОРМЛЕНИЕ ФИКТИВНОГО ВЫПОЛНЕИЯ ЗАКАЗА НОМБОБА
 СЧ Е5    ПРИЗНАК ФИКТИВНОГО ВЫПОЛНЕНИЯ
 МОД ТУС+22 ТУС НОМБОБА
 ЗП 8        НА 16-ЫЙ РЕГИСТР
 ПБ ДИСП1А   НА ВЫБРОС ЗАКАЗА
ДВИ1 УИА -42(М10)
 СЧ ШОЧМГ-24(М1)
 НЕД 0
 УИ М11
 СЧ Е48-1(М11)
 НТЖ ВСЕЕД
 ЗП ЗОНАТР
 СЧ ВСЕЕД
 НТЖ Е48-1(М14)
 ЗП РЯО5
 УИИ М5(М15)
 УИИ М6(М16)
 УИИ М4(М17)
 СЧ Е18
 ПВ ЗАХЗАН(М15)
ДВ СЧ ШОЧНАП+44(М10)
 СБР РЯО5
 РЗБ ЗОНАТР
 ЗП ШОЧНАП+44(М10)
ФШ ЦИКЛ ДВ(М10)
 СЧ ШОЧМГ-24(М1)
 ИЛИ Е48-1(М11)
 ЗП ШОЧМГ-24(М1)
ДВ1 СЧ ОЧКУС-2(М14)
 ЗП ОЧКУС-1(М14)
 СЧ ОЧЛИС-2(М14)
 ЗП ОЧЛИС-1(М14)
 СЧ ОЧКСУМ-2(М14)
 ЗП ОЧКСУМ-1(М14)
 СЧ Е48(М14)
 НТЖ Е48+1(М11)
 ПО ДВ2
 СЛИА -1(М14)
 ПБ ДВ1
ДВ2 СЧ ФКУС
 ЗП ОЧКУС-1(М11)
 СЧ ФКУСА
 ЗП ОЧЛИС-1(М11)
 СЧ Е18
 ПВ ГАШЗАН(М15)
 УИА 3
 УИИ М15(М5)
 УИИ М16(М6)
 УИИ М17(М4)
 УИИ М14(М11)
 ПБ Н533
ДИСП1А УИА ВХЛ(М11)
 ПБ СНЗКМЛ
ДИСМ СЧ РАОБЩ
 ИЛИ Е1
 ЗП РАОБЩ
ДИСПО УИА ’2003’
 СЧ ШЗЗОБМ
 НТЖ Е1
 ПО ВХСМЕ
 СЧ ШЗЗОБМ
 ИЛИ Е40
 НТЖ Е40           ГАШ.ПРИЗН.ЗАНЯТОСТИ П.О.МЛ ВЫДАЧЕЙ
 ЗП ШЗЗОБМ        ЗАКАЗА
ДИСО1А ПЕ ДИСПО3        ЕСТЬ ЗАКАЗЫ НА ОБРАБОТКУ ПРЕРЫВАНИЙ
 СЧ РАОБЩ
 И Е25
 ПО ДИ1
 НТЖ РАОБЩ
 ИЛИ Е1
 ЗП РАОБЩ
 СЧ РМЛ
 ИЛИ Е47П43
 ЗП РМЛ
ДИ1 СЧ ИВ25
 НТЖ Е19
 МОД ГУС
 ЗП 5
 СЧ ШЗЗПСЧ
 ПВ ВШГП(М15)
 СЧ ШСЗПСЧ
 ПВ ВШГ(М15)
 СЧ 0
 ЗП ШСЗПСЧ
 ЗП ШЗЗПСЧ
 СЧ УКАЧАЙ
 ИЛИ УКАЧП
 ПО ВИСП16
 ПБ БИНОТ2
ДИСПО3 НТЖ Е1
 ПО ВХСМЕ
 НЕД Е15П2
 И П77777
 УИ М16           N НАПР.(ПО ПРИОР.) ДЛЯ ОБР.ПРЕР.->М16
ПРЛ1 ЗП 6     ДЛЯ ОТЛАДКИ
 ПБ ВХДРМЛ
В587 УВВ ’4100’(М16)   ОПРОС СОСТОЯНИЯ МГ
 ЗП РЯО4
 РЕГ ’237’         ОПРОС ГРП
 И Е39(М16)
 ПЕ ПРЛ1          ЕСТЬ СИГНАЛ ПРЕРЫВ,ПОГАСИТЬ СПРМЛ
 ЗП МПРЛ          0->МПРЛ
 СЧ РЯО4
 И ОШМГ-3(М16)
 И СОЛ-3(М16)
 НТЖ СОЛ-3(М16)
 И П377
 ЗП ОКДВИЖ        NN МГ,ОТ К-РЫХ ПОЛУЧ.ПРЕРЫВ.->ОКДВИЖ
ПУЛ СЧ П377
 ЗП ОШМГ-3(М16)
 СЧ ОКДВИЖ
 УИА 3
ПРЛ2 ПО ДИСП4         ОБРАБОТАНЫ ФИКСИРОВ.ПРЕРЫВ.ОТ НАПР.
 ЗП ЗОНАТР
 СЧ Е48
 ЗП ИПЗМЛ+33
 СЧ ЗОНАТР
 ПВ ОБРПР(М11)    ОБРАБОТКА ПРЕРЫВАНИЯ
 ПЕ ДИСП5         ЗАКАЗ ВЫПОЛНЕН ИЛИ ВЫКЛ.МГ/КН.ЗП
 ПБ ПРЛ2Е
ПРЛ2В УВВ ’4100’(М16)
 И Е16(М15)
ПРЛ2К ПО ПРЛЕН
 УИА 1027
 СДА 64-16
 ЗП СМ
 СЧИ М16
 ВЧ П3
 СДА 64-3
 ЗП РМР
 СЧ СМ
 МОД РМР
 СДА 64
 ИЛИ ДЛЯНБ
 ЗП ДЛЯНБ
 УИА 3
ПР10 НОП -----
 СЧ ОЧКУС-1(М14)
 И Е11
 ПЕ ПР
ПР1 СЧ ШОЧНАП-3(М2)
 И Е48-1(М14)
 ПО ДИСП5Б
 НТЖ ШОЧНАП-3(М2)
 ЗП ШОЧНАП-3(М2)
 СЧ 0
 ЗП КУС2-1(М2)
 ПБ ПРЕС5
ПР2 УИА 3
 УИИ М16(М6)
 СЧ УХО
 НТЖ Е48-5(М2)
 ЗП УХО
 СЧ 0
 ЗП ШКЗЗП-5(М2)
 ПБ ПР1
ПРЛЕН МОДА (М10)
 ПВ (М11)         ПРОДОЛЖИТЬ РАБОТУ НАД ЗАКАЗОМ
ПРЛ2А ПО ДИСП5Б        ЗАКАЗ НЕ ВЫДАН,ИДЕТ ОБРАБ.ПРЕДЫД.ЗК
 НТЖ Е2Е1
 ПО ПРЛ2Г         ПРОВЕРКА ВЫБРАН ЛИ ЗАКАЗ
 НТЖ Е2Е1
ДИСП5 НТЖ Е1
 ПО ДИСП5А ТРЕБ ВЫБРОС ЗАК
 УИА ПРЛ2А(М13)
 ПБ АВД РАСП:ВКЛ,ВКЛ.ЗП,ПОВТ ЗАК
ДИСП4 УИА ’2003’
 ПБ ВХДРМЛ
ДИСП4А НОП ------
 СЧ ШЗЗОБМ
 ИЛИ Е47(М16)
 НТЖ Е47(М16)
 ЗП ШЗЗОБМ
 СЧ Е39(М16)
 ИЛИ МГРП
 ПБ Д1
ДИСП5А ПВ СНЗКМЛ(М11)   ВЫБРОС ЗАКАЗА
 УИА ПРЕС7(М10)
 ПБ ДЛЯДИС
*
*
ДИСП5В СЧ ШОЧМГ-24(М1)
 ПО ДИСП5Б        НЕТ ЗАКАЗОВ К ЭТОМУ МГ
 УИА ПРЛ2А(М11)
ПОДОБМ НЕД 0
 УИ М14
 СЧ МГНЕИС
 И Е32-24(М1)
 ПЕ ОБХОД
 СЧ ТЗНМЛ-24(М1)
 И БЕЗДУБ
 ПЕ ВЫБ1
 СЧ ОЧКУС-1(М14)
 СДА 64+24
 И П7777
 СДА 64-1
 ПБ ВЫБН1
ВЫБ1 СЧ ОЧКУС-1(М14)
 СДА 64+24
 И П7777
ВЫБН1 НОП
 ЗП ЗОНАТР
 СЧ ОЧКУС-1(М14)
 И Е21Е1
 НТЖ Е21Е1
 ПЕ ВЫБНВ2  НЕ ПОДВ С ЧТ СЛ
 СЧ ЗОНАТР
 ВЧ Е1
 ЗП ЗОНАТР
ВЫБНВ3 СЧ ОЧКУС-1(М14)
 И Е6
 СДА 64+5
 ИЛИ ЗОНАТР
 ЗП ЗОНАТР
 СЧ ОЧКУС-1(М14)
 И Е4
 ПО ВЫБНВ2 НЕ РАЗМ
 СЧ РЕЖРАЗ
 И Е38
 СДА 64+37
 ИЛИ ЗОНАТР
 ЗП ЗОНАТР
ВЫБНВ2 СЧ ТЗОНМЛ-24(М1)
 ПЕ ПОДВ5Б
 СЧ ТЗНМЛ-24(М1)
 И БЕЗДУБ
 ПО ВЫБВ2
 СЧ ЗОНАТР
 СЛЦ П7777
 ПО ПОДВ5Б
ВЫБВ2 НОП
 СЧ ЗОНАТР
 СЛЦ Е15П2
ПОДВ5Б НОП
 И П7777
 ЗП ЗОНАМЛ  ТЕК ЗОНА
 ПВ ЗОНТАБ(М10)
 СЧ СОЛ-3(М16)
 И Е32(М15)
 ПО ПОДВ1Е
 СЧ СОЛ-3(М16)
 ИЛИ Е40(М15)
 ЗП СОЛ-3(М16)
 ПБ ВХЛ
ПОДВ1Е СЧ ОЧКУС-1(М14)
 И Е2
 ПО ПОДВ1 НЕ ПОДВ БЕЗ ОТВ
 СЧ СОЛ-3(М16)
 ИЛИ Е32(М15)
 ЗП СОЛ-3(М16)
ПОДВ1 СЧ ОЧКУС-1(М14)
 И Е5
 ПЕ ЗКМЛ
 СЧ ОЧКУС-1(М14)
 И П7
 ПЕ ПОДВ2 ПОДВ
 СЧ ОЧКУС-1(М14)
 И Е18
 ПО ПОДВ3
 СЧ ОЧКУС-1(М14)
 И ПР14
 ПЕ ПОДВ2 ТЕСТ РЕЖ,РАЗМ
 СЧ ОЧКУС-1(М14)
 И Е21
 ПЕ ПОДВ4
ПОДВ2 СЧ ЗОНАТР
 НТЖ ЗОНАМЛ
 ПО ПОДВ92  МЛ ПОДВЕДЕНА
ПОДОШ СЧ ОЧКУС-1(М14)
 И Е18
ПОДВ3 ПЕ ПОДВ98
 СЧ ОЧЛИС-1(М14)
 И Е19
 ПЕ ПОДВ80
ПОДВ4 СЧ ЗОНАТР
 ВЧ Е1
 ЗП ЗОНАТР
 ПВ ЗОНТАБ(М10)
 СЧ ЗОНАТР
 НТЖ ЗОНАМЛ
 ПО ПОДВ93  ПОДВЕД К ПРЕДЗОНЕ
ПОДВ90 СЧ ОЧКУС-1(М14)
 ИЛИ Е21Е18
 ЗП ОЧКУС-1(М14)
ПОДВ98 УИА НЕСЧЕТ(М10)
 ПБ ЗОНТАБ
ПОДВ91 СЧ СОЛ-3(М16)
 ИЛИ Е16(М15)
 ЗП СОЛ-3(М16)
 СЧИ М10
ДВИЖМЛ НОП
 УИИ 4(М15)
 ПВ ПВВМЛ(М15)
ВОЗВМЛ НОП ПОСЛЕ АДАП ПРИ ВСЕХ ОПЕРАЦ
 СЧ СОЛ-3(М16)
 УИИ М15(4)
 ИЛИ Е8(М15)
 ЗП СОЛ-3(М16)
 ПБ ЗПМГ
ПОДВ82 СЧ СОЛ-3(М16)
 И Е40(М15)
 ПО ПОДВЕД
 ИЛИ Е16(М15)
 ИЛИ Е32(М15)
 ПВ ГАШСОЛ(М10)
ОБХОД2 УИА (М1)
 ПБ ВЫХ1
ПОДВЕД СЧ ЗОНАТР
 НТЖ ЗОНАМЛ
 ПЕ ПОДВЕС  НЕ ПОДВЕД
 СЧ СОЛ-3(М16)
 И Е48(М15)
 ПЕ ЗКМЛ
 СЧ СОЛ-3(М16)
 И Е32(М15)  ПОДВ НЕ МАТЕМ
 ПО ПОДВ83
 ПБ УНПР
ПОДВ83 СЧ ОЧКУС-1(М14)
 И Е1
 ПО ЗКМЛ
 ПБ ВЫХ1А
ПОДВ93 СЧ ОЧКУС-1(М14)
 ИЛИ Е21Е18
 ЗП ОЧКУС-1(М14)
ПОДВ92 СЧ ОЧКУС-1(М14)
 И П3
 ПО ЗКМЛ НЕ ПОДВ
 СЧ Е32(М15)
УНПР ИЛИ Е16(М15)
 ИЛИ Е40(М15)
 ПВ ГАШСОЛ(М10)  ПОГАС ПРИ ПОДВ
ВЫХ1А СЧ ОЧКУС-1(М14)
 И Е21Е1
 НТЖ Е21Е1
 ПЕ ВЫХОД1
 СЧ ТЗОНМЛ-24(М1)
 СЛЦ Е13
 ЗП ТЗОНМЛ-24(М1)
 ПБ ЗКМЛ
ДИС6 ПИНО ПРЛ2Б(М6)
 УИА 1(М6)
 СЧИ М2
 И Е1
 ПЕ ДИСП6А
 УИА -1(М6)
ДИСП6А СЧ ВСЕЕД
 МОДА (М6)
 НТЖ ШКЗЗП-5(М2)
 МОДА (М6)
 И ШОЧНАП-3(М2)
 ПО ДИС6
ДИСП7 НЕД 0
 УИ М14
 СЧИ М15
 ЗП ЯМ15
 СЧИ М16
 ЗП ЯМ16
 СЧ ОЧКУС-1(М14)
 СДА 64+39
 И Е3П1
 УИ М16           УСТ.В М16 НОМЕР НОВОГО НАПРАВЛЕНИЯ
 ПБ ПРЛ2Б
ДИСП7Г ПВ ЗКМЛА(М11)
ДИСП7А НТЖ Е1
 ПО ДИСП7Б        ТРЕБУЕТСЯ ВЫБРОС ЗАКАЗА
 НТЖ Е2Е1
 ПЕ ДИСП7В        ЗАКАЗ ВЫДАН
 УИА ДИСП7А(М13)
АВД ПВ АВ(М11)
 НТЖ Е1
 ПЕ АВД1          ВЫБРОС ЗАКАЗА НЕ ТРЕБУЕТСЯ
 СЧ Е1
 ПБ (М13)
АВД1 УИИ М11(М13)
 ПБ (М10)
ДИСП7Б ПВ СНЗКМЛ(М11)
ДИСП7В СЧ ЯМ15
 УИ М15
 СЧ ЯМ16
 УИ М16
ПРЛ2Б СЧ ОКДВИЖ
 ПБ ПРЛ2
ВЫХ1 СЧ Е1
 ПБ (М11)
ВЫКЛМГ УИА 5(М5)
 ПБ ВЫХ2
НЕТОБ УИА 7(М5)
 ПБ ВЫХ2
ВЫХ3 СЧ Е2Е1
 ПБ (М11)
*
*
* БЛОК РЕАКЦИИ НА ПРЕРЫВАНИЯ ПО ОКОНЧАНИЮ ОБМЕНА С МЛ
*            (РАБОТАЕТ ПРИ РАЗМЕТКЕ МЛ)
*
ПРОБМ СЧ Е32(М16)
 ПВ ТВ67А(М15)    ГАШ.МАСКИ ГРП 27(26)Р
 СЧ ВСЕЕД
 НТЖ Е32(М16)
 УИ 0
 РЕГ ’37’          ГАШ.ГРП 27(26)Р
 СЧ КУС2-1(М16)
 И Е4
 ПО БМВ           КУС НЕ ЗАДАЧИ РАЗМЕТКИ
 СЧ РЕЖРАЗ
 И Е48
 ПО ПРОБМ4        НЕ РАЗМЕТКА ЗОНЫ
ПРОБМ3 СЧ П5
 ПБ ПРОБМ2
ПРОБМ1 РЖА 3
 СЧ Е2Е1
 УВВ ’141’         УСТ.РЕЖИМ РАЗМЕТКИ В КВУ
 СЧ РЕЖРАЗ
 СДА 64+39
 И П7
 ПО ПРОБМ3        К-ВО УДЛИНЕНИЙ=0
 ИЛИ Е48
 УМН П30
 СЧМР 64
ПРОБМ2 ЗП ЗТРАЗМ        УСТ.ВЕЛИЧИНУ УДЛИНЕНИЯ
ПРОБМ5 СЧ 0
 ЗП ТРАЗМ         СБРОС СЧЕТЧИКА
 СЧ РЕЖРАЗ
 ИЛИ Е35           УСТ.ПРИЗНАК: ЕСТЬ ПРЕРЫВАНИЕ
 ЗП РЕЖРАЗ        ПО ОКОНЧАНИЮ ОБМЕНА
 ПБ БМВ
ПРОБМ4 СЧ РЕЖРАЗ
 И Е46
 ПО ПРОБМ1        НЕ СТИРАНИЕ
 ПБ ПРОБМ5
*
*
* БЛОК РЕАКЦИИ НА ПРЕРЫВАНИЯ ПО ПРОХОДУ ЗОНЫ
*
ВЫХОД1 СЧ ОЧКУС-1(М14)
 И ПР14
 ПЕ ОБХОДР        *С ЗАКАЗ ОТ РАЗМЕТКИ
 ПБ ОБХОД2
ЗКМЛ СЧ Е16(М15)
 ИЛИ Е24(М15)
 ПВ ГАШСОЛ(М10)   ПОГАСИТЬ ПРИЗНАКИ ПОДВОДА
ЗКМЛОЧ НОП ----
ЗКМЛА ПВ ДЛЯДИС(М10)
 СЧ ОЧКУС-1(М14)
 И Е18
 ПЕ ЗАКМЛ         БУДЕТ ЧТ
 СЧ УХО
 И Е48-5(М2)
 ПО ЗАКМЛ         ЗП РАЗРЕШЕНА
 СЧ Е48-1(М14)
 ИЛИ ШКЗЗП-5(М2)
 ЗП ШКЗЗП-5(М2)
ВЫХ0 СЧ 0
 ПБ (М11)
ЗАКМЛ НОП ,
 УИА 0(М17)
 СЧ Е48-1(М14)
 ЗП ЗОЧ2-5(М2)
 СЧ ОЧКУС-1(М14)  ЗАПОМИН.КУС ЗАКАЗА ПОСЛЕ ЕГО ВЫДАЧИ
 И Е48П43
 ПО СТ411         ОБЩИХ ЛЕНТ НЕ Д.Б
 СЧ ОЧКУС-1(М14)
 ЗП КУС2-1(М2)
 СЧ ОЧЛИС-1(М14)
 ЗП ЛИС2-1(М2)
 ПВ ПРИП1С(М10)
 СЧ КУС2-1(М2)
 И Е18           ЗП НА МЛ*
 ПЕ ЗКМЛ1А        НЕТ,СЧ С МЛ
 СЧ ТЗНМЛ-24(М1)
 И БЕЗДУБ
 ПО ЗАК1
 СЧ КУС2-1(М2)
 СДА 64+24
 И П7777
 ПБ ЗАК2
ЗАК1 СЧ КУС2-1(М2)
 СДА 64+24
 СДА 64-1
 И П7777
ЗАК2 НОП
 ЗП РЯО3          2∧ЗОНА+10+ДЕЛЬТА
 СДА 64-12
 ИЛИ РЯО3
 ЗП ТЗОНМЛ-24(М1)
ЗКМЛ1 СЧ КНАПР-1(М2)
 УИ М10
 СЧ КУС2-1(М2)
 И Е6
 СДА 64+5
 ПЕ ЗКМЛА5
ЗКМЛБ СЧ КУС2-1(М2)
 И Е4
 ПО ЗКМЛА5        КУС НЕ ЗАДАЧИ РАЗМЕТКИ
 СЧ РЕЖРАЗ
 И Е38           ВЫДЕЛЕНИЕ ПРИЗНАКА НЕЧЕТНОСТИ ЗОНЫ
 СДА 64+37
ЗКМЛА5 ИЛИ ТЗОНМЛ-24(М1)
 ЗП ТЗОНМЛ-24(М1)
 ИЛИ КЛЮЧ
 ЗП 3(М10)
 ЗП 6(М10)
 СЧ ВРЕМЯ
 ЗП 4(М10)
 СЧ ОЧКСУМ-1(М14)
 ЗП 7(М10)
 СЧ КУС2-1(М2)
 СБР НАПРМГ
 ЗП (М10)
 СЧ ГОД
 И Е42П1
 ИЛИ (М10)
 ЗП (М10)
 СЧ КУС2-1(М2)
 СДА 64+42
 ПО ОБЛ
 УИ М6
 МОД ТУСП-1(М6)
 СЧ ШИФРМ
ОБЛ1 ЗП 1(М10)
 СЧ ТЗНМЛ-24(М1)
 И Е12П1
 ЗП РЯО3
 СДА 64-30
 ИЛИ РЯО3
 ИЛИ Е24П16
 ЗП 2(М10)
 СЧ УПНОВ
 ИЛИ БЕЗДУБ
 И ТЗНМЛ-24(М1)
 ПО ООБЛ1
 СДА 64-26    *  *  *  *  ЕСЛИ ТОЛЬКО
 ЗП РЯО3
 СДА 64+18              БЕЗДУБ И УПНОВ = Е21,Е20  *  *  *  *
 ИЛИ РЯО3  47-46 И 29-28 РАЗР.-ИЗ 21-20 РАЗР. ТЗНМЛ.
 ИЛИ 2(М10)
 ЗП 2(М10)
ООБЛ1 НОП
 СЧ П3
 ПВ ПОК(М13)
 УИА КЧПОСЛ(М5)
 УИА 0
 СЧ ’3777’
ТОЧКЧ НОП
 УИА 3
 ЗП 5(М10)
 СЧ ПР13
 ПВ ПОК(М13)
 СЧ ТЗНМЛ-24(М1)
 И Е13
 ПЕ МГНОР     ЛЕНТА ЕС
 СЧ ВСЕ6
 УИА 0
 ЗП ’3777’
 УИА 3
ЗКМЛ4 ПБ ЗКМЛ4К
КЧПОСЛ СЧ РЭ4
 СТОП ’2421’
 СЧ Е32-24(М1)
 МОДА ОШОС
 ИЛИ
 МОДА ОШОС
 ЗП
 СЧ 0
 ПБ ТОЧКЧ
ОБЛ СЧ КУС2-1(М2)
 СДА 64+6
 И Е1
 УИ М6
 СЧ ШИМЛ(М6)
 ПБ ОБЛ1
ЗКМЛ1А СЧ КУС2-1(М2)
 И Е3
 ПЕ ЗКМЛ4         ТЕСТОВЫЙ РЕЖИМ
 СЧ КУС2-1(М2)
 И Е23
 ПО ЗКМЛ4         *С НЕ НАЛОЖЕНИЕ
 СЧ ОЧЛИС-1(М14)
 СЛЦ Е16
 ЗП ОЧЛИС-1(М14)
 И Е16Е17
 НТЖ Е16Е17        ЧИСЛО НАЛОЖ.=3
 ПО ЗКМЛ2         *С ЧИСЛО НАЛОЖЕНИЙ =3
 И Е16
 ПО ЗКМЛ4
 СЧ ТЗНМЛ-24(М1)
 И Е25
 ПО ЗКМЛ4
 СЧ Е26
 ИЛИ ТЗНМЛ-24(М1)
 ПБ ЗКМЛ3
ЗКМЛ2 СЧ КУС2-1(М2)
 НТЖ Е23           ГАШ.ПРИЗНАКА НАЛОЖЕНИЯ
 ЗП КУС2-1(М2)
 СЧ ОЧЛИС-1(М14)
 НТЖ Е16Е17
 ЗП ОЧЛИС-1(М14)   ГАШ.СЧК
 СЧ ТЗНМЛ-24(М1)
 СЛЦ Е25
ЗКМЛ3 ЗП ТЗНМЛ-24(М1)  ГАШ.СЧ-КА ОБР-НИЙ К МЛ
 ПБ ЗКМЛ4
ЗКМЛ4К УИА -7(М6)
 СЧИ М16
 И Е1
 СДА 64-10
 НТЖ Е11
 ЗП РЯО3
 СЧ КУС2-1(М2)
 И Е18
 ПО ЗКМЛ4А        ЗАПИСЬ
 СЧ КУС2-1(М2)
 И Е23
 ПО ЗКМЛ4З
 УИА ЗКМЛ4И(М10)
 ПБ ОЧ3И6         ОЧИСТИТЬ 3-Е И 6-Е СС
ЗКМЛ4З МОД КНАПР-1(М2)
 КК ’32’,7(М6)       ОЧИС СС
 ЦИКЛ ЗКМЛ4З(М6)
ЗКМЛ4И МОДА Н1193
 КОНК К’003600000’
ЗКМЛ4М СЧ КУС2-1(М2)
 ИЛИ Е19
ЗКМЛ4П НОП ---
 ИЛИ РЯО3
 ЗП КУС
 СЧ Е3    ПРОГОН НА ЗОНУ ВПЕРЕД
 ЗП КУСДВ-5(М2)
 СЧ ЛИС2-1(М2)
 СДА 64+36
 УИ М10       ПАРАМЕТР ПВВМЛ
 МОДА ОШОС
 СЧ
 И Е32-24(М1)
 ПО ДАЛЬ
 СЧ КУС
 ИЛИ Е18
 ЗП КУС
ДАЛЬ НОП
 СЧ КУС
 ПБ ДВИЖМЛ   НА ОБМЕН
НАБОБ3 СТОП ’1177’
ЗКМЛ4Л СЧИ М16
 ЗП РСЧРАЗ        РАЗРЕШ.РАБОТЫ СЧ-КА РАЗМ
 СЧ РЕЖРАЗ
 И Е47
 ПО ЗКМЛ4Е        НЕ УДЛИНЕНИЕ ЗОНЫ
 ПБ ЗКМЛ4Ж
ЗКМЛ4Е СЧ РЕЖРАЗ
 И Е46
 ПО ЗКМЛ4Б НЕ СТИРАН
 СЧ 0
ЗКМЛ4В ИЛИ РСЧРАЗ
 ЗП РСЧРАЗ ПР ФОРМИР БЛАНКА ПРИ РАЗМ
 СЧ Е2
 УВВ ’141’
ЗКМЛ4Ж СЧ Е32(М2)
 ИЛИ МГРП
 ЗП МГРП          ОТКРЫТИЕ МАСКИ ПРЕРЫВ.ПО КОНЦУ ОБМ.
 РЕГ ’36’
 СЧ РЯР
 ПБ ЗКМЛ4Г
ЗКМЛ4А СЧ КУС2-1(М2)
 И Е4
 ПЕ ЗКМЛ4И
 ПВ ТАБЗОН(М10)   УСТ.ЗОНАТР И ЗОНАМЛ
 УИА ЗКМЛ4И(М10)
 ПБ ЗАПАК    УПАКОВКА СС
 ПБ Н1241
В1241 УВВ ’4100’(М16)
 И Е16(М15)
 ПЕ ВЫКЛМГ        ВЫКЛЮЧЕН МГ
 УВВ ’4100’(М16)
 И Е24(М15)
 ПО ЗКМЛ4И
ВЫКЛЗП УИА 6(М5)
 СЧ ТЗНМЛ-24(М1)
 СЛЦ Е25
 ЗП ТЗНМЛ-24(М1)
ВЫХ2 СЧ Е2
 ПБ (М11)
ЗКМЛ4Б СЧ РЕЖРАЗ
 И Е48
 ПО ЗКМЛ4Г        КОНТР. ЧТЕНИЕ ЗОНЫ
 ПБ ЗКМЛ4В
*
ОБРПР НЕД Е3П1          ВЫДЕЛЕНИЕ СТАРШЕГО МГ НАПР.ОКОНЧИВШ.
 И Е3П1          ДВИЖЕНИЕ
 УИ М15
 НТЖ Е3П1
 УИ М17           НОМЕР МГ
 УИА ДЛЯДИС(М2)
 УИА Г(М10)
 ПБ ПРЛ17
Г СЧ ШОЧМГ-24(М1)
 ПЕ ПРУСТ
ЛОЖПР СЧ ОКДВИЖ
 ИЛИ Е8(М15)
 НТЖ Е8(М15)
 ЗП ОКДВИЖ
 ПБ ПРЛ2
Б61 НОП ----
Б62 УИА ВЫКМГ(М10)
 ПО ОБРПР1        МГ ВКЛЮЧЕН
 СЧ ВСЕЕД
 НТЖ Е27
 НТЖ Е26Е25
 И ТЗНМЛ-24(М1)
 ЗП ТЗНМЛ-24(М1)
 ПБ ГАШСМ1
ОБРПР1 СЧ ТЗНМЛ-24(М1)
 ПО ВЫКМГ         *С ТАБЛИЦА С НОМЕРОМ БОБИНЫ = 0
 СЧ СТМГ-24(М1)   СТАТИСТИКА МЛ: НАКОПЛЕНИЕ НАРАБОТКИ
 ПО НРБ           МГ БЫЛ СВОБОДЕН
 СДА 64+6
 НТЖ ТЗНМЛ-24(М1)
 И П7777
 ПО НРБ1          *С БОБИНА НА МГ ПРЕЖНЯЯ
НРБ2 СЧ СТМГ-24(М1)
 ПВ ДСЛ(М3)
 ПО Н1314
НРБ СЧИ М1
 ЗП СТМГ-24(М1)   N МГ
 СЧ ТЗНМЛ-24(М1)
 И П7777
 СДА 64-6          N БОБИНЫ
НРБ1 ИЛИ СТМГ-24(М1)
 СЛЦ Е25
 ЗП СТМГ-24(М1)
 И Е36
 ПО Н1314
 ПБ НРБ2
ВЫКМГ УИА 0(М17)
 СЧ СОЛ-3(М16)
 И Е16(М15)
 ПЕ ВЫКМГ1        МЛ УЧАСТВУЕТ В ПОДВОДЕ
 СЧ КУС2-1(М2)
 И Е4
 ПО ВЫКМГ1        НЕ РЕЖИМ РАЗМЕТКИ
 СЧ РЕЖРАЗ
 И Е46
 ПЕ ПРЛ3Б         *С Е46 В РЕЖРАЗ
ВЫКМГ1 СЧ СОЛ-3(М16)
 И Е24(М15)
 ПО ПРЛ3А         ПРЯМОЕ НАПРАВЛЕНИЕ
 СЧ Е2
ПРЛ3А УИ М10
 СЧ ТЗОНМЛ-24(М1)
 И П7777
 ЗП ЗОНАМЛ
 ПИО ПРЛ3А3(М10)
 СЛЦ П7777
ПРЛ3А1 И П7777
 ЗП ЗОНАМЛ
 СЧ ТЗОНМЛ-24(М1)
 ИЛИ П7777
 НТЖ П7777
 ИЛИ ЗОНАМЛ
 ЗП ТЗОНМЛ-24(М1)
 ПВ ТАБЗОН(М10)   УСТ.ЗОНАТР И ЗОНАМЛ
ПРЛ3Б СЧ ШОЧМГ-24(М1)
 НЕД 0
 УИ М14           НОМЕР ЗАКАЗА
 СЧ ОКДВИЖ
 НТЖ Е8(М15)
 ЗП ОКДВИЖ
 СЧ Е8(М15)
 УИА 11(М5)
 ИЛИ Е24(М15)
 ПВ ГАШСОЛ(М10)   ПОГАСИТЬ ПРИЗН.ДВИЖ.И ОБРАТНОГО НАПР
 СЧ МГНЕИС
 И Е32-24(М1)
 ПЕ АВ            МГ НЕИСПРАВЕН
 ПБ Н1348
ПРЛ3А3 СЛЦ Е1
 ПБ ПРЛ3А1
НОРМА СЧ СОЛ-3(М16)
 И Е16(М15)
 ПЕ ОХТЫ          *С ПОДВОД
 ПБ Н1351
НОРМА2 СЧ КУС2-1(М2)
 ПО ПР10
 УИА (М17)     ПРИЗНАК МЛ
 СЧ ЛИС2-1(М2)
 ПВ ПРИП1С(М10)
 СЧ КУС2-1(М2)
 И Е18
 ПЕ ПРЛСЧ         СЧИТЫВАНИЕ С МЛ
 СЧ КУС2-1(М2)
 И ПР14
 ПО НОРМА1        КУС НЕ ЗАДАЧИ РАЗМЕТКИ
 УИА 5(М10)
 ПВ ПРЛСЧА(М5)
 ПВ ВОС1(М10)     ВОССТ.ПОСЛ.СЛОВО
 УВВ ’4107’
 И П17
 СДА 64-30
ОБХОД1 ЗП РЯО5
 СЧ ЗОЧ2-5(М2)
 НЕД 0
 УИ М14
ОБХОД4 СЧ
 СДА 64+8
 СДА 64-6
 ИЛИ РЯО5          СОСТ.’УХО’,ВКЛ.МГ,КН.ЗП
 ЗП РЯО5          ДЛЯ ОТВЕТА ЗАДАЧЕ-ТЕСТУ ИЛИ РАЗМ.
 СЧИ М1
 ИЛИ РЯО5          НАПР.И МГ ФИЗ
 ЗП РЯО5
 СЧ ОЧКУС-1(М14)
 И Е4
 ПЕ ОБХОД
 СЧ ОЧКУС-1(М14)
 И Е21Е1
 НТЖ Е1
 ПО ОБХОД         ТОЛЬКО ПОДВОД
 СЧ 0
 ЗП КУС2-1(М2)
ОБХОД УИА ОБХ3(М10)
 ПБ ГАШСЧК
ЗН УИИ М15(М3)
 УИИ М16(М6)
 УИИ М3(М10)
 ПБ (М5)
В1404 УВВ ’4100’(М16)
 И Е16(М15)      МГ ВКЛЮЧЕН*
 ПЕ ВЫКЛМГ        ВЫКЛЮЧЕН МГ
ПРН СЧ ДРЛ
 И Е4
 ПЕ БЫЛОБМ        БЫЛА ПЕРЕДАЧА ПО КАНАЛУ
 СЧ ДРУЛ
 И Е34           КОЛЬЦО
 ПЕ ВЫКЛЗП
 ПБ НЕТОБ
*
БЫЛОБМ НОП ,
 СЧ ДРЛ+1   (КАНАЛ НЕ КОНЧИЛ)
 И Е45Е46
 СЧ ТЗОНМЛ-24(М1)
 ИЛИ Е45    ПРОПУСК УХО=1 ПРИ ЗАПИСИ В ЗОНУ
 ЗП ТЗОНМЛ-24(М1)
 СЧ ТЗНМЛ-24(М1)
 И БЕЗДУБ
 ПЕ ПРЗП7
 СЧ ТЗОНМЛ-24(М1)
 СЛЦ Е48
 ПЕ ПРЗП7 ОБМЕН ДУБЛЬ ЗОНОЙ
 ЗП ТЗОНМЛ-24(М1)
 СЧ КНАПР-1(М2)
 УИ М10
 СЧ 6(М10)
 СЛЦ Е1            +1 К N ЗОНЫ В 6-М И 3-М СЛУЖ.СЛОВАХ
 ЗП 6(М10)
 ЗП 3(М10)
 УИА ЗКМЛ4(М10)
 ПБ ВЫХ0          ТРЕБУЕТСЯ ЗАПИСЬ ДУБЛЬ-ЗОНЫ
ПРЗП7 ПВ РСП2(М10)
ПРЛЗП4 ПВ ПРЛВОС(М10)   ВОССТ.ПОСЛ.СЛОВО
 УИА 10(М5)
 СЧ ТЗОНМЛ-24(М1) НЕТ
 И Е45
 ПО ПРЛСЧМ
АВ УИИ М6(М16)
 УИИ М10(М15)
 СЧ ЛЕНТА
 ПВ ТБУФ(М15)
 СДА 64-8
 УИ М7
 УИА 3
 УИИ М15(М10)
 СЧ ЛИС2-1(М2)
 УИА 0(М17)      М17 := 0  - ДРАЙВЕР МЛ
 ПВ ПРИП1С(М10)
 УИИ М10(М15)
 ПБ (М7)
ОХТЫ СЧ ОЧЛИС-1(М14)
 И Е19
 УИА ПОДВ82(М10)
 ПО ВЫХ0
 НТЖ ОЧЛИС-1(М14)     РЕВЕРС
 ЗП ОЧЛИС-1(М14)
 ПБ НЕОХ
ОХ5017 УИА ПОДВ90(М10)
 ПЕ ВЫХ0          МГ ВЫКЛЮЧЕН
 УИА ЗКМЛ(М10)
 ПБ ВЫХ0
ПРЛ17А СДА 64+7
 И Е3П1
 УИ М17
 НТЖ Е3П1
 УИ М15
ПРЛ17 МОД КНАПР-1(М16)
 УИА (М1)
 СЛИ М1(М17)
 ПБ (М2)
ПРЛСЧ УИА ПРЛСЧВ(М5)
 СЧ ДРЛ
 И Е1
 ПО РАСПАК
 СЧ КУС2-1(М2)
 И Е5
 ПЕ РАСПАК
 УИА ПРЛ2А(М11)
 ПБ ЗКМЛ
ПРЛСЧ7 УИА 6(М10)
 УИА ’2003’        УСТ.БЛПР
ПРЛСЧА СЧ К2003         *С СЧИТЫВАНИЕ СЛУЖ.СЛОВ ПО М10
 ПВ ПОК(М13)      ГАШ.ПОК
 МОД КНАПР-1(М2)
 СЧ (М10)
 УИ 0
 ЗП СМ
ВЫХ СЧ К2013
 ПВ ПОК(М13)      УСТ.ПОК
 СЧ СМ
 ПБ (М5)
ПРЛСЧБ МОД И16
 УИА (М16)
 ПБ ВЫХ
ПРЛСЧВ ЗП РЯО5
 УИА 3(М10)        *С ЗА 3-М СЛУЖ.СЛОВОМ
 ПВ ПРЛСЧА(М5)
 ИЛИ РЯО5
 ЗП РЯО5          6-Е ∨ 3-Е СЛУЖ.СЛОВА
 СЧ КУС2-1(М2)
 И ПР14
 УИА 64(М5)
 ПЕ АВ
 УИА 2(М10)        *С ЗА 2-М СЛУЖ.СЛОВОМ
 ПВ ПРЛСЧА(М5)
 ЗП БОБ
 УИА 3
 УИА 5(М5)
 СЧ ГТ
 ПЕ АВ МГ ВЫКЛЮЧЕНА
 ПБ ПРЛСЧП
ПРЛКЛ УИА 1(М5)
 СЧ РЯО5
 СДА 64+12
 НТЖ РЯО5
 И П7777
 СДА 64+1
 ПЕ АВ            NN ЗОН ОТЛИЧАЮТСЯ В СТАРШИХ РАЗРЯДАХ
 УИА 12(М5)
 СЧ КУС2-1(М2)
 И Е5
 ПЕ АВ            ЗАКАЗ ОТ НОМБОБА
 СЧ ТЗНМЛ-24(М1)
 И Е39
 ПЕ ПРЛСЧО        БОБИНА НАЗВАНА ПО 7-МУ ПРИКАЗУ
 УИА -2(М5)
 УИА 64(М10)
НАБОБ1 СЧ БОБ
 СДА (М10)
 ПИО НАБОБ2(М5)
 ИЛИ БОБ
НАБОБ2 НТЖ ТЗНМЛ-24(М1)
 И Е12П1
 ПО ПРЛСЧО        НОМЕР БОБИНЫ ПРАВИЛЬНЫЙ
 УИА 64+30(М10)
 ЦИКЛ НАБОБ1(М5)
 УИА 3(М5)
 СЧ ТЗНМЛ-24(М1)
 И Е40
 ПЕ АВ            БОБ.ИЗМЕНЕНА ПО ДИРЕКТИВЕ
 СЧ КУС2-1(М2)
 И Е21
 ПЕ АВ
ПРЛСЧО СЧ РЯО5
 СЛЦ Е1
 НТЖ ЗОНАМЛ
 И П7777
 УИА 2(М5)
 ПЕ АВ            ДРУГАЯ ЗОНА
 СЧ ЛИС2-1(М2)
 ПВ ПРИП1С(М10)   НА УСТ.ПРИП. 1-ОЙ СТР.
 СЧ КУС2-1(М2)
 И Е21           БЫЛО СЧ.СЛУЖ.СЛОВ*
 ПО ПРЛСЧ1        *С НЕ СЧИТ. СЛУЖ. СЛОВ
 СЧ КУС2-1(М2)
 И Е11
 ПЕ ПРЛР11        *С КОНТРОЛЬ ЗП ЧТЕНИЕМ
 СЧ Е21Е18        ГАШ.ПРИЗН.ЧТ.СЛ.СЛОВ
 НТЖ ОЧКУС-1(М14)
 ЗП ОЧКУС-1(М14)
 ЗП КУС2-1(М2)
 УИА 9(М5)
 ПВ ГАШСМ1(М10)   ГАШЕНИЕ СМЕЩЕНИЯ
 СЧ ОЧЛИС-1(М14)
 И Е18
 ПЕ АВ            ПРИ ЧТ.СС ПРЕДЗОНЫ УЖЕ БЫЛА ОШИБКА
 СЧ УХО
 И Е48-5(М2)
 УИА ЗКМЛА(М10)
 ПО ВЫХ0          ЗП ПО НАПРАВЛЕНИЮ РАЗРЕШЕНА
 СЧ 0
 ЗП КУС2-1(М2)    ОСВОБОЖДЕНИЕ НАПРАВЛЕНИЯ
 СЧ Е48-1(М14)    ЗАКАЗ НЕ МОЖЕТ ВЫПОЛНЯТЬСЯ, НАПРАВЛ.
 ИЛИ ШКЗЗП-5(М2)
 ЗП ШКЗЗП-5(М2)
ДИСП5Б УИА (М6)
 ПБ ДИСП6А
ПРП ПАМ 0
 УИА -1023(М12)
 УИА ПРП3(М5)
ПРПА СЧ П3
 ПВ ПОК(М13)      ГАШ.ПОК
 СЧ 0
 УИ ’21’          ГАШ.БЛЗ,БЛП
ПРП1 СЧ ’3777’(М12)
 УИ 0
 ЦИКЛ ПРП1(М12)
 УИА ’2003’
 ПИО ПРП11(М4)
 ЗП 2(М4)
ПРП11 ПАМ 0
 СЧ ПР13
 ПВ ПОК(М13)      УСТ.ПОК
 УИА 5(М10)
 ПВ ПРЛСЧА(М5)
 ПВ ВОС1(М10)
 РЕГ ’101’
 СЧ ТЗНМЛ-24(М1)
 И Е13
 ПЕ БЛИСХ1    РАЗМЕТКА ЕС
ПРП2 СЧ 0
 ПБ ОБХОД1
ПРП3 УИА ’2000’
 ЗП ’3777’(М12)
 УИА ’2003’
 ПБ ПРПА
ПРЛСЧ1 УИА 5(М10)        ЗА 5-М СЛУЖ.СЛОВОМ
 ПВ ПРЛСЧА(М5)    ВЫБОР 5СЛУЖ.СЛОВА
 ПВ ВОС1(М10)     ВОССТАНОВИТЬ ПОСЛ.СЛОВО СТРАНИЦЫ
ПРЛР11 СЧ ДРЛ
 МОДА МОШ1
 И
 УИА 13(М5)
 ПЕ ПРЛСЧ4
 СЧ ДРЛ+1
 МОДА МОШ2
 И
 ПЕ ПРЛСЧ4  ОШИБКА ПРИ ЧТЕНИИ
 СЧ ДРУЛ
 МОДА МОШ3
 И
 ПО ПРЛСЧ5  ОШИБОК НЕТ
ПРЛСЧ4 ЗП ОШМЛ
 СЧ КУС2-1(М2)
 И Е23           ЕСТЬ ПРИЗНАК НАЛОЖЕНИЯ
 ПО АВ            ОШМ ИЛИ ОШВУ (НЕТ ПРИЗНАКА НАЛОЖ.)
ПРЛСЧ5 ЗП ОШМЛ
 УИА -1023(М12)
 УИА ПРЛСЧ2(М5)
 СЧ 0
 ПВ ПОК(М13)      ГАШ.ПОК
ПРЛСЧС СЛЦ ’3777’(М12)   КОНТР.СУММ-Е СТР.ОБМ.
 ЦИКЛ ПРЛСЧС(М12)
 УИА ’2003’
 ЗП РЯО5
 УИА 7(М10)        *С ЗА 7-М СЛУЖ.СЛОВОМ
 ПВ ПРЛСЧА(М5)    ЗА КОНТР-ОЙ СУММОЙ 7 СЛУЖ.СЛОВА
 НТЖ РЯО5
 УИА 3
 ПЕ ПРЛСЧ6        НЕТ СОВП.КОНТР.СУММ
 СЧ КУС2-1(М2)
 И Е11
 ПО ПРЛСЧМ        НЕ КОНТР. ЧТ
 СЧ КУС2-1(М2)
 И Е21
 ПЕ ПРЛСЧМ        ЧТ СЛ СЛОВ ПОСЛЕ ЗП
 СЧ РЯО5
 НТЖ СУММА-5(М2)
 УИА 8(М5)
 ПЕ АВ            НЕ ПРОХОДИТ ЗАПИСЬ (КС НЕ СОВПАЛИ)
 ЗП ШКЗЗП-5(М2)
 СЧ ВСЕЕД
 НТЖ Е48-5(М2)
 И УХО
 ЗП УХО           ЗП ПО НАПР-Ю РАЗРЕШЕНА
ПРЛСЧМ СЧ ЗОЧ2-5(М2)
 ПО СТ411         ОШ. В ОЧЕРЕДИ ЗАКАЗОВ К МБ(МЛ)
 НЕД 0
 УИ М14           М14: N ВЫПОЛНЕННОГО ЗАКАЗА
 УИА 3
 СЧ 0
 ЗП КУС2-1(М2)    ОСВ.НАПР-ИЯ
 СЧ ОЧЛИС-1(М14)
 ИЛИ Е16Е17
 НТЖ Е16Е17
 ЗП ОЧЛИС-1(М14)
 ПБ ОБХОД
ПРЛСЧ2 СЧ Е1            ОШИБКА МЛ
 ЗП ОШМЛ
ПРЛСЧ6 УИА 4(М5)
 ПБ АВ
ВЫБМГ УИА 37(М16)       ОШИБКА МЛ
 ПБ АНВО8А
*
*
*
ДЛЯДИС СЧИ М16
 СЛЦ П7
 СДА 64+1
 УИ М2
 ПБ (М10)
МДЧ НОП ,
 ЗП 2(М15)        N МАТ. ЛИСТА С 1-5 Р.Р
 СЧИ М12
 СДА 64-36         N ФИЗ. ЛИСТА С 37-46 Р.Р
 ИЛИ 2(М15)
 ЗП 2(М15)
 ПБ МДЧТЗП        ПЕРЕНОС ЗАКАЗА С МБ НА МД
ОЧ3И6 СЧ 0
 МОД КНАПР-1(М2)   ОЖКСТИТЬ 3-Е И 6-Е СС
 ЗП 3
 МОД КНАПР-1(М2)
 ЗП 6
 ПБ (М10)
*
ГАШСОЛ НТЖ ВСЕЕД         ПОГАСИТЬ В СОЛ РАЗРЯДЫ,
 И СОЛ-3(М16)    ЗАДАННЫЕ НА СМ.
 ЗП СОЛ-3(М16)
 ПБ (М10)
*
ПРЛВОС МОД КНАПР-1(М2)
 СЧ 5
 ПБ ВОС1
*
ЗОНТАБ СЧ ЗОНАТР        УСТ.В ТЗОНМЛ ЗОНАТР И ЗОНАМЛ
 СДА 64-12
 ИЛИ ЗОНАМЛ
 ЗП ТЗОНМЛ-24(М1)
 ПБ (М10)
*
ТАБЗОН СЧ ТЗОНМЛ-24(М1) УСТ.ЗОНАТР И ЗОНАМЛ ПО <ТЗОНМЛ>
 СДА 64+12
 И П7777
 ЗП ЗОНАТР
 СЧ ТЗОНМЛ-24(М1)
 И П7777
 ЗП ЗОНАМЛ
 ПБ (М10)
*
ГАШСЧК СЧ П7
 СДА 64-24
 НТЖ ВСЕЕД
 И ТЗНМЛ-24(М1)
 ЗП ТЗНМЛ-24(М1)
ГАШСМ1 СЧ СМЕЩ
 ПБ (М10)
ПОВКСМ СЧ КУС1Б-1(М16)
 И Е18
 ПЕ ПСЧБО         ПОВТОРИТЬ ОБМЕН ПРИ ЧТ
 СЧ ЛИС1Б-1(М16)
 УИА ПСЧБ1(М11)
 ПВ КСМ(М12)
 УИА 4(М15)
 ПБ ПСЧБО1
ПСЧБО СЧ ПР13
 ПВ ПОК(М13)      УСТ.ПОК
 СЧ ЛИС1Б-1(М16)            ПОВТОРЕНИЕ ЗАКАЗА
 ПВ ПРИП1С(М10)
ПСЧБО1 СЧ КУС1Б-1(М16)
 СДА 64+42
 УИ М5 N ЗАДАЧИ
 СДА 64-42
 УИА (М10)
 СДА 64+П
 НТЖ ТУС+10
 И Е48П42
 ПО ПС
 СЧ КУС1Б-1(М16)
 СДА 64+П
 НТЖ ТУС
 И Е48П42
 ПО ПС
 СЧ КУС1Б-1(М16)
 И ЕФ
 ПО ПС
 СЧ ОБМОТВ
 И Е48-1(М5)
 ПО ПСЧБЕ ПОВТОРЯТЬ БЕСКОНЕЧНО
ПС СЧ КУС1Б-1(М16)
 СБР НМБТР
 СДА 64+37
 ЗП ЯКУСМБ НАПР,МБ,ТРАКТ
 НТЖ ТАБОШ
 И П3777
 ПЕ ТРЦИКЛ        ПЕРВЫЙ СБОЙ ИЛИ ДР. ТРАКТ
 СЧ ТАБОШ
СИСТ2 СЛЦ Е45    НА 8 ОШИБОК
 ЗП ТАБОШ
 ПЕ ПСЧБЕ
 СЧ КУС1Б-1(М16)
 И ЕФ
 ПЕ СИ
 СЛИА ПЗ(М5)
СИ МОД ТУС-1(М5)
 УИА (М12)
 СЧ КУС1Б-1(М16)
 И ЕФ
 ПЕ ЛАК
ОШМБ УИА АНВО8А(М10)
 СЧИ М10
 ЗП 3(М12)
 СЧ 5(М12)
 И Е16П32
 ИЛИ П2007
 ЗП 5(М12)
 УИА 48(М10)
 СЧИ М15
 НТЖ Е3
 ПЕ БЕРУМ1        НЕ ОШ. МОЗУ->УВУ
 СЛИА 17(М10)       СБОЙ МАШИНЫ
БЕРУМ1 СЧИ М10
 ЗП 8(М12)
ЛАК УИА -1023(М10)
 СЧ КУС1Б-1(М16)
 И ЕФ
 ПО ПОР
 СЧ 0
 ЗП (М12)         ОТВЕТ ПОДКАЧКЕ
 СЧ ОБМОТВ
 И Е48-1(М5)
 ПЕ НЕНУЛИ
ПОР СЧ КУС1Б-1(М16)
 И Е18
 ПЕ НАНУЛИ   ЧТЕНИЕ
 СЧ ТУС-1(М5)
 НТЖ ТУС+10
 И Е48П42
 ПО НЕНУЛИ   КАЧКА
НАНУЛИ НОП
 СЧ П377
 УИА
НУЛИ ЗП 2047(М10)
 ЦИКЛ НУЛИ(М10)
НЕНУЛИ УИА 3
 СЧИ М15
 СДА 64-30
 ИЛИ Е35         ДЛЯ КОНТ
 ЗП ЯКУСМБ        ПРИЧ. СБОЯ
 СЧ КУС1Б-1(М16)
 СБР КМБТТ
 ПВ ФП(М5) СТАРШ Р-Д НАПР.,МБ,ТРАКТ,СЕКТОР
 СДА 64-2
 И Е43Е42 МЛАДШ.Р-ДЫ НАПР
 СДА 64+13
 УИА 8(М10)
 ПВ ФП(М5)
 УИА 4(М14)
 УИИ М2(М16)
ДАЙСТ СЛИА -2(М10)
 СЧ ИПЗМЛ+4
 ЗП ИПОБМ+4
 ПВ ПРЛСЧА(М5)
 УИА 3
 СДА 64+35
 ЗП РЯОМБ
 И П3            СЕКТОР
 СДА 64-14(М10)
 ПВ ФП(М5)
 СЧ АПСЧБ
 ЗП ИПОБМ+4
 СЧ РЯОМБ
 СДА 64+10
 СДА 64-8(М14)
 ПВ ФП(М5)        СЧЕТЧ. ЗП(ИЗ СЛ. СЛ.)
 СДА 64+24
 И Е1            СЧЕТЧИК ЗАПИСИ
 СДА 64-4(М14)
 ПВ ФП(М5)
 СЛИА -1(М14)
 ПИНО ДАЙСТ(М10)    НЕ ВСЕ СЕКТОРА
 СЧ РЯОМБ
 СДА 64+2
 И П377
 СДА 64-16
 ПВ ФП(М5)        МБ,ТРАКТ ИЗ СЛ. СЛОВА
 СЧ ЯКУСМБ
 ЗП 34(М12)       СЛОВО ДЛЯ ПЕЧАТИ ПО КЗ
 СЧ КУС1Б-1(М16)
 И ЕФ
 ПЕ СНМБ
 СЧ ЯЧЧП(М12)
 ИЛИ ЕЧП
 ЗП ЯЧЧП(М12)
 СЧ ЯЧОТБР(М12)
 ИЛИ ЕОТКЗ
 ЗП ЯЧОТБР(М12)
 ПБ СНМБ
ФП ИЛИ ЯКУСМБ
 ЗП ЯКУСМБ
 СЧ КУС1Б-1(М16)
 ПБ (М5)
ТРЦИКЛ СЧ ТАБОШ
 ПЕ ПСЧБЕ
 СЧ ЯКУСМБ
 ПБ СИСТ2
ОБЩМГ НОП 0
 ПБ СТ411         ОШ. В ОЧЕРЕДИ ЗАКАЗОВ К МБ(МЛ)
ЗКМЛ4Г ЗП ЗТРАЗМ
 СЧ 0
 ЗП ТРАЗМ
 ЗП РЯР
ЗКМЛ4Д УИА 3
 ПБ ЗКМ4Д
СМЕ1 УИА 1027
 СЧ ШЗЗОБМ
 НТЖ Е1
 ЗП ШЗЗОБМ
 ПБ ВХОДМЛ
ФИ5 ПВ ФФ(М16)
ФИ56 НОП 0
 ПРЕ 0
 СЧ ТБД-1(М6)
 ПРЕ 3
 СБР СЧК+1
 СДА 64+22
 ПБ ЗАНЗАК
ФФФ СЧ 35(М15)
 И Е16П32
 НТЖ Е16П32
 ПО СЧМБПР
 СЧ 35(М15)
 ПБ СЧМБ
СЧМБПР СЧ ЯЧСЧМБ(М15)
 СЛЦ Е46
 ПО ФТК
 ЗП ЯЧСЧМБ(М15)
 СЧ 35(М15)
 НТЖ Е16П32
СЧМБ СЛЦ Е16
 ЗП 35(М15)
ФТК НОП 0
 УИА (М6)
 УИА 1027
 СЧ ШЗНМБ(М15)
 СДА 64+ССТБД
ФТБ1 И П77777
 УИ М10
 УИА 3
 ПЕ ФТБ
 УИИ М10(М6)
ФТА СЧ ЯЧЧТР(М15)
 И ЕТР
 ПЕ ФТН1 ИСЧЕРПАН РЕСУРС ТРАКТОВ
 СЧ ИНФМБ
 И Е40
 ПО УЖЕЗП
 НТЖ ИНФМБ
 И Е47П1
 ЗП ИНФМБ
УЖЕЗП НОП ,
 СЧ НАЧФИЛ+2
 СДА 64+5
 УИ М1            М1 := НАЧАЛЬНЫЙ НОМЕР СТРОКИ ТЗФСТР
 УИА 1027
 ЗП СМ
 СЧ КОНФИЛ+2
 СДА 64+5
 ВЧОБ СМ
 УИ М4
ТВО НОП ,
 СЧ ТЗФСТР(М1)
 И Е2
 ПЕ НЕЛЬЗЯ        В ЭТОМ СЕГМЕНТЕ НЕЛЬЗЯ
 СЧ ТЗФСТР(М1)
 И Е48П17
 НТЖ Е48П17
 ПЕ ВОП           ЕСТЬ МЕСТО В  О П  ДЛЯ ТРАКТА
НЕЛЬЗЯ НОП ,
 СЛИА 1(М1)
 ЦИКЛ ТВО(М4)
 ПРЕ 3
 СЧ ТМБ
 ВЧ П12
 ПЕ ФТ4
 УИА (М1)
 УИА -15(М4)
 СЧ ИНФМБ
 И Е1
 УИ М3
 ПО ФТ2
 УИА 8(М1)
 УИА -7(М4)
 ПБ ФТ2
ВОП НЕД П77777
 УИ М4
 СЧ Е48(М4)
 ИЛИ ТЗФСТР(М1)
 ЗП ТЗФСТР(М1)  ЛИСТ ЗАНЯТ
 СЧ ОЗУ
 ВЧ Е1
 ЗП ОЗУ
 СЧ ФКУСМБ
 ИЛИ ЕТРА  ТР В ОП
 ЗП ФКУСМБ  СТРОКА ДЛЯ ТБД
 СЧИ М1
 СДА 64-5
 УИ М6
 СЛИ М6(М4)
 СЛИА ТОП-ТБД+1(М6)
 СЧ ШЗНМБ+1(М15)
 СДА 64+ССТБД
 И П77777
 УИ М10
 УИА ФТЗИ(М16)
 ПБ Ш
ФТН1 УИА ФТН(М17)
 ПБ ФИ
ФТ4 УИА 16(М1)
 УИА -LШЗНМБ+18(М4)
ФТ2 СЧ ШЗНМБО(М1)
 И Е48П17
 ПЕ ФТ3
 СЛИА 1(М1)
 ЦИКЛ ФТ2(М4)
 ПИО ФТ4(М3)
 УИА (М3)
 УИА (М1)
 УИА -7(М4)
 ПБ ФТ2
ФТВ СЧИ М6
 ПРЕ 1026
 ИЛИ ТБД-1(М10)
 ЗП ТБД-1(М10)
 ПРЕ 1027
ФТВ1 СЧ ШЗНМБ+1(М15)
 И ОССТБД
 ЗП ШЗНМБ+1(М15)
 СЧИ М6
 СДА 64-ССТБД
 ИЛИ ШЗНМБ+1(М15)
 ЗП ШЗНМБ+1(М15)
 ПБ (М16)
ФТ3 НЕД Е18П1
 УИ М3
 СЧИ М1
 СДА 64-5
 УИ М6
 СЛИ М6(М3)
 ПБ (М7)
ФТБ СЧ ФКУСМБ
 ПРЕ 2
 НТЖ ТБД-1(М10)
 ПРЕ 3
 УИИ М6(М10)
 И ТОПЛИС
 ПО ФТБЕ
 ПРЕ 2
 СЧ ТБД-1(М10)
 ПРЕ 3
 ПБ ФТБ1
ФТ1 СЛИА 1(М6)
 УИА 1027
 УИА (М17)
 СЧИ М1
 ВЧ ГРАНЬБ
 ПЕ ФТ1Б ТРАКТ НА БАРАБАНЕ
 УИА 1(М17)
ФТ1Б НОП ----
 СЧ ШЗНМБО(М1)
 НТЖ Е48(М3)
 ЗП ШЗНМБО(М1)
 СЧ ВСЕЕД
 НТЖ КДД
 ПРЕ 1026
 И ТБД-1(М6)
 ЗП ТБД-1(М6)
 ПРЕ 1027
 СЧ ТМБ(М17)
 ВЧ Е1
 ЗП ТМБ(М17)
 УИА ФТД(М16)
 ПИНО Ш(М17)
 СЧ Е1
 ЗП БЯК
Ш СЧ ЯЧЧТР(М15)
 СЛЦ ЕТМБ
 ЗП ЯЧЧТР(М15)
 СЧ УСМ+1
 И Е48П42
 ИЛИ ФКУСМБ  +НОМ ЗАД
 ПРЕ 1026
 ИЛИ ТБД-1(М6)
 ЗП ТБД-1(М6)
 ПРЕ 1027
 СЧ ШЗНМБ(М15)
 И Е31П17
 ПЕ ФТВ
 СЧИ М6
 СДА 64-ССТБД
 ИЛИ ШЗНМБ(М15)
 ЗП ШЗНМБ(М15)
 ПБ ФТВ1
ФТБЕ СЧИ М6
 ВЧОБ АКТБД
 ПЕ ФТЗИ    ТР В ОП
 СЧИ М6
 ВЧ Е1
 СДА 64+5
 УИ М1
 СЧИ М10
 ВЧ Е1
 И П37
 УИ М3
ФТД УИА ФТЗИ(М16)
 СЧ ИНФМБ
 СДА 64-8
 ИЛИ ИНФМБ
 И Е48
 ПЕ ФТЗ
ФФ НОП 0
 ПРЕ 2
 СЧ ТБД-1(М6)
 ПРЕ 3
 И СЧК+1
 НТЖ СЧК+1
 ПО ФТИ
 СЧ СЧК
 ПРЕ 2
 СЛЦ ТБД-1(М6)
 ЗП ТБД-1(М6)
 ПРЕ 3
 ПБ ФТЗ
ФТИ СЧ ВСЕЕД
 НТЖ СЧК+1
 ПРЕ 2
 И ТБД-1(М6)
 ЗП ТБД-1(М6)
 ПРЕ 3
ФТЗ УИА 1(М17)
 ПБ (М16)
 ВХОД ДИСО1А,ЗКМЛ1,МДЧ,ЗН,ПУЛ
ЭКВВОД ВНЕШ БЕЗДУБ
ЕСМЛ ВНЕШ УПНОВ,ОШОС
ДИСП70 ВНЕШ БУФМБ,АДРБМБ,СПУБ
ДИСП70 ВНЕШ РЭ4
ВЗУ ВНЕШ ОБХОДР
ВЗУ ВНЕШ ЗАНЗАК,ФТН,АНВО8А,ПРИП1С,ПОК,ВХЗОБ
ВЗУ ВНЕШ ДСЛ,МДЧТЗП,ВХОДМЛ
АВОСТ ВНЕШ СТ410,СТ411,СТ421
БОЭК1 ВНЕШ ПР,ВХСМЕ
ВЗУ ВНЕШ ЕФ,П,ВХЛ
АВМД ВНЕШ Е37П35,ИНФСБ,АВУСТ
ДИСП70 ВНЕШ ОЛИ,ГАШЗАН,ЗАХЗАН,Е48П37
ДИСП70 ВНЕШ КУС2,КСУМ1Б,КМБТТ,П7777,КСД3,ЛЕНТА,МБКУС
ДИСП70 ВНЕШ Е47П43,РМЛ,ШЗЗОБМ,ШЗЗПСЧ,ШИФРМ,ШКЗЗП,ШКИВ,ШОЧМГ
ДИСП70 ВНЕШ ШЗРАЗМ,ЯЧКЧ,ЯКУС,ЯКУСМБ,ЯЧКЧМБ,ЗАНПР,КРОМЕ,РЭ2
ДИСП70 ВНЕШ ФКУС,К,БМБ,БЯК,ММБН,ТУС,ШГ,БМВ,ТВ18,ИПОБМ
ДИСП70 ВНЕШ КНАПР,П77777,Е35П24,ШКСВН,Е42П1,Е48П43,П23,П777
ДИСП70 ВНЕШ ДЛЯНБ,Е34П25,ПЗ,ТУСП,УКАЧП
ДИСП70 ВНЕШ ШКОБМ,ЗАПВЕТ,И17,ТМБ,ОССТБД
ДИСП70 ВНЕШ СЧК,ИНФМБ,НБТ
ДИСП70 ВНЕШ ЖДЕТ,П12,ШЗНМБО,КДВ,Е18П1,Е47П1,Е31П17
ДИСП70 ВНЕШ БУДИЛ,Е24П1,Е21Е1,Е24П13,Е24П16,Е39П25,Е21Е18
ДИСП70 ВНЕШ Е16П32,Е16Е17,Е12Е11,(Е10П1,П1777),(Е12П1,П7777)
ДИСП70 ВНЕШ (Е6П1,П77),(Е2Е1,П3),ЕСЧ,П3,П5,П6,П7,Е15П2,Е26Е25
ДИСП70 ВНЕШ (Е2П1,П3),О25О11,(Е3П1,П7),(Е5П1,П37),ПР13,ПР14
ДИСП70 ВНЕШ П17,П37,П30,П2007,П377,П1777,П3777,РЯОМБ,РЯР,РМР
ДИСП70 ВНЕШ ЯМ15,ЯМ16,ВСЕ6,К2003,ВСЕЕД,ССЫЛКА,К2013,КСД1
ДИСП70 ВНЕШ РЯО1,РЯО2,РЯО3,РЯО4,РЯО5,РЯО,АПСЧБ,АСБОЙБ,БОБ
ДИСП70 ВНЕШ БИНОТ2,ИВ25,ВИСП16,ВРЕМЯ,ВРЕЗП,ГУС,ГОД,И16,ИПЗМЛ
ДИСП70 ВНЕШ ЗОЧ2,ЗОНАТР,ЗОНАМЛ,ЗТРАЗМ,ОШМГ,ШКПРЕ,КЛЮЧ,КУС1Б
ДИСП70 ВНЕШ ОЧМБ,ЗОЧ1Б,МГРП,МПРЛ,МГНЕИС,ИНФСЛП,НАПРМГ
ДИСП70 ВНЕШ НМБТР,ОЧКУС,ОЧКСУМ,ПРИП,РЕЖРАЗ,РАЗМГ,РСЧРАЗ,ОШМЛ
ДИСП70 ВНЕШ ОКДВИЖ,ОБРМБ,СОЛ,СМ,СТМГ,СУММА,СМЕЩ,СЧЗАК
ДИСП70 ВНЕШ СЧЗКМБ,ТАБОШ,ТАБЛО,ТЗНМЛ,ТЗОНМЛ,ТРАЗМ,ТВ67,ТВ67А
ДИСП70 ВНЕШ ТБУФ,СБОИМБ,УХО,УКАЧАЙ,ФКУСМБ,ШОЧНАП,ФИЗ,ШЗПОМБ
ДИСП70 ВНЕШ МБЛИС,ЯЛИС,ЯЛИСМБ,ЛИС1Б,ФКУСА,ОЧЛИС,ЛИС2,П14037
ДИСП70 ВНЕШ КОДИК,ЯЧСЧМБ,ШКМБ,ШКОМБ,ШКМЛ,ШКОМЛ
ДИСП70 ВНЕШС ДРБ,ДРУБ,Е48Е47,(КУС,КУСЕС),(ДРЛ,ДР)
ДИСП70 ВНЕШС ГТ,Е45Е46,СТАРП,АСЧББ3
ДИСП70 ВНЕШ ОЗУ,ТЗФСТР,ТОБ,П37777,УСМ
ДИСКИ ВНЕШС ОШДР
ЕСМЛ ВНЕШС ВХДРМЛ,ЗАПАК,РАСПАК,МОШ1,МОШ2,МОШ3,РСП2
ПВВ ВНЕШ ВОСЛМБ,ПВВМБ,ВОСРКЧ,ПВВМЛ
ДИСП70 ВНЕШ ГРАНЬБ,Е43Е42,КУСДВ
ДИСП70 ВНЕШ ШСЗПСЧ
ДИСП70 ВНЕШ КДД,ОБМОТВ
ПРЛ2Г ЭКВ ПРЛ2Б
ДИСКИ ВНЕШ РАОБЩ,СОД,МСТР
ХЛАМ ВНЕШ ВОС1,ТОПСН
ВИСП ВНЕШ ВШГ,ВШГП
РЕСУРС ВНЕШ ТЕСТ,РЕСУРС
ДИСКИ ВНЕШ П11
ДИСП70 ВНЕШ (ДРУЛ,ДРУ)
ЕСМЛ ВНЕШ Н533,Н1193,(ПРУСТ,УСТМГЗ),Н1314,Н1348,Н1351
ЕСМЛ ВНЕШ НЕОХ,Н1469,НАБ70,Н1241,ПОДВЕС
ХЛАМ ВНЕШ ПРЛСЧП
ЕСМЛ ВНЕШ (ЗКМ4Д,ЗКМЛ4Д),БЛИСХ1,МГНОР,ПРЕС5
КАЧКА ВНЕШ Е48П17,НАЧФИЛ,КОНФИЛ
ХЛАМ ВНЕШ НОРМА1,ОБХ3,ФИ,ФТЗИ,СНЗКМЛ,МОЖНО
ХЛАМ ВНЕШ ПРЕС7,ПРЛ2Е,ЗПМГ
КИТ ВНЕШ АКТБД,ТОПЛИС,Е48П42,ШИМЛ
СОСТАВ ВНЕШ ТБД,ССТБД,ЕТРА,ТОП,LШЗНМБ
СОСТАВ ВНЕШ ЯЧЧП,ЕЧП,ЯЧОТБР,ЕОТКЗ,ЯЧЧТР,ЕТР,ЕТМБ,ШЗНМБ
СОСТАВ ВНЕШ ЕОТКЛ,СДВМЛ
ДИСП70 ВНЕШ РЯТОП
ХЛАМ ВНЕШ НЕСЧЕТ,ПОДВ80
 ВХОД ОПРБ4,ПСЧББ3,ДИСП4А,ДИСМБ2
 ВХОД ЗКМЛ4П,ПРЛ2А,ДИСП4,ДИСП5В,АВ,ТАБЗОН,НЕТОБ,Б62
 ВХОД Б61,ВЫХ3,ДИСЛ,ДИСП1,В587,В1241
 ВХОД ПРЛКЛ,В1404,ПРЛ2,ОХ5017,ПРН,ВЫКЛМГ,ВЫКЛЗП,ЗКМЛ4М
 ВХОД ЗКМЛ4И,ВЫКМГ,ПРЛ3Б,НОРМА2,ВЫХ1,ЗОНТАБ,ПРЛСЧ7,ЗКМЛ
 ВХОД НАБОБ3,ТВМБ1,ПСЧББ,ПРЛСЧБ,ОПРБ,ПРОБМ,ДИСПМБ,ДИСМ
 ВХОД ДИСПО,ОТКРШГ,КСМ,ПРЛ17А,Д76,ПРЛСЧМ,СНМБ
 ВХОД КСЧБ4,ПРЛ17,ПРЛСЧА,ДЛЯДИС,ОЧ3И6,ПРП,ПРП2,ВЫБМГ,ВЫХ0
 ВХОД ЗКМЛ1А,ЗКМЛ4,НОРМА,ОБХОД,ДВОЧ1,ОПРБ6А,ФФФ
 ВХОД ПР2,ПР1,ПРЛВОС,ФИ56,ГАШСОЛ,ВЫХОД1,ФТД,ФИ5
 ВХОД ОБХОД4,СМЕ1,ЗКМЛ4Д,ПРЛ2В,ПРЛЕН,ПРЛ2К,ФТ1
 ВХОД ОПРБ6,ДВИЖМЛ,ОБЩМГ,ПОДВ91,ПОДОШ,ПОДОБМ
 ЭКВИВ (ОШМЕХП,Е28),(ВР,15),(Ф,42),(ЕВ,Е48)
*КОНЕЦ ЧАСТИ

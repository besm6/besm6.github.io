ПЕЧАТЬ СТАРТ ’10000’
 Б
 Е
 М
 УПОТР ПЕЧАТЬ(М7)
ИПЗ НОП
 КОНД В’0’
 КОНД М41В’3’
 ПАМ 5
 КОНД А(SТАСК)      М17 - СЧЕТЧИК МАГАЗИНА
 ПАМ 15            ИНДЕКС-РЕГИСТРЫ
*
*     КОДЫ ОПЕРАЦИЙ АЦПУ
*     ------------------
*
КОППЕЧ КОНД М44В’6’М30В’1’М20В’26’   ПИСАТЬ С ПРОПУСКОМ
КОППРГ КОНД М30Х’8В’М20В’1’          ПРОГОН
КОБУФП КОНД М44В’6’М30Х’63’М20В’14’  ЗАГРУЗКА БУФП
КОБУНС КОНД М44В’6’М30Х’FВ’М20В’16’  ЗАГРУЗКА БУНС
КОПППМ КОНД М44В’6’М30В’5’М20В’27’   СТИР.-ЗАПИСЬ ППМ
*
ПС7934 КОНД В’45’         КОНЕЦ СТРОКИ ЕС-7934
КТ7934 КОНД В’31’         КОНЕЦ ТЕКСТА ЕС-7934
СУЗ КОНД М40Х’4В’      СУЗ          ЕС-7934
 ПАМ 2
 КОНД М47В’1’       ВРЕМЯ
НАЧСИМ КОНД М24Х’7FFВ’Х’2’
КТОБУФ КОНД Х’0’
 ПАМ 1             ИСПОЛЬЗУЕТ ТВ40
КТОПУЛ КОНД Х’0’
КРЗШ КОНД П’      ’     ДЛЯ РАЗБОРКИ ШИФРА
 ПАМ 1
ВХОД НОП
 СЧ ШКАРУ
 УИА ’2003’
 И Е48Е47
 ПО КОНЕЦ
 ПБ НАЧАЦП
*
СТРОКА НОП
 УИА 3
 СЧ РАЦ
 И Е48Е47
 ПО ВХОД
 СЧ ШКАРУ
 И Е48Е47
 ПО ОДНОИЗ
*
*
НАЧАЦП НОП
 УИА ’2003’
 СЧ ШКАРУ
 НЕД П77777
 УИ М10
 СЧ Е48(М10)
 НТЖ ШКАРУ
 ЗП ШКАРУ
 СЧ Е48(М10)
 ИЛИ РАЦ
 ЗП РАЦ
 УИИ М6(М10)
 СЛИ М6(М7)
 СЧ ЕАЦП-П(М6)
 ИЛИ ШИФРАЦ
 ЗП ШИФРАЦ
 УИА 3
 СЧ 0
 ЗП НАБЗ-П(М6)
 МОДА SТАСК
 УИА (М17)
 СЧ 7
 И Е23(М10)
 ПО ПЕЧАЦ         ПЕЧАТЬ НА АЦПУ
 СДА 64-25
 РЗБ ШКППМ
 ПО ВЫДСБ1        ППМ ОТСУТСТВУЕТ
 НЕД П77777
 ЗП NЛОГ-П(М6)    ЛОГИЧЕСКИЙ НОМЕР
 СЧ Е1
ПЕЧАЦ УИ М2            АЦПУ ИЛИ ППМ
 МОДА ДАЙБУФ
 ПВ (М14)
 СЧ ПРЕДЕЛ
 И Е25(М10)      ЕС-7036
 ПО НЕ7036
 СЧ РЯБУНС-П(М6)
 ПО НЕ7036         БУНС НЕ НУЖЕН
 СЧ 0
 ЗП РЯБУНС-П(М6)
 УИА -10(М14)
 МОД АБЗАЦ-П(М6)
 УИА (М15)
*
*   ПОДГОТОВКА БУФП
*
РБУФП МОДА 11(М14)
 ЗП (М15)
 МОДА РБУФП
 ЦИКЛ (М14)
 СЧ Е41
 ЗП (М15)         1 КАНАЛ - НАЧАЛО БЛАНКА
 МОДА ЗГБУФП
 ПВ (М14)         ЗАГРУЗКА БУФП
*
*   ПОДГОТОВКА БУНС
*
 МОД АБЗАЦ-П(М6)
 УИА (М15)
 УИА -13(М14)
РБУНС НОП ,
 МОДА ЗНАКИ
 СЧ 13(М14)
 МОДА (М15)
 ЗП 13(М14)
 МОДА РБУНС
 ЦИКЛ (М14)
 МОДА ЗГБУНС
 ПВ (М14)         ЗАГРУЗКА БУНС
НЕ7036 НОП ,
 МОДА ДАЙСЕК
 ПВ (М13)
*
ОДНОИЗ СЧ ВЫБОР
 НТЖ Е1
 ЗП ВЫБОР
 УИ М10
 СЧ Е48(М10)
 И РАЦ
 ПО СТРОКА
 СЧ 7
 СБР Е23(М10)
 СДА 64+47
 УИ М2            АЦПУ ИЛИ ППМ
 УИИ М6(М10)
 СЛИ М6(М7)
 МОДА БУФЕР
 УИА (М16)
 МОДА БА0
 ПИО (М10)
 СЛИА ДЛБУФ*2(М16)
БА0 СЧИ М16
 ЗП АБУФ
НОВСЕК НОП ,
 СЧ ТЕКСИМ-П(М6)
 УИ М3
 СДА 64+24
 УИ М4
 СЧ РАЦ
 И Е40(М10)
 ПО ОЧСИМВ
 НТЖ РАЦ
 ЗП РАЦ
 СЧИ М2
 СЛЦ НАЧПОЗ-П(М6)
 ЗП ПОЗ-П(М6)     ОСТАВИТЬ МЕСТО ДЛЯ СУЗ
ОЧСИМВ НОП
 МОДА ВЫБСИМ
 ПВ (М16)
*
*////////////////////////////////////////////*
*///                                      ///*
*///  ДЕШИФРАЦИЯ БАЙТА ВЫХОДНОГО ПОТОКА   ///*
*///                                      ///*
*////////////////////////////////////////////*
*///                                      ///*
*
 ПО КОНЗОН        000        - КОНЕЦ ЗОНЫ
 ВЧ П141
 ПЕ ЭТОСИМ        001 - 140  - СИМВОЛ
 ЗП ПРОТЯЖ        ПРОТЯЖ := БАЙТ-141
 СЧ СИ
 И Е8
 ПЕ ПОЗИЦ         200 - 377  - ПОЗИЦИЯ
 СЧ СИ
 НТЖ П177
 ПО ПОВТОР        177        - ПОВТОР
 НТЖ П3
 ПО СЕКТОР        174        - НОВЫЙ СЕКТОР
 НТЖ Е1
 ПО КОНПОТ        175        - КОНЕЦ ВЫХОДНОГО ПОТОКА
 НТЖ П3
 ПО НОВЛИС        176        - ПРОТЯЖКА НА НОВЫЙ ЛИСТ
*///                                      ///*
*////////////////////////////////////////////*
*
*
***********************************************
*ПРОТЯЖКА БУМАГИ НА <ПРОТЯЖ> СТРОК (Б=141-173)*
***********************************************
*
ПРОТЯГ НОП ,
 МОДА ПРОТ34
 ПИНО (М2)
 МОДА ЛИСТ
 УИА (М13)
ПЕЧНАК НОП
 СЧ РАЦ
 И Е46(М10)
 ПО БЕЗЗМ
 НТЖ РАЦ
 ЗП РАЦ
 СЧ 0
 ЗП ПРОТ
 СЧ АБУФ
 СЛЦ АДЛБУФ
 МОДА ФПЕЧ
 ПВ (М14)
БЕЗЗМ СЧ ПРОТЯЖ
 И П3
 ЗП ПРОТ
 НТЖ ПРОТЯЖ
 СДА 64+1
 ЗП ПРОТЯЖ
 СЧ АБУФ
 МОДА ФПЕЧ
 ПВ (М14)
 СЧ Е2
 ЗП ПРОТ
ДОТЯГ СЧ ПРОТЯЖ
 ПО (М13)
 ВЧ Е1
 ЗП ПРОТЯЖ
 СЧ АБУФ
 МОДА ФПЕЧ
 ПВ (М14)
 ПБ ДОТЯГ
*
ЛИСТ НОП
 СЧ РАЦ
 И Е42(М10)
 ПО КОНСТР
 НТЖ РАЦ
 ЗП РАЦ
 МОДА ПРОГОН
 ПВ (М14)
*
КОНСТР НОП
 СЧИ М4
 СДА 64-24
 ЗП ТЕКСИМ-П(М6)
 СЧИ М3
 ИЛИ ТЕКСИМ-П(М6)
 ЗП ТЕКСИМ-П(М6)
 ПБ СТРОКА
*
ПРОТ34 НОП .             ПЕЧАТЬ СТРОКИ ЕС-7934                     4
 СЧ ПС7934
 ЗП СИМПЕЧ-П(М6)
ЗППР СЧ ПРОТЯЖ
 ПО КОНПРО
 ВЧ Е1
 ЗП ПРОТЯЖ
 МОДА ЗАНЕСИ
 ПВ (М15)
 ПБ ЗППР
КОНПРО СЧ КТ7934
 ЗП СИМПЕЧ-П(М6)
 МОДА ЗАНЕСИ
 ПВ (М15)
 МОД АБУФ
 СЧ
 И Е40П1
 ИЛИ СУЗ
 МОД АБУФ
 ЗП
 СЧ АБУФ
 ИЛИ КОПППМ
 СЧМ NЛОГ-П(М6)
 СДА 64-5
 УИ М11
 СЛИА ТИПАЦД(М11)
 МОДА ПУСКУС ТРОЙСТВА
 ПВ (М14)
 ПБ КОНСТР
*
*
**********************************************
*  КОНЕЦ ЗОНЫ                       (Б =000 )*
**********************************************
*
КОНЗОН НОП
 МОДА ВОЗБУФ
 ПВ (М14)
 СЧ РАЦ
 И Е44(М10)
 ПЕ ЕЩЕ
 СЧ РАЦ
 НТЖ Е48(М10)
 ЗП РАЦ
 СЧ ЕАЦП-П(М6)
 ПАМ 0
 ИЛИ ШКПЛАН
 ЗП ШКПЛАН        БЛПР НЕ ТРЕБУЕТСЯ
 ПБ СТРОКА
*
*
**********************************************
*  ОБРАБОТКА СИМВОЛА             (Б =001-140)*
**********************************************
*
ЭТОСИМ НОП ,
 СЧ СИ
 ВЧ Е1
 ЗП СИ
 УИ М16           КОД УПП В ИНТЕРВАЛЕ 0 - 137
 МОДА ТАБКОД+’200’(М16)
 КК ’46’,(5)      СЧИТ.ПОЛНОРАЗР. С КОР.ТЭГОМ
 СЧМР
 СДА 64+40
 ЗП СИМПЕЧ-П(М6)
 МОДА БЕЗЗАМ
 ПИНО (М2)          ЕС-7934
 СЧ СИ
 МОДА ДЕЛНА6
 ПВ (М16)
 УИ М16
 СЧ Е7
 ЗП СИМЗАМ-П(М6)
 МОДА ТДКОИ
 СЧ (М16)
 МОД ОСТАТ
 СД ДВБАЙТ
 И П377
 ЗП СИМПЕЧ-П(М6)
 СДА 64+6
 ПЕ БЕЗЗАМ
 СЧ СИМПЕЧ-П(М6)
 И П7
 УИ М16
 НТЖ СИМПЕЧ-П(М6)
 УИ М15
 МОДА ЗАМЕНА
 СЧ (М16)
 СДА 64(М15)
 И П377
 ЗП СИМПЕЧ-П(М6)
 МОДА ЗАМЕН1
 СЧ (М16)
 СДА 64(М15)
 И П377
 ЗП СИМЗАМ-П(М6)
 СЧ РАЦ
 ИЛИ Е46(М10)
 ЗП РАЦ
БЕЗЗАМ НОП
 МОДА ЗАНЕСИ
 ПВ (М15)
 ПБ ОЧСИМВ
*
*
**********************************************
*  ОБРАБОТКА НОМЕРА ПОЗИЦИИ      (Б =200-377)*
**********************************************
*
ПОЗИЦ НОП ,
 НТЖ СИ
 МОДА ЗППОЗ
 ПИО (М2)          АЦПУ
 СЛЦ Е1            СДВИГ ИЗ-ЗА СУЗ ЕС-7934
ЗППОЗ НОП
 СЛЦ НАЧПОЗ-П(М6)
 ЗП ПОЗ-П(М6)
 ПБ ОЧСИМВ
*
*
**********************************************
*  ПОВТОР                            (Б =177)*
**********************************************
*
ПОВТОР НОП ,
 МОДА ВЫБСИМ
 ПВ (М16)
ПОВТ ВЧ Е1
 ЗП СИ
 МОДА ЗАНЕСИ
 ПВ (М15)
 СЧ СИ
 ПЕ ПОВТ
 ПБ ОЧСИМВ
*
*
**********************************************
*  НОВЫЙ СЕКТОР                     (Б = 174)*
**********************************************
*
СЕКТОР НОП ,
 МОДА ДАЙСЕК
 ПВ (М13)
 ПБ НОВСЕК
*
*
**********************************************
*  КОНЕЦ ВЫХОДНОГО ПОТОКА ЗАДАЧИ    (Б = 175)*
**********************************************
*
КОНПОТ НОП ,
 СЧ РАЦ
 И Е44(М10)
 ПО ЕЩЕ
 МОД АБЗАЦ-П(М6)
 СЧ 1
 РЗБ КРЗШ
 ЗП ТКВАЦП+1
 МОД АБЗАЦ-П(М6)
 СЧ 1
 СДА 64-24
 РЗБ КРЗШ
 ЗП ТКВАЦП+2
 СЧИ М10
 СДА 64-8
 ИЛИ АЦПУ0
 ЗП ТКВАЦП+4
 УИА ТКВАЦП-П(М16)
 СЧ ЕАЦП-П(М6)
 ПАМ 0
 НТЖ ШИФРАЦ
 ЗП ШИФРАЦ        БЛПР НЕ ТРЕБУЕТСЯ
 СЧМР 64
 ПАМ 0
 ИЛИ ШКПЛАН
 ЗП ШКПЛАН
 ПБ ВЫДОПТ
ЕЩЕ НОП
 СЧИ М10
 СДА 64-32
 СЛЦ ТНАЦП
 ЗП ТЕККНЦ+1
 СЧ ГОД
 И П7
 СДА 64-24
 СЛЦ ТНЭВМ
 ЗП ТЕККНЦ+2
 СЧ ВРЕМЯ
 СЧМ 0
 УИА -5(М15)
ЦВЫВР СЧМ -2(М17)
 МОДА DIVВР+5
 УМН (М15)
 ЗП (М17)
 СЧМР 64
 МОДА МОDВР+5
 УМН (М15)
 ЗПМ -3(М17)
 ИЛИ (М17)
 СДА 64-4
 МОДА ЦВЫВР
 ЦИКЛ (М15)
 СДА 64-20
 ЗП -1(М17)
 РЗБ РЗБВРЕ
 СЛЦ ТВРЕ1
 ЗПМ ТЕККНЦ+3
 СДА 64-16
 РЗБ РЗБВРЕ
 СЛЦ ТВРЕ2
 ЗП ТЕККНЦ+4
 СЧИ М7
 СЛЦ ПОСЛЕД
 ЗП ТЕКСИМ-П(М6)
 СЧ Е44(М10)
 ИЛИ РАЦ
 ЗП РАЦ
 ПБ СТРОКА
*
*
**********************************************
*  ПЕРЕХОД НА НОВЫЙ ЛИСТ            (Б = 176)*
**********************************************
*
НОВЛИС НОП ,
 СЧ 1
 И Е47(М10)      ’1’ -> ЛИСТОВАНИЯ НЕТ
 ЗП (М17)
 НТЖ Е47(М10)
 СДА 64+5
 ИЛИ РАЦ
 ЗПМ РАЦ
 СБР Е47(М10)
 СДА 64+47
 ЗП ПРОТЯЖ        ЕСЛИ НЕ ЛИСТОВ., ТО ПРОТЯЖ. 1 СТРОКИ
 ПБ ПРОТЯГ
*
*////////////////////////////////////////////*
*///                                      ///*
*///        П О Д П Р О Г Р А М М Ы       ///*
*///                                      ///*
*////////////////////////////////////////////*
*
*
*
ДАЙСЕК НОП
 МОДА ВОЗБУФ
 ПВ (М14)
 СЧ НАБЗ-П(М6)
 И Е3
 ПЕ КОНЗОН
 МОДА ДАЙБУФ
 ПВ (М14)
 СЧ АБЗАЦ-П(М6)
 СДА 64-2
 СЛЦ НАБЗ-П(М6)
 СЛЦ КУСАЦП-П(М6)
 СЛЦ НОММБ
 ПВ ФИЗОБМ(М16)
 УИА 3
 СЧ АБЗАЦ-П(М6)
 СЛЦ НАЧСИМ
 ЗП ТЕКСИМ-П(М6)
 СЧ НАБЗ-П(М6)
 СЛЦ Е1
 ЗП НАБЗ-П(М6)
 ПБ (М13)
*
*
ДАЙБУФ НОП
 СЧ КТОБУФ
 ПЕ ИЗПУЛА        РЕЗИДЕНТНЫЙ БУФЕР ЗАНЯТ
 СЧ Е48(М10)
 ЗП КТОБУФ
 УИИ М15(М7)
 СЛИА ’1400’(М15)
 СЧИ М15
ВЫХДБ ЗП АБЗАЦ-П(М6)
 ПБ (М14)
ИЗПУЛА И Е48(М10)
 ПЕ (М14)
 СЧ КТОПУЛ
 И Е48(М10)
 ПЕ (М14)
 СЧ Е48(М10)
 ЗП КТОПУЛ
 СЧ Е1
 УИИ М12(М17)
 ПВ ТБУФ(М15)
 УИИ М17(М12)
 УИА 3
 СДА 64-8
 ПБ ВЫХДБ
*
ВОЗБУФ НОП
 СЧ КТОБУФ
 И Е48(М10)
 ПО ВПУЛ          ВЕРНУТЬ АБЗАЦ В ТБУФ
 НТЖ КТОБУФ
 ЗП КТОБУФ
 ПБ (М14)
ВПУЛ СЧ КТОПУЛ
 И Е48(М10)
 ПО (М14)
 НТЖ КТОПУЛ
 ЗП КТОПУЛ
 СЧИ М16
 СЧМ АБЗАЦ-П(М6)
 СДА 64-2
 УИИ М12(М17)
 ПВ ТБУФ(М15)
 УИИ М17(М12)
 УИА 3
 МОД (М17)
 УИА (М16)
 ПБ (М14)
*
ВЫБСИМ НОП
 СЧ (М3)
 МОДА ДВБАЙТ
 СД 5(М4)
 И П377
 ЗП СИ
 МОДА ПРОВ0
 ЦИКЛ (М4)
 УИА -5(М4)
 СЛИА 1(М3)
 ПБ (М16)
ПРОВ0 ПЕ (М16)
 СЧ (М3)
 МОДА МАСКИ
 И -5(М4)
 ПЕ ВЫБСИМ
 ПБ (М16)
*
ДЕЛНА6 НОП
 УМН ДЕЛЮ
 ЗП (М17)
 СЧМР 64
 УМН ДЕЛЮ1
 ЗПМ ОСТАТ
 ПБ (М16)
*
НАКЛАД НОП
 СЧ ПОЗ-П(М6)
 ЗП -1(М17)
 СЧ РАЦ
 И Е46(М10)
 СЧМ 0
 ЗП ПРОТЯЖ
 МОДА ПЕЧНАК
 ПВ (М13)         ПЕЧАТЬ НАКОПЛЕННОЙ СТРОКИ
 СЧ (М17)
 ИЛИ РАЦ
 ЗП РАЦ
 СЧ (М17)
 ЗПМ ПОЗ-П(М6)    ВОССТАН.ПОЗИЦИИ
 УИ М15           АДР.ВОЗВРАТА
ЗАНЕСИ НОП
 СЧИ М15
 УИА -1(М14)
 СЛИ М14(М2)       ДЛЯ ЕС-7934(7937)
 СЧМ ПОЗ-П(М6)
 МОДА ДЕЛНА6
 ПВ (М16)
 УИ М1
 СЧ ОСТАТ
 СДА 64-3
 УИ М5
ЗАНОС СЧ Е48П41
 СДА 64(М5)
 МОД АБУФ
 СЧМ (М1)
 НТЖ ПРОБЕВ
 И -1(М17)
 МОДА НЕТНАК
 ПИНО (М2)          ЕС-7934(7937)
 ПЕ НАКЛАД
НЕТНАК НОП
 СЧ ВСЕЕД
 НТЖ (М17)
 МОД АБУФ
 И (М1)
 СЧМ СИМПЕЧ-П(М6)
 СДА 64+8(М5)
 СЧМР 64
 ИЛИ (М17)
 МОД АБУФ
 ЗП (М1)
 СЛИА СИМЗАМ-СИМПЕЧ(М6)
 СЛИА ДЛБУФ(М1)
 МОДА ЗАНОС
 ЦИКЛ (М14)
 УИИ М6(М10)
 СЛИ М6(М7)
 СЧ ПОЗ-П(М6)
 СЛЦ Е1
 ЗП ПОЗ-П(М6)
 МОД (М17)
 ПБ
*
ЗГБУНС НОП ,             ЗАГРУЗКА БУНС
 СЧ КОБУНС
 ПБ ЗГБУФ
*
ЗГБУФП НОП ,             ЗАГРУЗКА БУФП
 СЧ КОБУФП
ЗГБУФ МОДА (М14)
 ПИНО (М2)          ЕС-7934
 СЧИМ М15
 ИЛИ (М17)
 ПБ ФИЗАЦП
*
ПРОГОН НОП ,             ПРОГОН БУМАГИ НА НАЧ.ЛИСТА
 СЧ КОППРГ
 ПБ ФИЗАЦП
*
ФПЕЧ   НОП ,             ПЕЧАТЬ СТРОКИ
 ИЛИ КОППЕЧ
 СЧМ ПРОТ
 СДА 64-33
 ИЛИ (М17)
*
ФИЗАЦП НОП
 СЧИМ М10
 СДА 64-5
 УИ М11
 СЛИА ТИПАЦ(М11)
ПУСКУС НОП .             ПУСК АЦПУ ИЛИ ППМ
 СЧ -1(М17)
*////////////////////////////
*////////////////////////////
*// ОБРАЩЕНИЕ К УСТРОЙСТВУ //
 ПВ ЕС(М15)
 СЧИ М15
 ПВ ДАЙКОТ(М15)
 СБР КСБДР
*////////////////////////////
*////////////////////////////
 ЗП РЯБУНС-П(М6)  РЯБУНС := КОТ ПВВЕС
 СЧ -1(М17)
 И П77777
 ПО НЕТРОС
 СЧИ М2
 СЛЦ НАЧПОЗ-П(М6)
 ЗП ПОЗ-П(М6)
 УИА -ДЛБУФ+1(М15)
 СЧ ПРОБЕВ
РОСП МОД -1(М17)
 ЗП ДЛБУФ-1(М15)
 МОДА РОСП
 ЦИКЛ (М15)
НЕТРОС СЛИА -1(М17)
 СЧ РЯБУНС-П(М6)
*
*  ТИПЫ СБОЕВ ИЗ ДРУ ДЛЯ СТАТИСТИКИ
*  1 - ОТКЛОНЕНИЕ КОМАНДЫ (ОК)
*  2 - ТРЕБУЕТСЯ ВМЕШ. ОПЕРАТОРА (ТВО)
*  3 - СБОЙ КАНАЛА (СБК)
*  4 - СБОЙ В ОБОРУДОВАНИИ (СБО)
*
 ПЕ ВЫДСБ
 СЧ Е25
 СЛЦ АЗУ
 ЗП АЗУ          КОЛ-ВО СТРОК
 ПБ (М14)       НЕТ СБОЯ
ВЫДСБ НЕД
 МОДА К6
 УМН
 СЧМР 64
 УИ М15
 СЧ Е43-6(М15)
 СЛЦ СТУСТР(М10)
 ЗП СТУСТР(М10)
ВЫДСБ1 УИА ТСБОЙ-П(М16)
 СЧ ЕАЦП-П(М6)
 СДА 64-24
 ПАМ 0
 ИЛИ ШИФРАЦ
 ЗП ШИФРАЦ        БЛПР НЕ ТРЕБУЕТСЯ
 СЧИ М10
 СДА 64-8
 ИЛИ АЦПУ0
 ЗП ТСБОЙ+1
*
ВЫДОПТ НОП
 МОДА ВОЗБУФ
 ПВ (М14)
 СЧ ГРАЦ
 СДА 64(М10)
 НТЖ ВСЕЕД
 И РАЦ
 ИЛИ Е40(М10)
 ЗП РАЦ
 СЛИ М16(М7)
 МОДА СТРОКА
 УИА (М15)
 УИИ М32(М15)
 СЧ Е23
 ПБ ТВ40
*
КОНЕЦ НОП
 ПВ ЗАНОСМ(М16)
 СЧ Е47
 ПБ ОТКАЗН
 СТРН
*
*//////////////////////////////////
*//                              //
*//     П Е Р Е М Е Н Н Ы Е      //
*//                              //
*//////////////////////////////////
*
РАЦ КОНД М38В’3’       ПРИЗНАКИ
*
*   А0    А1
*  Е48   Е47 - ИДЕТ ПЕЧАТЬ ЗОНЫ.
*  Е46   Е45 - В СТРОКЕ ЕСТЬ НАЛОЖЕНИЕ.
*  Е44   Е43 - ИДЕТ ПЕЧАТЬ ПОСЛЕДНИХ 3-Х СТРОК.
*  Е42   Е41 - РАЗРЕШЕНО ЛИСТОВАНИЕ НА ТР 1.
*  Е40   Е39 - ДЛЯ ЕС-7934 - ЗАПИСАТЬ 1 В ПОЗ ДЛЯ СУЗ
*
*
ВЫБОР ПАМ 1             ДЛЯ ВЫБОРА УСТРОЙСТВА
АБУФ ПАМ 1             АДРЕС БУФЕРА СТРОКИ
*
ТЕКСИМ ПАМ 2             УКАЗАТЕЛЬ БАЙТА В АБЗАЦЕ
*
СИМПЕЧ ПАМ 2             СИМВОЛ В СТРОКУ
*
СИМЗАМ ПАМ 2             СИМВОЛ НАЛОЖЕНИЯ
*
ОСТАТ ПАМ 1             ОСТАТОК ОТ ДЕЛЕНИЯ
*
СИ ПАМ 1             ТЕКУЩИЙ СИМВОЛ
*
ПРОТЯЖ ПАМ 1             ЧИСЛО ПРОТЯЖЕК ПОСЛЕ СТРОКИ
*
ПРОТ   ПАМ 1             ЧИСЛО ПРОТЯЖЕК ДЛЯ КОМАНДЫ
*
НАБЗ ПАМ 2             НОМЕР СЕКТОРА В ВЫХ.ПОТОКЕ
*
РЯБУНС КОНД (2)В’1’       РЯБУНС ≠ 0  - НУЖЕН БУНС
*
ПОЗ ПАМ 2             ПОЗИЦИЯ В СТРОКЕ
*
NЛОГ ПАМ 2             ЛОГ.НОМЕРА ЕС-7934
*
НАЧПОЗ ПАМ 2             СДВИГ ЛЕВОЙ ГРАНИЦЫ ТЕКСТА
*
*
*//////////////////////////////////
*//                              //
*//      К О Н С Т А Н Т Ы       //
КСБДР КОНД М41Х’7F’М15Х’1’
*//                              //
*//////////////////////////////////
*
П141 КОНД В’141’
*
ДВБАЙТ НОП .             СДВИГИ ДЛЯ БАЙТОВ
 КОНД М41В’150’
 КОНД М41В’140’
 КОНД М41В’130’
 КОНД М41В’120’
 КОНД М41В’110’
 КОНД М41В’100’
*
МАСКИ НОП
 КОНД В’-1’М40Х’FF’
 КОНД В’-1’М32Х’FFFF’
 КОНД В’77777777’
 КОНД В’177777’
 КОНД В’377’
 КОНД В’0’
*
*        КОНСТАНТЫ ДЛЯ ПЕРЕВОДА ВРЕМЕНИ
DIVВР НОП
 КОНД В’4000000002251030’
 КОНД В’4000000027235356’
 КОНД В’4000000213636624’
 КОНД В’4000002566063704’
 КОНД В’4000020304467230’
 КОНД В’4000243656050754’
МОDВР НОП
 КОНД М47В’1’F’1800000’
 КОНД М47В’1’F’180000’
 КОНД М47В’1’F’30000’
 КОНД М47В’1’F’3000’
 КОНД М47В’1’F’500’
 КОНД М47В’1’F’50’
*
*       КУСЫ ЧТЕНИЯ ВЫХОДНОГО ПОТОКА
КУСАЦП НОП
 КОНД М17В’1’М2В’20’    20 ТРАКТ
 КОНД М17В’1’М2В’22’    22 ТРАКТ
*
ЕАЦП КОНД В’1’          АЦПУ-0
 КОНД В’4’          АЦПУ-1
*
ГРАЦ КОНД Х’АААААААААААА’   МАСКА ДЛЯ ГАШЕНИЯ РАЦ
*
*     ТАБЛИЦА ПЕРЕКОДИРОВКИ УПП -> ДКОИ (ЕВСDIС)
*     ------------------------------------------
* БАЙТ МЕНЬШЕ 40Х ЕСТЬ ССЫЛКА НА БАЙТЫ В ТАБЛИЦАХ
* "ЗАМЕНА" И "ЗАМЕН1" ДЛЯ ИМИТАЦИИ ЛИТЕР АЦПУ-128,
* ОТСУТСТВУЮЩИХ В ЗНАКОВОМ НАБОРЕ АЦПУ ЕС-7033 И Т.П.,
* ПУТЕМ НАЛОЖЕНИЯ.
*
ТДКОИ НОП
 КОНД Х’F0F1F2F3F4F5’      0 1 2 3 4 5
 КОНД Х’F6F7F8F94Е60’      6 7 8 9 + -
 КОНД Х’616В4В40В87С’      / , . ПРЮ ↑
 КОНД Х’4D5DЕ77Е5Е4А’      ( ) Х = ; [
 КОНД Х’5А5С7F7D7В4С’      ] * ’ ’ ≠ <
 КОНД Х’6Е7АС1ВАС2ВF’      > : А Б В Г
 КОНД Х’ВСС5ЕСFАСВСС’      Д Е Ж З И Й
 КОНД Х’D2СЕD4С8D6DС’      К Л М Н О П
 КОНД Х’D7С3Е3ЕВВЕЕ7’      Р С Т У Ф Х
 КОНД Х’ВВFЕFВFDЕFЕЕ’      Ц Ч Ш Щ Ы Ь
 КОНД Х’FСВ8DDС4С6С7’      Э Ю Я D F G
 КОНД Х’С9D1D3D5D8D9’      I J L N Q R
 КОНД Х’Е2Е4Е5Е6Е96D’      S U V W Z НАДЧ
 КОНД Х’0008Е550105F’      < > V ∧ ) ОТРИЦ
 КОНД Х’18206С286А60’      : = % ◇ I -
 КОНД Х’014F7F096F7D’
*=================================================
*
*     ТАБЛИЦЫ ЗАМЕНЫ - НАЛОЖЕНИЯ
*     --------------------------
ЗАМЕНА НОП
 КОНД Х’Е27Е7А5D6Е4С’
 КОНД Х’00000000ЕЕ4В’
*
ЗАМЕН1 НОП
 КОНД Х’6А6D60606D6D’
 КОНД Х’000000007D60’
*
*     НАБОР СИМВОЛОВ АЦПУ ЕС-7036
*     ---------------------------
*
ЗНАКИ КОНД Х’4А4В4С4D4Е4F’
 КОНД Х’505А5В5С5D5Е’
 КОНД Х’5F60616В6С6D’
 КОНД Х’6Е6F6А7А7В7С’
 КОНД Х’7D7Е7FВ8ВАВВ’
 КОНД Х’ВСВЕВFС1С2С3’
 КОНД Х’С4С5С6С7С8С9’
 КОНД Х’СВСССЕD1D2D3’
 КОНД Х’D4D5D6D7D8D9’
 КОНД Х’DСDDЕ2Е3Е4Е5’
 КОНД Х’Е6Е7Е8Е9ЕВЕС’
 КОНД Х’ЕЕЕFF0F1F2F3’
 КОНД Х’F4F5F6F7F8F9’
 КОНД Х’FАFВFСFDFЕFF’
*
ПРОБЕВ КОНД Х’404040404040’
*     СООБЩЕНИЯ ОПЕРАТОРУ
*     -------------------
*
ТСБОЙ ТЕКСТ П’4СБОЙ АЦПУ0↑’
ТКВАЦП ТЕКСТ П’4     525219860424  КВ  АЦПУ0↑’
АЦПУ0 КОНД П’АЦПУ0↑’
*
*     ПОСЛЕДНИЕ СТРОКИ ВЫДАЧИ
*     -----------------------
ТЕККНЦ НОП
 КОНД П’00БЧРФ’М40В’261’М32В’32’                АЦПУ
 КОНД П’/100ЮГ’М24В’271’М16В’272’             -0  ЭВ
 КОНД П’Н/1000’М16В’300’М8В’301’В’301’        М-0
 КОНД П’11 11 ’                               00.00.
 КОНД П’110000’М24В’32’М16В’142’М8В’32’В’177’ 00*
 КОНД М40В’177’М32В’142’М24В’032’М16В’177’М8В’175’В’142’
 КОНД М40В’116’М32В’177’М24В’203’М16В’200’М8В’177’В’203’
 КОНД М40В’200’М32В’177’М24В’203’М16В’146’М8В’175’В’000’
*
ТНЭВМ КОНД П’Н/1000’М16В’300’М8В’301’В’301’
ТНАЦП КОНД П’/100ЮГ’М24В’271’М16В’272’
РЗБВРЕ КОНД П’  0  0’
ТВРЕ1 КОНД П’11 11 ’
ТВРЕ2 КОНД П’110000’М24В’32’М16В’142’М8В’32’В’177’
ПОСЛЕД КОНК В’77773’
 КОНК А(ТЕККНЦ-П)
*
*
*
*//////////////////////////////////
*//                              //
*//         Б У Ф Е Р А          //
*//                              //
*//////////////////////////////////
*
ДЛБУФ ЭКВ 23
АДЛБУФ КОНД А(ДЛБУФ)
АБЗАЦ ПАМ 2
БУФЕР КОНД (ДЛБУФ)Х’404040404040’
 КОНД (ДЛБУФ)Х’404040404040’
 КОНД (ДЛБУФ)Х’404040404040’
 КОНД (ДЛБУФ)Х’404040404040’
*
*     МАГАЗИН
SТАСК ПАМ 5
П ЭКВ ПЕЧАТЬ
*
 АДРЕС П+’1400’
 ПАМ 256
*
*
*//////////////////////////////////
*//                              //
*//        В Н Е Ш Н И Е         //
*//                              //
*//////////////////////////////////
*
*
ДИСП70 ВНЕШ ШКАРУ,Е48Е47,П377,П7,П177,ГОД,СТУСТР,Е48П43
ДИСП70 ВНЕШ ВРЕМЯ,П3,П77777,ШИФРАЦ,ШКПЛАН
ДИСП70 ВНЕШ НОММБ,ВСЕЕД,ТВ40,ОТКАЗН
ДИСП70 ВНЕШ ЗАНОСМ,ПРЕДЕЛ,Е40П1,ТБУФ,АЗУ
*
ВЗУ ВНЕШ ФИЗОБМ,ТАБКОД
*
ТЕРМ ВНЕШ КОТПВВ
*
КИТ ВНЕШ Е48П41
*
МОТТ ВНЕШ ДЕЛЮ,ДЕЛЮ1,ШКППМ
*
ПВВ ВНЕШ (ЕС,ПВВЕС),ДАЙКОТ,ТИПАЦ,ТИПАЦД
*
АВМД ВНЕШ К6
 ФИНИШ

 ТЕСТООП :  , NAME,
 PROGRAM :  ,ENTRY,
 .
 . ПРОГРАММА ВКЛЮЧАЕТ В СЕБЯ 
 . НАЛАДОЧНЫЙ И КОМПЛЕКСНЫЙ ТЕСТЫ
 .
 . НАЛАДОЧНЫЙ ТЕСТ СЛУЖИТ ДЛЯ 
 . ПРОСТОЙ ПРОВЕРКИ РАБОТЫ КАНАЛА
 . КАДОПАМ-КРК-ДКС(ЭЛЕКТРОНИКА-60 N1)
 . 
 . КОМПЛЕКСТЫЙ ТЕСТ ВКЛЮЧАЕТ 7 ПОДТЕСТОВ
 . 1 - ЗАП.-ЧТ. ПО ОДНОМУ АДРЕСУ
 . 2 - ПЕРЕБОР АДРЕСОВ
 . 3 - ЗАП.-ЧТ. ЧЕРЕДОВАНИЯ ЕДИНИЦЫ-НУЛИ
 . 4 - ЗАП.-ЧТ. ЧЕРЕДОВАНИЯ НУЛИ-ЕДИНИЦЫ
 . 5 - ЗАП.-ЧТ. "БЕГУЩЕЙ" ЕДИНИЦЫ
 . 6 - ЗАП.-ЧТ. "БЕГУЩЕГО" НУЛЯ
 . 7 - ЗАП.-ЧТ. РАЗЛИЧНЫХ КОНФИГУРАЦИЙ
 . ПОДТЕСТЫ ВЫПОЛНЯЮТСЯ ОДИН ЗА ДРУГИМ.
 . ПОДТЕСТ 7 ПЕРЕДАЕТ УПРАВЛЕНИЕ НА ПОДТЕСТ 1.
 . ГРАНИЦА ПРОВЕРЯЕМЫХ АДРЕСОВ ПО УМОЛЧАНИЮ 10-67777(8).
 . ИМЕЕТСЯ ВОЗМОЖНОСТЬ РУЧНОЙ УСТАНОВКИ ГРАНИЦ С ТУМБ. РЕГ.
 .
 . УПРАВЛЕНИЕ ТЕСТОМ:
 . ДЛЯ ОБОИХ ТЕСТОВ:
 . 
 . 1Т.Р. 25-27РР N НАПР КРК
 .          24Р=1 ВЫБРОС ТЕСТА ИЗ РЕШ.
 . 4Т.Р. 25Р=1 ПЕРЕХОД НА НАЛАД. ТЕСТ.
 .       25Р=0 КОМПЛЕКСНЫЙ ТЕСТ.
 .
 . ДЛЯ НАЛАД. ТЕСТА
 .
 . 1Т.Р. 1-15РР - ЗАДАННЫЙ АДРЕС
 . 4Т.Р. 1-16 - ЗАДАННЫЙ КОД
 .         22Р - ЧТЕНИЕ ПО ЗАДАННОМУ АДРЕСУ
 .         23Р - ЗАП. ПО ЗАД. АДРЕСУ ЗАД. КОДА
 .         22Р=23Р=0 - ЗАПИСЬ ПО ЗАД. АДР. ЗАД. КОДА
 .         ЗАТЕМ ЧТЕНИЕ ТАМ ЖЕ.
 .
 . ДЛЯ КОМПЛ. ТЕСТА:
 .
 . 1Т.Р. 1-15РР - НАЧ. АДРЕС ПРИ РУЧНОМ ВЫБ. ГРАНИЦ
 . 4Т.Р. 1-15РР - КОН. АДРЕС ПРИ РУЧНОМ ВЫБ. ГРАНИЦ
 .         21Р=1 - РУЧНОЙ ВЫБОР ГРАНИЦ
 .         21Р=0 - АВТОМ. ВЫБОР ГРАНИЦ
 .         22Р=1 - ОСТАНОВ ПРИ ОШИБКЕ
 .         23Р=1 - ПЕРЧАТЬ СООБЩЕНИЯ ОБ ОШИБКАХ
 УС	 :  , EQU  ,133В
 	   7, BASE,*
	    , ITA ,
	    , XTA ,
	    , ATI ,6
	    , ATI ,5
	    , ATX ,NPR
	    , NTR ,3
 СТАРТ2	:   , *65 ,1
	    , ATX ,ТР1
	    , ASN ,64+24
	    , AAX ,=7
	    , AOX ,=7701 0000 4000 0000
	    , *50 ,УС .УСТ. N НАПРАВЛ
 СТАРТ0	:  1, VTM ,
 СТАРТ  :   , BSS , 
 	    , *65 ,1 .ЧТЕНИЕ ТУМБ. РЕГ.
	    , AEX ,ТР1
	    , U1A ,СТАРТ2
	    , *65 ,4
	    , ATX ,ТР4
	    , XTA ,ТР1
	    , AAX ,Е24Р 
	    , U1A ,ВЫБРОС
	    , XTA ,ТР4
	    , AAX ,Е24Р .ТЕСТ ВКЛЮЧЕН
	    , UZA ,ПАУЗА
	    , XTA ,ТР4
	    , AAX ,Е25Р .НАЛАД. ТЕСТ:
	    , U1A ,ТН
	  13, VJM ,ФАД .ФОРМ.ДИАП.АДР.
	   1, UTM ,1   .УВЕЛИЧ. СЧЕТЧ. N ТЕСТ
	   1, UJ  ,ПЕРЕКЛ-1
 ПЕРЕКЛ :   , BSS ,
	:   , UJ  ,Т1 .ЗП-ЧТ ПО АДР. НИЖ. ГР.
	:   , UJ  ,Т2 .ЗП-ЧТ АДР. В АДР. 
	:   , UJ  ,Т3 .ЗП-ЧТ ВСЕ 1
	:   , UJ  ,Т4 .ЗП-ЧТ ВСЕ 0
	:   , UJ  ,Т5 .БЕГУЩАЯ 1
	:   , UJ  ,Т6 .БЕГУЩАЯ 0
	:   , UJ  ,Т7 .ЦИКЛ СЛ. КОН.=113
	:   , UJ  ,СЧЕТ .УВЕЛИЧ. СЧЕТ ПРОХОД.
 TH :	    , BSS ,  .НАЧАЛО НАЛАД. ТЕСТА
 	  13, VJM ,ФАДР
	    , XTA ,ТР4
	    , AAX ,Е22Р
	    , U1A ,ЦЧТ
	    , XTA ,ТР4
	    , AAX ,Е23Р.
	    , U1A ,ЦЗП
 ЦЗЧ	:   , BSS , .ЗАП-ЧТЕН.
 	   3, VTM ,1-200
 ЦЗЧ1  :    , XTA ,ТР4
 	   2, Z13 ,20000B
	   2, Z03 ,20000B
	    , ASN ,64+24
	    , ATI ,6
	   3, VLM ,ЦЗЧ1
	    , UJ  ,СТАРТ
 ЦЗП  :	    , BSS , .ТОЛЬКО ЗАП.
	   3, VTM ,1-200
 ЦЗП1 :	    , XTA ,ТР4
 	   2, Z13 ,20000B
	   3, VLM ,ЦЗП1
	    , UJ  ,СТАРТ
 ЦЧТ  :     , BSS , .ТОЛЬКО ЧТ.
 	   3, VTM ,1-200
 ЦЧТ1 :    2, Z03 ,20000B
 	    , ASN ,64+24
	    , ATI ,6
	   3, VLM ,ЦЧТ1
	    , UJ  ,СТАРТ
 ФАДР :     , BSS , .ФОРМИР. АДР. ДЛЯ НАЛАД. ТЕСТА.
            , XTA ,ТР1
	    , ATI ,2
	  13, UJ  ,
 ВЫБРОС :   , BSS ,
 	    , XTA ,NPR .ВЫВОД ТЕСТА ИЗ РЕШЕНИЯ
	    , ATX ,NR
	  12, VJM ,NUM
	    , XTA ,РR
	    , ATX ,ТВЫБ+8
	    , ITA ,5
	    , ATX ,NR
	  12, VJM ,NUM
	    , XTA ,PR
	    , ATX ,ТВЫБ+12
	  14, VTМ ,ТВЫБ
	    , ITS ,14
	  14, VTM ,ТВЫБ+12
	    , ITS ,14
	  14, VTM ,10
	    , ITS ,14
	    , CALL,PRINT8
 ВЫБ	:   , SJ  ,
 ПАУЗА  :   , XTA ,=14
 	    , *50 ,7 700В
	    , UJ  ,СТАРТ
 Т1    :    , BSS ,
           3, VTM ,1  .ТЕСТ1 ЗП-ЧТ. РД.
	    , XTA ,НАД
	    , ATI ,2
	    , XTA ,ЕКОН
	    , ATX ,Р
 Т1А   :   2, Z13 ,20000В .ЗП ПО АДР. НИЖ. ГР.
           2, Z03 ,20000В .ЧТ. ТАМ ЖЕ 
	    , ASN ,64+24
	    , AAX ,E16
	    , ATX ,Р1
	  13, VJM ,СР1СЛ .СЧ КОД ВЕРНЫЙ:
	    , XTA ,Р
	    , ARX ,ЕКОН
	    , ATX ,Р
	   3, VLM ,Т1А
	    , UJ  ,СТАРТ
 Т2   :     , BSS ,
            , XTA ,НАД .ТЕСТ ЗП-ЧТ А-(А)
	    , ATI ,2
 Т2А  :     , ITA ,2
 	   2, Z13 ,20000В
	    , AEX ,ВАД .РОСП. ПАМЯТИ
	    , UZA ,Т2В
	   2, UTM ,1
	    , UJ  ,Т2А
 Т2В  :     , BSS ,
 Т2ВА :     , ITA ,2
            , ATX ,Р
	   2, Z03 ,20000В .ЧТЕНИЕ
	    , ASN ,64+24
	    , AAX ,E16
	    , ATX ,Р1
	  13, VJM ,СР1СЛ
	    , XTA ,Р
	    , AEX ,НАД
	    , UZA ,СТАРТ
	   2, UTM ,-1
	    , UJ  ,Т2ВА
 Т3  :      , BSS ,
            , XTA ,Е16 .ТЕСТ ЗП-ЧТ ВСЕ 1
	    , ATX ,Р
	    , UJ  ,Т34
 Т4  :      , BSS ,
            , XTA , .ТЕСТ ЗП-ЧТ ВСЕ 0
	    , ATX ,Р
	    , UJ  ,Т34
 Т34 :      , BSS ,
            , XTA ,НАД
	    , ATI ,2
 Т34ЗП :    , XTA ,Р .ЦИКЛ ЗП.
	   2, Z13 ,20000В
	    , ITA ,2
	    , AEX ,ВАД
	    , UZA ,Т34ЧТ
	   2, UTM ,1
	    , UJ  ,Т34ЗП
 Т34ЧТ  :   , BSS ,  .ЦИКЛ ЧТ.
            , XTA , НАД
	    , ATI ,2
 Т34ВА  : 13, VJM ,ОБРАБ
 	    , UJ  ,Т34ВА
 Т5     :   , BSS ,
            , XTA ,    .БЕГУЩИЙ 0
	    , ATX ,P3
	    , UJ  ,Т56
 Т6     :   , BSS ,
            , XTA ,E16
	    , ATX ,P3
	    , UJ  ,T56
 Т56    :   , BSS ,
            , XTA ,Е1
	    , ATX ,Р2
	    , XTA ,НАД
	    , ATI ,2
 Т56А   : 13, VJM ,ФОР
            , XTA ,Р
	   2, Z13 ,20000В
	    , ITA ,2
	    , AEX ,ВАД
	    , UZA ,Т56В
	   2, UTM ,1
	    , UJ  ,Т56А
 Т56В    :   , BSS ,
            , XTA ,Е1
	    , ATX ,Р2
	    , XTA ,НАД
	    , ATI ,2
 Т56ВА  : 13, VJM ,ФОР
          13, VJM ,ОБРАБ
	    , UJ  ,Т56ВА
 ФОР	:   , BSS ,
            , XTA ,P3
	    , AEX ,P2
	    , ATX ,P
	    , XTA ,P2
	    , ASN ,64-1
	    , AAX ,E16
	    , ATX ,P2
	  13, U1A ,
	    , XTA ,Е1
	    , ATX ,P2
	  13, UJ  , 
 ОБРАБ  :   , BSS ,
           2,Z03  ,20000В
	    , ASN ,64+24
	    , AAX ,E16
	    , ATX ,P1
	    , ITA ,13
	    , ATX ,P8
	  13, VJM ,СР1СЛ
	    , XTA ,P8
	    , ATI ,13
	    , ITA ,2
	    , AEX ,ВАД
	    , UZA ,СТАРТ
	   2, UTM ,1
	  13, UJ  ,
 ФАД   :    , BSS ,
 	    , XTA ,ТР4
	    , AAX ,E21Р
	    , U1A ,ФАД1
	    , XTA ,ЕНАД
	    , ATX ,НАД
	    , XTA ,ЕВАД
	    , ATX ,ВАД
	  13, UJ  , 
 ФАД1  :    , XTA ,ТР1
 	    , AAX ,E15
	    , ATX ,НАД
	    , XTA ,ТР4
	    , AAX ,Е15
	    , ATX ,ВАД
	  13, UJ  ,
 Т7   :     , BSS ,
           3, VTM ,1-20 .ЦИКЛ СЛОЖЕНИЯ 
	    , XTA ,ЕКОН
	    , ATX ,P
 T7A1 :     , XTA ,НАД
 	    , ATI ,2
	    , XTA ,P
	    , ATX ,P2
 Т7А  :    2, Z13 , 20000В
	    , ITA ,2
	    , AEX ,ВАД
	    , UZA ,Т7В
	   2, UTM ,1
	    , XTA ,Р
	    , ARX ,ЕКОН
	    , ATX ,Р
	    , UJ  ,Т7А
 Т7В  :     , BSS ,
 	    , XTA ,НАД
	    , ATI ,2
	    , XTA ,P2
	    , ATX ,P
 Т7ВА :    2, Z03 ,20000В
            , AAX ,Е16
	    , ATX ,P1
	  13, VJM ,СР1СЛ
	    , ITA ,2
	    , AEX ,ВАД
	    , UZA ,R
	   2, UTM ,1
	    , XTA ,Р
	    , ARX ,ЕКОН
	    , ATX ,P
	    , UJ  ,Т7ВА
 R    :    3, VLM ,Т7А1
            , XTA ,NPR
	    , A+X ,=1
	    , ATX ,NPR
	    , UJ  ,СТАРТ
 СЧЕТ :	    , BSS ,
            , XTA ,NPR .УВЕЛИЧ. СЧЕТЧ.
	    , A+X ,=1  .ЧИСЛА ПОВТОР. ТЕСТА
	    , UJ  ,СТАРТ0
 СР1СЛ :    , BSS ,
 	    , XTA ,P
	    , AAX ,E16
	    , AEX ,P1
	    , U1A ,ERR
	  13, UJ  ,
 ERR   :    , BSS ,   .НЕСРАВНЕНИЕ КОДА
           5, UTM ,1
	   2, Z03 ,20000В .ПОВТ. ЧТ.
	    , ASN ,64+24
	    , AAX ,E16
	    , ATX ,P7
	    , *65 ,4
	    , AAX ,E23P .НАДО ПЕЧАТЬ
	    , U1A ,ERRA
 МЕ5   :    , *65 ,4
 	    , AAX ,E26Р .ОСТ. ПРИ ОШ.
	    , U1A ,СТОП
	  13, UJ  ,
 ME2   :    , *65 ,4
            , AAX ,E22P
	    , U1A ,СТОП1
	  13, UJ  ,
 СТОП :    2, Z33 ,  .МЕРТЫВЫЙ СТОП
          13, UJ  ,
 СТОП1 :    , XTA ,=14 .СТОП С ОЖИДАНИЕМ 
	    , *50 ,7 700B
	    , UJ  ,ME2
 ERRA  :    , XTA ,P
            , AAX ,E16
	    , ATX ,NR
	  12, VJM ,NUM
	    , XTA ,PR
	    , ATX ,TERR+4
	    , XTA ,P1
	    , ATX ,NR
	  12, VJM ,NUM
	    , XTA ,PR
	    , ATX ,TERR+6
	    , ITA ,2
	    , ATX ,NR
	  12, VJM ,NUM
	    , XTA ,PR
	    , ATX ,TERR+9
	    , ITA ,1
	    , ATX ,NR
	  12, VJM ,NUM
	    , XTA ,PR
	    , ATX ,TERR+12
	    , XTA ,P7
	    , ATX ,NR
	  12, VJM ,NUM
	    , XTA ,PR
	    , ATX ,TERR+14
	    , ITA ,13
	    , ATX ,P5
	  14, VTM ,TERR
	    , ITS ,14
	  14, VTM ,TERR+14
	    , ITS ,14
	  14, VTM ,10
	    , ITS ,14
	    , CALL,PRINT8
	    , XTA ,P5
	    , ATI ,13
	    , UJ  ,ME5
 NUM  :     , BSS ,   .ФОРМИРОВАНИЕ ЧИСЛА В КОДАХ ISO
            , XTA ,
	    , ATX ,PR
	   4, VTM ,1-6
	  11, VTM ,8
	    , XTA ,NR
 NUM1 :     , AAX ,=7
            , AOX ,=60
	  11, ASN ,64-8
	    , AOX ,PR
	    , ATX ,PR
	  11, UTM ,-8
	    , XTA ,NR
	    , ASN ,64+3
	    , ATX ,NR
	   4, VLM ,NUM1
	  12, UJ  ,
 P	:   , BSS , 1
 P1	:   , BSS , 1
 P2	:   , BSS , 1
 P3	:   , BSS , 1
 P4	:   , BSS , 1
 P5	:   , BSS , 1
 P6	:   , BSS , 1
 P7	:   , BSS , 1
 P8	:   , BSS , 1
 NPR	:   , BSS , 1
 NR	:   , BSS , 1
 PR	:   , BSS , 1
 ВАД	:   , BSS , 1
 НАД	:   , BSS , 1
 ВРЕ	:   , BSS , 1
 ТР1	:   , BSS , 1
 ТP4	:   , BSS , 1
 Е15	:   , LOG , 77 777 .15 РАЗРЯДОВ
 Е22Р	:   , LOG , 10 000 000 .22 РАЗРЯД
 Е23Р	:   , LOG , 20 000 000 .23 РАЗРЯД
 Е14	:   , LOG , 37 777 .14 РАЗРЯДОВ
 Е24Р	:   , LOG , 40 000 000 .24 РАЗРЯД
 Е25Р	:   , LOG , 100 000 000 .25 РАЗРЯД
 Е26Р	:   , LOG , 200 000 000 .26 РАЗРЯД
 ЕКOН	:   , LOG , 113 351 .KONST
 Е16	:   , LOG , 177 777 .16 РАЗРЯДОВ
 ЕНАД	:   , LOG , 10      .НАЧ. ПРОВ. АДР. ОЗУ ДКС
 ЕВАД	:   , LOG , 67777   .КОН. ПРОВ. АДР. ОЗУ ДКС
 Е1	:   , LOG , 1
 Е21Р	:   , LOG , 4 000 000 .21Р
 ТВЫБ   :   , ISO ,24HТЕСТ ВЫВЕДЕН ИЗ РЕШЕНИЯ;
	    , CONT,24H КОЛИЧ. ПРОХОДОВ ТЕСТА:
	    , CONT,6HЧИСЛО 
	    , CONT,18H ;ОШИБОК ДАННЫХ:
	    , CONT,6HЧИСЛО
 TERR   :   , ISO ,24HОШИБКА ДАННЫХ; ЭТАЛОН
	    , CONT,6H; КОД:
	    , CONT,6HЧИСЛО
	    , CONT,12H ; АДРЕС:
	    , CONT,12H ; ТЕСТ N
	    , CONT,6HЧИСЛО
	    , CONT,6HПОВТ:
	    , CONT,6HЧИСЛО
	    , END ,



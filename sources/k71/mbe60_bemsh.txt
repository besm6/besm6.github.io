ШИФР 419900ЗС5^
ОЗУ 32^
ТРА 100^
АЦП 64^
ЕЕВ1А3
*name
*      call yesmemory
*      system
*main mbe60
*bemsh
inp$$$
 macro
&l call &t
&l пво &t.(М15)
 пам 0
 mend
 macro
&l э70 &t
&l кк '70',&t
 пам 0
 mend
 macro
&l э74 &t
&l кк '74',&t
 пам 0
 mend
МВE60 старт 1 ТЕСТ ЗАПИСИ ЧЕРЕЗ ДКС
PRINT8 extrd PRINT8
US	экв '133' .ДО 1.06.82 БЫЛО: US экв '132'
          УПОТР *(М12)
	  УИА	*(М12)
*
* ДО 1.06.82  БЫЛО : СЧ КОНД В'20 000 001'
*
*	   СЧ =В'7701000040000001'
          СЧ МАСЭ50 .ТЕSТ МОDЕ FОR Е60
          Э50 US
               И Е15П1
          УИА ОШКМТР(М16)
          ПЕ ВUG
*  ПРОВЕРКА ЗП/ЧТЕНИЯ В Э60
          РЖА 3
          СЧ =В'1'
          кк 26,'74000' 
           СЧИ 
          кк 26,'4000' 
           СДА 64+24
           И Е16П1
           НТЖ =В'1'
          УИА ОШЗП(М16)
           ПЕ ВUG
*
*   ***АВС. ЗАГРУЗЧИК ДЛЯ Э-60***
*
*   НАЧ. УСТ-КА
СМ400	ОПАМ	1
РАGЕ	ОЛПАМ	1024
*
LОDR	 НОП
*  ФОРМИРОВАНИЕ ИНФ-ГО СЛОВА ЧТЕНИЯ С МЛ
           МОДА СМ400
          СЧ 
          ЗП (М17)
           И Е18П1
 	ИЛИ Е46   .КОД ЧТЕНИЯ С МЛ
          УИА РАGЕ(М16)
           СЧИМ М16
           СДА 64-20
          ИЛИ (М17)
          ЗПМ INFМDL
*  УСТ-КА ВЕРХНЕЙ ГРАНИЦЫ ЗАГРУЗКИ
           СДА 64+24
           И =В'37'
           ЗП LОNG
*  ОБРАБ-КА ЗАПИСИ М-400
ВЕG	   ПВ RD16(М14) .ЧТ.1-ГО СЛОВА ЗАПИСИ
           ПО ВЕG
           ЗП КS
           НТЖ =В'1'
          УИА ОПЛЗПМ(М16)
           ПЕ ВUG  .НЕПРАВ.СТРУКТ. ЗАПИСИ
          ПВ RD16(М14) .ЧТЕНИЕ ДЛИНЫ В БАЙТАХ
           НТЖ =В'6'
           ПО LЕND
          СЧ RLОС
           И =В'1'
           УИ М13  .ОТМЕТКА НЕЧ-ГО КОЛ-ВА БАЙТОВ
           РЖА 3
          СЧ RLОС
           СДА 64+1
           ВЧОБ =В'3'
          УИА ОШДЛЗП(М16)
           ПО ВUG  .НЕПРАВ.ДЛИНА ЗАПИСИ
           УИ (М10)
          СЛИА 1(М10)
          ПВ RD16(М14).ЧТЕНИЕ АДРЕСА
           И =В'1'
          УИА ОШАДР(М16)
           ПЕ ВUG
          СЧ RLОС
           СДА 64+1
           УИ М11
*  ЦИКЛ. РАСКРУТКА ИНФ. ЗАПИСИ
LР	ПВ RD16(М14)
*  ЗАПИСЬ СЛОВА В Э60
          кк 26,'70000'(М11)
           СЧИ 
          кк 26,(М11)
           СДА 64+24
           И Е16П1
           НТЖ RLОС
          УИА ОЗПЭ60(М16)
           ПЕ ВUG  .ОШ-КА ПРИ ЗАПИСИ В Э-60
          СЛИА 1(М11) .МОДИФ-ЦИЯ АДРЕСА
          ЦИКЛ LР(М10)
*  ПРОВЕРКА КС ЗАПИСИ
          ПВ RD16(М14)
          ПИО LР1(М13)
           И E8П1  .ОБР-КА НЕЧ-ГО БАЙТА
          кк 26,'70000'(М11)   .ЗП. НЕЧ-ГО БАЙТА
LР1	 НОП
          СЧ КS
          УИА ОШКС(М16)
           ПО ВЕG
ВUG	 СЧИМ М16
           СЧИМ М16
          УИА 20(М16)
           СЧИМ М16
          CALL РRINТ8
           Э74 0 
LЕND	 НОП
          ПВ RD16(М14) .ЧТЕНИЕ АDР. ЗАГРУЗКИ
           УИ М13
          ПВ RD16(М14) .ЧТ КС
          СЧ КS
          УИА ОШКСКЦ(М16)
           ПЕ ВUG
*  ПЕЧАТЬ АДРЕСА СТАРТА ПРОГО Э-60
           СЧИ М13
           СДА 64-32
           РЗБ МПРОГО
           ИЛИ МПРОГ1
           ЗП МLОС1
          УИА МLОС(М16)
           СЧИМ М16
          УИА МLОС1(М16)
           СЧИМ М16
          УИА 20(М16)
           СЧИМ М16
          CALL РRINТ8
          УИА МLОС(М16)
           СЧИМ М16
          УИА В'3'(М16)
           СЧИМ М16
          ПВ VТ60(М15)
           Э74 0 
МLОС	ТЕКСТ Д'АDР СТАРТА'
МLОС1	КОНД Д'000000'
КS	КОНД В'0'  .ЯЧЕЙКА КОНТРОЛЬНОЙ СУММЫ
RLОС	КОНД В'0'  .РАБ. ЯЧЕЙКА
SW	КОНД В'0'  .КОНСТ.СДВИГА 16*N
ВWОRD	КОНД В'440002400013'  .ТЕКУЩЕЕ СЛОВО Б6 НЗ ЛИСТА
АВW	КОНД В'2000'  .АDРЕС ТЕКУЩЕЙ ЯЧЕЙКИ РАGЕ
LОNG	КОНД В'0'  .МАХ ГРАНИЦА ЗАПИСИ В Э-60
ТLОNG	КОНД В'0'  .КОЛ-ВО ПРОЧИТ. Х СТР.
INFМDL	КОНД В'0'  .АДРЕС БУФЕРА НА МЛ
МПРОГО	КОНД X'177777'
МПРОГ1  КОНД Х'303030303030'
*АДРЕСА  ИСП. VT60 В ПАМ. ДКС
VT60А1 экв '77672'
VT60А2 экв  '77673'
*
Е8П1  КОНД В'377'
Е15П1 конд В'77777'
Е16П1 конд В'177777'
Е18П1 КОНД В'777777'
E11   КОНД В'2000'
Е46   КОНД М45В'1'
МАСЭ50 КОНД В'7701000040000001'
*ДИАГНОСТИКА ОШИБОК
ОШКМТР   КОНД Д'ОШКМТР'
ОШЗП	 КОНД Д'ОШ ЗП1'
ОПЛЗПМ	 КОНД Д'ПЛЗПМ4'
ОШДЛЗП	 КОНД Д'ДЛЗП<'
ОШАДР	 КОНД Д'НЕЧАДР'
ОЗПЭ60	 КОНД Д'ОШЗПЭ6'
ОШКС	 КОНД Д'ОШ КС'
ОШКСКЦ	 КОНД Д'ОШКСКЦ'
*ДИАГНОСТИКИ
ЧТЛИС	КОНД Д'СТР>>'
*  П/ПР ВЫБОРКИ СЛОВА М4 Н3 СЛОВА Б6
RD16	 НОП
          СЧ SW
           ПЕ RD161
*  П/ПР ВЫБОРКИ СЛОВА Н3 ЛИСТА РАGЕ
RD48	 НОП
          СЧ АВW
	    НТЖ Е11
*           НТЖ =В'2000'
           ПЕ RD481
* П/ПР ЧТЕНИЯ ОЧЕРЕДНОГО ЛИСТА
RDРАG	 НОП
           ЗП АВW
          СЧ ТLОNG
           СЛЦ =В'1'
           ЗП ТLОNG
          ВЧ LОNG
          УИА ЧТЛИС(М16)
           ПО ВUG
*  ЧТЕНИЕ ЛИСТА
          Э70 INFМDL
*  МОD INF
          СЧ INFМDL
           СЛЦ =В'1'
           ЗП INFМDL
RD481	 НОП
          СЧ АВW
           УИ М16
           СЛЦ =В'1'
           ЗП АВW
           МОДА РАGЕ
          СЧ (М16) 
           ЗП ВWОRD
          СЧ =В'60'
RD161	ВЧ =В'20'
           ЗП SW
           УИ М16
          СЧ ВWОRD
          СДА 64(М16)
           И Е16П1
           ЗП RLОС
           СДА 64+8
           СЛЦ RLОС
           СЛЦ КS
           И Е8П1
           ЗП КS
          СЧ RLОС
          ПБ (М14)
VТ60	 НОП
*  ПРОГР. ВЫВОДА НА VТ60 ИЗ Б6
*  ФОРМАТ:САLL VJ-60(НАЧ.АDР,ДЛИНА)
           УИ М16
          СЧ (М16) 
           РЖА 3
           ВЧОБ =В'1'
           УИМ М16  .114=1-ДЛИНА
           УИ М11
VТL	СЧ (М11) 
             ЗП RLОС
VТ0	УИА 1-1000(М3)
VТ1	МОДА VT60A1
	кк 26,0
                СДА 64+24
               И =В'200'
               НТЖ =В'200'
               ПО VТ2
            ЦИКЛ VТ1(М3)
                   УИМ 
                ПБ (М15)
VТ2	СЧ RLОС
                  ПЕ VТL1
               СЛИА 1(М11)
               ЦИКЛ VТL(М16)
                  УИМ 
               ПБ (М15)
VТL1	 СДА 64-8
           ЗП RLОС
           СЧМР 
           МОДА VT60А2  .АДРЕС VТ60
          кк 26,'70000' .ВЫВОД СНМ
                   ПБ VТ0
m1 equ 1
m2 equ 2
m3 equ 3
m4 equ 4
m5 equ 5
m6 equ 6
m7 equ 7
m10 equ '10'
m11 equ '11'
m12 equ '12'
m13 equ '13'
m14 equ '14'
m15 equ '15'
m16 equ '16'
m17 equ '17'
          финиш
eoi$$$
trn$$$
0-0
end$$$
*libra:2
*call dtran(МВE60)
base:10,0
*super
*assem
*read:1
*end file
``````
еконец


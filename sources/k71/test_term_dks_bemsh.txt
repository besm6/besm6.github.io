ШИФР 419900ЗС5^
ОЗУ 32^
ТРА 100^
АЦП 64^
ЕЕВ1А3
*name
*bemsh
inp$$$
 macro
&l call &t
&l пво &t.(М15)
 пам 0
 mend
* последующие макросы введены для удобства сравнения
* результата компиляции с Мадленом, их при желании можно удалить
 macro
&l пв &t
&l пво &t
 пам 0
 mend
 macro
&l э50 &t
&l кк '50',&t
 пам 0
 mend
 macro
&l э53 &t
&l кк '53',&t
 пам 0
 mend
 macro
&l э64 &t
&l кк '64',&t
 пам 0
 mend
 macro
&l э71 &t
&l кк '71',&t
 пам 0
 mend
 macro
&l э74 &t
&l кк '74',&t
 пам 0
 mend
ТЕSТЕR старт 1 ОТЛАДКА ЭМУЛЯТОРА ТЕРМИНАЛА
ОКНО extrd OKHO
*        
*         ПРОГРАММА-ПАРТНЕР ДЛЯ ОТЛАДКИ ЭМУЛЯТОРА ТЕРМИНАЛА
*         БЭСМ-6 НА М-400( Э60)
*        
N         ЕQU 0  .НОМЕР М-400
*ПРИ N Э-60=0 НАЧИНАЮТСЯ ПОСТОЯННЫЕ
*ПОПЫТКИ ЗАПИСИ В ДКС, ПРИВОДЯЩИЕ
*К СБОЮ АЦПУ. ПОХОЖЕ ЭТО
*ПРИЗНАК АВТОМАТИЧЕСКОГО РЕЖИМА РАБОТЫ С ДКС,
*КОГДА РЕЖИМ ПРИЕМА-ВЫДАЧИ, ОПРЕДЕЛЯЕТСЯ  О С
*
          УПОТР *(М7)
          УИА *(М7)
          РЖА 3
connct    УИА N(М5)
          УИА 0(М3)
          УИА 0(М4)
          СЧИ М5
          ИЛИ Е23
          ЗП IW
          СЧ К7703 !В КОНФУС 7705
          Э50 '133' .ЗАПРОС НА КОММУТАЦИЮ
          ПБ НАРРУ
* НЕ ИСПОЛЬЗ. ФРАГМЕНТ?
          СЧ B40 !НЕТ ПЕРЕДАЧИ УПРАВЛЕНИЯ
          Э50 '7700'
          ПБ connct
*
НАРРУ     УИА decodr(М16)
          СЧИ М16
          Э53 '11' .АДРЕС ДЕШИФРАТОРА
          СЧ ЕМАSК .МАСКА ШКАЛЫ СОБЫТИЙ
          Э53 '12'
          Э71 АLL7 .ОПРОС ПОДКЛЮЧЕНИЯ ТЕРМ
          НЕД 
          УИ М10 .НОМЕР Н-ТЕРМИНАЛА
          СЧ ЕМАSК
          ИЛИ Е48А
          Э53 '21'
АGАIN     СЧ О24П10
          Э50 '7700' .УСТ. БУДИЛЬНИКА
          Э53 '17' .ЖДУ СОБЫТИЙ
          ПБ АGАIN
IFIЕLD    ПАМ 11
*         (48-25Р)-МАСКА ШК . СОБЫТИЙ
*         (24-1Р) -ШК . СОБЫТИЙ
decodr    ЗП ЕSСАLЕ !ЧТО ЗАПИСАЛИ?
* СЧ Е48П25
* Э50 '7700' .ОТМЕНА БУДИЛЬНИКА
           Э53 '13'  .ЗАКРЫЛИСЬ
          СЧ ЕSСАLЕ
          И B200
          ПО decod1
*  СДЕЛАЛИ ВКН
          СЧ =M42B'77'
          call ОКНО
          Э74 0 !У НАС ПОСЛЕ ВКН ПРОДОЛЖАЕТСЯ
*ЗАПИСЬ В ДКС
*
decod1       СЧ ЕSСАLЕ  = 0
          И ЕАV .17Р
          ПО vdks
*  АVОSТ
          Э50 '101'
          call ОКНО
          Э74 0
vdks       СЧ ЕSСАLЕ
          И ЕDКS
          ПЕ RЕQ .ЗАПРОС ОТ ДКС
IFОUТ     СЧ ЕSСАLЕ
          ПО УЕS .НА ОЖИДАНИЕ
          И ЕОUТ
          ПЕ ТОUТ .ЗАВЕРШ. ВЫВОД НА Н-ТЕРМИНАЛ
IFINР     СЧ ЕSСАLЕ
          И ЕINР
          ПЕ ТINР .ЗАВЕРШ. ВВОД С Н-ТЕРМИНАЛА
          СЧ ЕSСАLЕ
          И ЕТIМ
          ПЕ IFТIМЕ .БУДИЛЬНИК
УЕS       Э53 '17' .ОЖИДАНИЕ
IFТIМЕ    СЧИ 0
          Э53 '14' .РАЗРЕШЕНИЕ СОБЫТИЙ
          Э53 '15' .ВОЗВРАТ В ТОЧКУ ПРЕРЫВАНИЯ
АLАRМ     call ОКНО
          Э74 0 
*        
*         ДЕШИФРАТОР ЗАПРОСОВ ОТ М-400 ( Э60)
*        
RЕQ       СЧ ЕSСАLЕ
          ИЛИ ЕDКS
          НТЖ ЕDКS
          ЗП ЕSСАLЕ
          МОД SFIFО(М5)
          кк 26,1 .СЧЕТЧИК ПРЕРЫВАНИЙ ПОЛУЧАТЕЛЯ
          СДА 64+24
          ЗП IСNТ
          МОД SFIFО(М5)
          кк 26,0
          СДА 64+24
          НТЖ IСNТ
          ПО IFОUТ .НЕ НАШЕ ПРЕРЫВАНИЕ
DЕС       СЧ IСNТ
          УИ М16
          МОД SFIFО(М5)
          кк 26,2(М16)
          СДА 64+24
          УИ М2 .НОМЕР АБОНЕНТА
          СЛИА 1(М16)
          СЧИ М16
          И Е4П1
          ЗП IСNТ
          СЛИА -48(М2)
          ПИО ОS(М2) .СИСТЕМНЫЙ ЗАПРОС ОТ S-МАШИНЫ
          СЛИА 48(М2)
          СЧ Е48
          СДА 64(М2)
          МОДА (М5)
          И SАRМ
          ПЕ АСТIV
          call ОКНО
          Э74 0   .НЕТ ПОЛУЧАТЕЛЯ
АСТIV     СЧИ М2
          И Е6
          ПО ТRЕQ .ЗАПРОС ОТ S-ТЕРМИНАЛА
          call ОКНО
          Э74 0   .ПУТАНИЦА
ЕNDЕС     МОД SFIFО(М5)
          кк 26,0 .СЧЕТЧИК ПРЕР. ИСТОЧНИКА
          СДА 64+24
          И Е4П1
          НТЖ IСNТ
          ПЕ DЕС .ЕСТЬ ЕЩЕ ПРЕРЫВАНИЯ
          СЧ IСNТ
          МОД SFIFО(М5)
          кк 26,'70000'+1 .НАРАЩИВАНИЕ СЧЕТЧИКА
          ПБ IFОUТ .НА ОБРАБОТКУ ДРУГИХ СОБЫТИЙ
*        
*         ПРИШЛА КОМАНДА ОТ СИСТЕМЫ
*        
ОS        МОД SРRЕQ(М5)
          кк 26,0
          СДА 64+24
          И B7
          УИ М13 .КОД КОМАНДЫ
          ПО ВАD
          НТЖ B10
          ПО ВАD
          МОДА SWIТСН
          ПБ -1(М13)
SWIТСН    ПБ NЕWТЕR
          МОДА 
          ПБ NЕWТАS
          МОДА 
          ПБ dsable
          МОДА 
ВАD       call ОКНО
          Э74 0  .НЕСУЩЕСТВУЮЩИЙ КОД КОМАНДЫ
*        
*         ПОДКЛЮЧЕНИЕ НОВОГО ТЕРМИНАЛА
*        
NЕWТЕR    МОДА (М5)
          СЧ SАRМ
          И Е48П25
* call ОКНО
          ПЕ NОN .УЖЕ ЗАНЯТА ЛИНИЯ
          СЧ Е48
          СДА 64(М10)
          МОДА (М5)
          ИЛИ SАRМ
          МОДА (М5)
          ЗП SАRМ
          СЧИ М5
          СДА 64-4
          мода ТУSАВ
          ЗП (М10) .НОМЕР МАШИНЫ
          МОД SРRЕQ(М5)
          кк 26,1
          СДА 64-18
          мода ТУSАВ
          ИЛИ (М10)
          мода ТУSАВ
          ЗП (М10) .ОПИСАТЕЛЬ АБОНЕНТА S-МАШИНЫ
          МОД SРRЕQ(М5)
          кк 26,2
          СДА 64+25
          И Е15П1
          УИ М13 .АДРЕС ОБЛАСТИ АБОНЕНТА
          СДА 64-24
          мода ТУSАВ
          ИЛИ (М10)
          мода ТУSАВ
          ЗП (М10)
*        
*         ОТВЕТ
*        
          СЧИ 0
          ЗП АNSW
АNS       СЧИ М10 .НОМЕР ТЕРМИНАЛА
          МОД НРRЕQ(М5)
          кк 26,'70000'+1
          СЧ АNSW
          МОД НРRЕQ(М5)
          кк 26,'70000'+2
          СЧ Е2 .КОД КОМАНДЫ
          МОД НРRЕQ(М5)
          кк 26,'70000'+0
          УИА 48(М2) .НОМ. АБОНЕНТА-ОС
          ПВ ТАКЕ(М14)
          СЧ АNSW
          ПО z15
          call ОКНО
z15       НОП
          УИИ М2(М10)
          ПБ SТЕRМО
NОN       СЧ Е1
          ЗП АNSW
          ПБ АNS
*        
*        ПОДКЛЮЧЕНИЕ НОВОЙ ЗАДАЧИ
*        
NЕWТАS    call ОКНО
          Э74 0 
*        
*         ОТКЛЮЧЕНИЕ ТЕРМИНАЛА
*
dsable    ноп
          мода SАRМ
          СЧ (М5)
          И Е17П1
          мода SАRМ
          ЗП (М5)
          ПБ ЕNDЕС
*        
*         ПОСТУПИЛ ЗАПРОС ОТ S-ТЕРМИНАЛА
*        
ТRЕQ      ноп
          мода ТУSАВ
          СЧ (М2)
          СДА 64+24
          И Е15П1
          СЛЦ SРSНIF .СДВИГ НА ПОЛЕ S-ТЕРМИНАЛА
          УИ М12 .АДР. ИНФ. ПОЛЯ ТЕРМИНАЛА
          кк 26,(М12)
          СДА 64+24
          И B7
          УИ М13 .КОД КОМАНДЫ
          СЛИА -4(М13)
          МОДА ТЕRIN .4
          ПИО (М13)
          СЛИА -1(М13)
          МОДА ЕNDЕС
          ПИНО (М13)
          ПИО SТЕRМО(М4)
          УИА 0(М4)     .БЫЛ ОТВЕТ НА
          ПБ ЕNDЕС      .ПРИГЛАШЕНИЕ
*        
*         ЗАВЕРШЕНА ВЫДАЧА НА S-ТЕРМИНАЛ
*        
SТЕRМО    ноп
          мода ТУSАВ
          СЧ (М2)
          СДА 64+24
          УИ М1
          СЧИ 0    .ГАСИМ
          кк 26,'70000'+1(М1) .Н-СТАТУС
          СЧИ М1
          СЛЦ НРSНIF .+СМЕЩЕНИЕ НА Н-БУФЕР
          УИ М1 .АДР. БУФЕРА КОМАНД ДЛЯ S-ТЕРМИНАЛА
          СЧ В43В12
          кк 26,'70000'+3(М1) .ПРИГЛАШЕНИЕ
          СЧ B5
          кк 26,'70000'(М1) .КОД КОМАНДЫ
          СЧИ М1
          СЛЦ В3
          СДА 64-1
          кк 26,'70000'+1(М1) .АДРЕС МАССИВА
          СЧ Е1
          кк 26,'70000'+2(М1) .ДЛИНА=1 СЛОВО
          УИА ЕNDЕС(М14)
          УИА 1(М4) .ПРИЗ. ВЫДАЧИ ПРИГЛ. В S
          ПБ ТАКЕ
*        
*         ВОЗЬМИ МАССИВ С S-ТЕРМИНАЛА
*        
ТЕRIN     кк 26,1(М12)
          СДА 64+25
          УИ М13 .АДРЕС БУФЕРА S-ТЕРМИНАЛА
          УИА -5(М16)
          УИА ВUFТ(М11) .АДРЕС БУФЕРА Н-ТЕРМИАЛА
NЕХТW     кк 26,(М13)
          СДА 64+24
          ЗП R1
          УИА -1(М3)
NЕХТВ     И Е8П1
          ЗП SУМ
          ПО ЕАR .КОНЕЦ МАССИВА
          ХТА (М11)
          СДА 64-8
          ИЛИ SУМ
          ЗП (М11)
          МОДА WВ
          ЦИКЛ (М16)
          УИА -5(М16)
          СЛИА 1(М11)
WВ        СЧ R1
          СДА 64+8
          МОДА NЕХТВ
          ЦИКЛ (М3)
          СЛИА 1(М13)
          ПБ NЕХТW
ЕАR       ХТА (М11)  .ДОДВИЖКА
          СДА 64-8
          ЗП (М11)
          МОДА ЕАR
          ЦИКЛ (М16)
          СЧИ  0   .ГАСИМ
          СЛИА -2(М12)
          кк 26,'70000'(М12)    .S-СТАТУС
          СЛИА 2(М12)
          УИА ВUFТ(М1)
          СЧИ М10
          СДА 64-12
          ИЛИ FОRОUТ
          ЗП IW
          call ОКНО
          Э64 INFР
          Э71 IW .ЗАПУСК ВЫДАЧИ
          ПБ ЕNDЕС
*        
*         ЗАВЕРШЕНА ВЫДАЧА НА Н-ТЕРМИНАЛ
*        
ТОUТ      СЧ ЕSСАLЕ
          ИЛИ ЕОUТ
          НТЖ ЕОUТ
          ЗП ЕSСАLЕ
          СЧ prompt
          УИА ВUFТ(М1)
          ЗП (М1)
          УИА 1(М3) . Н-ТЕРМ. НА ПРИЕМЕ
          СЧИ М10 .НОМ. Н-ТЕРМИНАЛА
          СДА 64-12
          ИЛИ FОRIN
          ЗП IW
          Э71 IW .ЗАПУСК ПРИЕМА
          ПБ УЕS
*        
*         ЗАВЕРШЕН ПРИЕМ С Н-ТЕРМИНАЛА
*        
ТINР      СЧ ЕSСАLЕ
          ИЛИ ЕINР
          НТЖ ЕINР
          ЗП ЕSСАLЕ
          ПИО IFОUТ(М3) .НЕ БЫЛО ЗАПУСКА ПРИЕМА
          СЧ Е48А
          СДА 64-1(М10)
          ЗП МАSК
          СДА 64+24
          ИЛИ МАSК
          ЗП МАSК
          СЧИ М10
          ИЛИ FORSTT
          ЗП IW
          Э71 IW .ОПРОС СОСТОЯНИЯ
          ЗП КАNS
          СЧИМ М10
          НТЖ (М17)
          И Е6П1
          ПЕ ЕRz .НЕТ НАШ. VТ
          СЧ КАNS
          СБР Е26Е25 .26-25.РР.
          ЗП КАNS
          ПО ЕRz
          НТЖ Е48А
          ПО ТINР1
          СЧ КАNS
          НТЖ Е47
          ПО ТОUТ .ПЕРЕЗАПУСТИТЬ ВВОД(БУД)
ЕRz       НОП
          call ОКНО
          Э74 0  .ОБРВ СВЯЗИ
ТINР1     УИА ВUFТ(М13)
          МОДА (М5)
          СЧ SАRМ
          И Е48П25
          НЕД Е15П1
          УИ М2 .НОМ.S-ТЕРМИНАЛА
          мода ТУSАВ
          СЧ (М2)
          СДА 64+24
          УИ М12
          СЛЦ НРSНIF
          ЗП АК .АДРЕС ОБЛАСТИ S-ТЕРМИНАЛА
          СЧ Е13 .УСТАНОВКА В Н-СТАТУС
          кк 26,'70000'+1(М12)    .ПРИЗНАКА ВЫВОДА
          УИА -1(М12) .СЧЕТЧ. БАЙТ В СЛОВЕ S-МАШИНЫ
          УИА 0(М1) .СЧЕТЧ.СЛОВ S-МАШИНЫ
          ПБ NЕХWОR
NЕХSУМ    СЧ НWОRD
          СДА 64-8
          ЗП НWОRD
          СЧМР 
          ПО ЕND .КОНЕЦ МАССИВА
          МОДА WSУМ
          ЦИКЛ (М12)
          СДА  64-8
          ИЛИ SWОRD .СТАРШИЙ БАЙТ-СЛЕВА
          МОД АК
          кк 26,'70000'+3(М1) .ГОТОВО S-СЛОВО
          СЧИ М1
          МОДА (М5)
          НТЖ МАХLG
          ПО ЕND .БУФ.ПЕРЕПОЛНЕН
          СЛИА 1(М1)
          УИА -1(М12)
          ПБ nxsym1
WSУМ      ЗП SWОRD
nxsym1    МОДА NЕХSУМ
          ЦИКЛ (М6)
NЕХWОR    УИА -5(М6)
          ХТА (М13)
          ЗП НWОRD
          СЛИА 1(М13)
          ПБ NЕХSУМ
*        
*         ПОСЛЕДНЕЕ СЛОВО МАССИВА
ЕND       МОДА ЕND1
          ПИО (М12)
          СЧИ 0
          ЗП SWОRD
ЕND1      СЧ SWОRD
          МОД АК
          кк 26,'70000'+3(М1)
*        
*         ВЫДАЧА КОМАНДЫ(ПРИМИ МАССИВ) В S-МАШИНУ
*        
          СЧ B4
          МОД АК
          кк 26,'70000'+0 .КОД КОМАНДЫ
          СЧ АК
          СЛЦ В3
          СДА 64-1
          МОД АК
          кк 26,'70000'+1 .АДРЕС МАССИВА
          СЧИ М1
          СЛЦ Е1
          МОД АК
          кк 26,'70000'+2 .ДЛИНА МАССИВА
          УИА УЕS(М14)
          УИИ М2(М10)
          ПБ ТАКЕ
*         ВЫДАЧА ПРЕРЫВАНИЯ В S-МАШИНУ
*         М5-НОМ . МАШИНЫ, М2-НОМ. АБОНЕНТА НА Н-МАШИНЕ
ТАКЕ      МОД НFIFО(М5)
          кк 26,0 .СЧЕТЧИК ПРЕР.ОТ Н-МАШИНЫ
          СДА 64+24
          И Е4П1
          УИ М1
          СЛЦ Е1
          И Е4П1
          МОД НFIFО(М5)
          кк 26,'70000'+0 .ЗАПИСЬ НОВ. ЗНАЧ. СЧЕТЧИКА
          мода ТУSАВ
          СЧ (М2)
          СДА 64-6
          СЧМР 0
          МОД НFIFО(М5)
          кк 26,'70000'+2(М1) .НОМ. АБОНЕНТА
* МОД WRКS(М5)
* кк 26,'70000'+0 .ВЫДАЧА ПРЕРЫВАНИЯ
          СЧ B200
          МОДА '77775'
          кк 26,'70000'
          UJ (М14) *В ОРИГИНАЛЕ М12
INFР      кк 0,(М1)
          кк 0,10(М1)
          кк 5,16(М2)
          кк '22',5(М10)
FОRIN     кк '35',(М1)
          кк '100',211(М1)
FОRОUТ    кк '025',(М1)
          кк '100',211(М1)
FORSTT    кк '130',1
          кк 0,0
          мода okho
ЕМАSК     конд в'200217'
*        1-БУДИЛЬНИК,
*        2-ПРЕРЫВАНИЕ ОТ S-МАШИНЫ,
*        3-КОНЕЦ ПРИЕМА С ТЕРМИНАЛА,
*        4-КОНЕЦ ВЫДАЧИ НА ТЕРМИНАЛ
*        6-СДЕЛАЛИ ВКН
*        17-АВАРЙНАЯ СИТУАЦИЯ
*         АДРЕС БУФЕРА ПРЕРЫВАНИЙ ОТ S-МАШИНЫ
SFIFО     КОНД В'110'
          КОНД В'0'
*         АДРЕС БУФЕРА ПРЕРЫВАНИЙ ОТ Н-МАШИНЫ
НFIFО     КОНД В'132'
          КОНД В'0'
*         АДРЕС БУФЕРА СИСТЕМНЫХ ЗАПРОСОВ S-МАШИНЫ
SРRЕQ     КОНД В'154'
          КОНД В'0'
*         АДРЕС БУФЕРА СИСТЕМНЫХ ЗАПРОСОВ Н-МАШИНЫ
НРRЕQ     КОНД В'157'
          КОНД В'0'
*         АДРЕС ЗАПИСИ (В РКС) ДЛЯ ПРЕРЫВАНИЯ S-МАШИНЫ
НРSНIF    КОНД В'5' .СДВИГ ДЛЯ Н-ВЫДАЧ
SРSНIF    КОНД В'2' .СДВИГ ДЛЯ S-ВЫДАЧ
WRКS      КОНД В'4'
          КОНД В'0'
ЕАV       КОНД В'000000' .17Р !!! maybe 200000
ЕDКS      КОНД В'2' .2Р
ЕINР      КОНД В'4' .3Р
ЕОUТ      КОНД В'10' .4Р
ЕТIМ      КОНД В'1' .1Р
Е48       КОНД М47В'1'
Е6        КОНД В'40'
МАХLG     КОНД В'1171'
          КОНД В'1171'
SУМ       ПАМ 1
SАRМ      ПАМ 2
АNSW      ПАМ 1
ТУSАВ     ПАМ 48
А48       конд м46в'3'
ВUFТ      ПАМ 211
АК        ПАМ 1
НWОRD     ПАМ 1
SWОRD     ПАМ 1
МАSК      ПАМ 1
IСNТ      ПАМ 1
АLL7      КОНД В'-1'
В40 конд в'40'
В200 конд в'200'
Е23 конд в'20000000'
Е1 конд в'1'
Е2 конд в'2'
Е13 конд в'10000'
В3 конд в'3'
Е47 конд м46в'1'
В4 конд в'4'
В5 конд в'5'
prompt конд М40В′12′М32В′43′М24В′0′Д'   '
В7 конд в'7'
К7703 конд М36B'7703'
Е48А конд м47в'1'
В10 конд в'10'
О24П10 конд B'7777777700000777'
Е48П25 конд М24В'77777777'
Е26Е25 конд М24В'3'
В43В12 конд М8В′43′В′12′
Е15П1 конд В'77777'
Е4П1 конд В'17'
Е17П1 конд В'377777'
Е6П1 конд В'77'
Е8П1 конд В'377'
          литер
КАNS      ПАМ 1
IW        ПАМ 1
ЕSСАLЕ    ПАМ 1
R1        ПАМ 1
m1 equ 1
m2 equ 2
m3 equ 3
m4 equ 4
m5 equ 5
m6 equ 6
m7 equ 7
m10 equ '10'
m11 equ '11'
m12 equ '12'
m13 equ '13'
m14 equ '14'
m15 equ '15'
m16 equ '16'
m17 equ '17'
          финиш
eoi$$$
trn$$$
0-0
end$$$
*libra:2
*call dtran(tester)
base:7,0
*super
*assem
*read:1
*end file
``````
еконец


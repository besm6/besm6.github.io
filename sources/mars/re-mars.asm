*****************************************************
* АРХИВНАЯ СИСТЕМА МАРС-6:                          *
* РАБОТА С АССОЦИАТИВНЫМИ МАССИВАМИ КЛЮЧ-ДАННЫЕ     *
* ВО ВНЕШНЕЙ ПАМЯТИ (ДИЗАССЕМБЛИРОВАНИЕ, АНАЛИЗ)    *
* ИСХОДНО В. И. ФИЛИППОВ, 1978-80                   *
*****************************************************
       macro
&l     m16=  &a
&l     МОДА  &a-bdvect(m13)
       УИА   (M16)
       mend
       macro
&l     do    &op,&base,&idx
&l     МОД   &idx-bdvect(m13)
       &op   &base
*      mnote * &op &base [ &idx ]
       mend
       macro
&l     call  &dest,&reg
&l     МОДА  &dest
       ПВ    (&reg)
       mend
       macro
&l     link &dest,&reg
&l     МОДА  &dest
       УИА   (&reg)
       mend
       macro
&l     dirty &i
&l     СЧ    WНIСН
       ПО    drt&sndx
       СЧ    Е&i
drt&sndx  СЛЦ   Е1
       ИЛИ   DIRТУ
       ЗП    DIRТУ
       mend
       macro
&l     ++    &a
&l     сч    &a
       слц   е1
       зп    &a
       mend
       macro
&l     cycle &a,&r
&l     слиа -1(&r)
       мода &a
       пино (&r)
       mend
       macro
&l     цикл  &dest,&reg
&l     МОДА  &dest
       vlm   (&reg)
       mend
        macro
&l      переп    &a,&b
&l      мод     &a
        сч      -1(М16)
        мод     &b
        зп      -1(М16)
        слиа    -1(М16)
        mend
БАНДИТ СТАРТ ’2000’
М4     ЕQU   4 базовый регистр кода
М13    ЕQU   11 указатель на вектор данных
М5     ЕQU   5
М6     ЕQU   6
М7     ЕQU   7
М16    ЕQU   14
       УПОТР ВDVЕСТ(М13)
       УПОТР БАНДИТ(М4)
       КОНД  В’0’
       КОНД  П’БАНДИТ’
SТАRТ  СЧИ   М16 основная точка входа, вызов по М16
       ПБ    ЕNТЕR
АВDV   ПАМ   1
АRСН   КОНД  В’1550063’ dflt каталог архива НУ 53, зона 63
       СЧИ   М16  дополнительная точка входа?
       ЗП    SАVМ16
       ПБ    ЕNТЕR2
SАVМ16 ПАМ   1
D00010 КОНД  П’190280’  дата версии?
D00011 ПАМ   1
D00012 ПАМ   1
SАVМ13 ПАМ   1
GОТО   ПАМ   1
D00015 ПАМ   1
USRLОС ПАМ   1
SАVМ7  ПАМ   1
SАVМ6  ПАМ   1
SАVМ5  ПАМ   1
JМРОFF ПАМ   1
D00023 ПАМ   1
D00024 ПАМ   1
D00025 ПАМ   1
D00026 ПАМ   1
NЕGКЕУ ПАМ   1
D00030 ПАМ   1
D00031 ПАМ   1
D00032 ПАМ   1
SАVМ4  ПАМ   1
remlen ПАМ   1
WОRК   ПАМ   1
temp   ПАМ   1
ИСЭ70  ПАМ   1
D00040 ПАМ   1
SАVRК  ПАМ   1
* Все команды приходят на дешифровщик с 0 на См.
SWIТСН ПБ    СМD0
       ЗП
* Команда 01: установка текущей записи на начало
К01    ЗП    NЕGКЕУ := 0
       ПБ    СМD1
* Команда 02: установка текущей записи на конец
К02    СЧ    VSЕЕD (без 48 р)
       ПБ    FIND
* Команда 03: шаг назад
К03    СЧ    Е1
       ЗП
* Команда 04: шаг вперед
К04    УИ    М16
       ПБ    СМD4
К05    ЗП    D00011
       ПБ    СМD5
К06    ЗП    IDХ
       ПБ    СМD6
К07    СЧ    off7
       ПБ    СМD7
* Команда 10: создать базу/каталог; сама потом выполняет 21
К10    ЗП    WНIСН
       ПБ    МКСТL
* Команда 11: поиск элемента по ключу
К11    СЧ    КЕУ
       ПБ    FIND
* Команда 12: скопировать текущий элемент в дескриптор базы
К12    СЧИ   М13
       ПБ    СМD12
К13    ЗП    ITMLEN
       ПБ    РR425
* Команда 14: проверка совпадения ключей
* Если команда последняя или за ней не 00, то неудача - ошибка,
* иначе пропустить 00 и следующие 3 команды
К14    СЧ    СURКЕУ
       ПБ    СКЕХSТ
* Команда 15: проверка несовпадения ключей; аналогично 14
К15    СЧ    СURКЕУ
       ПБ    СКDNЕХ
* Команда 16: поиск символа конца строки
К16    ВЧ    МУLЕN
       ПБ    СМD16
* Команда 17: поиск слова-признака конца
К17    ВЧ    МУLЕN
       ПБ    СМD17
* Команда 20: копирование из памяти в текущий элемент
К20    СЧ    МУLОС
       ПБ    СМD20
* Команда 21: сохранение буфера на диск
К21    СЧ    МУLОС
       ПБ    FLUSН
* Команда 22: копирование текущего элемента в память пользователя
К22    СЧ    АDЕSСR
       ПБ    СРУОUТ
* Команда 23: освобождение памяти элемента
К23    СЧ    АDЕSСR
       ПБ    FRЕЕ
* Команда 24: проверка "пароля"
К24    СЧ    GIVЕNР
       ПБ    СНКРSW
* Команда 25: установить текущую базу по дескриптору
К25    СЧ    DВDЕSС
       ПБ    SЕТСТL
К26    СЧ    КЕУ
       ПБ    СМD26
К27    ЗП    limit
       ПБ    СМD27
К30    СЧ    ORGCMD
       ПБ    NЕХТ
* Команда 31: установить корневой каталог в качестве текущего
К31    СЧ    Е12
       ПБ    RООТ
К32    СЧ    LОС12
       ПБ    СМD32
К33    СЧ    АDЕSСR
       ПБ    РR342
К34    СЧ    LОС41
       ПБ    СМD34
* Команда 35: запись буферов на диск
К35    СЧ    DIRТУ
       ПБ    SАVЕ
К36    СЧ    LОС12
       ПБ    СМD36
* Команда 37: пропустить следующую команду
К37    СЧ    СURСМD
       СДА   64+12
* Команда 40: останов, нормальное завершение
К40    ПБ    NЕХТ
К41    СЧ    СМРОFF
       ПБ    К43
К42    СЧ    off42
К43    ЗП    JМРОFF
       ПБ    СМD43
* Команда 44: adescr := loc41(М13)
К44    СЧ    LОС41
       ПБ    СМD44
* Команда 45: установить флаг работы с базой
К45    СЧ    ВDТАВ
       ПБ    LОСК
К46    СЧ    МУLОС
       ПБ    СМD46
* Команда 47: уход на первую ячейку bdvect
К47    СЧ    LОС41
       ПБ    ОUТАDR-ВDVЕСТ(М13)
* Команда 50 хх: cmd := bdvect[bdvect[xx]++]
К50    СЧ    В3
       ПБ    СМD50
* Команда 51 хх: myloc := bdvect[bdvect[xx]++]
К51    СЧ    AMYLOC
       ПБ    СМD51
* Команда 52 xx yy: bdvect[xx] := bdvect[bdvect[yy]++]
К52    СЧ    СURСМD
       ПБ    СМD52
* Команда 53 xx yy: bdvect[xx] := bdvect[yy]
К53    СЧ    СURСМD
       ПБ    МОVЕ
* Команда 54 xx: bdvect[xx] := bdvect[012]
К54    СЧ    СURСМD
       ПБ    СМD54
* Команда 55: немедленный возврат
К55    МОД   SАVМ16
       ПБ
********************
* команда 32: *bdvec[38] = bdvec[10]
* и установка флага dirty
СМD32  do    зп,,АIТЕМ
       dirty 2
       ПБ    (М6)
********************
ЕNТЕR  ЗП    SАVМ16
       СЧИ   М13
       ЗП    SАVМ13
       СЧРЖ  ’7777’
       ЗП    SАVRК
       РЖА   ’3’
       СЧ    АВDV
       УИ    М13
       СЧИ   М7
       ЗП    SАVМ7
       СЧИ   М5
       ЗП    SАVМ5
       do    сч,,ВDТАВ
       НТЖ   RООТFL
       ПО    А00141 уже видим что хотим
       СЧ    Е70МSК
       ПО    А00141 нет тек. массива, нечего читать
       СЧ    ВDТАВ
       СДА   64-20
       ИЛИ   Е40
       ИЛИ   Е70МSК
       ЗП    ИСЭ70
       Э70   ИСЭ70 считали таблицу массива
А00141 СЧИ   М6
       ЗП    SАVМ6
ЕNТЕR2 СЧ    ORGCMD
       ПБ    NЕХТ
drtnxt СЧ    Е1
       ИЛИ   DIRТУ
       ЗП    DIRТУ
* если GOTO не 0, переходим по содержимому и обнуляем его
RTNEXT МОД   GОТО
       УИА   (М5)
       СЧ
       ЗП    GОТО
       МОДА  (М5)
       ПИНО  (М5)
* выборка следующей команды
СМD0   СЧ    СURСМD
       СДА   64+6
NЕХТ   ПО    err2 команды кончились
       ЗП    СURСМD
       И     Е6П1
       УИ    М5
А00153 link  СМD0,m6  возврат на выборку след. команды
       СЧ
       ЗП    GОТО
       МОДА  (М5)
       ПБ    SWIТСН
* команда 11: поиск ключа в текущем массиве       
FIND   ЗП    NЕGКЕУ
       ПО    ВАDNАМ ключ не должен быть нулевым
       И     Е48
       ПЕ    ВАDNАМ в ключе не должно быть 48 р.
*      IDX := 0
СМD1   ЗП    IDХ 
       СЧ    NЕGКЕУ
       НТЖ   VSЕЕD
       ЗП    NЕGКЕУ теперь в NEGKEY инверсный ключ
СМD1А  m16=  LОС56
       СЧ    LОС20
       ПО    А00212
А00164 СЧ    -1(М16)
       И     Е10П1
       УИ    М5
       ПО    А00172
А00166 МОДА  (М5)
       СЧ    -2(М16)
       СЛЦ   NЕGКЕУ
       ПЕ    А00172
       СЛИА  -2(М5)
       МОДА  А00166
       ПИНО  (М5)
А00172 СЛИА  -2(М5)
       СЧИ   М5
       ИЛИ   Е31
       do    зп,array,idx
       СЧ    1(М16)
       И     Е48
       ПО    А00371
       СЧ    IDХ
       СЛЦ   Е2
       ЗП    IDХ
       МОДА  (М16)
       СЧ    1(М5)
       link  А00215,m6
* процедура  возвращается по М6
* установка текущей записи по дескриптору на СМ
РR202  m16=  LОС116
А00203 do    зп,loc20,idx
       И     Е19П1
       ЗП    LОС116
       СЧИ   М16
       ЗП    USRLОС
       СЛЦ   Е2
       ЗП    LОС50
       СЧ    LОС116
       НТЖ   LОС246
       ПО    (М6)
       СЧ    LОС116
       ЗП    LОС246
       ПБ    А00530
*******************************
А00212 link  СМD1А,m6
А00213 СЧ    Е11
       m16=  LОС54
       ПБ    А00203
А00215 m16=  LОС120
       ПБ    А00164
* команда 54: mem[bdvec[next_insn]] := bdvec[10] ???       
СМD54  СДА   64+6
       ЗП    СURСМD
       И     Е6П1
       УИ    М16
       СЧ    LОС12
       МОДА  (М16)
       МОД   (М13)
       ЗП
       ПБ    (М6)
*******************************
* команда 53: bdvec[next_insn] := bdvec[next_next_insn]
МОVЕ   СДА   64+6
       ЗП    СURСМD
       И     Е6П1
       УИ    М16
       СЧ    СURСМD
       СДА   64+6
       ЗП    СURСМD
       И     Е6П1
       УИ    М5
       МОДА  (М5)
       СЧ    ВDVЕСТ
       ПБ    stbdv
*******************************
* команда 50: curcmd := bdvec[3] := *bdvec[next_insn]++
СМD50  link  NЕХТ,m6
* команда 51: bdvec[11] := *bdvec[next_insn]++
СМD51  УИ    М16
       СЧ    СURСМD
       СДА   64+6
       ЗП    СURСМD
       И     Е6П1
       УИ    М5
       СЛИ   М5(М13)
       ++    (М5)
       МОД   (М5)
       СЧ    -1
stbdv  МОДА  (М16)
       ЗП    ВDVЕСТ
       ПБ    (М6)
*******************************
* команда 52: bdvec[next_insn] := *bdvec[next_next_insn]++
СМD52  СДА   64+6
       ЗП    СURСМD
       И     Е6П1
       ПБ    СМD51
*******************************
СМD46  СДА   64+18
       И     Е15П1
       ВЧ    LОС53
       ЗП    LОС42
       СЧ    МУLОС
       СДА   64+33
       И     В17777
       ПО    skip5
       ЗП    МУLЕN
       ПБ    (М6)
*******************************
* команда 45: захват базы
LОСК   СДА   64-20
       ИЛИ   Е70МSК
       ЗП    WОRК
       ИЛИ   Е40
       ЗП    ИСЭ70
       Э70   ИСЭ70
       do    сч,1,ВDТАВ
       НТЖ   Е32
       do    зп,1,ВDТАВ
       И     Е32
       ПО    СLАSН
       do    зп,,bdbuf
       Э70   WОRК
       ПБ    (М6)
СМD44  ЗП    АDЕSСR
       ПБ    (М6)
СМD36  do    уиа,(М16),LОС50
       МОД   IDХ-ВDVЕСТ(М13)
       МОД   АRRАУ-ВDVЕСТ(М13)
* *(loc50 + array[idx] + 1) := acc
       ЗП    1(М16)
       ПБ    А01160
А00267 call  totlen,m5
А00270 СЧ    NЕGКЕУ
       УИ    М16
       ВЧ    СURРОS
       ПЕ    А00274
       ЗП    NЕGКЕУ
       do    уиа,(М16),СURРОS
       УИА   0(М5)
А00274 МОД   JМРОFF
       ПБ    ИЗБАЗЫ
* перепись блока [aitem] -> [usrloc]
ИЗБАЗЫ МОДА  ГОТОВО
       ПИО   (М16)
       do    СЧ,-1(М16),aitem
       МОД   USRLОС
       ЗП    -1(М16)
       СЛИА  -1(М16)
       ПБ    ИЗБАЗЫ
* перепись блока [usrloc] -> [aitem]
* зачем-то признак dirty ставится в цикле
ВБАЗУ dirty 1
       МОДА  ГОТОВО
       ПИО   (М16)
       МОД   USRLОС
       СЧ    -1(М16)
       do    ЗП,-1(М16),aitem
       СЛИА  -1(М16)
       ПБ    ВБАЗУ
СРАВН  do    СЧ,-1(М16),aitem
       МОД   USRLОС
       НТЖ   -1(М16)
       ПЕ    К37
       cycle СРАВН,m16
ГОТОВО СЧ    USRLОС
       СЛЦ   СURРОS
       ЗП    USRLОС
А00317 МОДА  А00325
       ПИО   (М5)
       СЧ    АIТЕМ
       СЛЦ   NЕGКЕУ
       ЗП    АIТЕМ
       СЧ    СURРОS
       ВЧ    NЕGКЕУ
       ЗП    СURРОS
       do    СЧ,,aitem
       ЗП    LОС41
       ПБ    (М6)
А00325 СЧ    LОС220
       СДА   64+20
       И     Е19П1
       ПЕ    А00267
       СЧ    JМРОFF
       НТЖ   Е5     = ГОТОВО-ИЗБАЗЫ
       ПО    А00332 не бывает 
       СЧ    NЕGКЕУ
       ПО    (М6)
А00332 УИА   7(М16) шагз. велик ???
       ПБ    SКIР
*******************************
СМD7   ЗП    JМРОFF
       СЧ    LОС42
А00334 ЗП    NЕGКЕУ
       СЛЦ   LОС53
       ЗП    LОС53
       СЧ    МУLОС
       ЗП    USRLОС
       ПБ    А00270
*******************************
СМD43  СЧ    МУLЕN
       ПБ    А00334
А00340 call  totlen,m5
       ПБ    К07
* процедура возвращается по М6       
РR342  call  totlen,m5
       do    СЧ,,aitem
       ЗП    LОС41
       И     Е15П1
       ЗП    ITMLEN
       СЧ
       ЗП    LОС53
       ПБ    (М6)
* команда 24: "проверка пароля"
СНКРSW НТЖ   SАVЕDР
       ПО    СМD0
       УИА   17(М16) неверный пароль
       ПБ    ЕRR
* команда 15: проверка, что ключа нет       
СКDNЕХ НТЖ   КЕУ
       ПЕ    СМD0
       УИА   9(М16) ключ уже есть
       ПБ    SКIР
* команда 14: проверка, что ключ есть   
СКЕХSТ НТЖ   КЕУ
       ПО    СМD0
       УИА   8(М16) ключа нет
       ПБ    SКIР
*******************************
А00355 СЛЦ   Е2
       УИ    М5
       do    НТЖ,-1,loc50
       И     Е10П1
       ПЕ    А00367
       do    СЧ,-1,loc50
       И     Е29П11
       ПО    NОNЕХТ нет след. записи
       СДА   64+10
       call  РR202,m6
       do    СЧ,ВDТАВ,idx
       СЛЦ   Е16
       do    ЗП,ВDТАВ,idx
       УИА   0(М5)
А00367 СЧИ   М5
       ИЛИ   Е31
       do    ЗП,АRRАУ,idx
А00371 do    СЧ,(М5),loc50
       ЗП    СURКЕУ
       do    СЧ,1(М5),loc50
       ЗП    АDЕSСR
       ПБ    RTNEXT
* команда 5
СМD5   link  FLUSН,m6
* подготовка к формированию метаданных листа       
РR376  СЧ    LОС20
       ЗП    D00012
       СЧ    В41 зачем писать столько слов - загадка
       ЗП    МУLЕN
       СЧ    Е2
       ЗП    D00010
       link  D00010,m16
       СЧИ   М16
       ЗП    USRLОС
       ПБ    (М6)
* команда 6
СМD6   СЧ    В41
       ЗП    ITMLEN
       m16=  LОС54
       СЧ    АDЕSСR
       ПБ    А00203
* выполняем команду 4 как процедуру по М6
STEP   СЧИ   М6
       ЗП    GОТО
* команда 4: шаг вперед или назад
СМD4   do    СЧ,АRRАУ,idx
       ПО    ЕМРТУ нет тек. записи
       УИ    М5
       МОДА  А00355
       ПИО   (М16) шаг вперед
       МОДА  А00415
       ПИО   (М5)
А00414 СЛИА  -2(М5)
       ПБ    А00367
А00415 do    СЧ,-1,loc50
       СДА   64+29
       ПО    NОРRЕD
       call  РR202,m6
       do    СЧ,ВDТАВ,idx
       ВЧ    Е16
       do    ЗП,ВDТАВ,idx
       СЧ    LОС117
       И     Е10П1
       УИ    М5
       ПБ    А00414
* процедура возвращается по М6       
РR425  do    УИА,(М16),dblen
       СЧ
       ЗП    ITMLEN
А00427 do    СЧ,-1(М16),frebas
       ПО    А00431
       ВЧ    Е1
А00431 СЛЦ   ITMLEN
       ЗП    ITMLEN
       cycle А00427,m16
       ПБ    (М6)
********************
* команда 16: поиск символа конца
СМD16  УИ    М16
       СЧ    МУLОС
       СЛЦ   МУLЕN
       ЗП    temp
       СЧ    LОС40
       СДА   64+8
       ПЕ    А00440
       СЧ    LОС40
А00440 СДА   64-40
       ЗП    LОС116
       УИА   -4(М5)
А00442 СДА   64+8
       ИЛИ   LОС116
       ЦИКЛ  А00442,М5
       ЗП    LОС116
А00445 УИА   -5(М5)
       МОД   temp
       СЧ    (М16)
       НТЖ   LОС116
А00447 СДА   64+40
       УИ    М6
       МОДА  А00455
       ПИО   (М6)
       СЧМР
А00452 ЦИКЛ  А00447,М5
А00453 ЦИКЛ  А00445,М16
       УИА   10(М16) нет симв. конца
       ПБ    ЕRR
А00455 СЧМР
       НТЖ   LОС116
       ЗП    D00031
       СЧ    LОС40
       СДА   64+8
       ПО    А00470
       СЧ    LОС40
       СДА   64-40
       МОДА  А00465
       ПИО   (М5)
       НТЖ   D00031
       СДА   64+40
       ПО    А00470
       СЧ    D00031
       НТЖ   LОС116
       ПБ    А00452
А00465 МОД   temp
       МОДА  1(М16)
       НТЖ
       СДА   64+40
       ПЕ    А00453
       СЛИА  1(М16)
А00470 do    СЛИА,1(М16),mylen
       СЧИ   М16
       ЗП    МУLЕN
       ПБ    СМD0
* команда 17: поиск слова конца       
СМD17  УИ    М16
       СЧ    МУLОС
       СЛЦ   МУLЕN
       ЗП    temp
       МОД   temp
       СЧ    (М16)
       НТЖ   LОС40
       ПО    А00470
       ЦИКЛ  СМD17,М16
А00500 УИА   11(М16) сист. ошибка
       ПБ    ЕRR
********************
gzne0  СЧ    ВDВUF буфер ненулевой зоны
       ПБ    gz1
* считывание зоны с номером на СМ в соотв. буфер
* процедура возвращается по М7
getzon ЗП    WНIСН
       ИЛИ   RООТFL
       ЗП    D00015 ключ зоны
       И     Е10П1
       ПЕ    gzne0 номер зоны не 0
       СЧ    ВDТАВ буфер 0-й зоны
gz1    ЗП    curbuf
       УИ    М16
       СЧ    (М16)
       НТЖ   D00015
       ПО    (М7) зона уже какая надо
       СЧ    WНIСН
       ПО    gz2 хотим нулевую, bdbuf неинтересен
       СЧ    DIRТУ
       И     Е2
       ПО    gz2 сохранение bdbuf не требуется
       НТЖ   DIRТУ
       ЗП    DIRТУ
       СЧ    ВDВUF
       СДА   64-20
       ИЛИ   Е70МSК
       ЗП    ИСЭ70
       СЧ    (М16)
       И     Е10П1
       СЛЦ   ИСЭ70
       ЗП    ИСЭ70
       Э70   ИСЭ70 сохранили старую зону
gz2    СЧ    curbuf
       СДА   64-20
       ИЛИ   WНIСН
       ИЛИ   Е40
       СЛЦ   Е70МSК
       ЗП    ИСЭ70
       Э70   ИСЭ70 считали новую зону
       do    УИА,(М16),curbuf
       СЧ    (М16)
       НТЖ   D00015
       ПО    (М7) считали зону с ожидаемым ключом
corupt УИА   2(М16) затерт лист
       ПБ    ЕRR
********************
А00530 link  А01423,m5
* нахождение общей длины элемента
* процедура возвращается по М5       
totlen ЗП    D00040
       И     Е10П1
       call  getzon,m7
А00533 СЧ    D00040
       И     Е20П11
       ПО    ZЕRКЕУ
       ЗП    WОRК
       СДА   64-29
       ЗП    temp
       СЧ    1(М16)
       И     Е20П11
       ПО    DЕLRЕС
       ВЧ    WОRК
       ПО    А00555
       СЛ    WОRК
А00541 СДА   64+10
       ЗП    LОС116
А00542 do    СЧ,1(М16),loc116
       ЗП    LОС220
       НТЖ   temp
       И     Е48П40
       ПО    А00550
       СЧ    LОС116
       ВЧ    Е1
       ЗП    LОС116
       ИЛИ
       ПЕ    А00542
       ПБ    DЕLRЕС
А00550 СЧ    LОС220
       И     Е10П1
       СЛЦ   Е1
       СЛЦ   curbuf
       ЗП    АIТЕМ
       СЧ    LОС220
       И     Е20П11
       СДА   64+10
       ЗП    СURРОS
       ПБ    (М5)
А00555 СЧ    WОRК
       ПБ    А00541
Е20П11 КОНД  В’0000000003776000’
********************
SКIР   СЧ    СURСМD
       СДА   64+6
       ПО    ЕRR
       И     Е6П1
       ПЕ    ЕRR
skip5  СЧ    СURСМD
       СДА   64+30
       ПБ    NЕХТ
ZЕRКЕУ УИА   1(М16)
       ПБ    ЕRR
DЕLRЕС УИА   3(М16)
       ПБ    ЕRR
ВАDNАМ УИА   4(М16)
       ПБ    ЕRR
СОRR1Р УИА   5(М16)
       ПБ    ЕRR
ОVЕRFL УИА   6(М16)
       ПБ    ЕRR
СLАSН  УИА   13(М16)
       ПБ    ЕRR
ЕМРТУ  УИА   14(М16)
       ПБ    ЕRR
NОРRЕD УИА   15(М16)
       ПБ    SКIР
NОNЕХТ УИА   16(М16)
       ПБ    SКIР
* команда 31: переключение на корневой каталог       
RООТ   ЗП    RООТFL
       СЧ    АRСН
       ЗП    DВDЕSС
       ПБ    SЕТСТL
* команда 12: копирование текущего элемента в дескриптор массива       
СМD12  СЛЦ   off12 смещение DBDESC от BDVECT
       ЗП    МУLОС
       СЧ    В3
       ЗП    МУLЕN
       СЧ    АDЕSСR
       ПБ    СРУОUТ
* команда 25: переключение на массив, указанный в DBDESC       
SЕТСТL И     Е18П1
       ЗП    Е70МSК
       СЧ    DВDЕSС
       СДА   64+18
       ЗП    DВLЕN
       СЧ    ВDТАВ
       СДА   64-20
       СЛЦ   Е70МSК
       ИЛИ   Е40
       ЗП    ИСЭ70
       Э70   ИСЭ70
       СЧ    ВDТАВ
       ЗП    curbuf
       УИ    М16
       СЧ    (М16)  в нулевом слове массива хотим видеть
       НТЖ   RООТFL ровно то же, что было в каталоге
       ПЕ    СОRR1Р иначе "испорчен 1-й лист"
       ЗП    IDХ := 0
       ЗП    LОС246 := 0
       ЗП    WНIСН := 0
       СЧ    3(М16)
       И     Е10П1
       СЛЦ   ВDТАВ
       СЛЦ   Е2
       ЗП    frebas
       ПБ    А00213
ЕRR    СЧ    ЕRНNDL
       ПО    err1 если установлен обработчик ошибок
       ЗП    SАVМ16 потом вернемся на него
err1   УИИ   М7(М16)
       СЛИ   М16(М16)
       МОДА  (М16)
       СЧ    МSG-2
       ЗП    D00010  первое слово текста ошибки
       МОДА  1(М16)
       СЧ    МSG-2
* сюда приходим с СМ=0, когда кончились команды
err2   ЗП    D00011 второе слово текста ошибки
       СЧ    LОС6
       ПО    SАVЕ
       НТЖ   Е1
       ПЕ    ЕХIТ1
       do    УИА,(М16),bdtab
       СЧ    1(М16)
       И     Е32
       ПО    SАVЕ не было признака занятости
       НТЖ   1(М16)
       ЗП    1(М16) сбрасываем его
       ПБ    WRТАВ нужно записать таблицу
SАVЕ   СЧ    DIRТУ
       И     Е1
       ПО    WRВUF 
WRТАВ  СЧ    ВDТАВ
       СДА   64-20
       ИЛИ   Е70МSК
       ЗП    ИСЭ70
       Э70   ИСЭ70
WRВUF  СЧ    DIRТУ
       И     Е2
       ПО    ЕХIТ
       СЧ    ВDВUF
       СДА   64-20
       ИЛИ   Е70МSК
       ЗП    ИСЭ70
       do    СЧ,,bdbuf
       И     Е10П1
       СЛЦ   ИСЭ70
       ЗП    ИСЭ70
       Э70   ИСЭ70
ЕХIТ   СЧ , выход с очисткой признака модификации
       ЗП    DIRТУ
ЕХIТ1  РЖ    SАVRК
       СЧИ   М7
       link  D00010,m16
       МОД   SАVМ6
       УИА   (М6)
       МОД   SАVМ7
       УИА   (М7)
       МОД   SАVМ5
       УИА   (М5)
       do    УИА,(М4),loc33
       МОД   3(М16)
       УИА   (М13)
       МОД   -1(М16)
       ПБ , возврат в вызвавшую систему
* процедура возвращается по М6       
РR662  do    ЗП,,aitem
       dirty 1
       ПБ    (М6)
***************************       
А00667 СЧ    IDХ
       ПО    А01160
       СЧ    LОС246
       call  FRЕЕ,m6
       СЧ    Е1
       ЗП    LОС42
       СЧ    LОС117
       ЗП    D00025
       И     Е48П30
       ЗП    D00032
       СДА   64+29
       ПО    А00703
       call  А00340,m6
       ИЛИ   Е29П11
       НТЖ   Е29П11
       ЗП    LОС117
       СЧ    D00025
       И     Е29П11
       ИЛИ   LОС117
       call  РR662,m6
А00703 call  РR1232,m5
СМD27  СЧ    LОС50
       УИ    М16
       do    СЛЦ,АRRАУ,idx
       УИ    М5
       НТЖ   LОС50
       ЗП    LОС116
       СЧ    -1(М16)
       И     Е10П1
       ЗП    temp
       СЛЦ   LОС50
       ЗП    WОRК
А00712 СЧ    2(М5)
       ЗП    (М5)
       СЛИА  1(М5)
       СЧИ   М5
       НТЖ   WОRК
       ПЕ    А00712
       СЧ    temp
       ВЧ    Е2
       НТЖ   temp
       НТЖ   -1(М16)
       ЗП    -1(М16)
       И     Е10П1
       ПО    А00667
       СЧ    LОС116
А00721 И     Е15П1
       ПЕ    А01160
       СЧ    IDХ
       ПО    А01160
       СЧ    (М16)
       ЗП    D00011
       link  А00730,m5
       СЧИ   М5
       ЗП    GОТО
       СЛЦ   Е1
       ЗП    SАVМ4
       ПБ    А01160
А00730 ПБ    А01242
А00731 СЧ    D00011
       do    УИА,(М16),loc50
       МОД   IDХ-ВDVЕСТ(М13)
       МОД   АRRАУ-ВDVЕСТ(М13)
       ЗП    (М16)
       do    СЧ,АRRАУ,idx
       ПБ    А00721
FRЕЕ   call  totlen,m5
А00737 СЧ    1(М16)
       СДА   64+10
       И     Е15П1
       ЗП    temp
       СЧ    LОС116
       УИ    М5
А00742 НТЖ   temp
       ПО    А00747
       МОДА  (М5)
       СЧ    2(М16)
       МОДА  (М5)
       ЗП    1(М16)
       СЛИА  1(М5)
       СЧИ   М5
       ПБ    А00742
А00747 СЧ    LОС220
       И     Е10П1
       ЗП    D00031
А00751 СЛИА  -1(М5)
       МОДА   А00760
       ПИО   (М5)
       МОДА  (М5)
       СЧ    1(М16)
       И     Е10П1
       ВЧ    D00031
       ПО    А00751
       МОДА  (М5)
       СЧ    1(М16)
       СЛЦ   СURРОS
       МОДА  (М5)
       ЗП    1(М16)
       ПБ    А00751
А00760 СЧ    1(М16)
       И     Е10П1
       ЗП    WОRК
       НТЖ   D00031
       ПО    А00773
       СЧ    D00031
       ВЧ    WОRК
       ПЕ    А00500
       УИ    М5
       СЧ    WОRК
       СЛЦ   curbuf
       ЗП    WОRК
       СЛЦ   СURРОS
       ЗП    D00031
А00767 МОД   WОRК
       СЧ    (М5)
       МОД   D00031
       ЗП    (М5)
       cycle А00767,m5
А00773 СЧ    1(М16)
       ВЧ    Е11
       СЛЦ   СURРОS
       ЗП    1(М16)
       СЧ    frebas
       УИ    М16
* увеличиваем кол-во св. места       
       do    СЧ,(М16),which
       СЛЦ   СURРОS
       СЛЦ   Е1
       do    ЗП,(М16),which
       dirty 1
       СЧ    LОС220
А01005 СДА   64+20
       И     Е19П1
       ПЕ    FRЕЕ
       СЧ    Е1
       ИЛИ   DIRТУ
       ЗП    DIRТУ
       ПБ    (М6)
********************
DАТЕ   СЧ    LОС14
       И     Е33П16
       ЗП    WОRК
       Э50   ’114’
       СБР   D01566
       ИЛИ   WОRК
       ИЛИ   МУLЕN
       ЗП    D00023
       ПБ    (М7)
********************
FLUSН  ЗП    USRLОС
       call  РR1022,m6
       СЧ    D00024
       ЗП    LОС12
       ПБ    СМD0
РR1022 call  DАТЕ,m7
А01023 СЧ
       УИА   0(М5)
       ЗП    D00030
       СЧ    frebas
       УИ    М16
       do    СЧ,(М16),which
       ЗП    WОRК
       ++    МУLЕN
       ВЧ    WОRК
       ПО    nospac нет места в зоне
       СЧ    WНIСН
chunk  call  getzon,m7 после этого m16 = curbuf
       УИИ   М7(М16)
       СЧ    1(М7)
       СДА   64+10
       УИ    М16
А01035 СДА   64-39
       do    НТЖ,1(М16),curbuf
       И     Е48П40
       ПО    А01044
       do    СЧ,1(М16),curbuf
       do    ЗП,2(М16),curbuf
       СЛИА  -1(М16)
       СЧИ   М16
       МОДА  А01035
       ПИНО  (М16)
А01044 СЛИА  1(М16)
       СЧИ   М16
       ЗП    LОС116
       СДА   64-39
       ИЛИ   D00030
РR1047 ЗП    D00030
       СДА   64+39
       СДА   64-10
       ИЛИ   WНIСН
       ЗП    D00024
       СЧ    МУLЕN
       УИ    М16
       ВЧОБ  1(М7)
       СЛЦ   Е11
       ЗП    1(М7)
       И     Е10П1
       ЗП    WОRК
       СЛЦ   curbuf
       СЛЦ   Е1
       ЗП    temp
       МОДА  А01062
       ПИНО  (М5)
       СЛИА  -1(М16)
       ++    temp
А01062 СЧИ   М16
       ПО    А01067 нечего копировать
А01063 МОД   USRLОС      * memcpy
       СЧ    -1(М16)     * [temp] <- [usrloc]
       МОД   temp        * length m16
       ЗП    -1(М16)     *
       cycle А01063,m16  *
А01067 СЧ    МУLЕN
       СДА   64-10
       ИЛИ   WОRК
       ИЛИ   D00030
       do    ЗП,1(М7),loc116
       do    УИА,(М16),frebas база св. места в зонах
       do    СЧ,(М16),which * уменьшаем
       ВЧ    МУLЕN          * кол-во
       ВЧ    Е1             * свободного
       do    ЗП,(М16),which * места
       МОДА  А01111
       ПИО   (М5)
       dirty 1
       СЧ    D00024
       СДА   64-20
       ЗП    D00030
       СЧ    remlen
       ЗП    МУLЕN
А01105 cycle А01264,m5
       link  ОVЕRFL,m6
       СЧ    D00030
       ПБ    А01005
А01111 СЧ    D00023
       МОД   temp
       ЗП    -1
       dirty 2
       ПБ    (М6)
А01116 СЧ
       ЗП    LОС20
       СЧ    LОС12
       link  ОVЕRFL,m6
       ПБ    FRЕЕ
СМD26  ЗП    D00011
       СЧ    LОС12
       ЗП    АDЕSСR
       ПБ    А01140
А01123 do    СЧ,АRRАУ,idx
       СДА   64+15
А01125 ЗП    D00012
       СДА   64+15
       УИ    М16
       СЧМР
       МОД   SАVМ4
       ПО
       СЧ    D00012
       СЛИА  -1(М16)
       МОДА  А01133
       ПИО   (М16)
       СЛЦ   Е2
А01133 ВЧ    Е1
       ЗП    D00012
       call  STEP,m6
       СЧ    D00012
       ПБ    А01125
А01136 СЧ    D00032
       СДА   64+29
       ИЛИ   Е48
А01140 ЗП    D00024
       СЧ    LОС50
       УИ    М16
       do    СЛЦ,АRRАУ,idx
       СЛЦ   Е2
       УИ    М5
       СЧИ   М5
       ЗП    limit
       СЧ    -1(М16)
       И     Е10П1
       УИ    М6
       СЛИ   М6(М16)
* раздвижка на 2 слова        
раздвг СЧИ   М6
       НТЖ   limit
       ПО    раздв2
       СЧ    -2(М6)
       ЗП    (М6)
       СЧ    -1(М6)
       ЗП    1(М6)
       СЛИА  -2(М6)
       ПБ    раздвг
раздв2 СЧ    D00011
       ЗП    (М5)
       СЧ    D00024
       ЗП    1(М5)
       СЧ    -1(М16)
       СЛЦ   Е2
       ЗП    -1(М16)
А01160 СЧИ   М16
       ВЧ    Е1
       ЗП    USRLОС
       СЧ    IDХ
       ПЕ    А01167
А01163 СЧ    В41
       ЗП    МУLЕN
       СЧ    -1(М16)
       НТЖ   Е6
       ПО    А01252
       СЧ    LОС20
       ПБ    А01311
А01167 СЧ    -1(М16)
       И     Е10П1
       НТЖ   Е7
       ПО    А01174
А01171 СЧ    -1(М16)
       И     Е10П1
       СЛЦ   Е1
       ЗП    МУLЕN
       СЧ    LОС246
       ПБ    А01311
А01174 СЧ    IDХ
       СДА   64-1
       СЛЦ   В41
       ЗП    WОRК
       СЧ    LОС55
       НТЖ   В36
       ПЕ    А01201
       СЧ    WОRК
       СЛЦ   В44
       ЗП    WОRК
А01201 call  РR425,m6
       ВЧ    WОRК
       ПЕ    А01116
       СЧ    LОС157
       ЗП    D00025
       СЧ    LОС246
       СДА   64-29
       ЗП    LОС157
       СЧ    LОС117
       И     Е29П11
       ИЛИ   LОС157
       ИЛИ   Е6
       ЗП    LОС157
       СЧ    USRLОС
       СЛЦ   Е6
       ЗП    USRLОС
       СЧ    В41
       ЗП    МУLЕN
       call  РR1022,m6
       СЧ    D00025
       ЗП    LОС157
       СЧ    LОС160
       ЗП    D00011
       СЧ    LОС117
       ЗП    D00025
       СДА   64+29
       СДА   64-19
       ИЛИ   D00024
       СДА   64-10
       ИЛИ   Е6
       ЗП    LОС117
       И     Е29П11
       СДА   64-19
       ЗП    D00032
       m16=  LОС120
       link  А01231,m5
       СЧИ   М5
       ЗП    GОТО
       СЧИ   М16
       ВЧ    Е1
       ЗП    USRLОС
       ПБ    А01171
А01231 link  А01136,m5
РR1232 СЧИ   М5
       ЗП    SАVМ4
       СЧ    Е1
       ЗП    LОС42
       СЧ    D00025
       СДА   64+10
       И     Е19П1
       ПО    А01242
       call  А00340,m6
       СДА   64-19
       СДА   64+19
       ИЛИ   D00032
       call  РR662,m6
А01242 m16=  LОС56
       СЧИ   М16
       ЗП    LОС50
       СЧ    IDХ
       ПО    drtnxt
       ВЧ    Е2
       ЗП    IDХ
       НТЖ
       ПО    А01123
       link  А01123,m6
       do    СЧ,LОС20,idx
       ПБ    РR202
А01252 call  РR425,m6
       ВЧ    В101
       ПЕ    А01116
       call  РR1022,m6
       m16=  LОС56
       СЧ    D00024
       ИЛИ   Е48
       ЗП    1(М16)
       СЧ    Е2
       ЗП    -1(М16)
       ПБ    А01163
long   СЧ    USRLОС
       СЛЦ   МУLЕN
       ВЧ    Е1
       ЗП    USRLОС теперь указ. на посл. слово
       do    УИА,(М5),dblen
А01264 do    СЧ,-1(М5),frebas
       ВЧ    Е2
       ПЕ    А01105
       СЛЦ   Е1
       ЗП    WОRК
       СЛИА  -1(М5)
       ВЧ    МУLЕN
       ПЕ    А01274
       СЧ    USRLОС
       ВЧ    МУLЕN
       СЛЦ   Е1
       ЗП    USRLОС вернули на начало
       СЧИ   М5
       УИА   0(М5)
       ПБ    chunk
А01274 СЧ    МУLЕN
       ВЧ    WОRК
       ЗП    remlen
       СЧ    WОRК
       ЗП    МУLЕN
       ВЧОБ  USRLОС
       ЗП    USRLОС
       СЧИ   М5
       СЛИА  1(М5)
       ПБ    chunk
nospac СЧ    МУLЕN
       ВЧ    B1775
       ПО    long в одну зону не поместится
* кусок не длинный: ищем зону, в которую он поместится
       УИА   -1(М7)
А01303 СЛИА  1(М7) 
       СЧИ   М7
       НТЖ   DВLЕN
       ПО    long таких не нашлось, придется дробить
       СЧ    МУLЕN
       do    ВЧ,(М7),frebas
       ПО    А01303
       СЧИ   М7
       ПБ    chunk нашлась, пишем
* команда 20: замена значения у тек. записи        
СМD20  ЗП    USRLОС
       СЧ    АDЕSСR
А01311 ЗП    D00012
       link  drtnxt,m6
       call  totlen,m5 найдем общую длину записи
       ВЧ    Е1
       УИ    М5
       НТЖ   МУLЕN
       ПЕ    А01336 старая и новая длины не равны
       МОДА  А01323
       ПИО   (М5) значение нулевой длины
* копирование в существующее место       
А01317 МОД   USRLОС
       СЧ    -1(М5)
       do    ЗП,(М5),aitem
       cycle А01317,m5
А01323 УИИ   М5(М16)
       call  DАТЕ,m7
       do    ЗП,,aitem
       dirty 1
       СЧ    LОС220
       И     Е39П21
       ПО    RTNEXT
       НТЖ   LОС220
       do    ЗП,1(М5),loc116
       СЧ    LОС220
       ПБ    А01005
А01336 call  DАТЕ,m7
       do    УИА,(М16),curbuf
       СЧ    1(М16)
       СДА   64+10
       УИ    М5
       СЛИА  1(М5)
       ВЧОБ  1(М16)
       И     Е10П1
       СЛЦ   СURРОS
       ВЧ    Е2
       ЗП    WОRК
       ВЧ    МУLЕN
       ПЕ    А01361
А01346 СЧИ   М6
       ЗП    D00010
       СЧ    LОС220
       ЗП    D00026
       И     Е10П1
       ЗП    LОС220
       call  А00747,m6
       ++    МУLЕN
       УИА   0(М5)
       do    УИА,(М7),curbuf
       СЧ    D00026
       И     Е48П40
       call  РR1047,m6
       СЧ    D00026
       МОД   D00010
       УИА   (М6)
       ПБ    А01005
А01361 call  РR425,m6
А01362 do    СЧ,,aitem
       И     Е15П1
       СЛЦ   ITMLEN
       ВЧ    МУLЕN
       ПЕ    ОVЕRFL 
       do    УИА,(М16),curbuf
       СЧ    WОRК
       ЗП    МУLЕN
       call  А01346,m6
       СЧ    USRLОС
       СЛЦ   МУLЕN
       ЗП    USRLОС
       СЧ    D00023
       И     Е15П1
       ВЧ    МУLЕN
       ЗП    МУLЕN
       МОД   USRLОС
       СЧ    -1
       ЗП    D00023
       call  А01023,m6
       СЧ    D00012
       call  totlen,m5
       СЧ    D00024
       СДА   64-20
       ИЛИ   LОС220
       do    ЗП,1(М16),loc116
       dirty 2
       ПБ    RTNEXT
***************************
* команда 34: преобразование дескриптора массива в текстовый формат
СМD34  СДА   64-33
       РЗБ   П77777
       ИЛИ   SРАСЕ
       ЗП    LОС40
       СЧ    LОС41
       РЗБ   DАТМSК
       ИЛИ   SРАСЕ
       ЗП    LОС41
       СЧ    ЕОТ
       ЗП    LОС42
       ПБ    СМD0
***************************
СРУОUТ call  РR342,m6
А01415 link  СМD0,m6
       СЧ    МУLОС
       ЗП    USRLОС
       СЧ    МУLЕN если макс. длина указана
       ПЕ    СНКLЕN ее нужно проверить
enough ++    АIТЕМ
       СЧ    СURРОS
       ВЧ    Е1
       ЗП    СURРОS
А01423 УИ    М16
       СЧИ   М16
       ПО    А01431
СОLООР do    СЧ,-1(М16),aitem
       МОД   USRLОС
       ЗП    -1(М16)
       cycle СОLООР,m16
А01431 СЧ    USRLОС
       СЛЦ   СURРОS
       ЗП    USRLОС
       СЧ    LОС220
       И     Е39П21
       ПО    (М6) закончили перепись
       СДА   64+20
       ПБ    А00530 следующий сегмент
СНКLЕN ВЧ    ITMLEN
       ПО    enough
       УИА   12(М16) длина записи больше длины в памяти
       ПБ    ЕRR
***************************
* команда 10: создание массива
МКСТL  СЧ    DВDЕSС дддднузззз
       СДА   64+18  осталась длина
       ЗП    DВLЕN  сохраним
       УИ    М5
       СЧ    DВDЕSС 
       И     Е18П1  осталось нузззз
       ЗП    Е70МSК
       СЧ    ВDТАВ
       УИ    М6
       ЗП    curbuf = bdtab
       СДА   64-20  в поле номера листа
       ИЛИ   Е70МSК
       ЗП    temp сделали заготовку ис э70
       СЧ    Е10П1  01777
       ЗП    1(М6)  в слово 1 bdtab
А01447 СЛИА  -1(М5)
* формируем таблицу кол-ва свободных слов
       СЧ    B1776 
       do    ЗП,(М5),bdbuf
       СЧИ   М5    номер зоны массива
       ИЛИ   RООТFL
       ЗП    (М6)  в слово 0 bdtab
       СЧИ   М5
       СЛЦ   temp
       ЗП    ИСЭ70
       Э70   ИСЭ70
       МОДА  А01447
       ПИНО  (М5)
       СЧ    ВDВUF
       ЗП    frebas
       ЗП    МУLОС
       СЧ    Е11   02000
       ЗП    LОС20
       СЧ
       ЗП    D00011
       call  РR376,m6
       call  РR1022,m6 пишем метаданные
       СЧ    DВLЕN
       ЗП    МУLЕN
       ВЧОБ  B1731
       do    ЗП,,bdbuf
       УИА   17(М5) уход на команду 17
       ПБ    А00153
МSG    КОНД  П’НУЛ.УК’
       КОНД  П’ЛЮЧ  0’В’172’
* 2
       КОНД  П’ЗАТЕРТ’
       КОНД  П’ ЛИСТ0’В’172’
* 3
       КОНД  П’НЕТ ЗА’
       КОНД  П’ПИСИ 0’В’172’
* 4
       КОНД  П’НЕДОП.’
       КОНД  П’ ИМЯ.0’В’172’
* 5
       КОНД  П’ЗАТЕРТ’
       КОНД  П’ 1 Л.0’В’172’
* 6
       КОНД  П’ПЕРЕПО’
       КОНД  П’ЛНЕНА0’В’172’
* 7
       КОНД  П’ШАГЗ. ’
       КОНД  П’ВЕЛИК0’В’172’
* 8
       КОНД  П’НЕТ ИМ’
       КОНД  П’ЕНИ  0’В’172’
* 9
       КОНД  П’ИМ.УЖЕ’
       КОНД  П’ ЕСТЬ0’В’172’
* 10
       КОНД  П’НЕТ С.’
       КОНД  П’КОНЦА0’В’172’
* 11
       КОНД  П’СИСТ.О’
       КОНД  П’ШИБКА0’В’172’
* 12
       КОНД  П’ДЛ.ЗАП’
       КОНД  П’>ДЛИН0’В’172’
* 13
       КОНД  П’ОДНОВР’
       КОНД  П’.РАБ.0’В’172’
* 14
       КОНД  П’НЕТ ТЕ’
       КОНД  П’КУЩЕЙ0’В’172’
* 15
       КОНД  П’НЕТ ПР’
       КОНД  П’ЕД.З.0’В’172’
* 16
       КОНД  П’НЕТ СЛ’
       КОНД  П’ЕД.З.0’В’172’
* 17
       КОНД  П’НЕПР.П’
       КОНД  П’АРОЛЬ0’В’172’
VSЕЕD  КОНД  В’3777777777777777’
Е1     КОНД  П’000001’
off7   КОНД  a(a00317-a00275)
Е12    КОНД  П’000080’
СМРОFF КОНД  a(сравн-a00275)
off42  КОНД  a(a00301-a00275)
В3     КОНД  П’000003’
AMYLOC КОНД  a(myloc-bdvect)
Е2     КОНД  П’000002’
Е40    КОНД  В’0010000000000000’
Е6П1   КОНД  П’00000D’
Е48    КОНД  В’4000000000000000’
Е10П1  КОНД  В’0000000000001777’
Е31    КОНД  П’00F000’
Е19П1  КОНД  В’0000000001777777’
Е11    КОНД  П’000040’
Е15П1  КОНД  В’0000000000077777’
В17777 КОНД  В’0000000000017777’
Е32    КОНД  В’0000020000000000’
Е5     КОНД  В’0000000000000020’
Е29П11 КОНД  В’0000003777776000’
Е16    КОНД  В’0000000000100000’
В41    КОНД  В’41’ длина метаданных
Е48П40 КОНД  В’7770000000000000’
off12  КОНД  a(dbdesc-bdvect)
Е18П1  КОНД  В’0000000000777777’
Е48П30 КОНД  В’7777774000000000’
Е33П16 КОНД  В’0000077777700000’
D01566 КОНД  В’0000377774000000’
Е6     КОНД  П’00000А’
Е7     КОНД  П’00000F’
В36    КОНД  В’0000000000000036’
В44    КОНД  П’00000Д’
В101   КОНД  П’00000G’
B1775  КОНД  В’1775’
Е39П21 КОНД  В’0007777774000000’
П77777 КОНД  П’077777’
SРАСЕ  КОНД  П’ 00000’
DАТМSК КОНД  П’03 1  ’
ЕОТ    КОНД  В’3641540000000000’
B1776  КОНД  В’1776’
B1731  КОНД  В’1731’
       ПАМ   124
       ПАМ   ’32000’
ВDVЕСТ ПАМ   1
ОUТАDR ПАМ   2
ORGCMD ПАМ   2 командное слово запроса
СURСМD ПАМ   1 текущее значение ком. слова
LОС6   ПАМ   1
GIVЕNР ПАМ   1
КЕУ    ПАМ   1 ключ запроса
ЕRНNDL ПАМ   1
LОС12  ПАМ   1
МУLОС  ПАМ   1
LОС14  ПАМ   1
МУLЕN  ПАМ   1
ВDВUF  ПАМ   1
ВDТАВ  ПАМ   1
LОС20  ПАМ   1
АRRАУ  ПАМ   7
DВDЕSС ПАМ   1
RООТFL ПАМ   1
SАVЕDР ПАМ   1
LОС33  ПАМ   1
frebas ПАМ   1 база массива св. слов в bdtab
АDЕSСR ПАМ   1 адрес дескриптора текущего эл-та
limit  ПАМ   1
СURКЕУ ПАМ   1
LОС40  ПАМ   1 
LОС41  ПАМ   1 копия дескриптора массива 
LОС42  ПАМ   2 
Е70МSК ПАМ   1
СURРОS ПАМ   1
АIТЕМ  ПАМ   1
ITMLEN ПАМ   1
LОС50  ПАМ   1
DВLЕN  ПАМ   2
LОС53  ПАМ   1
LОС54  ПАМ   1
LОС55  ПАМ   1
LОС56  ПАМ   1
       ПАМ   31
LОС116 ПАМ   1
LОС117 ПАМ   1
LОС120 ПАМ   1
       ПАМ   30
LОС157 ПАМ   1
LОС160 ПАМ   32
LОС220 ПАМ   17
curbuf ПАМ   1
IDХ    ПАМ   2
WНIСН  ПАМ   2
LОС246 ПАМ   1
DIRТУ  ПАМ   345
       ФИНИШ

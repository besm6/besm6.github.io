*^
*^
**********************************************^
*                                            *^
*    Д И А Л О Г О В А Я    С И С Т Е М А    *^
*                К  Р  А  Б                  *^
*                                            *^
*--------------------------------------------*^
*  ПРОГРАММА Ф1. МОДУЛЬ КОМП                 *^
*  КОМПЕНСАТОР ВЕРСИЙ ОПЕРАЦИОННЫХ СИСТЕМ    *^
**********************************************^
*^
КОМП СТАРТ '10000'^
*^
* МОДИФИКАТОРЫ^
*-------------^
*^
М1 ЭКВ 1^
М2 ЭКВ 7^
М3 ЭКВ 3^
М4 ЭКВ 4^
М7 ЭКВ 2^
М10 ЭКВ 8^
М11 ЭКВ 9^
М13 ЭКВ 11^
М15 ЭКВ 13^
М16 ЭКВ 14^
*^
*  ВНЕШНИЕ   МЕТКИ^
*-----------------^
*^
КТР1 ВНЕШН Ф1.КТР1^
КТР2 ВНЕШН Ф1.КТР2^
П7777 ВНЕШН Ф1.П7777^
Е24П1 ВНЕШН Ф1.Е24П1^
ШИФЗАД ВНЕШН Ф1.ШИФЗАД^
ТЛФ ВНЕШН Ф1.ТЛФ^
Е48 ВНЕШН Ф1.Е48^

РЯ1 ВНЕШН Ф1.РЯ1^
ПРВЫВ ВНЕШН Ф1.ПРВЫВ^
Е2 ВНЕШН Ф1.Е2^
ВСБ ВНЕШН Ф2.ВСБ^
ЗСБ ВНЕШН Ф2.ЗСБ^
Е48П41 ВНЕШН Ф1.Е48П41^
МНПРОБ ВНЕШН Ф2.МНПРОБ^
П77 ВНЕШН Ф1.П77^
ИР16К ВНЕШН Ф1.ИР16К^
КЗОБЛ ВНЕШН Ф1.КЗОБЛ^
КОТОБЛ ВНЕШН Ф1.КОТОБЛ^
НОБЛ ВНЕШН Ф1.НОБЛ^
Е47 ВНЕШН Ф1.Е47^
Е27 ВНЕШН Ф1.Е27^
Е10 ВНЕШН Ф1.Е10^
Е1 ВНЕШН Ф1.Е1^
ТИПОС ВНЕШН Ф1.ТИПОС^
*^
* ФОРМИРОВАНИЕ СЛУЖЕБНЫХ ЗАДАЧ^
*-----------------------------^
*^
ВКЛСЛ НОП^
 ВХОДН ВКЛСЛ^
 СЧИ М16^
 ЗП ИР16К^
 СЧ ТИПОС^
 НТЖ Е1^
 ПЕ (М16) НЕ  И Т М^
 Э62 '7236'^
 МОД ИР16К^
 ПБ 0^
*^
* ФОРМИРУЕМЫЕ ЗАДАЧИ НЕ СЛУЖЕБНЫЕ^
*--------------------------------^
*^
ВЫКСЛ НОП^
 ВХОДН ВЫКСЛ^
 СЧИ М16^
 ЗП ИР16К^
 СЧ ТИПОС^
 НТЖ Е1^
 ПЕ (М16) НЕ  И Т М^

 Э62 '7235'^
 МОД ИР16К^
 ПБ 0^
*^
* ВЫДАЧА ЗАКРЫТИЯ ФОРМИРОВАНИЯ^
*-----------------------------^
*^
ПЗФ НОП^
 ВХОДН ПЗФ^
 СЧИ М16^
 ЗП ИР16К^
 МОД ТИПОС^
 ПБ ППЗФ^
ППЗФ НОП ПЕРЕКЛЮЧАТЕЛЬ^
 МОДА 0^
 ПБ ППЗФ0^
 МОДА 0^
 ПБ ППЗФ1^
 МОДА 0^
 ПБ ППЗФ2^
ППЗФ0 НОП^
 Э65 6^
 И Е47^
 МОД ИР16К^
 ПБ 0^
ППЗФ1 НОП^
 Э65 6^
 И Е27^
 МОД ИР16К^
 ПБ 0^
ППЗФ2 НОП^
 СЧ КТР1^
 И Е47^
 МОД ИР16К^
 ПБ 0^
*^
* ЧТЕНИЕ 1-ОЙ УПРАВЛЯЮЩЕЙ ЯЧЕЙКИ^
*-------------------------------^
*^
ЧТУПР1 НОП^
 ВХОДН ЧТУПР1^
 СЧИ М16^
 ЗП ИР16К^
 МОД ТИПОС^
 ПБ УПР1^
УПР1 НОП ПЕРЕКЛЮЧАТЕЛЬ^
 УИА 6(М16)^
 ПБ УПР10^
 УИА 1(М16)^
 ПБ УПР10^
 СЧ КТР1^
 ПБ УПР11^
*^
УПР10 НОП^
 Э65 (М16)^
УПР11 НОП^
 МОД ИР16К^
 ПБ 0 ВЫХОД^
*^
* ЧТЕНИЕ 2-ОЙ УПРАВЛЯЮЩЕЙ ЯЧЕЙКИ^
*-------------------------------^
*^
ЧТУПР2 НОП^
 ВХОДН ЧТУПР2^
 СЧИ М16^
 ЗП ИР16К^
 МОД ТИПОС^
 ПБ УПР2^
УПР2 НОП ПЕРЕКЛЮЧАТЕЛЬ^
 УИА 4(М16)^
 ПБ УПР20^
 УИА 2(М16)^
 ПБ УПР20^
 СЧ КТР2^
 ПБ УПР21^
*^
УПР20 НОП^
 Э65 (М16)^
УПР21 НОП^
 МОД ИР16К^
 ПБ 0 ВЫХОД^
*^
* ЧТЕНИЕ СТАНДАРТНЫХ ПАСПОРТОВ^
*-----------------------------^
*^
ЧТСП НОП^
 ВХОДН ЧТСП^
 СЧИ М16^
 ЗП ИР16К^
 МОД ТИПОС^
 ПБ ПЧТСП^
ПЧТСП НОП ПЕРЕКЛЮЧАТЕЛЬ^
 УИА ЧТПИТМ(М15)^
 ПБ ЧТСП0^
 УИА ОБЛСП(М15)^
 ПБ ЧТСП1^
 УИА ЧТПИТМ(М15)^
 ПБ ЧТСП0^
ЧТСП0 НОП^
 Э70 (М15)^
 Э70 1(М15)^
 МОД ИР16К^
 ПБ 0 ВЫХОД^
ЧТПИТМ НОП^
 КОНД В'0010110000330543'^
 КОНД В'0010120000330544'^
ЧТСП1 НОП^
 СЧ НОБЛ^
 НТЖ (М15)^
 ПО ЧТСП1А ОБЛАСТЬ ЗАКАЗАНА^
 Э72 КОТОБЛ^
 СЧ (М15)^
 ЗП НОБЛ^
 ИЛИ КЗОБЛ^
 Э50 '131'^
ЧТСП1А НОП^
 УИА ЧТПИПМ(М15)^
 Э70 (М15)^
 Э70 1(М15)^
 Э70 2(М15)^
 Э70 3(М15)^
 МОД ИР16К^
 ПБ 0 ВЫХОД^
ОБЛСП КОНД Х'4112' НОМЕР ОБЛАСТИ СТАНДАРТНЫХ ПАСПОРТОВ^
ЧТПИПМ НОП^
 КОНД В'0010110000330000'^
 КОНД В'0010120000330001'^
 КОНД В'0010130000330002'^
 КОНД В'0010140000330003'^
*^
* ЧТЕНИЕ КАТАЛОГА ШИФРОВ^
*-----------------------^
*^
ЧТКШИФ НОП^
 ВХОДН ЧТКШИФ^
 СЧИ М16^
 ЗП ИР16К^
 МОД ТИПОС^
 ПБ ПЧТШ^
ПЧТШ НОП ПЕРЕКЛЮЧАТЕЛЬ^
 УИА ЧТШИТМ(М15)^
 ПБ ЧТКШ0^
 УИА ОБЛКШ(М15)^
 ПБ ЧТКШ1^
 УИА ЧТШИТМ(М15)^
 ПБ ЧТКШ0^
ЧТКШ0 НОП^
 Э70 (М15)^
 МОД ИР16К^
 ПБ 0 ВЫХОД^
ЧТШИТМ КОНД В'0010360000330545'^
ЧТКШ1 НОП^
 СЧ НОБЛ^
 НТЖ (М15)^
 ПО ЧТКШ1А ОБЛАСТЬ ЗАКАЗАНА^
 Э72 КОТОБЛ^
 СЧ (М15)^
 ЗП НОБЛ^
 ИЛИ КЗОБЛ^
 Э50 '131'^
ЧТКШ1А НОП^
 УИА ЧТШИПМ(М15)^
 Э70 (М15)^
 МОД ИР16К^
 ПБ 0 ВЫХОД^
ОБЛКШ КОНД Х'4135' НОМЕР ОБЛАСТИ КАТАЛОГА ШИФРОВ^
ЧТШИПМ КОНД В'0010360000330000'^
*^
* ЧТЕНИЕ КЛЮЧЕЙ ДИАЛОГОВЫХ ЗАДАЧ^
*-------------------------------^
*^
ЧТКЛ НОП^
 ВХОДН ЧТКЛ^
 СЧИ М16^
 ЗП ИР16К^
 МОД ТИПОС^
 ПБ ПЧТКЛ^
ПЧТКЛ НОП ПЕРЕКЛЮЧАТЕЛЬ^
 УИА ЧТКИТМ(М15)^
 ПБ ЧТКЛ0^
 УИА ОБЛКЛ(М15)^
 ПБ ЧТКЛ1^
 УИА ЧТКИТМ(М15)^
 ПБ ЧТКЛ0^
ЧТКЛ0 НОП^
 Э70 (М15)^
 МОД ИР16К^
 ПБ 0 ВЫХОД^
ЧТКИТМ КОНД В'0010360000330675'^
ЧТКЛ1 НОП^
 СЧ НОБЛ^
 НТЖ (М15)^
 ПО ЧТКЛ1А ОБЛАСТЬ ЗАКАЗАНА^
 Э72 КОТОБЛ^
 СЧ (М15)^
 ЗП НОБЛ^
 ИЛИ КЗОБЛ^
 Э50 '131'^
ЧТКЛ1А НОП^
 УИА ЧТКИПМ(М15)^
 Э70 (М15)^
 МОД ИР16К^
 ПБ 0 ВЫХОД^
ОБЛКЛ КОНД Х'4113' НОМЕР ОБЛАСТИ КЛЮЧЕЙ^
ЧТКИПМ КОНД В'0010360000330000'^
*^
* ЗАПИСЬ РАЗДЕЛА ПАСПОРТА "КОД СИС" ДЛЯ ВЕРСИИ ИПМ^
* ПРИ БЫСТРОМ ФОРМИРОВАНИИ, КОГДА УКАЗАН РАЗДЕЛ "ВЫВ"^
*----------------------------------------------------^
*^
ЗПКСИС НОП^
 ВХОДН ЗПКСИС^
 СЧ ТИПОС^
 НТЖ Е1^
 ПЕ (М16) ВЕРСИЯ НЕ ИПМ^
 СЧ ПРВЫВ^
 И Е2^
 ПО (М16) НЕТ РАЗДЕЛА "ВЫВ"^
 УИА ТКСИС(М3)^
 УИА -5(М10)^
ЗПКС1 НОП^
 ПВ ВСБ(М15)^
 НТЖ Е48П41^
 ПО (М16) КОНЕЦ ВСТАВКИ^
 НТЖ Е48П41^
 ПВ ЗСБ(М15)^
 ПИО МНПРОБ(М11) ГРАНИЦА^
 ПБ ЗПКС1^
ТКСИС КОНД П'КОД СИ'^
 КОНД П'С00000'М32В'115'М24В'341'М16В'377'^
*^
* ВЫДАЧА ПОДЧИНЕННЫХ ЗАДАЧ МОНИТОРА АБОНЕНТОВ^
*--------------------------------------------^
*^
ДАЙПЗ НОП^
 ВХОДН ДАЙПЗ^
 СЧИ М16^
 ЗП ИР16К^
 УИА -47(М16)^
 СЧ 0^
ДЗП0 НОП^
 МОДА (М15)^
 ЗП 47(М16)^
 ЦИКЛ ДЗП0(М16)^
 МОД ТИПОС^
 ПБ ДПЗ^
ДПЗ НОП ПЕРЕКЛЮЧАТЕЛЬ^
 СЧ 0^
 ПБ ДПЗИТМ^
 СЧ 0^
 ПБ ДПЗИПМ^
 СЧ 0 ***> СЧ Е48^
 ПБ ДПЗИТМ ***> ПБ ДПЗЭЛБ^
ДПЗИТМ НОП^
ДПЗИПМ НОП^
 Э62 '61'^
 ИЛИ Е48^
 НТЖ Е48^
 ЗП РЯ1^
 УИА -15(М16)^
ДПЗ1 НОП^
 МОДА (М15)^
 СЧ 15(М16)^
 МОДА (М15)^
 ЗП 31+15(М16)^
 СЧ 0^
 МОДА (М15)^
 ЗП 15(М16)^
 ЦИКЛ ДПЗ1(М16)^
 СЧ РЯ1^
 МОД ИР16К^
 ПБ 0 ВЫХОД^
*^
ДПЗЭЛБ НОП^
 Э62 '61'^
 МОД ИР16К^
 ПБ 0 ВЫХОД^
*^
* ВЫДАЧА ОСТАНОВЛЕНЫХ ПОДЧИНЕННЫХ ЗАДАЧ^
*--------------------------------------^
*^
ДАЙОПЗ НОП^
 ВХОДН ДАЙОПЗ^
 СЧИ М16^
 ЗП ИР16К^
 МОД ТИПОС^
 ПБ ДОПЗ^
ДОПЗ НОП ПЕРЕКЛЮЧАТЕЛЬ^
 СЧ 0^
 ПБ ДОПЗ0^
 СЧ 0^
 ПБ ДОПЗ1^
 СЧ 0^

 ПБ ДОПЗ0^
ДОПЗ0 НОП^
 Э62 '101'^
 МОД ИР16К^
 ПБ 0 ВЫХОД^
ДОПЗ1 НОП^
 Э62 '101'^
 ИЛИ Е48^
 НТЖ Е48^
 МОД ИР16К^
 ПБ 0 ВЫХОД^
*^
*  НАХОДИТСЯ ЗДЕСЬ ВРЕМЕННО^
***************************^
***************************^
*^
* БЛОК ПОИСКА КОНТЕКСТА (ДЛЯ ПОЧТЫ)^
*----------------------------------^
*^
*  ВХОДНЫЕ ЗНАЧЕНИЯ:^
*    М13 - АДРЕС ЭТАЛОННОЙ СТРОКИ^
*     М3 - АДРЕС НАЧАЛА ПОИСКА^
*    РЯ5 - АДРЕС КОНЦА ФАЙЛА^
*  КОДВС - КОДИРОВКА ФАЙЛА ПОИСКА^
*  ВЫХОДНЫЕ ЗНАЧЕНИЯ:^
*     М7 = 0 - КОНТЕКСТ НЕ НАЙДЕН^
*^
ПКНТ НОП^
 ВХОДН ПКНТ^
 УИА -5(М10)^
ПКНТ1 НОП^
 УИА (М7)^
 УИИ М1(М13)^
 УИА -5(М4)^
 ПВ ВСК(М15)^
 ЗП СИ^
ПКНТ2 НОП^
 ПВ ВСД(М15)^
 ЗП СИ1^
 НТЖ ПП341^
 ПО ПКНТ4 КОНЕЦ СТРОКИ^
 СЧ СИ^
 НТЖ СИ1^
 ПЕ ПКНТ2 ПЕРВЫЙ СИМВОЛ НЕ СОВПАЛ^
 СЧИ М3^
 ЗП РЯ2^
 СЧИ М10^
 ЗП РЯ2+1^
 УИА 1(М7)^
ПКНТ3 НОП^
 ПВ ВСК(М15)^
 ЗП СИ^
 НТЖ Е48П41^
 ПО (М16) КОНТЕКСТ НАЙДЕН^
 ПВ ВСД(М15)^
 НТЖ СИ^
 ПО ПКНТ3 СИМВОЛ СОВПАЛ^
 СЧ РЯ2^
 УИ М3^
 СЧ РЯ2+1^
 УИ М10^
 ПБ ПКНТ1^
*^
ПКНТ4 НОП КОНЕЦ СТРОКИ^
 СЧИ М3^
 ЗП РЯ2^
 СЧИ М10^
 ЗП РЯ2+1^
ПКНТ5 НОП^
 ПВ ВСД(М15)^
 НТЖ ПП341^
 ПО ПКНТ5 КОНЕЦ СТРОКИ^
 СЧИ М3^
 ВЧОБ РЯ5^
 ПЕ (М16) ПРОСМОТРЕН ВЕСЬ ФАЙЛ^
 СЧ РЯ2^
 УИ М3^
 СЧ РЯ2+1^
 УИ М10^
 ПБ ПКНТ1^
*^
*  БЛОК ВЫБОРА СИМВОЛА^
*---------------------^
*^
ВСК НОП^
 ВХОДН ВСК^
*^
*  ВХОДНЫЕ ЗНАЧЕНИЯ:^
*    М1  - АДРЕС СЛОВА^
*    М4  - НОМЕР БАЙТА^
*  ВЫХОДНЫЕ ЗНАЧЕНИЯ:^
*    СМ  - БЫБРАННЫЙ СИМВОЛ^
*  ВЫХОД ПО М15^
*^
 СЧ (М1)^
 СБР П377(М4)^
 МОДА (М15)^
 ЦИКЛ (М4)^
 СЛИА 1(М1)^
 УИА -5(М4)^
 ПБ (М15)^
*^
*  БЛОК ВЫБОРА СИМВОЛА С ПЕРЕКОДИРОВКОЙ^
*--------------------------------------^
*^
ВСД НОП^
 ВХОДН ВСД^
*^
*  ВХОДНЫЕ ЗНАЧЕНИЯ:^
*    М3  - АДРЕС СЛОВА^
*    М10 - НОМЕР БАЙТА^
*  КОДВС - КОДИРОВКА^
*  ВЫХОДНЫЕ ЗНАЧЕНИЯ:^
*    СМ  - БЫБРАННЫЙ СИМВОЛ^
*  ВЫХОД ПО М15^
*^
 СЧИ М15^
 ЗП РЯВСД+1^
 СЧ (М3)^
 СБР П377(М10)^
 ЦИКЛ ВСД1(М10)^
 СЛИА 1(М3)^
 УИА -5(М10)^
ВСД1 НОП^
 ЗП РЯВСД^
 СЧ КОДВС^
 ПО ВСД2 БЕЗ ПЕРЕКОДИРОВКИ^
 СЧ РЯВСД^
 СДА 64+40^
 УИ М15^
 СЧ ТАБКОД(М15)^
 МОД КОДВС^
 СДА 0^
 И Е48П41^
 МОД РЯВСД+1^
 ПБ 0^
ВСД2 НОП^
 СЧ РЯВСД^
 МОД РЯВСД+1^
 ПБ 0^
*^
РЯВСД ВНЕШН Ф1.РЯВСД^
КОДВС ВНЕШН Ф1.КОДВС^
ТАБКОД ВНЕШН Ф1.ТАБКОД^
СИ ВНЕШН Ф1.СИ^
СИ1 ВНЕШН Ф1.СИ1^
ПП341 ВНЕШН Ф1.ПП341^
РЯ2 ВНЕШН Ф1.РЯ2^
РЯ5 ВНЕШН Ф1.РЯ5^
П377 ВНЕШН Ф1.П377^

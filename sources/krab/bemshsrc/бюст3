*
*
*23456789012345678901234567890123456789012345678901234
**********************************************
*                                            *
*    Д И А Л О Г О В А Я    С И С Т Е М А    *
*                К  Р  А  Б                  *
*                                            *
*--------------------------------------------*
*  БЮДЖЕТНО-СТАТИСТИЧЕСАЯ ПОДСИСТЕМА.        *
*  Ч А С Т Ь  3                              *
*  БЛОК БЮДЖЕТА                              *
**********************************************
*
*
*
*
ПДГФИО НОП
*
*    ПОДГОТОВКА ФИО В КАНОНИЧЕСКОЙ ФОРМЕ
*
*    ВХОДНАЯ ИНФОРМАЦИЯ:
*         М2,М3 - КООРДИНАТЫ БУФЕРА ПРИЕМА С ТЕРМИНАЛА
*    ВЫХОДНАЯ ИНФОРМАЦИЯ:
*         СМ=0 - ОТКАЗ ОТ ВВОДА НОВОГО ПОЛЬЗОВАТЕЛЯ
*         СМ<>0 - ВСЕ О.К.
*         ФАМ2,ФАМ2+1 - ТЕКСТ ФИО В КАНОНИЧЕСКОМ ВИДЕ
*    ИСПОЛЬЗУЮТСЯ РЕГИСТРЫ:
*         М2,М3,М13  - ДЛЯ БЛОКА  В С
*         М5,М6,М7   - РАБОЧИЕ
*         М1,М17     - В СЛУЧАЕ ОШИБКИ
*         М16        - АДРЕС ВОЗВРАТА
*
 УИА (М5)
 УИА -11(М6)
 УИА -6(М7)
 СЧ
 ЗП ФАМ2+1
НОВОС0 НОП
 ПВ ВС(М13)
 НТЖ ОООООП
 ПО НОВОС0
 СЧ РЯВС
 НТЖ Е8П1
 ПО (М16)     ПУСТАЯ ПОСЫЛКА - ВОЗВРАТ
 СЧ РЯВС
 ВЧ П15
 И Е6П1
 ЗП ФАМ2
НОВОСЦ НОП
 ПВ ВС(М13)
 НТЖ Е8П1
 ПО НОВОСК ФИО ПЕРВИЧНО ОБРАБОТАНО
 СЧ РЯВС
 ВЧ П15
 И Е16П1
 ЗП РЯ1   БАЙТ В КАНОНИЧЕСКОМ ВИДЕ
 СЧ ФАМ2(М5)
 СДА 64-6
 ИЛИ РЯ1
 ЗП ФАМ2(М5)
 ЦИКЛ НОВОС7(М7)  ЦИКЛ ПО ЗАПОЛНЕНИЮ СЛОВА
 СЛИА 1(М5)
 УИА -3(М7)
НОВОС7 ЦИКЛ НОВОСЦ(М6)
 УИА СИМБ12(М1)
 ПВ ВЫВОД(М17)     ВЫВОД ДИАГН.: СИМВОЛОВ > 12
 ПБ НАЧБЮД
НОВОСК НОП
 СЧ ФАМ2(М5)
 СДА 64-6
 ИЛИ Е2
 ЗП ФАМ2(М5)
 ЦИКЛ НОВОСК(М7)
 ПИНО НОВОС8(М5)   ЕСЛИ СИМВОЛОВ БОЛЬШЕ 8
 УИА -3(М7)
 УИА 1(М5)
 ПБ НОВОСК   ДОРАБОТКА ВТОРОГО СЛОВА
НОВОС8 НОП
 СЧ ВСЕЕД
 ПБ (М16)
*
*$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
*
КТГ    НОП ,  БЛОК УСТАНОВКИ  К А Т Е Г О Р И Й
*             ПОЛЬЗОВАТЕЛЯМ И ГРУППАМ
*
 ПВ ПОДГОТ(М13)
 ПВ НАЙПРО(М15)
 СЧ РЯВС
 ВЧОБ Е3П1
 ПЕ НЕТКАТ    КАТЕГОРИЯ > 7
 СЧ БЮДЖП+1
 ПЕ КТГПОЛ  РАБОТА С ПОЛЬЗОВАТЕЛЕМ
 СЧ ГРУППА
 ПО НЕТРП    НЕТ РАБОЧЕГО ПОЛЯ
*
КТГГРУ НОП
 ПВ ПРАВО2(М13)
 СЧ ГРУППА
 И Е16П1
 ЗП РЯ5  ДЛЯ П.П. ГЛАДИМ
 СДА 64+8
 ЗП ЗОНРЯ
 ПВ ИЗ10В8(М13)
 МОД РЯ2    НОМЕР ОТДЕЛА
 СЧ БЮДОТ1
 И Е47П45
 СДА 64+44
 ВЧ РЯВС
 ПЕ НБЮД
 ПВ ЗАХВАТ(М13)
 СЧ RСДВИГ+1
 ЗП РЯ4
 СЧ РЯВС
 ЗП РЯ6
 ПВ ГЛАДИМ(М13)   УМЕНЬШИМ КАТЕГОРИИ
*
 СЧ РЯВС
 СДА 64-44
 ЗП РЯВС
 СЧ Е47П45
 НТЖ ВСЕЕД
 И ГРУППА
 ИЛИ РЯВС
 ЗП ГРУППА
 ПВ ЗПГР(М13)
 ПВ ОСВОБ(М13)
 ПБ ББ6
*
*-----------------------------------------------------
*
КТГПОЛ НОП
 ПВ ДИСПРА(М13)
 ПО КТГПС   ОБХОД КОНТРОЛЯ ДЛЯ ДИСПЕТЧЕРА И АДМ.СИС.
 Э70 ЧТСЛЗО
 СЧ БЮДЖП+1
 И Е8П1
 НТЖ Е8П1
 ПО КТГСИС  КАТЕГОРИЯ ДЛЯ СИС.ВХОДА
 СЧ БЮДЖП+1
 И Е8П1
 ПЕ КТГПГР   КАТЕГОРИЯ ГРУППОВОГО ПОЛЬЗОВАТЕЛЯ
 ПВ ПРАВО1(М13)
 СЧ БЮДЖП+1
 СДА 64+16
 И Е8П1
 ЗП ЗОНРЯ
 ПВ ИЗ10В8(М13)
 МОД РЯ2
 СЧ БЮДОТ1
 ЗП БКТГ
 ПБ КТГП1
КТГСИС ПВ ПРАВО0(М13)
 ПБ КТГПС   НА ЗАПИСЬ КАТЕГОРИИ
КТГПГР ПВ ПРАВО3(М13)
 СЧ БЮДЖП+1
 И Е24П1
 СДА 64+8
 ЗП ГРУППА
 ПВ ГРУНАЙ(М13)
 ПО НБЮД
 СЧ ГРУППА
 ЗП БКТГ
КТГП1 НОП
 СЧ БКТГ
 И Е47П45
 СДА 64+44
 ВЧ РЯВС
 ПЕ НБЮД
КТГПС СЧ РЯВС   ЗАПИСЬ КАТЕГОРИИ
 СДА 64-4
 ЗП РЯВС
 СЧ Е3П1
 СДА 64-4
 НТЖ ВСЕЕД
 И БЮДЖП+2
 ИЛИ РЯВС
 ЗП БЮДЖП+2
 ПБ ЗАПШМ    НА ЗАПИСЬ БЮДЖЕТА ПОЛЬЗОВАТЕЛЯ
НЕТКАТ НОП  КАТЕГОРИЯ >7
 ПВ ДИАКТГ(М13)
 ПБ ББ1
*
*
ДИАКТГ НОП
*
*   ВЫВОД:  КАТЕГОРИЯ>7
*
 УИА ЗАПКАТ(М1)
 ПВ ВЫВОД(М17)
 ПБ (М13)
*
*
*       БЛОК ИЗМЕНЕНИЯ БЮДЖЕТА  О Т Д Е Л А
*
*$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
*
ОТД    НОП ,  ИЗМЕНЕНИЕ БЮДЖЕТА ОТДЕЛА
*
 ПВ ПРАВ04(М13)  ГЛАВНЫЙ АДМ. ИЛИ ДИСПЕТЧЕР
 ПВ ПОДГОТ(М13)
 УИА -1(М5)
 СЧ
 ЗП БОТ1
 ЗП ЗОНРЯ
*
 СЧ ВСЕЕД      ПРИЗНАК ТОГО,ЧТО ПАРАМЕТР НЕ ИЗМ.
 ЗП БЮФИО
 ЗП КЗОН
 ЗП КЧЕЛ
 ЗП БГР
 ЗП КЗАД
 ЗП БКТГ
 ЗП ОАРХ
 ЗП БКТР
 ЗП ШИТП    ШКАЛА АРХИВОВ
 ЗП НУЛОТД  0 - ПРИЗНАК ОБНУЛИ ВСЕ БЮДЖЕТЫ ОТДЕЛА
*
 ПВ НАЙПРО(М15)
 ПВ ПРОЧИС(М13)
 СЧ РЯВС
 ЗП ЗОНРЯ
 ПВ ЧИСЛА(М13)
 СЧ ЗОНРЯ
 ЗП БОТ1     НОМЕР ОТДЕЛА В Д.Д.
 ЗП НОМОДД
*
 ПВ ИЗ10В8(М13)
 СЧ РЯ2
 ЗП НОМОТД  НОМЕР ОТД. В 8-ОМ ВИДЕ
*
*  РАЗБОР ПАРАМЕТРОВ ДИРЕКТИВЫ
*
ОТ2 НОП
 СЧ РЯВС
 НТЖ Е8П1
 ПО ОТДЗПП  ЗАПИСЬ ПАРАМЕТРОВ
 СЧ РЯВС
 НТЖ ОООООП
 ПЕ ИЩИПАР  БУДЕМ ИСКАТЬ ПАРАМЕТР
 ПВ НАЙПРО(М15)
 ПБ ОТ2
*
ИЩИПАР НОП , НАЧАЛО ПОИСКА ПАРАМЕТРА
 ПВ ВЫБДИР(М17)  В РЯВС - ПЕРВАЯ БУКВА
*
 УИА ОТП1А(М7)
 ПВ АДМПРА(М13)  ПРОВЕРКА НА ПРАВО "АДМ.СИС."
 ПО ОТНАЙ1  ЭТО АДМИНИСТРАТОР СИСТЕМЫ
 УИА ОТП1Д(М7)  ЭТО ДИР-ВЫ ДЛЯ ДИСПЕТЧЕРА
*
ОТНАЙ1 НОП ,  ПОИСК В ПЕРВОМ ПЕРЕКЛЮЧАТЕЛЕ
 СЧ (М7)
 ПО ИЩИСЛП  ЗДЕСЬ НЕТ. ИЩИ В СЛЕДУЮЩЕМ
 НТЖ ТЕКДИР
 И МАСДИР
 ПО (М7)  НАШЛИ
 ЦИКЛ ОТНАЙ1(М7)
*
*     ПЕРЕКЛЮЧАТЕЛЬ ПАРАМЕТРОВ ГРУППЫ 1
*
ОТП1А НОП
       ПБ   ОТДДН   БЮД. ОТД. ДИНАМИЧЕСКИЙ
       КОНК П'ДИН'
       ПБ   ОТДСТ   БЮД. ОТД. СТАТИЧЕСКИЙ
       КОНК П'СТА'
       ПБ   ОТДЗК   ЗАКРЫТЬ АРХИВ
       КОНК П'ЗАК'
       ПБ   ОТДОТ   ОТКРЫТЬ АРХИВ
       КОНК П'ОТК'
       ПБ   ОТДЗКВ  ЗАКРЫТЬ АРХИВ ВСЕМ ПОЛЬЗ.ОТД
       КОНК П'ЗКВ'
       ПБ   ОТДОТВ  ОТКРЫТЬ АРХИВ ВСЕМ ПОЛЬЗ.ОТД
       КОНК П'ОТВ'
КОНИСК ПБ   ОТДИСК  ИСКЛЮЧИТЬ ВСЕХ ПОЛЬЗ.ОТДЕЛА
       КОНК П'ИСК'
       ПБ   ОТДНУЛ  ОБНУЛИТЬ ВСЕ БЮДЖЕТЫ ОТДЕЛА
       КОНК П'НУЛ'
*
*  НИЖЕ СЛЕДУЮЩИЕ ПАР-РЫ МОЖЕТ ИЗМЕНЯТЬ
* НЕТОЛЬКО АДМИНИСТРАТОР СИСТЕМЫ, НО И ДИСПЕТЧЕР.
*
ОТП1Д  ПБ   ОТДКТД  КОНТРОЛЛЕР "ДА" (ВКЛЮЧЕН)
       КОНК П'КТД'
       ПБ   ОТДКТН  КОНТРОЛЛЕР "НЕТ" (ВЫКЛЮЧЕН)
       КОНК П'КТН'
*
       КОНД В'0'  ПРИЗНАК КОНЦА ПЕРЕКЛЮЧ.
*
*.............................................
*
ИЩИСЛП НОП , НАЧАЛО ПОИСКА ВО 2-М ПЕРЕКЛЮЧАТЕЛЕ
 УИА ОТП2А(М7)
 ПВ АДМПРА(М13)  ПРОВЕРКА НА ПРАВО "АДМ.СИС."
 ПО ОТНАЙ2  ЭТО АДМИНИСТРАТОР СИСТЕМЫ
 УИА ОТП2Д(М7)  ЭТО ДИР-ВЫ ДЛЯ ДИСПЕТЧЕРА
*
ОТНАЙ2 НОП ,  ПОИСК ВО ВТОРОМ ПЕРЕКЛЮЧАТЕЛЕ
 СЧ (М7)
 ПО ОШИФР  ПАРАМЕТРА НЕТ. "ОШ. В ДИР."
 НТЖ ТЕКДИР
 И МАСДИР
 ПО ОТ3  НАШЛИ. СНАЧАЛА ОБЩИЕ ПРЕДВ. ДЕЙСТВИЯ
 ЦИКЛ ОТНАЙ2(М7)
*
*     ПЕРЕКЛЮЧАТЕЛЬ ПАРАМЕТРОВ ГРУППЫ 2
*
ОТП2А НОП
      ПБ   ОТДЗН   ИЗМЕНЕНИЕ БЮДЖ. ЗОН
      КОНК П'ЗОН'
      ПБ   ОТДНМ   ИЗМЕНЕНИЕ БЮДЖ. НОМЕРОВ
      КОНК П'НОМ'
      ПБ   ОТДГР   ИЗМЕНЕНИЕ БЮДЖ. ГРУПП
      КОНК П'ГРУ'
      ПБ   ОТДКТГ  ИЗМЕНЕНИЕ КАТЕГОРИИ ОТДЕЛА
      КОНК П'КТГ'
      ПБ   ОТДФИО  ИЗМЕНЕНИЕ БЮДЖЕТА ФИО
      КОНК П'ФИО'
*
*  НИЖЕ СЛЕДУЮЩИЕ ПАР-РЫ МОЖЕТ ИЗМЕНЯТЬ
* НЕТОЛЬКО АДМИНИСТРАТОР СИСТЕМЫ, НО И ДИСПЕТЧЕР.
*
ОТП2Д  НОП
      ПБ   ОТДЗД   ИЗМЕНЕНИЕ БЮДЖЕТА ЗАДАЧ
      КОНК П'ЗДЧ'
*
       КОНД В'0'  ПРИЗНАК КОНЦА ПЕРЕКЛЮЧ.
*
*:::::::::::::::::::::::::::::::::::::::::::::
*
ОТ3 НОП
 ПВ НАЙПРО(М15)
 СЧ РЯВС
 ЗП ЗОНРЯ
 ВЧОБ П11
 ПЕ ОТ2
 УИА -4(М5)
 ПВ ЧИСЛА(М13)
 ПВ ИЗ10В8(М13)
 ПБ (М7)    НА ОБРАБОТКУ ПАРАМЕТРА
*
*
ОТДФИО НОП
 СЧ РЯ2
 ЗП БЮФИО
 ПБ ОТ2
*
*
ОТДНМ НОП
 СЧ РЯ2
 ВЧОБ Ф255
 ПО ОТДНМ0
 ПВ УПВС(М13)
 УИА ЧБ255(М1)
 ПВ ВЫВОД(М17)
 ПВ ВВС(М13)
 ПБ ОТ2    НА ОБРАБОТКУ КОНЦА ДИРЕКТИВЫ
ОТДНМ0 СЧ РЯ2
 ЗП КЧЕЛ
 ПБ ОТ2
*
ОТДГР НОП
 СЧ РЯ2
 ВЧОБ Ф30
 ПО ОТДГР0
 ПВ УПВС(М13)
 УИА ГБЗО(М1)
 ПВ ВЫВОД(М17)
 ПВ ВВС(М13)
 ПБ ОТ2      НА ОБРАБОТКУ КОНЦА ДИРЕКТИВЫ
ОТДГР0 СЧ РЯ2
 ЗП БГР
 ПБ ОТ2
*
ОТДЗД НОП
 СЧ РЯ2
 ВЧОБ Е6П1
 ПО ОТДЗД0
 ПВ УПВС(М13)
 УИА ЗДБ63(М1)
 ПВ ВЫВОД(М17)
 ПВ ВВС(М13)
 ПБ ОТ2  НА ОБРАБОТКУ КОНЦА ДИРЕКТИВЫ
ОТДЗД0 СЧ РЯ2
 ЗП КЗАД
 ПБ ОТ2
ОТДЗН СЧ РЯ2
 ЗП КЗОН
 ПБ ОТ2
ОТДСТ НОП
 СЧ Е1
 ПБ ОТДСД
*
ОТДДН НОП
 СЧ
ОТДСД ЗП ОАРХ
 ПВ НАЙПРО(М15)
 ПБ ОТ2
*
*
ОТДКТД НОП ,  КОНТРОЛЛЕР "ДА" (ВКЛЮЧЕН)
 СЧ Е44  ПРИЗНАК КОНТРОЛ. ВКЛЮЧЕН
 ЗП БКТР
 ПВ НАЙПРО(М15)
 ПБ ОТ2
*
ОТДКТН НОП ,  КОНТРОЛЛЕР "НЕТ" (ВЫКЛЮЧЕН)
 СЧ Е44
 НТЖ ВСЕЕД
 ЗП БКТР
 ПВ НАЙПРО(М15)
 ПБ ОТ2  К СЛЕД.ПАРАМЕТРУ
*
ОТДКТГ НОП
 СЧ РЯ2
 ВЧОБ Е3П1
 ПО ОТДКТ0    ВСЕ О'КЕУ
 ПВ УПВС(М13)
 ПВ ДИАКТГ(М13)    ДИАГНОСТИКА: КАТЕГОРИЯ>7
 ПВ ВВС(М13)
 ПБ ОТ2
ОТДКТ0 СЧ РЯ2
 СДА 64-44
 ЗП БКТГ
 ПБ ОТ2
*
*................................
*
ОТДЗКВ НОП ,  ЗАКРЫТЬ АРХИВ ВСЕМ ПОЛЬЗ. ОТДЕЛА
 СЧ ВСЕЕД
 ЗП ПРИЗН1
 ПБ ОТДЗК0
*
*................................
*
ОТДОТВ НОП ,  ОТКРЫТЬ АРХИВ ВСЕМ ПОЛЬЗ. ОТДЕЛА
 СЧ ВСЕЕД
 ЗП ПРИЗН1
 ПБ ОТДОТ0
*
*................................
*
ОТДЗК НОП ,  ЗАКРЫТЬ АРХИВ У ОТДЕЛА
 СЧ
 ЗП ПРИЗН1  ПРИЗНАК ЗАКРЫТИЯ ТОЛЬКО В ОТДЕЛЕ
ОТДЗК0 НОП
 СЧ ВСЕЕД
 ЗП ПРИЗНК    ПРИЗНАК ДИР-ВЫ ЗАКРЫТИЯ
 ПБ ОТК1     НА ПОЛУЧЕНИЕ ШКАЛ
*
*................................
*
ОТДОТ НОП ,  ОТКРЫТЬ АРХИВ ТОЛЬКО У ОТДЕЛА
 СЧ 0
 ЗП ПРИЗН1
ОТДОТ0 НОП
 СЧ 0
 ЗП ПРИЗНК  ПРИЗНАК ДИР-ВЫ ОТКРЫТИЯ
*
ОТК1 НОП
 СЧ
 ЗП ШИТП     ШКАЛА АРХИВОВ
 УИА -23(М3)
ОТК2 ПВ НАЙПРО(М15)
 СЧ РЯВС
 ЗП РЯ2
 ЗП ЗОНРЯ
 ВЧОБ П11
 ПЕ ОШИФР
 ПВ ВС(М13)
 ВЧОБ П11
 ПЕ ОТК33
 СЧ ЗОНРЯ
 СДА 64-4
 ИЛИ РЯВС
 ЗП ЗОНРЯ
 ПВ ИЗ10В8(М13)
 ПВ НАЙПРО(М15)
 СЧ РЯ2
 ВЧОБ Ф24
 ПО ОТК3
 ПВ ОШДИР(М13)  ОШИБКА В ДИРЕКТИВЕ
 ПБ ББ6
ОТК33 НОП
 СЧ РЯВС
 НТЖ ОООООП
 ПЕ ОТК3
 ПВ НАЙПРО(М15)
ОТК3 МОД РЯ2
 СЧ Е24-1    ПРИЗНАК АРХИВА
 ИЛИ ШИТП
 ЗП ШИТП     ШКАЛА АРХИВОВ
ОТК6 НОП
 СЧ РЯВС
 НТЖ ЗПТ
 ПЕ ГРДИР ОБРАБОТКА ГРУППОВОЙ ДИР-ВЫ: ЗАК/ОТК
ОТК4 ЦИКЛ ОТК2(М3)
*
ГРДИР НОП ,  ОБРАБОТКА ГРУПП.ДИР. ЗАКР/ОТКР АРХ.
 СЧ ПРИЗН1
 ПО ОТ2  ЭТО НЕ ГРУППОВАЯ
*
*  ЦИКЛ ПО КЛЮЧАМ. ПОИСК ВСЕХ ПОЛЬЗ. ОТДЕЛА
* И КОРРЕКТИРОВКА ИХ ШКАЛЫ АРХИВОВ
*
 СЧ ШИТП
 СДА 64-24
 ЗП РАР16
 ПВ АРХПОТ(М13)
 ПБ ОТ2
*
*::::::::::::::::::::::::::::::::::::::::::::::::
*
ОТДИСК НОП ,  ИСКЛЮЧ. ВСЕХ ПОЛЬЗОВ. ДАННОГО ОТДЕЛА
 МОД РАБАРХ
 СЧ Е48-1
 ЗП РАР16  ТЕКУЩИЙ АРХИВ В ПОЗ.ВИДЕ
 ЗП ПРИЗНК  .NЕ. 0 - ЗАКР.АРХИВА
 СЧ КОНИСК
 ПВ АРХПОТ(М13)
*
*    ТЕПЕРЬ НАДО ИСКЛЮЧИТЬ ЗАНЯТЫЕ ЗОНЫ
*
 СЧ БОТ1
 СДА 64-16
 ЗП РАР16
 ПВ ХВАТЬ(М13)
 ПВ ЧКТАРХ(М16)
 МОД ДЛНАРХ
 УИА -1(М13)  ДЛИНА АРХИВА - 1
*
ОТДИС0 НОП
 СЧ ПОЛКТ2(М13)
 НТЖ РАР16
 И Е24П17
 ПЕ ОТДИС1  ЗОНА ДРУГОГО ОТДЕЛА
 ЗП ПОЛКТ2(М13)
 ЗП ПОЛКТ1(М13)
*
ОТДИС1 НОП
 ПИО ОТДИС2(М13)
 СЛИА -1(М13)
 ПБ ОТДИС0  НА СЛЕД. ЗОНУ
*
ОТДИС2 НОП
 ПВ ЗКТАРХ(М16)
 ПВ ОСВОБ(М13)
 ПВ НАЙПРО(М15)
 ПБ ОТ2  ОБРАБОТКА СЛЕД. ПАРАМЕТРА ДИР-ВЫ "ОТД"
*
ОТДНУЛ НОП ,  ОБНУЛИ ВСЕ БЮДЖЕТЫ ОТДЕЛА
 СЧ 0
 ЗП НУЛОТД
 ПВ НАЙПРО(М15)
 ПБ ОТ2  ОБРАБОТКА СЛЕД. ПАРАМЕТРА ДИР-ВЫ "ОТД"
*
*++++++++++++++++++++++++++++++++++++++++++++++++
*
ПРЗИСК ПАМ 1
*
АРХПОТ НОП ,  ОТКР./ЗАКР. АРХИВА ДЛЯ ВСЕХ ПОЛЬЗ.
*             ОТДЕЛА
* СМ    - = КОНИСК - ПРИЗНАК ИСКЛЮЧАТЬ ИЗ КАТПОЛ, ЕСЛИ
*           НЕТ ОТКРЫТОГО АРХИВА
* М13   - АДРЕС ВОЗВРАТА;
* БОТ1  - НОМЕР ОТДЕЛА (2-10);
* РАР16 - ШКАЛА АРХИВОВ (Р:48-25);
* ПРИЗНК - 0 - ОТКР./ .NЕ. 0 - ЗАКР.
*
 ЗП ПРЗИСК
 СЧИ М13
 ЗП РАР13
*
 СЧ БОТ1
 СДА 64-16
 ЗП РЯ2
*
 ПВ ЗАХВАТ(М13)
 УИА -3(М13)
АРХП0 НОП
 УИА -1024(М15)
 Э70 ЧТКЛЮ1+3(М13)
*
АРХП1 НОП
 СЛИА 2(М15)
 СЧ КЛЮЧИ+1022(М15)
 НТЖ ВСЕЕД
 ПЕ АРХП2 РАЗБОР КЛЮЧА
 Э70 ЗПКЛЮ1+3(М13)
 ПБ АРХПГ  РАБОТА С ГРУППАМИ
*
АРХП2 НОП ,  СМОТРИМ ЧТО ЗА КЛЮЧ
 СЧ КЛЮЧИ+1022(М15)
 НТЖ РЯ2
 И Е24П17
 ПЕ АРХП3  ПОЛЬЗ. ИЗ ДРУГОГО ОТДЕЛА
*
* КОРРЕКТИРУЕМ  ШКАЛУ АРХИВОВ
*
 СЧ КЛЮЧИ+1022(М15)
 ИЛИ РАР16
 ЗП КЛЮЧИ+1022(М15)  ОТКРЫЛИ АРХИВЫ
*
 СЧ ПРИЗНК
 ПО АРХП3  ЗАДАНО ОТКРЫТИЕ
 СЧ КЛЮЧИ+1022(М15)
 НТЖ РАР16
 ЗП КЛЮЧИ+1022(М15)  ЗАКРЫЛИ АРХИВЫ

 И Е48П25
 ПЕ АРХП3  ЕСТЬ ОТКРЫТЫЙ АРХИВ
 СЧ ПРЗИСК
 НТЖ КОНИСК
 ПЕ АРХП3  ИСКЛЮЧАТЬ НЕ НАДО
 ЗП КЛЮЧИ+1022(М15)
 ЗП КЛЮЧИ+1023(М15)
 СЧ Е1
 ЗП УПРКЛЮ
*
АРХП3 НОП
 ПИНО АРХП1(М15)  СЛЕД. ПОЛЬЗ.
 Э70 ЗПКЛЮ1+3(М13)
 ЦИКЛ АРХП0(М13)
*
АРХПГ НОП ,  ИЩЕМ ГРУППЫ ДАННОГО ОТДЕЛА
*
 СЧ БОТ1
 СДА 64-8
 ЗП РЯ2
 ЗП РАР17  ПРИЗНАК ГРУПП У ОТДЕЛА НЕ НАШЛИ
*
 СЧ РАР16
 СДА 64+24
 ЗП РЯ3  ШК. АРХИВА В ЛЕВОЙ ПОЛОВИНЕ
*
 УИА -99(М13)
 Э70 ЧТСЛЗО
*
АРХПГ0 НОП
 СЧ БЮДГР1+99(М13)
 ПО АРХПГ1  НЕТ ГРУППЫ
 НТЖ РЯ2
 И Е16П9
 ПЕ АРХПГ1  ДР. ОТДЕЛ
 ЗП РАР17
*
 СЧ БЮДГР2+99(М13)
 ИЛИ РЯ3
 ЗП БЮДГР2+99(М13)
 СЧ ПРИЗНК
 ПО АРХПГ1  ОТКРЫТИЕ
*
 СЧ БЮДГР2+99(М13)
 НТЖ РЯ3
 ЗП БЮДГР2+99(М13)
*
АРХПГ1 НОП
 ЦИКЛ АРХПГ0(М13)
 СЧ РАР17
 ПЕ RАРХП  ГРУПП НЕ БЫЛО. НЕЧЕГО ПИСАТЬ.
 Э70 ЗПСЛЗО
*
RАРХП НОП
 ПВ ОСВОБ(М13)
 МОД РАР13
 ПБ ,  ВОЗВРАТ
*
*     ЗАНЕСЕНИЕ ПАРАМЕТРОВ
*
ОТДЗПП НОП ,  ЗАПИСЬ СКОРРЕКТИРОВАННЫХ ПАРАМЕТРОВ
*
*  СНАЧАЛА ОБРАБАТЫВАЕМ ЗОНЫ
*
 СЧ КЗОН
 НТЖ ВСЕЕД
 ПО ОБРНУЛ   НЕТ ИЗМЕНЕНИЯ
 СЧ БОТ1   ГОТОВИМ ПАРАМЕТРЫ
 ЗП РЯ6
*
 ПВ ХВАТЬ(М13)
 ПВ ЧКТАРХ(М16)
 ПВ НСОТ(М13)
*
 СЧ КЗОН   ЗОНЫ В 8-ИЧН
 ВЧ РЯ8   РАСПРЕД.ЗОНЫ
 ВЧ РЯ7   СВОБОДНЫЕ ЗОНЫ ОТДЕЛА
 ПО ОТЗ1  ЗОНЫ НЕ МЕНЬШЕ РАСПРЕД
 ЗП РЯ1
 СЧ РЯ2
 ВЧ РЯ8  РАСПРЕДЕЛЕНО
 ПО ОТ71
 ПВ МАЛБЮД(М13)
 ПВ ОСВОБ(М13)
 ПБ ОБРНУЛ
ОТ71 НОП
 СЧ РЯ5      ПРИЗНАК
 ЗП РЯ6
 СЧ РЯ1
 ЗП РЯ7
 ПВ ЗОННЕТ(М13)
 ПВ ЗКТАРХ(М16)
 ПВ ОСВОБ(М13)
 ПБ ОБРНУЛ
ОТЗ1 ЗП РЯ1  ЗОН > РАСПРЕД.
 ВЧОБ РЯ11    СВОБОДНЫЕ ЗОНЫ АРХИВА
 ПО ОТЗ2
 ПВ НЕТБДЖ(М13)
 ПВ ОСВОБ(М13)
 ПБ ОБРНУЛ
ОТЗ2 СЧ РЯ1
 ЗП РЯ7
 СЧ
 ЗП РЯ6
 ПВ ЗОНДАЙ(М13)
 ПВ ЗКТАРХ(М16)
 ПВ ОСВОБ(М13)
*
ОБРНУЛ НОП ,  ОБРАБОТКА "НУЛЬ ОТДЕЛУ"

 ПВ ЗАХВАТ(М13)
 Э70 ЧТСЛЗО
*
 СЧ НУЛОТД  0 - ПРИЗНАК "ОБНУЛИ ВСЕ БЮДЖЕТЫ ОТДЕЛА"
 ПЕ ОБРЛЮД  НА ОБРАБОТКУ ЛЮДЕЙ
*
 МОД НОМОТД
 ЗП  БЮДОТ1
 МОД НОМОТД
 ЗП  БЮДОТ2
*
ОБРЛЮД НОП ОБРАБОТКА ЛЮДЕЙ
 СЧ КЧЕЛ
 НТЖ ВСЕЕД
 ПО ОТГ4     НА ГРУППУ
 СЧ БОТ1
 ЗП РЯ5
 СЧ RСДВИГ
 ЗП РЯ4
 ПВ ЛЮДИ(М13)
 МОД НОМОТД
 СЧ БЮДОТ1
 И Е16П9
 СДА 64+8
 ВЧОБ КЧЕЛ
 ПО ОТ10    НОВ  > СТАР
 СЧ БОТ1
 ЗП РЯ4
 ПВ ЛЮБГР(М13)  ПОДСЧЕТ НОМЕРОВ РАСПР-Х ГРУППАМ ОТДЕЛ
 СЧ РЯ6
 СЛЦ РЯ5
 ВЧОБ КЧЕЛ
 ПО ОТ11
 ПВ МАЛБЮД(М13)
 ПБ ОТГ4     НА ГРУППУ
ОТ10 ЗП РЯ1
 СЧ РЯ10   ВСЕГО ПОЛЬЗОВАТЕЛЕЙ КРАБ'А
 СЛЦ РЯ1
 ВЧОБ Ф2047
 ПО ОТ11
 ПВ НЕТБДЖ(М13)
 ПБ ОТГ4
ОТ11 СЧ КЧЕЛ
 СДА 64-8
 ЗП КЧЕЛ
 СЧ ВСЕЕД
 НТЖ Е16П9
 МОД НОМОТД
 И БЮДОТ1
 ИЛИ КЧЕЛ
 МОД НОМОТД
 ЗП БЮДОТ1
ОТГ4 НОП ЧИСЛО ГРУПП В ОТДЕЛЕ(РЕАЛИЗАЦИЯ)
 СЧ БГР
 НТЖ ВСЕЕД
 ПО ОТ13
 СЧ БОТ1
 ЗП ЗОНРЯ
 ПВ ИЗ10В8(М13)
 СЧ Е21П17
 НТЖ ВСЕЕД
 ЗП ШИТП+1     Н21П17
 СЧ БОТ1
 ЗП РЯ4
 ПВ ЛЮБГР(М13)
 МОД НОМОТД
 СЧ БЮДОТ1
 ЗП РЯ1   БЮДЖЕТ ОТДЕЛА
 И Е21П17
 СДА 64+16
 ВЧОБ БГР
 ПО ОТГ40    НОВ > СТАР
 СЧ РЯ12     СКОЛЬКО ГРУПП ИМЕЕТ ОТДЕЛ РЕАЛЬНО
 ВЧОБ БГР
 ПО ОТГ41  ВСЕ НОРМАЛЬНО
 ПВ МАЛБЮД(М13)
 ПБ ОТ13   НА ОБРАБ.ЗАДАЧ
ОТГ41 СЧ БГР
 СДА 64-16
 ЗП БГР
 СЧ РЯ1
 И ШИТП+1      Н21П17
 ИЛИ БГР
 МОД НОМОТД
 ЗП БЮДОТ1
 ПБ ОТ13   НА ОБРАБ.ЗАДАЧ
ОТГ40 СЛЦ РЯ9
 ВЧОБ Ф99
 ПО ОТГ41
 ПВ НЕТБДЖ(М13)
 ПБ ОТ13   НА ОБРАБ.ЗАДАЧ
ОТ13 НОП  РАБОТА С ЗАДАЧАМИ
 СЧ КЗАД
 НТЖ ВСЕЕД
 ПО ОТ15  ОБРАБОТКА РЕЖИМА БЮДЖЕТИРОВАНИЯ
 СЧ КЗАД
 СДА 64-37
 ЗП КЗАД
 СЧ Е43П38
 НТЖ ВСЕЕД
 ЗП РЯ9
 МОД НОМОТД
 СЧ БЮДОТ1
 И РЯ9
 ИЛИ КЗАД
 МОД НОМОТД
 ЗП БЮДОТ1
ОТ15 НОП
 СЧ ОАРХ
 НТЖ ВСЕЕД
 ПО ОТ16   НА ОБРАБОТКУ КАТЕГОРИИ ОТДЕЛА
 СЧ ОАРХ
 ПЕ ОТ151
 МОД НОМОТД
 СЧ БЮДОТ1
 ИЛИ Е48
 МОД НОМОТД
 ЗП БЮДОТ1
 ПБ ОТ16   НА ОБРАБОТКУ КАТЕГОРИИ ОТДЕЛА
ОТ151 НОП  СТАТИЧЕСКИЙ
 МОД НОМОТД
 СЧ БЮДОТ1
 ИЛИ Е48
 НТЖ Е48
 МОД НОМОТД
 ЗП БЮДОТ1
ОТ16 НОП
 СЧ БКТГ
 НТЖ ВСЕЕД
 ПО ОТ14    НА ОБРАБОТКУ АРХИВОВ
 СЧ Е47П45
 НТЖ ВСЕЕД
 МОД НОМОТД
 И БЮДОТ1
 ИЛИ БКТГ
 МОД НОМОТД
 ЗП БЮДОТ1
 СЧ БОТ1
 ЗП РЯ5   ПАРАМЕТРЫ ДЛЯ П.П. ГЛАДИМ
 СЧ RСДВИГ
 ЗП РЯ4    СДВИГ ДЛЯ ОТДЕЛА
 СЧ БКТГ
 СДА 64+44
 ЗП РЯ6
 ПВ ГЛАДИМ(М13)     КАТЕГОРИИ ПОЛЬЗОВАТЕЛЕЙ ОТДЕЛА
 СЧ Е47П45
 НТЖ ВСЕЕД
 ЗП РЯ8   ==> Н47П45
 УИА -99(М1)    ГЛАДИМ КАТЕГОРИИ ГРУПП
ОТ160 СЧ БЮДГР1+99(М1)
 НТЖ ВСЕЕД
 ПО ОТ14    НА ОБРАБОТКУ АРХИВОВ
 СЧ БЮДГР1+99(М1)
 И Е16П1
 СДА 64+8
 НТЖ БОТ1   НОМЕР ОТДЕЛА В Д.Д.
 ПЕ ОТ161   НА КОНЕЦ ЦИКЛА
 СЧ БЮДГР1+99(М1)
 И Е47П45
 СДА 64+44
 ВЧОБ РЯ6
 ПО ОТ161   НА КОНЕЦ ЦИКЛА
 СЧ БЮДГР1+99(М1)
 И РЯ8     <--->   Н47П45   И П.П. ГЛАДИМ(М13)
 ИЛИ БКТГ
 ЗП БЮДГР1+99(М1)
ОТ161 ЦИКЛ ОТ160(М1)
*
*
ОТ14 НОП    ОБРАБОТКА АРХИВОВ
 СЧ ШИТП
 НТЖ ВСЕЕД
 ПО ОТ18  НА ПРОВЕРКУ КОНТРОЛЛЕРА
 СЧ ШИТП  ШКАЛА АРХИВОВ
 ПО ОТ18  НА ПРОВЕРКУ КОНТРОЛЛЕРА
 СЧ ПРИЗНК
 ПЕ ЗАКОТ     ДИРЕКТИВА ЗАКРЫТИЯ АРХИВА ОТДЕЛА
 МОД НОМОТД    НОМЕР ОТДЕЛА В 8-ИЧН
 СЧ БЮДОТ2
 ИЛИ ШИТП    ШКАЛА АРХИВОВ
 МОД НОМОТД
 ЗП БЮДОТ2
 ПБ ОТ18  НА ПРОВЕРКУ КОНТРОЛЛЕРА
ЗАКОТ НОП
 СЧ ШИТП     ШКАЛА АРХИВОВ
 НТЖ ВСЕЕД
 МОД НОМОТД
 И БЮДОТ2
 МОД НОМОТД
 ЗП БЮДОТ2
ОТ18 НОП  КОНТРОЛЛЕР
 СЧ БКТР
 ПО ОТ17  КОНТРОЛЛЕР ВЫКЛЮЧЕН. НА ЗАПИСЬ ЗОНЫ
 СЧ БКТР
 НТЖ Е44
 ПО ОТ1848  ВКЛЮЧИТЬ КОНТРОЛЛЕР
 МОД НОМОТД
 СЧ БЮДОТ1
 И БКТР
 МОД НОМОТД
 ЗП БЮДОТ1
 ПБ ОТ17  ЗАПИСЬ ЗОНЫ
ОТ1848 НОП
 МОД НОМОТД
 СЧ БЮДОТ1
 ИЛИ БКТР
 МОД НОМОТД
 ЗП БЮДОТ1
 ПБ ОТ17  НА ЗАПИСЬ ЗОНЫ
*
*
ОТ17 НОП  ЗАПИСЬ СЛЗО
 Э70 ЗПСЛЗО
 ПВ ОСВОБ(М13)
*
 СЧ РАБФИО
 ПО ОТ19  С ФИО РАБОТА ЗАПРЕЩЕНА
 СЧ БЮФИО
 НТЖ ВСЕЕД
 ПО ОТ19  ПАРАМЕТР НЕ ИЗМЕНЯЛСЯ
 ПВ ЗАХ253(М13)
 ПВ ФИОЧИС(М13)
 СЧИ М3
 ЗП РЯ3  ЧИСЛО ПОЛЬЗОВАТЕЛЕЙ ОТДЕЛА В КАТАЛОГЕ  Ф И О
 ВЧОБ БЮФИО
 ПО ОТФИО1   НОВ.GЕ.РАСП
 ПВ МАЛБЮД(М13)
 ПВ ОСВОБ(М13)
 ПБ НАЧБЮД
ОТФИО1 НОП
 СЧИ М4
 СЛЦ РЯ3
 ВЧ БЮФИО
 ПО ОТФИО2     НА ЗАПИСЬ НОВОГО БЮДЖЕТА
 ПВ ОСВОБ(М13)
 ПВ НЕТБДЖ(М13)
 ПБ НАЧБЮД
ОТФИО2 НОП
 ПВ ОСВОБ(М13)
 ПВ ЗАХВАТ(М13)
 Э70 ЧТСЛЗО
 МОД НОМОТД
 СЧ БЮДОТ1
 ИЛИ Е8П1
 НТЖ Е8П1
 ИЛИ БЮФИО
 МОД НОМОТД
 ЗП БЮДОТ1
 Э70 ЗПСЛЗО
 ПВ ОСВОБ(М13)
ОТ19 НОП
 ПБ НАЧБЮД
*
*
МАЛО УИА НЕЛЬЗЯ(М1)
 ПВ ВЫВОД(М17)
 ПВ ОСВОБ(М13)
 ПБ НАЧБЮД
*
*$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
*
НОМ    НОП ,  БЛОК ВЫЗОВА НА РАБОЧЕЕ ПОЛЕ БЮДЖЕТА
*             ПОЛЬЗОВАТЕЛЯ
*
 ПВ ПОДГОТ(М13)
 ПВ ШИФНОВ(М15)
 ПО ОШИФР
*
НОМНОВ ПВ ПОЛОТД(М17)  ВХОД ИЗ ДИР-ВЫ "НОВ"
 ПВ ПОЛЬЗ(М13)
 ПВ ПОЛКЛЮ(М13)
 СЧ РЯДУР
 ПО НЕТПОЛ
 МОД РАБАРХ
 СЧ Е48-1
 И БЮДЖП+1
 ПЕ ББ1     БЮДЖЕТ ПОЛЬЗОВАТЕЛЯ ВЫЗВАН
 СЧ НОМАРХ
 ЗП МАСПР
 СЧ БЮДЖП+1
 И Е48П25
 ЗП РЯ10
НОМ00 НОП
 ПО НЕТВАР
 НЕД
 ЗП РАБАРХ
 СЧ РЯ10
 МОД РАБАРХ
 НТЖ Е48-1
 ЗП РЯ10
 ПВ ДИСОТК(М13)
 ПВ ДИСКИ(М14)
 ПЕ АРТАРН     ВЫВОД "АРХ 1 ===> АРХ 2"
 СЧ АРХИВ
 ЗП РАБАРХ
 ПВ ДИСКИ(М14)
 ПО КОНЕЦ    НЕТ АРХИВНЫХ ДИСКОВ
 СЧ РЯ10
 ПБ НОМ00
*
АРТАРН НОП
 СЧ РАБАРХ
 ПВ ПЕРЕВ(М13)
 ЗП РЯ1
 СЧ НОМАРХ
 И Н16П1
 ИЛИ РЯ1
 ЗП МАСПР+2
 СЧ СТРЕЛК
 ЗП МАСПР+1
 СЧ ВСЕЕД
 ЗП МАСПР+3
*
 ПВ НАСАРХ(М17)  НАСТРОЙКА НА АРХИВ
 УИА МАСПР(М1)
 ПВ ВЫВОД(М17)
 ПБ ББ1
*
*$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
*
СИС СЧ ФЛОТД
 ПЕ ОШИФР
 СЧ Е8П1
 ЗП ШФР
 ПВ ПОЛКЛЮ(М13)
 ПБ ББ1
*
******************************************************
*
ВЫНЕГЕ УИА НЕТГЕН(М1) НЕТ ПАР.ГЕН.
 ПВ ВЫВОД(М17)
 ПБ КОНЕЦ
*
******************************************************
*
*      РАБОТА  С  Д И С К А М И
*
*
ДИСКИ СЧ РАБАРХ
 УИ М11
 СЧ ТАРМДК-1(М11)
 ЗП РЯ1
 И Е43П38
 ЗП НАПР
 СДА 64-14
 ЗП РЯ2
 СДА 64+42
 ЗП РЯ4
 СЧ РЯ1
 И Е16П1
 ИЛИ КЗАКБ
 Э50 '131'
 ПО ДИСПАР   ЗАКАЗ ПРОШЕЛ,НА ПОДТВЕРЖДЕНИЕ ПАРОЛЕЙ
 НТЖ Е1
 ПЕ КОНЕЦ
ЗАПРОС НОП
 СЧ АРХИВ
 ЗП РАБАРХ
 СЧ 0     ПРИЗНАК - ЗАКАЗ НЕ ПРОШЕЛ
 ПБ (М14)
ДИСПАР СЧ РЯ1
 СДА 64+16
 И Е12П1
 ИЛИ К67НАП
 ЗП РЯ1
 СЧ ПАРМД-1(М11)
 СДА 64+36
 И Е12П1
 ИЛИ Е15
 УИ М15
 СЧ ТАРМДК-1(М11)
 И Е16П1
 ЗП РЯ4
 Э50 '121'
 СЧ ПАРМД-1(М11)
 СДА 64+24
 И Е12П1
 УИ М15
 СЧ РЯ4
 Э50 '121'
 СЧ К67НАП
 Э50 '156'
 ПО Э50112  ДИСК 7.25
*
 СЧ ТАРМДК-1(М11)
 СДА 64+16
 И Е17П1
 СДА 64+5
 ИЛИ К67НАП
 ЗП РЯ1
Э50112 СЧ РЯ1
 Э50 '112'
 СЧ РАБАРХ
 ПВ ПЕРЕВ(М13)
 ЗП РЯ1
 СЧ НОМАРХ
 И Н16П1
 ИЛИ РЯ1
 ЗП НОМАРХ
 ПБ (М14)
*
******************************************************
*
НЕТДИС НОП
*
*    ВЫВОД ДИАГНОСТИКИ НЕТ МД ...
*
 СЧ РЯ2
 ЗП ФАМ+1
 СЧ РЯ1
 ЗП ФАМ
 И Е16П1
 ЗП ЗОНРЯ
 ПВ ИЗ10В8(М13)
 СЧ РЯ2
 ПВ ПЕРЕВ(М13)
 ИЛИ НМД1
 ЗП НЕТМД+1
 УИА НЕТМД(М1)
 ПВ ВЫВОД(М17)
 ПБ (М14)
*
*
К67НАП КОНД В'670000'
*
******************************************************
*
*   ОТКАЗ  ОТ  МД ПО НАПРАВЛЕНИЮЮ 67
*
*
ДИСОТК Э72 КОНОТК   ОТКАЗ ОТ МД.
 ПБ (М13)
*
******************************************************
*
ФИОЧИС НОП
*
*
*    ПОДСЧЕТ ЧИСЛА ПОЛЬЗОВАТЕЛЕЙ ОТДЕЛА,
*    ЗАРЕГЕСТРИРОВАННЫХ В ОС Д И С П А К
*    И РЕЗЕРВА КАТАЛОГА  Ф И О
*
*
*     ВХОДНАЯ ИНФОРМАЦИЯ:
*           НОМОДД  -  НОМЕР ОТДЕЛА В 2-10
*     ВЫХОДНАЯ ИНФОРМАЦИЯ:
*           М3  -  КОЛИЧЕСТВО ПОЛЬЗОВА
*           М4  -  РЕЗЕРВ В КАТАЛОГЕ  Ф И О
*     ИСПОЛЬЗУЮТСЯ РЕГИСТРЫ:
*           М3,М4 -  РЕЗУЛЬТАТ
*           М13  -  АДРЕС ВОЗВРАТА
*           М1,М2  -  РАБОЧИЕ
*
 СЧ РАБФИО
 ПО (М13)  ЗАПРЕТ РАБОТЫ С КАТ.ФИО
*
 УИА (М3)
 УИА (М4)
 СЧ НОМОДД
 ПО (М13)
 УИА -2(М1)
ФИОЧ1 НОП
 УИА -1023(М2)
 Э70 ЧТФИО+2(М1)
ФИОЧ2 НОП
 СЧ ПОЛФИО+1024(М2)
 РЗБ Е16П1
 ПЕ ФИОЧ4
 СЛИА 1(М4)   СЧЕТЧИК СВОБОДНЫХ ФИО
ФИОЧ4 НОП
 СЧ ПОЛФИО+1024(М2)
 РЗБ Е8П1
 НТЖ НОМОДД
 ПЕ ФИОЧ3
 СЛИА 1(М3)  СЧЕТЧИК ПОЛЬЗОВАТЕЛЕЙ ОТДЕЛА
ФИОЧ3 СЛИА 1(М2)
 ЦИКЛ ФИОЧ2(М2)
 ЦИКЛ ФИОЧ1(М1)
 ПВ ЧКТАРХ(М16)
 ПБ (М13)
*
******************************************************
*
ФИОИЗМ НОП
*
*
*    ИЗМЕНЕНИЕ  Ф.И.О. ПОЛЬЗОВАТЕЛЯ
*
*
 ПВ ПОДГОТ(М13)
 СЧ РАБФИО
 ПЕ РАЗФИО  РАБОТА С КАТ.ФИО РАЗРЕШЕНА
 УИА НЕТФИО(М1)
 ПВ ВЫВОД(М17)
 ПБ НАЧБЮД
*
РАЗФИО НОП
 ПВ ПРАВО2(М13)
 ПВ ПДГФИО(М16)    ПОДГОТ. ТЕКСТА ФИО В КАНОНИЧ. ВИДЕ
 ПО ББ1    БЫЛ ОКТАЗ ОТ ИЗМЕНЕНИЯ ФИО
 СЧ БЮДЖП+1
 ПО НЕТРП   НЕТ РАБОЧЕГО ПОЛЯ
 И Е24П1
 СДА 64+8
 ЗП ШФР   ШИФР ПОЛЬЗОВАТЕЛЯ В СМЫСЛЕ ОС
 ПВ ЗАХ253(М13)
 УИА -2(М1)
ФИО01 НОП
 Э70 ЧТФИО+2(М1)
 УИА -1023(М2)
ФИО02 СЧ ПОЛФИО+1024(М2)
 РЗБ Е16П1
 НТЖ ШФР
 ПЕ ФИО03
 СЧ ФАМ2
 ЗП ПОЛФИО+1023(М2)
 СЧ ПОЛФИО+1024(М2)
 ИЛИ Е24П1
 НТЖ Е24П1
 ИЛИ ФАМ2+1
 ЗП ПОЛФИО+1024(М2)
 Э70 ЗПФИО+2(М1)
 ПВ ОСВОБ(М13)
 ПВ ЧКТАРХ(М16)
 ПБ ББ1
ФИО03 СЛИА 1(М2)
 ЦИКЛ ФИО02(М2)
 ЦИКЛ ФИО01(М1)
 ПБ КОНЕЦ
*
*$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
*
ЗОН    НОП ,  БЛОК ВЫДЕЛЕНИЯ  З О Н   ДЛЯ  ГРУПП
*             И ПОЛЬЗОВАТЕЛЕЙ
 СЧ ГРУППА
 ПЕ ЗОНГР
 СЧ БЮДЖП+1
 ПО НЕТРП    НЕТ РАБОЧЕГО ПОЛЯ
*
 СЧ
 ЗП ЗОНРЯ
 СЧ ТСИС
 ЗП БОТ1
*
 ПВ ПОДГОТ(М13)
 ПВ НАЙПРО(М15)
 ПВ ПРОЧИС(М13)
 СЧ РЯВС
 ЗП ЗОНРЯ
 УИА -3(М5)
 ПВ ЧИСЛА(М13)
 ПВ ИЗ10В8(М13)
 СЧ РЯ2
 ЗП БОБ  ЗОН  В 8-ИЧН
 СЧ БЮДЖП+1
 И Е24П1
 ЗП ШФР
 НТЖ Е8П1
 ПО ЗОНСИС   ЗОНЫ СИСТЕМНОГО ВХОДА
 СЧ ШФР
 ЗП РЯ5
 СДА 64+16  ГОТОВИМ
 ЗП РЯ6   ПАРАМЕТРЫ
*
 ПВ ХВАТЬ(М13)
 ПВ ЧКТАРХ(М16)  ПРОЧИТАЛИ КАТАЛОГИ
*
 ПВ НСПОЛ(М13)
 СЧ РЯ7   СТАР.БЮД
 ВЧОБ БОБ   НОВ.БЮД
 ПЕ ЗН13 НОВ<СТАР
 ЗП РЯ1
 СЧ БЮДЖП+1
 И Е8П1
 ПО ЗОН2   ПОЛЬЗОВАТЕЛЬ НЕ ГРУППОВОЙ
 ПВ ПРАВО3(М13)
 СЧ РЯ5
 И Н377
 ИЛИ П376
 ЗП РЯ5   ФОРМИРУЕМ
 СДА 64+8 ПАРАМЕТРЫ
 ЗП РЯ6
 ПВ НСГР(М13)
 СЧ РЯ7   СВОБОДН.ЗОНЫ В ГРРУП
 ВЧ РЯ1   ИЗМЕНЕНИЕ ЗОН
 ПЕ НБЮД
 СЧ РЯ5    ГОТОВИМ ПАРАМЕТРЫ ДЛЯ ОБРАБ.ЗОН
 ЗП РЯ6
 СЧ ШФР
 ЗП РЯ5
 ПБ ЗН3
ЗОН2 ПВ ПРАВО1(М13)
 СЧ РЯ5
 И Н16П1
 ИЛИ КОНОТД     '377''376'
 ЗП РЯ5     ГОТОВИМ ВХОД.ПАРАМЕТР
 СДА 64+16
 ЗП РЯ6
 ПВ НСОТ(М13)       ПОЛУЧИМ ЗОНЫ ОТДЕЛА
 СЧ РЯ5
 ЗП РЯ6    ПРИЗН.СВОБ.ЗОНЫ
 СЧ ШФР
 ЗП РЯ5
 СЧ РЯ7
 ВЧ РЯ1
 ПО ЗН3
НБЮД УИА НЕТБЮД(М1)
 ПВ ВЫВОД(М17)
 ПВ ОСВОБ(М13)
 ПБ ББ6
ЗН13 ЗП РЯ1
 СЧ РЯ8
 ВЧОБ БОБ
 ПЕ МАЛЗОН
 СЧ ШФР
 ЗП РЯ6
 ПВ ПРАВО3(М13)
ЗН3 УИА ЗАПЗОН(М13)
 СЧ РЯ1
 ЗП РЯ7
 СЧ РЯ1
 ВЧ
 ПО ЗОНДАЙ
 ПБ ЗОННЕТ
*
******************************************************
*
ЗАПЗОН НОП
 ПВ ЗКТАРХ(М16)
 ПВ ОСВОБ(М13)
 ПБ ББ1
*
******************************************************
*
ЗОНСИС НОП ОБРАБОТКА БЮДЖЕТА(ЗОНЫ) СИС.ВХОДА
 ПВ ПРАВО0(М13)
 СЧ Е8П1
 ЗП РЯ5
 ПВ ХВАТЬ(М13)
 ПВ ЧКТАРХ(М16)
 ПВ НСПОЛ(М13)
 СЧ РЯ7      СТАРЫЙ БЮДЖЕТ
 ВЧОБ БОБ   НОВЫЙ БЮДЖЕТ
 ПЕ ЗСИС1      НОВ<СТАР.
 ЗП РЯ1
 ПВ НСОТ(М13)      ПОЛУЧИМ СВОБОДНЫЕ ЗОНЫ АРХИВА(РЯ11)
 СЧ РЯ11
 ВЧ РЯ1
 ПЕ НБЮД
ЗСИС2 УИА ЗАПЗОН(М13)
 СЧ РЯ1
 ЗП РЯ7
 СЧ Е8П1
 ЗП РЯ6
 ЗП РЯ5
 СЧ РЯ1
 ВЧ
 ПЕ ЗОННЕТ
 СЧ
 ЗП РЯ6
 ПБ ЗОНДАЙ
ЗСИС1 ЗП РЯ1  ИЗМЕНЕНИЕ ЧИСЛА ЗОН
 СЧ РЯ8    ЗАНЯТО ФАЙЛАМИ
 ВЧОБ БОБ
 ПЕ МАЛЗОН
 ПБ ЗСИС2
МАЛЗОН УИА МАЛЗН(М1)
 ПВ ВЫВОД(М17)
 ПВ ОСВОБ(М13)
 ПБ ББ1
ОШЗОН ПВ ОШВД(М13)
 ПБ ББ1
*
*$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
*
ИТГ    НОП ,  ИТОГОВЫЕ ДАННЫЕ ПО ТЕКУЩЕМУ АРХИВУ
*
 УИА LF2(М1)
 ПВ ВЫВОДА(М17)  ДВЕ ПРОТЯЖКИ НА АЦПУ
*
 ПВ ДАЙИТГ(М13)  ВЫДАЧА ИТОГОВЫХ ДАННЫХ
 УИА ИТГВЫВ(М1)
 ПВ ВЫВОД(М17)
*
 УИА LF2(М1)
 ПВ ВЫВОДА(М17)  ДВЕ ПРОТЯЖКИ НА АЦПУ
 ПБ НАЧБЮД
*
*
ДАЙИТГ НОП ,  П/П ВЫДЫЧИ ИТОГОВЫХ ДАННЫХ
 СЧИ М13
 ПБ ДАЙИТГ+2
 ПАМ 1  ДАЙИТГ+1 : ДЛЯ АДРЕСА ВОЗВРАТА
*
 МОДА ДАЙИТГ+1
 ЗП ,  ЗАПИСАЛИ АДРЕС ВОЗВРАТА
*
 ПВ ЛЮДИ(М13)
 СЧ РЯ10   ЧИСЛО ПОЛЬЗОВАТЕЛЕЙ СИСТЕМЫ
 ПВ ПЕРЕВ(М13)
 СДА 64-8
 ИЛИ ПООООП
 ЗП РЯ19
 СЧ РЯ17   ЧИСЛО ПОЛЬЗОВАТЕЛЕЙ ТЕК. АРХИВА
 ПВ ПЕРЕВ(М13)
 СДА 64-8
 ИЛИ ПООООП
 ЗП РЯ20
 ПВ НСОТ(М13)
 СЧ ДЛНАРХ  ЧИСЛО ЗОН ТЕК. АРХИВА
 ПВ ПЕРЕВ(М13)
 СДА 64-8
 ИЛИ ПООООП
 ЗП РЯ21
 СЧ РЯ11   ЧИСЛО СВОБОДНЫХ ЗОН ТЕК. АРХИВА
 ПВ ПЕРЕВ(М13)
 СДА 64-8
 ИЛИ ПООООП
 ЗП РЯ22
*
 МОД ДАЙИТГ+1
 ПБ ,  ВОЗВРАТ
*
*

 STATSUM:,NAME,
C
C  CБOP CTATИCTИKИ ПO
C  OБЩИM БИБЛИOTEKAM B
C  MOHИTOPHOЙ CИCTEME
C    "ДYБHA"
C
 NLIB:,LC,1.
  ,WTC,P
  9,VTM,MESS+1
  9,ATX,
  13,UZA,
  ,XTA,=:4
  ,WTC,NLIB
  ,ASN,64-1
  ,AAX,SUMSCAL
  13,UZA,.БЛOKИPOBKA
  ,AOX,SCAL
  ,ATX,SCAL
  ,ITA,13
  ,XTS,MESS
  ,U1A,NF
  ,FUN,70100B+117B
  ,ASN,64-36
  ,AUX,=6H'''7''7''7''7'''
  ,AOX,=6HL0000:
  ,ATX,MESS
C
C...   ФOPMAT COOБЩEHИЯ :
C   L<HOMEP PASS>:<CПИCOK ИMEH>
C      B KAЖДOM COOБЩEHИИ - HE БOЛEE
C      5 ИMEH.    HAПPИMEP :
C   L0120:FUMILI  -01 FCCLOS  -02
C
  NF:,XTA,NLIB
  ,ASN,64-42
  ,AUX,=6H'''''''7''7'''
  ,AOX,=6H'''''15''20''20'''
  ,ITS,9
  15,ATX,
  ,CALL,TEXISO
  9,AEX,1
  9,ATX,1
  ,XTA,=2
  ,ARX,P
  ,ATX,P
  ,AEX,=12
  CH:,U1A,RET
  ,WTC,P
  14,VTM,MESS+1
  ,ATX,P
  ,XTA,=6H
  14,ATX,
  ,XTA,INF
  ,*72,4
  RET:15,WTC,
  ,UJ,
  ENDSTAT:,ENTRY,
  ,XTA,SCAL
  ,FUN,74100B+573B
  ,XTA,P
  13,UZA,
  ,ITA,13
  ,XTS,
  ,UJ,CH
  INF:15,Z37,
  15,Z36,MESS
  SUMSCAL:,OCT,7577 7.1-4, 6-17 OБCП
  SCAL:,BSS,1
  P:,BSS,1
  MESS:,BSS,12
 ,END,.
 SUMOVER:,NAME,.PEДAKTИPOBAHИE
C                           OVERLAY'EB
C    Б.Б.CAXAPOB.           ПPOИЗBOЛЬHOЙ
C                           ПPИPOДЫ.
C*
C*   *CALL SUMOVER:P1,P2,LIM,COM
C*
C*   P1 - YKAЗATEЛЬ OCHOBHOГO OVERLAY.
C*   P2 - YKAЗATEЛЬ OVERLAY-ДOБABKИ.
C*        CЮДA ЖE ЗAПИCЫBAETCЯ HOBAЯ
C*        BEPCИЯ OV-БИБЛИOTEKИ.
C*   LIM - OГPAHИЧИTEЛЬ ДЛИHЫ HOBOЙ
C*        BEPCИИ БИБЛИOTEKИ.
C*   COM - ПPOИЗBOЛЬHЫЙ KOMMEHTAPИЙ.
C*
C*   *CALL DELOVER - ИCKЛЮЧEHИE PAЗДEЛOB
C*   *CALL ORDOVER - YПOPЯДOЧEHИE
C*   ПAPAMETPЫ - AHAЛOГИЧHO SUMOVER,
C*   ПЛЮC CПИCOK ИMEH PAЗДEЛOB HA
C*   ПOCЛEДYЮЩИX KAPTAX ДAHHЫX.
C*
C*   ПEЧATЬ ИЗ ПPOГPAMMЫ
C*   БЛOKИPYETCЯ KAPTOЙ
C*     *NO LIST
C*
 MONITOR*:,SUBP,
 DATA*:,LC,2
 MONCARD/:,LC,
 OUT*OUT:,LC,1
 STARSTA:,LC,1
 *MONCRD*:LC,BLOCK,*MONCAR*(12)
 PARAMT*:,SUBP,
 PT6/G:,SUBP,
  NOLISFL*:,LC,1
 LIST:PC,BLOCK,LCO(1024)
 ,CONT,LCD(1024)
 ,CONT,LCN(1024)
 ,CONT,LPO(1024)
 ,CONT,LPN(1024)
 NL:,P/P,(LIST)16
 *C:7,BASE,*C
  ,FUN,75B.KOM.CBEPTKA
 ,XTA,=31 0000
 ,ATX,*O*
 ,XTA,=32 0000
 ,ATX,*D*
 ,XTA,=100
 ,ATX,*Г*
 14,VTM,-10B
 ,XTA,=6H
 ,UTC,OUT*OUT
 ,ATX,.ПEPEXBAT OШИБOK.
 ЫГЫ:14,UTC,LCN+1766B
 ,ATX,10B
 14,VLM,ЫГЫ
 ,XTA,DATA*
 ,UTC,LCN+1764B
 ,ATX,
 ,XTA,DATA*+1
 ,UTC,LCN+1765B
 ,ATX,
 ,XTA,=6HOVERLA
 ,UTC,LCN+1762B
 ,ATX,
 ,XTA,=6HY OT
 ,UTC,LCN+1762B
 ,ATX,1
 9,VTM,
 9,MTJ,10
 13,VJM,PARAMT*
 11,VZM,*99
 9,VTM,*O*
 10,VTM,4
 13,VJM,PARAMT*
 9,VTM,*D*
 10,VTM,4
 13,VJM,PARAMT*
 9,VTM,*Г*
 10,VTM,5
 13,VJM,PARAMT*
 9,VTM,
 10,VTM,1
 13,VJM,PARAMT*
 14,VTM,-10B
 *COMCOM:14,UTC,*MONCAR*
 ,XTA,10B
 14,UTC,LCN+1766B
 ,ATX,10B
 14,VLM,*COMCOM
 *99:,BSS,
C     A HE ПCИX ЛИ HAC BЫЗBAЛ
 12,VTM,ПCИX1
 ,XTA,*O*
 ,AEX,*D*
 ,UZA,TELEX.TOЧHO,- ПCИX
 12,VTM,ПCИX2.
 ,XTA,*O*.
 ,AEX,=3 0000.
 ,UZA,TELEX.-ПCИX
 ,XTA,*D*
 ,AEX,=3 0000.
 ,UZA,TELEX.TAK-TAKИ ПCИX
 ,XTA,*O*
 ,AOX,*O
 ,ATX,/O
 ,XTA,*D*
 ,AOX,*D
 ,ATX,/D
 ,XTA,*Г*
 ,UTC,LCN+1777B
 ,ATX,
 ,ARX,*D*
 14,VJM,IWCOR
 ,ATX,*Г*
 ,TAPE,/O
 ,UTC,LCO
 ,XTA,1762B
 ,AEX,=6HOVERLA
 12,VTM,ДOT
 ,UIA,TELEX
 ,WTC,SPECWORK.PEЖИM PAБOTЫ
 ,UJ,*
 :,UJ,ИCK.ИCKЛЮЧEHИE
 :,UJ,YПOP.YПOPЯДOЧИBAHИE
 :,BSS,.ДOПOЛHEHИE
 ,TAPE,/D
 ,UTC,LCD
 ,XTA,1762B.
 ,AEX,=6HOVERLA
 ,UIA,TELEX
 12,VTM,OCA*
 1,VTM,LCO-2
 13,VJM,PRICATA
 12,VTM,DCA*
 1,VTM,LCD-2
 13,VJM,PRICATA
 1,XTA,1
 ,ASN,64+35
 ,AAX,=777
 ,ATI,13
C---- ЗOHY KATAЛOГA И HEПOЛHYЮ TOЖE HYЖHO CЧИTATЬ -----
 13,UTM,2.A ББ HE CЧИTAЛ!
CПИШEM ДOПЛOHEHИE БCЗ HA MБ 0/3
 2,VTM,LPO
 ,ITA,2
 ,ASN,64-20
 ,AOX,*D*
 ,AOX,=:001
 ,ATX,/O
 ,ITA,2
 ,ASN,64-20
 ,AOX,=3 0000
 ,ATX,21B
 EX1:13,VZM,EOEX1
 13,UTM,-1
 ,TAPE,/O
 ,XTA,/O
 ,ARX,=1
 14,VJM,IWCOR
 ,ATX,/O
 ,DRUM,21B
 ,XTA,21B
 ,ARX,=1
 14,VJM,IWCOR
 ,ATX,21B
 ,UJ,EX1
 EOEX1:4,VTM,LCO
 6,VTM,LCD
 8,VTM,LCN
 1,VTM,LPO
 MC:4,XTA,
 ,UZA,ДOП
 ,AEX,=1.A HE ИCKЛ$ЧEHA
 ,UZA,*14.ЛИ OVERLEЯ
 ,AEX,=1.  HET
 6,MTJ,14
 *12:14,XTA,
 ,UZA,*13
 4,AEX,
 ,UZA,ЗAMEHA
 14,UTM,2
 ,UJ,*12
 *13:4,MTJ,10
 ,XTA,*O*
 13,VJM,ПEPOB
 *14:4,UTM,2
 ,UJ,MC
 ЗAMEHA:,XTA,=3 0000
 14,MTJ,10
 13,VJM,ПEPOB
 ,XTA,=1
 14,ATX,
 ,UJ,*14
 ДOП:6,XTA,
 ,UZA,FIN
 ,AEX,=1
 ,UZA,БЫЛA YЖE
 6,MTJ,10
 ,XTA,=3 0000
 13,VJM,ПEPOB
 БЫЛA YЖE:6,UTM,2
 ,UJ,ДOП
 FIN:,XTA,*D*
 14,VTM,LCN
 ,ITS,14
 ,ASN,64-20
 15,AOX,
 ,ATX,21B
  ,UTC,LCN+1777B
 ,XTA,
 ,UZA,****
 ,TAPE,WO
 KATAЛГ:,BSS,
  12,VTM,LCO
  12,XTA,1400B
  ,AEX,=6HSTALOA
  ,UZA,WRSTL
  12,VTM,LCD
  12,XTA,1400B
  ,AEX,=6HSTALOA
  ,UZA,WRSTL
  WRCTLG:,BSS,
 ,TAPE,21B
 12,VTM,NCA*
 1,VTM,LCN-2
 13,VJM,PRICATA
 ,UJ,MONITOR*+1.K STAR*
C
C...   ПEPEПИCЬ CTAT.ЗAГPYЗЧИKA
C      И ПPOЧEГO БAPAXЛA CИCTEMЫ
C
  WRSTL:,UTC,LCN
  ,XTA,400B
  ,U1A,WRCTLG.HEЛЬЗЯ
  1,VTM,400B-1760B
  /WR/SL:1,UTC,1760B
  12,XTA,
  ,UTC,LCN
  1,CTX,1760B
  1,VLM,/WR/SL
  ,UJ,WRCTLG
 IWCOR:,ATX,WD.KOPPEKЦИЯ
 ,ARX,=10 0000.ИHФ. CЛOBA
 ,AAX,=40 0000.OБMEHA
 ,UIA,M*YK
 ,XTA,WD.
 ,AAX,=3740.
 ,ASN,64-7.
 ,ARX,WD.
 ,AAX,=7777 7777 7777 0037
 ,ATX,WD
 M*YK:,XTA,WD.
 14,UJ,.BOЗBPAT
 ****:,BSS,
 12,VTM,MAЛO MЛ
 ,PRINT,IT
 ,UJ,KATAЛГ
 DELOVER:,ENTRY,.
C           ПO CПИCKY HA П/K:
 7,VTM,*C
 5,VTM,LCO.
 ,XTA,=1
 *60:,ATX,SPECWORK
 ,UJ,*C
 ORDOVER:,ENTRY,.
C           ПO CПИCKY HA П/K:
 7,VTM,*C
 5,VTM,LCD.
 ,XTA,=2
 ,UJ,*60
C
C        ФOPMИPOBAHИE CПИCKA YПOPЯДOЧИBAHИЯ
C
 BKAT:,XTA,NAMOV.
 4,ATX,.
 4,UTM,2
 ,UJ,ПOПEЧ.
C
C        ИЗBЛEЧEHИE ДOПOЛHИTEЛЬHOЙ ИHФOPMAЦИИ
 ИCK:14,VTM,MONCARD/.
 ,ITS,14.
 ,CALL,READ*.
 14,VTM,MONCARD/.
 ,ITS,14.
 14,UTM,13.
 ,ITS,14.
 ,ITS,
 ,CALL,PRINT8.
 10,VTM,9.
 13,VJM,PARAMT*.
 ДAЙ EЩE:10,VTM,3.
 13,VJM,PARAMT*.
 11,MTJ,2.
 ,XTA,
 ,UTC,*MONCRD*+2.
 ,ATX,
 14,VTM,*MONCRD*
 ,ITS,14.
 14,VTM,NAMOV.
 ,ITS,14.
 ,CALL,ISO*TEX*
 ,XTA,NAMOV.ПO ЖEЛAHИЮ
 ,UZA,ПOПEЧ.BOЛKOBA
 5,MTJ,14.
 ЦИCK:14,XTA,.
 ,UZA,MISPAR.ИCKAЛИ HE        HAШЛИ.
 ,AEX,NAMOV.
 ,UIA,EЩИ.
 ,WTC,SPECWORK.EЩE ПEPEKЛЮЧATEЛЬ.
 ,UJ,*
 :,UJ,EXOV
 :,UJ,B KAT.
 EXOV:,XTA,=1.ДOЛOЙ
 14,ATX,.ЭTY OVERLEЮ.
 ПOПEЧ:2,VZM,ИCK.BCЯ KAPTA
 2,UTM,-56B.A HE ''.'' ЛИ
 2,VIM,ДAЙ EЩE
 ,UJ,EOEX1.
 EЩИ:14,UTM,2
 ,UJ,ЦИCK.
 MISPAR:,BSS,.
 ,XTS,NAMOV
 14,VTM,TYЖE.
 ,ITS,14.
 13,VJM,PT6/G.
 12,VTM,TYЖE.
 ,*64,IT
 ,UJ,ПOПEЧ.
 YПOP:14,VTM,-1023
 *51:14,UTC,LCO+1023.
 ,XTA,
 14,UTC,LCD+1023.
 ,ATX,
 ,XTA,
 14,UTC,LCO+1023.
 ,ATX,
 14,VLM,*51
 5,VTM,LCD
 4,VTM,LCO
 6,VTM,LCO.
 ,XTA,*O*
 ,ATX,=3 0000
 ,UJ,ИCK
 PRICATA:13,MTJ,4
  ,XTA,NOLISFL*
  ,U1A,NOPRIC
 ,PRINT,IT
 6,VTM,-510
 *100:1,UTM,2
 1,XTA,
 ,UIA,*101
 6,VTM,
 ,XTA,=4342 5702 5704 5057
 *101:15,ATX,
 14,VTM,BUT
 ,ITS,14
 13,VJM,PT6/G.
 1,XTA,1
 ,ATX,BUT+3
 ,PRINT,IC
 6,VLM,*100
 4,UJ,
  NOPRIC:1,UTM,2
  1,XTA,
  ,U1A,NOPRIC
  6,VTM,
  13,UJ,
 IC:,,BUT
 ,,BUT+3
 :,005,
 ,,
 :2,023,16
 8,,
 *O:,010,NL
 ,,
 *D:,010,NL+100B
 ,,
 ПEPOB:,ATX,RN
 10,XTS,
 8,ATX,
 ,XTA,CBOБN
 10,XTS,1
 ,ASN,64-18
 ,ASN,64+18
 15,AOX,
 ,AOX,=:4
 8,ATX,1
 10,XTA,1
 ,ASN,64-1
 ,ASN,64+31
 10,XTS,3
 ,ASN,64-1
 ,ASN,64+31
 ,NTR,3
 15,A-X,
 ,ATX,LENOV
 ,ASN,64-30
 ,ARX,CBOБN
 8,ATX,3
 ,ATX,CBOБN
 ,XTA,LENOV
 ,ASN,64-5
 ,AAX,=7 7777
 ,ATX,LWO
 8,XTA,1
 ,ASN,64+30
 ,ASN,64-5
 ,AAX,=1777
 ,ATI,9
 9, UTM,-1024
 8,XTA,1
 ,ASN,64+35
 ,AAX,=777
 ,ARX,*D*
 14,VTM,LPN
 ,ITS,14
  ,ASN,64-20
 15,AOX,
 14,VJM,IWCOR.
 ,ATX,WN
 ,AEX,WO
 ,UZA,TYT
 ГAC/2:,BSS,
 ,TAPE,WO
 ,XTA,WN
 ,ATX,WO
 TYT:10,XTA,1
 ,ASN,64+30
  ,ASN,64-5
 ,AAX,=1777
 ,ATI,11
 11,UTM,-1024
 8,UTM,2
 10,XTA,1
 ,ASN,64+35
 ,AAX,=777
 ,ARX,RN
 ,ITS,1
 ,ASN,64-20
 15,AOX,
 ,AOX,=:001
 14,VJM,IWCOR
 ,ATX,RN
 ,AEX,RO
 ,UZA,TYTA
 ,TAPE,RN
 ,XTA,RN
 ,ATX,RO
 TYTA:,BSS ,
 ,ITA,13
 ,ITS,
 *33:,XTA,LWO
 ,UZA,KOHEЦ
 13,VJM,READ
 13,VJM,WRITE
 ,XTA,LWO
 ,NTR,3
 ,A-X,=1
 ,ATX,LWO
 ,UJ,*33
 KOHEЦ:,STI,
 ,STI,13
 10,MTJ,14
 13,UJ,
 READ:11,VZM,RNZ
 ,UTC,LPO
 11,XTA,1024
 11,UTM,1
 13,UJ,
 RNZ:,XTA,RO
 ,ARX,=1
 14,VJM,IWCOR
 ,ATX,RO.!!!
 ,TAPE,RO
 11,VTM,-1024
 ,UJ,READ
 WRITE:9,VZM,WNZ
 ,UTC,LPN
 9,ATX,1024
   9,UTM,1
 13,UJ,
 WNZ:,BSS ,
 ,XTS,WO
 ,AEX,*Г*
 ,AAX,=77 7777
 ,UIA,XBATAET
 ,UTC,LCN+1777B
 ,ATX,
 ,XTA,=0220 0000 0220 0000
 ,*75,ГAC
 ,CTX,ГAC/2
 XBATAET:,BSS,
 ГAC:,BSS,
 ,TAPE,WO
C---- ПPOЧИCTKA ЛИCTA OБMEHA -----
 9,VTM,-1023
 ,XTA,
 ЧCT:9,UTC,LPN+1023
 ,ATX,
 9,VLM,ЧCT
 ,XTA,WO
 ,ARX,=1
 14,VJM,IWCOR
 ,STX,WO
 9,VTM,-1024
 ,UJ,WRITE
 TELEX:,*64,IT
 ,UJ,MONITOR*+1.K STAR*
 WD:,OCT,
 IT:12,,
 12,,
 :,001,
 8,,
 SPECWORK:,LOG,3
 NAMOV:,OCT,
 TYЖE:,GOST,12H
 ,CONT,12H-A EE HET '133''231'
 MAЛO MЛ:,GOST,12HMAЛO MЛ '133''231'
 OCA*:,GOST,12H'201'OLD OVERL.'231'
 DCA*:,GOST,12H'205'SUB OVERL.'231'
 NCA*:,GOST,12H'205'NEW OVERL.'231'
 ДOT:,GOST,18HИCПOPЧEH OVERLAY'133''231'
 ПCИX1:,GOST,21HФAЙЛ ЧTEHИЯ И ЗAПИCИ
 ,CONT,12HCOBПAДAЮT '133''231'
 ПCИX2:,GOST,23HHEПPИEMЛEMЫЙ YKAЗATEЛЬ
 ,CONT,18HЧTEHИЯ (ЗAПИCИ) '133''231'
 *Г*:,OCT,
 BUT:,GOST,12H
 ,GOST,1H'231'
 ,OCT,
 CBOБN:,OCT,100040
 WO:,LOG,3 0000
 /O:,BSS,1
 /D:,BSS,1
 WN:,BSS,1
 RO:,BSS,1
 RN:,BSS,1
 LENOV:,BSS,1
 LWO:,BSS,1
 *O*:,BSS,1
 *D*:,BSS,1
 ,END,
  S WRITE :,NAME,
C
C***  SWRITE,SREAD (LUN,AB,AE,COD)
C
  FMRTAPE:,SUBP,
  *1:B,BLOCK,I13,TYP,I14,ACOD,AE,AB,ALUN
C
  ,ITS,14
  14,VTM,42B
  ,UJ,B
C
  S READ:,ENTRY,
C
  ,ITS,14
  14,VTM,41B
  B:,ITS,14
  ,ASN,64-42
  14,BASE,B
  ,ITS,13
  15,WTC,1-ALUN
  ,XTS,
  ,AAX,=177
  15,AOX,-TYP
  ,ATX,IW
  ,AAX,=:01
  ,ASN,64+2
  ,AOX,=:005
  ,ATX,SIGN
  15,WTC,-ACOD
  ,XTA,
  ,U1A,*1
  ,XTS,IW
  ,CALL,FMRTAPE
  *2:15,XTA,-AB
  ,AAX,=77777
  ,ATX,IW
  15,XTA,-AE
  ,AAX,=77777
  ,ASN,64-24
  *3:,AOX,IW
  ,AOX,SIGN
  15,ATX,
  ,CALL,FMRTAPE
  15,WTC,-I13
  13,VTM,
  15,WTC,-I14
  14,VTM,
  15,UTM,-ALUN-1
  13,UJ,
  *1:,AEX,=6HREC
  ,XTS,=:001
  ,STX,SIGN
  ,UZA,*3
  ,UJ,*2
C---
  SRWEND*:,ENTRY,
  ,UTC,=:004
  ,XTS,
  ,UJ,FMRTAPE
  IW  :,LOG,
  SIGN:,LOG,
  ,END,
  SYS/BCB:,NAME,
C***********************************
C*
C*   BBOД KOДOBЫX ЗAДAЧ
C*
C*   ДEKAБPЬ 1979 Г.  CAXAPOB, ДABЫДOB
C*
C*   SYS*BCB - BBOД C ЧИTAЛKИ
C*   SYS/BCB - BBOД C MБ 01
C*   Э60/MБ  - ЗAKAЗ ПEPEXOДA B PEЖИM
C*             ЧTEHИЯ KAPT ПO Э060 C MБ
C*
C************************************
C*
  13,VJM,Э60/MБ
  SYS*BCB:,ENTRY,
  ,UTC,DEBUG
  ,*67,
  K:,ITA,
  15,VTM,-13
  L:15,ATI,14
  15,VLM,L
  ,*73,.*****
  DEBUG:,Z00,K
  9,,
  Э60/MБ:,ENTRY,
  KCOUNT*:LC,BLOCK,KC
  SCEM:,EQU,83
  Э60/MБ:,XTA,KC+1
  ,ASN,64-15
  ,AOX,KC
  ,*50,213B
  ,*50,70100B+SCEM
  ,UTC,=:0011.Э60+Э63
  ,AOX,
  ,*57,5
  13,UJ,
  ,END,
 SYSLDL:,NAME,
C********** ДAHИЛOB Ю.Л 20/10/83 ЛГЭM
C*
C*   SUBROUTINE SYSLDL (NMPART)
C*
C*   ПEЧATЬ ФPAГMEHTA TAБЛИЦЫ ЗAГPYЗKИ
C*   C ЗAДAHHOГO ИMEHИ PAЗДEЛA NMPART
C*      (ИHTEPФEЙC K П/П PRINPART)
C*   ИMЯ PAЗДEЛA ЗAДAETCЯ B MACCИBE
C*   NMPART (2 CЛOBA)  B  KOДE  ISO.
C*
C*   ПEЧATAETCЯ ФPAГMEHT TPП OT ЗAДAHHOГO
C*   ИMEHИ ДO KOHЦA TPП.
C*
C*   HYЖEH ЗAKAЗ LIBRARY 23.
C*
C    KOHEЦ TPП OПPEДEЛЯETCЯ PP.31-45 17B.
C****************************************
 PRINPART:,SUBP,
 B:13,MTJ,11
C.....   AДPEC ISO-ИMEHИ HA CYMMATOPE
 ,CALL,ISOTEX.LIBRARY 23
 12,BASE,B
 ,ATX,NMPT
 ,ITA,11
 ,ATX,RET
C.....   PRINPART БAЗИPOBAH ПO REL
C        БAЗA БEPETCЯ ИЗ 43B, TAK KAK
C        PRINPART B OCHOBHOM ПPEДHAЗHAЧEH
C.....   ДЛЯ PAБOTЫ HA OБMEHHOM ЛИCTE.
 ,XTA,43B
 ,ATX,SAV43
 14,VTM,PRINPART
 ,ITA,14
 ,ATX,43B
 ,XTA,NMPT
 13,VJM,PRINPART
 ,XTA,SAV43.BOCCTAHOBЛEHИE
 ,STX,43B  .43B
 ,WTC,RET
 ,UJ,
 NMPT:,BSS,1
 SAV43:,BSS,1
 RET:,BSS,1
 ,END,
      T1         :,NAME,
      **         :,LC,  14
      READ1      :,SUBP,
      WRITE1     :,SUBP,
 *00000    :14 ,  XTA,
                 ,ASN,  50B
            12 ,  VTM,   *00000
            12 ,  AOX,  3
            12 ,  STX,  30B
            13 ,   UJ,
                 ,ATX,
                 , Z00,     **
      R         :,ENTRY,
                 ,ITS,  13
            12 ,  VTM,   *00000
            12 ,  ATX,  31B
            12 ,  XTS,  30B
            13 ,  VJM,  READ1
            12 ,  VTM,   *00000
            12 ,  XTA,  30B
            12 ,  AOX,  27B
            12 ,  ATX,  30B
            12 ,  XTA,  31B
                 ,STI,  13
            13 ,   UJ,
                 ,UTC,
      W         :,ENTRY,
                 ,ITS,  13
            12 ,  VTM,   *00000
            12 ,  ATX,  31B
            12 ,  XTS,  30B
            13 ,  VJM,  WRITE1
            12 ,  VTM,   *00000
            12 ,  XTA,  30B
            12 ,  AOX,  27B
            12 ,  ATX,  30B
            12 ,  XTA,  31B
                 ,STI,  13
            13 ,   UJ,
                 ,UTC,
      EW        :,ENTRY,
                 ,ITS,  13
            12 ,  VTM,   *00000
            12 ,  ATX,  31B
                 ,XTS,
            13 ,  VJM,  WRITE1
            12 ,  VTM,   *00000
            12 ,  XTA,  31B
                 ,STI,  13
            13 ,   UJ,
 *00027    :,     LOG,  40000000 00000000.
 BSS       :     ,BSS,  2
      ,END,
      T2         :,NAME,
      **         :,LC,  14
      READ2      :,SUBP,
 *00000    :14 ,  XTA,
                 ,ASN,  50B
            12 ,  VTM,   *00000
            12 ,  AOX,  3
            12 ,  STX,  12
            13 ,   UJ,
                 ,ATX,
                 , Z00,     **
      R2        :,ENTRY,
                 ,ITS,  13
            12 ,  VTM,   *00000
            12 ,  ATX,  13
            12 ,  XTS,  12
            13 ,  VJM,  READ2
            12 ,  VTM,   *00000
            12 ,  XTA,  12
            12 ,  AOX,  11
            12 ,  ATX,  12
            12 ,  XTA,  13
                 ,STI,  13
            13 ,   UJ,
                 ,UTC,
 *00013    :,     LOG,  40000000 00000000.
 BSS       :     ,BSS,  2
      ,END,
 TAB/ITG*:,NAME,2.10.81 (16.3.82)
C*****************************************
C*     П/П - TAБЛИЦA  ДЛЯ  ПEPEBOДA:
C* =======================================
C* I  ISO-TEXT  I  TEXT-ISO  I  ISO-YПП  I
C* I  PP. 45-40 I  PP. 31-25 I  PP. 8-1  I
C* =======================================
C*
C*   B PAЗPЯДAX  48-46 - TИП CИMBOЛA (ISO):
C*   0 - БYKBA.
C*   1 - PAЗДEЛИTEЛЬ.
C*   2 - ЦИФPA 0-7.
C*   3 - ЦИФPA 8-9.
C*****************************************
C* 0-KOДY ISO COOTBETCTBYET  ПPOБEЛ  YПП!
C*........................................
 00,OCT,1000 0040 0000 0217.ПPOБEЛ
 01,OCT,1
 02,OCT,1000 0142.  Б
 03,OCT,1000 0143.  Ц
 04,OCT,1000 0144.  A
 05,OCT,1000 0146 0000 0135.  Ф 'Ь
 06,OCT,1000 0147 0000 0224.  Г YMH.
 07,OCT,1000 0151.  И
 10,OCT,1000 0056.  TOЧKA
 11,OCT,1
 12,OCT,1000 0052.  *
 13,OCT,1000 0152.  Й
 14,OCT,1000 0154.  Л
 15,OCT,1000 0161.  Я
 16,OCT,1000 0166 0000 0316.  Ж <=
 17,OCT,1000 0057 0000 0117./ >=
 20,OCT,1000 0060 0000 0233.
 21,OCT,1000 0061.  1
 22,OCT,1000 0062.  2
 23,OCT,1000 0063.  3
 24,OCT,1000 0064.  4
 25,OCT,1000 0065 0000 0331.  5 ^
 26,OCT,1000 0066 0000 0221.  6 CTPEЛKA
 27,OCT,1000 0067 0000 0020.  7 ДECЯTKA
 30,OCT,1000 0070 0000 0034.  8 NE
 31,OCT,1000 0071 0000 0136.  9 ГPAД.
 32,OCT,1000 0170 0000 0124.    +
 33,OCT,1000 0000 0000 0032.
 34,OCT,1000 0160 0000 0122.  П
 35,OCT,1000 0055 0000 0325.  - EQ
 36,OCT,1000 0053 0000 0320.  +
 37,OCT,1000 0171 0000 0323.  Ы
 40,OCT,0000 0172 0000 0217.PROBEL З
 41,OCT,1000 0101 0000 0133.  A !
 42,OCT,1000 0102 0000 0334.  B "
 43,OCT,1000 0103.  C
 44,OCT,1000 0104 0000 0127.D $
 45,OCT,1000 0105 0000 0326.  E %
 46,OCT,1000 0106 0000 0121.  F &
 47,OCT,1000 0107 0000 0337.  ' G
 50,OCT,1000 0110 0000 0222.  ( H
 51,OCT,1000 0111 0000 0023.  ) I
 52,OCT,0120 0112 0000 0031.
 53,OCT,1360 0113 0000 0212.+ K
 54,OCT,1000 0114 0000 0015., L
 55,OCT,1350 0115 0000 0013.- M
 56,OCT,1100 0116 0000 0016.. N
 57,OCT,0170 0117 0000 0214./ O
 60,OCT,2200 0120 0000 0200.0 P
 61,OCT,2210 0121 0000 0001.1 Q
 62,OCT,2220 0122 0000 0002.2 R
 63,OCT,2230 0123 0000 0203.3 S
 64,OCT,2240 0124 0000 0004.4 T
 65,OCT,2250 0125 0000 0205.5 U
 66,OCT,2260 0126 0000 0206.6 V
 67,OCT,2270 0127 0000 0007.7 W
 70,OCT,3300 0130 0000 0010.8 X
 71,OCT,3310 0131 0000 0211.9 Y
 72,OCT,1000 0132 0000 0037.  Z :
 73,OCT,1000 0173 0000 0026.  Щ ;
 74,OCT,1000 0174 0000 0235.  Э <
 75,OCT,1000 0175 0000 0025.  Щ =
 76,OCT,1000 0176 0000 0236.  Ч >
 77,OCT,1000 0140 0000 0130.  Ю ?
 100,OCT,1
 101,OCT,0410 0000 0000 0040.A
 102,OCT,0420 0000 0000 0242.B
 103,OCT,0430 0000 0000 0061.C
 104,OCT,0440 0000 0000 0277.D
 105,OCT,0450 0000 0000 0045.E
 106,OCT,0460 0000 0000 0100.F
 107,OCT,0470 0000 0000 0301.G
 110,OCT,0500 0000 0000 0255.H
 111,OCT,0510 0000 0000 0302.I
 112,OCT,0520 0000 0000 0103.J
 113,OCT,0530 0000 0000 0052.K
 114,OCT,0540 0000 0000 0104.L
 115,OCT,0550 0000 0000 0054.M
 116,OCT,0560 0000 0000 0105.N
 117,OCT,0570 0000 0000 0256.O
 120,OCT,0600 0000 0000 0260.P
 121,OCT,0610 0000 0000 0106.Q
 122,OCT,0620 0000 0000 0307.R
 123,OCT,0630 0000 0000 0310.S
 124,OCT,0640 0000 0000 0062.T
 125,OCT,0650 0000 0000 0111.U
 126,OCT,0660 0000 0000 0112.V
 127,OCT,0670 0000 0000 0313.W
 130,OCT,0700 0000 0000 0265.X
 131,OCT,0710 0000 0000 0263.Y
 132,OCT,0720 0000 0000 0114.Z
 133,OCT,1000 0000 0000 0227.[
 134,OCT,1
 135,OCT,1000 0000 0000 0230.]
 136,OCT,1000 0000 0000 0315.^
 137,OCT,1000 0000 0000 0332._
 140,OCT,0770 0000 0000 0075.Ю
 141,OCT,1000 0000 0000 0040.A
 142,OCT,0020 0000 0000 0241.Б
 143,OCT,0030 0000 0000 0266.Ц
 144,OCT,0040 0000 0000 0244.Д
 145,OCT,1000 0000 0000 0045.E
 146,OCT,0050 0000 0000 0064.Ф
 147,OCT,0060 0000 0000 0043.Г
 150,OCT,1000 0000 0000 0265.X
 151,OCT,0070 0000 0000 0250.И
 152,OCT,0130 0000 0000 0051.Й
 153,OCT,1000 0000 0000 0052.K
 154,OCT,0140 0000 0000 0253.Л
 155,OCT,1000 0000 0000 0054.M
 156,OCT,1000 0000 0000 0255.H
 157,OCT,1000 0000 0000 0256.O
 160,OCT,0340 0000 0000 0057.П
 161,OCT,0150 0000 0000 0076.Я
 162,OCT,1000 0000 0000 0260.P
 163,OCT,1
 164,OCT,1000 0000 0000 0062.T
 165,OCT,1000 0000 0000 0263.Y
 166,OCT,0160 0000 0000 0046.Ж
 167,OCT,1000 0000 0000 0242.B
 170,OCT,0320 0000 0000 0073.Ь
 171,OCT,0370 0000 0000 0272.Ы
 172,OCT,0400 0000 0000 0247.З
 173,OCT,0730 0000 0000 0070.Ш
 173,OCT,0740 0000 0000 0274.Э
 175,OCT,0750 0000 0000 0271.Щ
 176,OCT,0760 0000 0000 0067.Ч
 177,OCT,1
 ,END,
 TABLE2* :  ,NAME,                        .
         :   ,  OCT  ,0000000000000060    .
         :   ,  OCT  ,0000 0000 0000 0061 .1
         :   ,  OCT  ,0000 0000 0000 0062 .2
         :   ,  OCT  ,0000 0000 0000 0063 .3
         :   ,  OCT  ,0000 0000 0000 0064 .4
         :   ,  OCT  ,0000 0000 0000 0065 .5
         :   ,  OCT  ,0000 0000 0000 0066 .6
         :   ,  OCT  ,0000 0000 0000 0067 .7
         :   ,  OCT  ,0000 0000 0000 0070 .8
         :   ,  OCT  ,0000 0000 0000 0071 .9
         :   ,  OCT  ,0000 0000 0000 0053 .+
         :   ,  OCT  ,0000 0000 0000 0055 .-
         :   ,  OCT  ,0000 0000 0000 0057 ./
         :   ,  OCT  ,0000 0000 0000 0054 .,
         :   ,  OCT  ,0000 0000 0000 0056 . .
 SBLK    :   ,  OCT  ,0000 0000 0000 0040 .BLANK
 ,LOG,27.ДECЯTЬ
 ,LOG,77.BOПPOC
         :   ,  OCT  ,0000 0000 0000 0050 .(
         :   ,  OCT  ,0000 0000 0000 0051 .)
 ,LOG,6.MULTIPLY
         :   ,  OCT  ,0000 0000 0000 0075 .=
         :   ,  OCT  ,0000 0000 0000 0073 .)
         :  ,OCT,0000 0000 0000 0133
         :   ,  OCT  ,0000 0000 0000 0135 .]
         :   ,  OCT  ,0000 0000 0000 0052 .*
         :   ,  OCT  ,0000 0000 0000 0047 .'
         :   ,  OCT  ,0000 0000 0000 0047 .'
 ,LOG,30.NE
         :  ,OCT,0000 0000 0000 0074
         :   ,  OCT  ,0000 0000 0000 0076 .>
         :   ,  OCT  ,0000 0000 0000 0072 .:
         :  ,OCT,0000 0000 0000 0101.A
         :  ,OCT,0000 0000 0000 0142.Б
         :  ,OCT,0000 0000 0000 0102.B
         :  ,OCT,0000 0000 0000 0147.Г
         :  ,OCT,0000 0000 0000 0144.Д
         :  ,OCT,0000 0000 0000 0105.E
         :  ,OCT,0000 0000 0000 0166.Ж
         :  ,OCT,0000 0000 0000 0172.З
         :  ,OCT,0000 0000 0000 0151.И
         :  ,OCT,0000 0000 0000 0152.Й
         :  ,OCT,0000 0000 0000 0113.K
         :  ,OCT,0000 0000 0000 0154.Л
         :  ,OCT,0000 0000 0000 0115.M
         :  ,OCT,0000 0000 0000 0110.H
         :  ,OCT,0000 0000 0000 0117.O
         :  ,OCT,0000 0000 0000 0160.П
         :  ,OCT,0000 0000 0000 0120.P
         :  ,OCT,0000 0000 0000 0103.C
         :  ,OCT,0000 0000 0000 0124.T
         :  ,OCT,0000 0000 0000 0131.Y
         :  ,OCT,0000 0000 0000 0146.Ф
         :  ,OCT,0000 0000 0000 0130.X
         :  ,OCT,0000 0000 0000 0143.Ц
         :  ,OCT,0000 0000 0000 0176.Ч
         :  ,OCT,0000 0000 0000 0173.Ш
         :  ,OCT,0000 0000 0000 0175.Щ
         :  ,OCT,0000 0000 0000 0171.Ы
         :  ,OCT,0000 0000 0000 0170.Ь
         :  ,OCT,0000 0000 0000 0174.Э
         :  ,OCT,0000 0000 0000 0140.Ю
         :  ,OCT,0000 0000 0000 0161.Я
         :   ,  OCT  ,0000 0000 0000 0104 .D
         :   ,  OCT  ,0000 0000 0000 0106 .F
         :   ,  OCT  ,0000 0000 0000 0107 .G
         :   ,  OCT  ,0000 0000 0000 0111 .I
         :   ,  OCT  ,0000 0000 0000 0112 .J
         :   ,  OCT  ,0000 0000 0000 0114 .L
         :   ,  OCT  ,0000 0000 0000 0116 .N
         :   ,  OCT  ,0000 0000 0000 0121 .Q
         :   ,  OCT  ,0000 0000 0000 0122 .R
         :   ,  OCT  ,0000 0000 0000 0123 .S
         :   ,  OCT  ,0000 0000 0000 0125 .U
         :   ,  OCT  ,0000 0000 0000 0126 .V
         :   ,  OCT  ,0000 0000 0000 0127 .W
         :   ,  OCT  ,0000 0000 0000 0132 .Z
 ,LOG,134.HAДЧEPK (OБP.CЛEШ)
 ,LOG,16.LE
 ,LOG,17.GE
 ,LOG,36.OR
 ,LOG,46.AND
 ,LOG,34.IMPL
 ,LOG,37.NOT
 ,LOG,32.DIV
 ,LOG,35.EQUIV
         :   ,  OCT  ,0000 0000 0000 0045 .(
         :   ,  OCT  ,0000 0000 0000 0044 . .EQ. DOLLAR
 ,LOG,136.BEPT.ЧEPTA
 ,LOG,25.LINE
         :   ,  OCT  ,0000 0000 0000 0137 .UNDERLINE
         :   ,  OCT  ,0000000000000041    .
  ,LOG,42.ДBOЙHAЯ KABЫЧKA
  ,LOG, 5.TBEPДЫЙ ЗHAK
  ,LOG,100.
  ,LOG,47.AПOCTPOФ
            ,END,
 TAKECRD*:,NAME,.
C--------
 '
C******   БYФEPИЗAЦИЯ ФAЙЛA BBOДA
C         ДИAГHOCTИKA ПO BBOДY
 '
C......   HA BXOДE :
C         PEЗYЛЬTAT / ДИAГHOCTИKA B OUTC
C         И14 = 0 - ДOCБPOC
C           .NE.0 - HE KOHEЦ
 '
C......   CBЯЗKИ C PEЗИДEHTOM MOHИTOPA
 WORINF*:LC,BLOCK,MON CNT.
C......   OГPAHИЧИTEЛЬ ЗAПИCИ
 CHKWORD*:LC,BLOCK,LIM NZ
C......   ИCПOЛЬЗYEMЫE П/П
 DRUMTAP*:,SUBP,.
 PRINT8:,SUBP,.
C......   PAЗHOE
 *CARDS:LC,BLOCK,(44),OUTC(25)
 ,CONT,(99)
 BUFFER:PC,BLOCK,BUF(1024)
 NP:,P/P,(BUF)16
C......   ИCПOЛЬЗYEMЫE ИP :
 *8:,BLOCK,B,T,LW,LR,AR
 C:B,BASE,*.
 ,ITA,13.
 ,ATX,I13.
 ,ITA,14.
 ,ATX,M END.ДOCБPOC
 ,UIA,NO END. - HET
 ,XTA,FINAL.
 ,ATX,OUTC.AБC.KOHEЦ ФAЙЛA
 NO END:,WTC,COUNT.
 LW,VTM,.YKAЗ.ПO ЛИCTY
 AR,VTM,OUTC.
 ,XTA,OUTC.
 ,ASN,64+40.
 ,ATI,T.CПEЦ.KAPTA
 ,UZA,NON TEXT. - ДA
 LR,VTM,1-14.
 TRAN:AR,XTA,.CЛOBO B
 LW,UTC,BUF+1023. ЛИCT
 ,ATX,.
 LW,VLM,NO PAGE....
 ,TAPE,INFW.ЗAПИCЬ ЗOHЫ
 ,XTA,INFW.KOPPEKЦ.
 ,CALL,DRUMTAP*.ИHФ.CЛOBA
 ,ATX,INFW....
 LW,VTM,1-1024.
 NO PAGE:T,VZM,NO TEX.БЛOK.ПPOB.'NL'
 AR,XTA,.
 ,AAX,C377.ПPOБA 'NL'
 ,AEX,S/NL.
 ,UIA,NO TEX.
 LR,VTM,0.KOHEЦ TEKCT.ПK
 NO TEX:AR,UTM,1.
 LR,VLM,TRAN.
 ,XTA,M END.БЫЛ ДOCБPOC
 ,UIA,RET. - HET
 LW,UTC,.ПPOБA : ECTЬ
 14,VTM,1024-1.ЧTO HA ЛИCTE
 14,VZM,RET. - HET
 ,TAPE,INFW.ЗAПИCЬ ЗOHЫ
 RET:,BSS,.
 ,ITA,LW.
 ,ATX,COUNT.
 ,WTC,I13.
 ,UJ,.
 NON TEXT:,BSS,.
C......   CПEЦ.KAPTA ИЛИ ДИAГHOCTИKA
 ,XTA,OUTC.
 ,ATI,LR.
 ,ASN,64+15.
 ,UZA,NT 1.CПEЦ.KAPTA
C......   BЫДAЧA ДИAГHOCTИKИ
 14,VTM,OUTC+1.AДP.HAЧAЛA
 ,ITS,14.
 ,ITS,14.ПOЛYЧEHИE
 ,ITS,LR.AДP.KOHЦA
 ,AEX,C77777.
 15,ARX,.
 ,AAX,C77777.
 14,VTM,6.
 ,ITS,14.N ПOЗИЦИИ
 ,CALL,PRINT8. - ПEЧATЬ
 ,UJ,RET.
 NT 1:,XTA,OUTC.
 ,ARX,=1.
 ,ATX,OUTC.
 ,UJ,TRAN.HA ПEPEПИCЬ
 INITTAKE:,ENTRY,.
C******   YCTAHOBOЧHЫЙ BXOД ЗAПИCИ
 B,BASE,C.
 ,ITA,13.
 ,ATX,I13.
 ,XTA,MON CNT.
 ,ARX,INFW0.
 ,ATX,INFW.
 ,XTA,C03 0000.
 ,ATX,LIM NZ.
 LW,VTM,1-1024.
 ,UJ,RET.
C--------
 INFW0:15,37,NP.
 ,000,.
 FINAL:,LOG,7760 0000 0000 0012.
 C377:,LOG,377.
 S/NL:,LOG,12.
 C03 0000:,LOG,03 0000.
 C77777:,LOG,77777.
C......
 COUNT:,BSS,1.
 INFW:,BSS,1.
 M END:,BSS,1.
 I13:,BSS,1.
 ,END,.
  TAK TAP:,NAME,
C*
C*******************************
C*
C*  SUBROUTINE TAKTAP(LUN)
C*  OTKAЗ OT ЛEHTЫ/ФAЙЛA.
C*  LUN : 1-15 ИЛИ 30B-67B
C*  - ЛOГИЧECKИЙ HOMEP MЛ/ФAЙЛA.
C*
C*******************************
C*
  RFMRTFLG:LC,BLOCK,F
  TAPELIST:LC,BLOCK,(1),S(2)
  B:,ATI,14
  12,BASE,B
  14,XTA,
  ,ATI,11
  ,AAX,=60
  ,U1A,TAK
C...  OTKAЗ B FMRTAPE
  ,ITA,11
  10,VTM,-1
  :,AEX,F
  ,U1A,*+2
  ,ATX,F
  ,*70,=0010 3700 0007 0000
  :,XTA,=4000 0200 0010 0000
  11,ASN,63
  :10,XTS,S+1
  15,AOX,-1
  15,AEX,-1
  10,STX,S+1
  10,VLM,*-2
  11,UTM,40B
  TAK:11,*63,700B-30B
  ,*57,4000B
  15,XTA,
  13,UJ,
  ,END,
 TAPENAM*:,NAME,.
 MCW:,EQU,502B.
 T1MT:,EQU,504B.
  TNMT:,EQU,505B
 TELOUT:,EQU,510B.
 REWINDSC:,EQU,517B.
 STATUSMT:,EQU,520B
 BIT17:,EQU,737B.
 LIST:,PC,1024.
 ,ITS,1
 1,VTM,-12
 SAV:1,ITS,14
 1,VLM,SAV
 7,BASE,*
 DISP:,IFSEQ,D,DD**
 3,VTM,
 ,*63,MCW. AДPEC ГYC'A
 ,ATI,12.
 12,*63,. ГYC
 ,AAX,MДHЗ.
 ,AOX,NMЛ.
 ,ATX,PЯ1.
 6,VTM,-31.
 5,VTM,L3.
 2,VTM,-2
 L1:,BSS,.
 ,*63,T1MT.
 ,ATI,12.
 12,UTC,.
 6,*63,31.
 ,AEX,PЯ1.
 ,ASN,64+36.
 ,UZA,L*
 6,VLM,L1.
 ,UJ,ERRCA.
 L*:,BSS,
 6,UTM,37B
 ,ITA,6
 ,ASN,64+3
 ,ARX,C35B
 ,ATX,AШHГ
 ,*63,STATUSMT
 ,ATI,13
 ,WTC,AШHГ
 13,*63,-10B
 ,ITS,6
 ,AAX,=7
 ,AEX,=7
 ,ATI,4
 4,*63,750B
 15,AAX,
 ,ASN,64-8
 ,ATX,TИПYC.БЭCM-MФ(0), MФ EC (1)
 6,UTM,-37B
 L2:14,VTM,CARD.
 ,ITS,14.
 ,CALL,READ*.
 14,VTM,CARD.
 ,ITS,14.
 14,UTM,13.
 ,ITS,14.
 ,XTS,.
 ,CALL,PRINT8.
 5,UJ,.
 L3:,XTA,CARD.
 ,AEX,=6HNAME
 ,UZA,L4.
 ,XTA,CARD.
 ,AEX,=6HRENAME
 ,U1A,L31
 ,XTA,=1.
 ,UJ,L4
 L31:,XTA,CARD
 ,AEX,=6HFRNAME
 ,U1A,ERRCA
 ,XTA,=2
 L4:,BSS,.
 ,ATX,TYPE. NAME(0), RENAME(1)
 5,VJM,L2.
 8,VTM,NEWNAM.
 5,VJM,ДAЙИMЯ.
 5,VJM,L2
 ,XTA,CARD
 ,AAX,=6H'370''370''370''370'''''
 ,AEX,=6H0000''''
 ,UIA,ERRCA
 ,XTA,CARD
 ,APX,=6H'7''7''7''7'''''
 ,ASN,64+36
 ,AEX,=1
 ,UZA,PAЗMEC
 ,AEX,=1
 ,NTR,3
 ,X-A,=3777
 ,UIA,ERRCA
 ,A-X,=4002
 ,ATI,4
 ,XTA,TYPE.
 ,U1A,RENAME.
 ,XTA,TИПYC
 ,UZA,NAME
 ,XTA,INF0W
 ,AOX,BIT44
 ,ATX,INF0W
 4,MTJ,3
 3,UTM,5
 REW:1,VTM,-1
 8,VJM,REWIND
 NAME:,*76,N1.
 N1:,BSS,.
 13,VTM,N2
 ,WTC,REWINDSC.
 ,XTA,.
 6,AOX,BIT17.
 6,AEX,BIT17.
 ,WTC,REWINDSC.
 ,ATX,.
 N12:,24,2000B.
 ,XTA,NEWNAM.
 ,24,2003B.
  ,WTC,TNMT
  6,ATX,31.ИMЯ MЛ
 13,MTJ,33B.
 ,MTJ,27B.
 3,IJ,.
 HETOTД:,*76,HTД
 HTД:14,UTC,-HTД
 14,VTM,GOTO
 ,ITA,14
 14,VJM,4152B
 14,VTM,*+1
 14,MTJ,33B
 :,MTJ,27B
 3,IJ,
 N2:,XTA,.
 14,VTM,-1023.
 N3:14,ATX,LIST+1023.
 14,VLM,N3.
 14,VTM,LIST.
 ,ITA,14.
 ,ASN,64-20.
 ,AOX,INF0W.
 ,ATX,INFW.
 5,VZM,RN001
 N4:1,VTM,-1
 ,TAPE,INFW
 ,XTA,INFW.
 ,ARX,=1.
 ,AAX,=7777777777773777
 ,ATX,INFW.
 4,VLM,N4.
 3,VZM,CMOTKA
 ,XTA,INFW
 ,AAX,=:0000 7700 0077
 2,MTJ,14
 14,UTM,-1
 14,V1M,KOЧET
 ,AOX,=:001
 KOЧET:,ATX,INFW
 3,MTJ,4
 8,VTM,N4
 2,VLM,REWIND
 CMOTKA:,XTA,TИПYC
 ,UZA,ПOДBOД
 1,VTM,
 8,VJM,REWIND
 END:14,VTM,ENDTEXT.
 ,ITS,14.
 14,UTM,2.
 ,ITS,14.
 ,XTS,=10.
 ,CALL,PRINT8.
 ,STX,.
 14,VTM,13
 REST:14,STI,
 14,UTM,-1
 14,V1M,REST
 13,UJ,.
 ПOДBOД:,BSS,
 ,TAPE,=20000000300000
 ,UJ,END
 PAЗMEC:,XTA,INF0W.ПPИЗHAK
 ,AOX,BIT43.PAЗMETKИ
 ,AOX,BIT44
 ,ATX,INF0W.B PEЖИME EC
 4,VTM,
 ,UJ,REW
 RENAME:5,VTM,
 ,UJ,REW
 FRNAME:,BSS,
 ЧT:,XTA,INFW
 ,AOX,=10000000000000
 ,ATX,INFR
 ,TAPE,INFR
 ,UTC,LIST
 ,XTA,
 1,VTM,-1
 8,VJM,REWIND
 ЗAП:13,VTM,ПOEXAЛИ
 ,*76,B RWSC*0
 B RWSC*0:,WTC,REWINDSC
 ,XTA,
 6,AOX,BIT17
 6,AEX,BIT17
 ,WTC,REWINDSC
 ,ATX,
 13,MTJ,33B
 ,MTJ,27B
 3,IJ,
 REWIND:,XTA,TИПYC
 8,UZA,
 ,XTA,
 14,VTM,-20
 ZTOCRD:14,ATX,CARD+20
 10,VTM,CARD
 14,VLM,ZTOCRD
 ,XTA,=30
 10,ATX,2
 ,XTA,=370 0001
 10,ATX,14B
 ,XTA,=100
 10,ATX,15B
 ,*57,4
 1,VLM,REWIND
 8,UJ,
 RN001:,XTA,TYPE
 ,AEX,=1
 ,AOX,TИПYC
 ,U1A,FRNAME
 RN01:4,UTM,1
 ,TAPE,INFW
 ,XTA,INFW
 ,ARX,=1
 ,ATX,INFW
 ,AAX,=4000
 ,UZA,RN01
 ,XTA,INFW
 ,AEX,=10000000004000
 ,ATX,INFR
 RN1:,TAPE,INFR.
 ПOEXAЛИ:,BSS,
 ,XTA,INFR.
 ,AEX,=10 0000 0000 0000.
 ,ATX,INFW.
 ,TAPE,INFW.
 ,XTA,INFR.
 ,ARX,=1.
 ,AAX,=7777777777773777
 ,ATX,INFR.
 4,VLM,RN1.
 ,UJ,CMOTKA
 ДAЙИMЯ:,BSS,.
 ,XTA,CARD
 ,AEX,SCRATCH
 ,UIA,NOSC
 8,ATX,
 ,XTA,CARD+1
 ,ASN,64-8
 ,ATX,CARD+1
 ,UJ,ИMЯБOБ
 NOSC:,BSS,
 ,XTA,CARD.
 ,ATX,WCARD.
 ,XTA,CARD+1.
 ,AAX,=6H'377''377'''''''''
 ,ATX,WCARD+1.
 14,VTM,WCARD.
 ,ITS,14.
 ,ITS,8.
 8,MTJ,1
 ,CALL,ISO*TEX*.
 1,MTJ,8
 8,XTA,.
 ,UZA,STA
 ,AEX,NOCHK.
 5,UZA,.MЛ - NO CHECK
 8,XTA,
 ,AEX,STANDNAM
 ,UZA,STA
 ИMЯБOБ:8,XTA,.
 ,AAX,B48/13.
 ,XTS,CARD+1
 ,CALL,MANTO*.ПEPEBOД
 ,UZA,HETO. N  БOБ
 15,AOX,.B XИTPЫЙ KOД
 TYДЫ EГO:8,ATX,
 5,UJ,
 STA:,XTA,STCODE
 ,UJ,TYДЫ EГO
 HE TO:,STX,
 DISP:  ,ENDIF,
 ERRCA:,BSS,.
 14,VTM,ERTEXT.
 ,ITS,14.
 14,UTM,5.
 ,ITS,14.
 ,XTS,=10.
 ,CALL,PRINT8.
 ,SJ,.
 ENDTEXT:,BSS,.
 ,ISO,18H***   KOHEЦ   ***
 ERTEXT:,BSS,.
 ,ISO,31H***   OШИБKA B ИHФOPMAЦИИ   ***
 GO TO:,BSS,.
 ,GOST,6H'374''344''274''354''370''360'.'P'HE TOT
 ,GOST,6H'370''354''264''314''254''310'.T ДИCП
 ,OCT,.
 MДHЗ:,OCT,77.
 NMЛ:,OCT,0030.
 STCODE:,BSS,.
 ,LOG,2245 1252 5122 4512.
 C35B:,LOG,35
 INF0W:,LOG,303774
 BIT44:,OCT,02
 BIT43:,OCT,01
 SCRATCH:,ISO,8HSCRATCH
 NOCHK:,TEXT,8HNO CHECK
 B48/13:,OCT,7777 7777 7777.
 STANDNAM:,TEXT,8HSTANDNAM
 WCARD:,BSS,3.
 PЯ1:,BSS,1.
 TYPE:,BSS,1.
 NEWNAM:,BSS,1.
 OLDNAM:,BSS,1.
 INFW:,BSS,1.
 INFR:,BSS,1.
 AШHГ:,BSS,1
 TИПYC:,BSS,1
 CARD:,BSS,21
 ,END,.
 TCATALOG:,NAME,.19.04.73
C*
C******************  P.З.ЗAЛЯЛOB.
C*
C*      L I B R E F E R
C*   ПPOГPAMMA ДЛЯ ПEЧATИ BЗAИMHЫX CCЫЛOK
C*   MEЖДY ПPOГPAMMAMИ, BXOДЯЩИMИ B БИБЛИ-
C*   OTEKY ЗAДAHHOГO TИПA.
C*
C*   BЫЗOB C ПOMOЩЬЮ YПP.KAPTЫ  *CALL
C*   *CALL LIBREFER:<TИП БИБЛ.>,<PEЖИM>
C*
C*   <PEЖИM>: ПYCTO ИЛИ REFERS - ПEЧATЬ CCЫЛOK
C*            RADDR - ИCПOЛЬЗOBAHИE ИCTИH.AДPECOB
C*            MACROS - ИCПOЛЬЗOBAHИE ЭKCTPAKOДOB
C*   <TИП БИБЛ.> : TEMP - BPEMEHHAЯ БИБЛИOTEKA
C*            PERM - ПOCTOЯHHAЯ БИБЛИOTEKA
C*            TAPE,<YK.MЛ> - ЛИЧHAЯ БИБЛИOTEKA
C*
C*   ПPИMEPЫ:
C*      *CALL LIBREFER:TAPE,67100
C*      *CALL LIBREFER:TEMP,REFERS
C*      *CALL LIBREFER:PERM,MACROS
C*      *CALL LIBREFER:TAPE,30000,RADDR
C*
C************************************************
C*
 CTLG:,PC,2048
 ORDER:,PC,1024
 REFERS:PP,BLOCK,SRCAT(1024),SECAT(1024)
 ,CONT,SEXECAT(1024),CRCAT(1024),RAECAT(1024)
 RACAT:,PP,1024
 CECAT:,PP,2048
 COMCAT:,PP,1024
 SEXTCAT:,PP,1024
 BUFFER:,PP,1024
 BUFDEF:,EQU,ORDER
 COMBORD:,PP,1024
  RMACRTB:,EQU,REFERS
 MACROTB:,EQU,RMACRTB+1024
 PRBUF:,LP,330
 *MONCRD*:,LC,12
 NLC: ,P/P,(CTLG)16
 *1:,BLOCK,C,NP,R
 ,CONT,B
 ,CONT,A
 ,CONT,GR
 ,CONT,F,S,NS
 TIW:,EQU,47B
 IW:,EQU,60B
C-----------------------
 BS:,BSS,
 B,BASE,*
 ,UJ,TCTL
 TCATDOR:,ENTRY,
 C,VTM,
 ,UJ,TCT
 C,VTM,
 ,UJ,PCTL
 CATALOG*:,ENTRY,
 B,VTM,BS
 ,ITA,13
 ,ATX,=111113
 13,VJM,BS+1
 13,VJM,BS+2
 RET:,BSS,
 ,WTC,=111113
 ,UJ,
C---BPEM БИБ.---------
 TCTL:,BSS,
 C,VTM,1
 TCT:,BSS,
 B,VTM,BS
 13,MTJ,GR
 ,XTA,
 ,ATX,FTAP
 ,CALL,DATAPR*
 14,VTM,TEMPLIB
 A,VJM,HEAD
 ,XTA,TIW
 A,VJM,RDCAT
 A,VTM,*2
 13,VTM,
 SEEKEND:13,UTM,2
 13,UTC,CTLG
 ,XTA,1
 ,AAX,=:4
 ,UZA,SEEKEND
 ,XTA,C7ALL
 13,UTC,CTLG
 ,ATX,1
 A,UJ,
 PLCATALO:,ENTRY,
 B,VTM,BS
 ,ITA,13
 ,ATX,=111113
 ,ATX,FTAP
  ,XTA,
 ,CALL ,DATAPR*
 14,VTM,TAPLIB
 A,VJM,HEAD
 10,VTM,
 ,CALL,PARAMT*
 ,XTA,=67 0000
 ,ATX,ATAPE
 10,VTM,4
 9,VTM,ATAPE
 ,CALL,PARAMT*
 ,XTA,ATAPE
 A,VJM,RDCAT
 13,VJM,CHCTLG
 C,VTM,
 GR,VJM,*2
 ,XTA,
 ,CALL,DATAPR*
 14,VTM,TAPLIB
 A,VJM,HEAD
 ,WTC,=111113
 GR,VTM,
 C,VTM,1
 ,UJ,*2
 HEAD:,ITS,14
 14,UTM,2
 ,ITS,14
 14,VTM,21
 ,ITS,14
 ,CALL,PRINT8//
 A,UJ,
 CHCTLG:,BSS,
 14,VTM,
 :,UTC,CTLG+2
 14,XTA,1
 ,AEX,C7ALL
 13,UZA,
 14,UTM,2
 ,ITA,14
 ,AEX,=3776
 ,U1A,CHCTLG+1
 14,VTM,ERRCAT
 A,VJM,HEAD
 GR,VTM,RET
 ,UJ,END+1
 RDCAT:,BSS,
 ,AAX,=7777 4077 7777 7777
 ,AOX,TW
 ,ATX,XX
 ,*70,XX
 ,XTA,XX
 ,ARX,=1000 000 0001
 ,ATX,XX
 ,*70,XX
 A,UJ,
 PCATALOG:,ENTRY,
 C,VTM,1
 PCTL:,BSS,
 B,VTM,BS
 13,MTJ,GR
 ,XTA,
 ,ATX,FTAP
 ,CALL,DATAPR*
 14,VTM,PERLIB
 A,VJM,HEAD
 ,XTA,IW
 A,VJM,RDCAT
C---------------
 *2:,BSS,
 13,VTM,/1
 BORDE:,BSS,
 14,VTM,CTLG+2
 A,VTM,
 ORDE:14,UTC,
 ,XTA,1
 ,AEX,C7ALL
 ,UZA,*1
 ,ITA,14
 A,UTC,ORDER
 ,ATX,
 14,UTM,2
 A,UTM,1
 ,UJ,ORDE
 *1: ,BSS,
 A,VZM,END+1
 ,ITA,A
 ,ARX,=I1
 ,ATX,CATLENG
 C,VZM,/1
 A,MTJ,9
 10,VTM,ORDER
 ,UJ,YPOR
C---------------
 /1:,BSS,
 F,VTM,
C---ПEЧATЬ KOHЦA-------
 ,XTA,SPACE
 ,ATX,CBHO+2
 ,XTA,HO
 ,ATX,CBHO+1
 ,UTC,CTLG
 ,XTA,
 ,WTC,FTAP
 10,VTM,
 10,VZM,DRM
 ,ASN,64-23
 ,XTS,ATAPE
 ,APX,=77 0777
 ,ASN,64+33
 ,NTR,3
 ,A-X,=1040
 ,ATX,DIFT
 ,ASN,64-33
 15,ARX,
 ,ATX,LOC
 ,AUX,=6H'''''''7''7''7'
 ,ARX,=6H''''''000
 ,AOX,CBHO+1
 ,ATX,CBHO+1
 ,XTA,LOC
 ,ASN,64-9
 ,AUX,=6H'7''7''''3''7'
 ,AOX,=6H00 00
 ,UJ,RETCB
 DRM:,BSS,
 ,ASN,64-27
 ,AUX,MASK82
 ,ARX,TR8CB
 ,ATX,LOC
 ,ASN,64+32
 ,ARX,CBHO+1
 ,ATX,CBHO+1
 ,XTA,LOC
 ,ASN,64-16
 ,AUX,= 0017777700177777
 ,ARX,CBHO+2
 RETCB:,BSS,
 ,ATX,CBHO+2
 14,VTM,CBHO
 A,VJM,HEAD
 A,VTM,-1
C-------------------------
 ,XTA,AIII
 ,ATX,ALE
 ,XTA,MXL
 ,ATX,MXLENG
 ,XTA,SPACE
 ,ATX,PRINT-1
 14,VTM,1-10
 ,XTA,PRINT-1
 ERASE:,BSS,
 14,ATX,PRINT+9
 14,VLM,ERASE
 R,VJM,PR
 NP,VTM,-5
 ,UJ,NEXTP
C---------------
 STRING: ,BSS,
 C,XTA,
 ,AUX,MASK
 R,VJM,TRNSL
 NP,ATX,PRINT
 C,XTA,
 ,ASN,64-36
 ,AUX,MASK
 NP,ATX,PRINT+1
 C,XTA,1
 ,AAX,=:2
 ,UZA,ENT
 C,XTA,1
 ,WTC,FTAP
 10,VTM,
 10,VZM,NOTAP
      ,AAX,=:0037 77
 ,XTS,DIFT
 ,ASN,64-30
 15,ARX,
 ,AAX,=:0007 77
 ,ASN,64-3
 C,XTS,1
 ,ASN,64-1
 ,AAX,=:37
 15,AOX,
 ,AUX,=:0707 0000 0707 07
 ,ARX,TR8CBT
 ,UJ,RETT
 NOTAP:,BSS,
 ,ASN,64-2
 ,AUX,MASK81
 ,ARX,TR8
 RETT:,BSS,
 ,ATX,LOC
 C,XTA,1
 ,ASN,64-18
 ,AUX,MASK8
 ,ARX,TR8M
 ,ATX,LONG
 ,XTA,LOC
 ,ASN,64+30
 ,AAX,=77
 NP,AOX,PRINT+1
 R,VJM,TRNSL
 NP,ATX,PRINT+1
 ,XTA,LOC
 ,ASN,64-18
 ,AUX,=6H'77''77''77''77'''''
 ,XTS,LOC
 ,AUX,=77
 15,AOX,
 ,ARX,TR
 NP,ATX,PRINT+2
 ,XTA,LOC
 ,ASN,64-4
 ,AAX,=:176
 NP,ATX,PRINT+3
 ,XTA,LONG
 ,ASN,64+24
 NP,AOX,PRINT+3
 ,ARX,TR
 NP,ATX,PRINT+3
 ,XTA,LONG
 ,ASN,64-24
 ,ARX,TR
 NP,ATX,PRINT+4
 ,UJ,NEXTP
 ENT:,BSS,
 C,MTJ,14
 NS/:14,XTA,-1
 ,AAX,=:2
 14,UTM,-2
 ,UIA,NAM
 ,UJ,NS/
 NAM:,BSS,
 14,XTA,
 14,MTJ,13
 ,AUX,=37477
 NP,AOX,PRINT+1
 R,VJM,TRNSL
 ,ARX,=2000 000
 NP,ATX,PRINT+1
 13,XTA,
 ,ASN,64-12
 ,AUX,MASK
 R,VJM,TRNSL
 NP,ATX,PRINT+2
 C,XTA,1
 ,ASN,64-33
 ,AUX,MASK8
 ,ARX,TR8M
 ,ATX,LOC
 ,ASN,64+24
 ,ARX,TR
 ,ARX,=:022
 NP,ATX,PRINT+3
 ,XTA,LOC
 ,ASN,64-24
 ,ARX,TR
 NP,ATX,PRINT+4
 NEXTP:NP,MTJ,14
 NP,UTM,5
 14,UTM,-5
 14,VIM,CTNUE
 R,VJM,PR
 CTNUE:,BSS,
C--ПPOBEPKA HA HAЧAЛO БЛOKA--
 F,VIM,NOUT
 A,MTJ,14
 ,WTC,MXLENG
 14,UTM,1
 14,VIM,NOUT
C--ЗAПOMИHAHИE KOHЦA HOBOГO БЛOKA--
 ,WTC,ALE
 A,UTM,
C--YCTAHOBKA HOBOГO БЛOKA--
 ,XTA,ALE
 ,ASN,64+24
 ,ATI,14
 14,XTA,
 ,ATX,ALE
 ,ITA,A
 ,ARX,ALE
 ,AEX,=77777
 ,ATX,MXLENG
 NOUT:,BSS,
 F,VIM,*7
 A,UTM,1
 *7:,BSS,
C--BЫXOД ЗA KATAЛOГ---
 13,VTM,
 A,MTJ,14
 F,UTC,
 14,UTM,1
 ,ITA,14
 ,ARX,=I1
 ,X-A,CATLENG
 ,UZA,*3
 13,VTM,1
 *3:,BSS,
C--BЫБOPKA ИMEHИ---
 F,UTC,
 A,WTC,ORDER
 C,VTM,
C--ПEPEБPOC ФЛAГA---
 F,VIM,*6
 ,WTC,ALE
 F,VTM,
 ,UJ,*5
 *6:F,VTM,
 *5:,BSS,
 13,VZM,*4
 F,VIM,END
 ,UJ,NEXTP
 *4:,BSS,
 ,UJ,STRING
 PR:14,VTM,PRINT
 ,ITS,14
 14,UTM,10
 ,ITS,14
 14,VTM,3
 ,ITS,14
 ,CALL,PRINT8//
 NP,VTM,
 14,VTM,1-10
 ,XTA,PRINT-1
 ER:,BSS,
 14,ATX,PRINT+9
 14,VLM,ER
 R,UJ,
 END:R,VJM,PR
 ,UTC,1
 ,CALL,PRINT8//
 GR,UJ,
 YPOR:,BSS,
 ,ITA,13
 ,ATX,I13
 YPP:9,MTJ,12
 12,UTM,-1
 12,VZM,YOUT
 10,J+M,12
 ,ITA,12
 ,ATX,N9
 10,MTJ,11
 ,XTA,
 ,ATX,XX
 NN:11,WTC,
 ,XTA,
 ,ATX,RA
 11,WTC,1
 ,XTA,
 ,ATX,RB
 CONT1:13,VJM,ALEB
 ,UZA,YFIN
 11,XTA,
 11,XTS,1
 11,STX,
 11,ATX,1
 ,ATX,XX
 YFIN:11,UTM,1
 ,ITA,11
 ,AEX,N9
 ,U1A,NN
 9,UTM,-1
 ,XTA,XX
 ,U1A,YPP
 YOUT:,WTC,I13
 ,UJ,
 ALEB:,BSS,
 ,ITA,5
 ,ITS,9
 ,ITS,11
 ,XTS,
 5,VTM,-7
 AL:,XTA,RA
 ,ASN,64-6
 ,ATX,RA
 ,YTA,
 ,ATI,12
 12,XTA,T
 ,ATX,RA1
 ,XTA,RB
 ,ASN,64-6
 ,ATX,RB
 ,YTA,
 ,ATI,12
 12,XTA,T
 ,ATX,RB1
 ,NTR,3
 ,A-X,RA1
 ,U1A,AL2
 ,XTA,RA1
 ,AEX,RB1
 ,U1A,AL3
 5,VLM,AL
 AL3:,STX,
 ,STI,11
 ,STI,9
 ,ATI,5
 ,XTA,
 13,UJ,
 AL2:,STX,
 ,STI,11
 ,STI,9
 ,ATI,5
 ,XTA,=1
 13,UJ,
 TRNSL:,BSS,
 14,VTM,1-6
 **:,ASN,64+8
 ,ATX,WC
 ,YTA,
 ,AUX,=7700030
 ,ATI,S
 ,ASN,64+15
 ,ATI,NS
 NS,XTA,TAB
 S,ASN,64-24
 ,AAX,=:776
 ,AOX,WC
 14,VLM,**
 R,UJ,
C
C      ПEЧATЬ CПPABOK O БИБЛИOTEKE
C------------------------------
C
 LIBREFER:,ENTRY,
 B,VTM,BS
 ,ITA,13
 ,ATX,=111113
 A,VTM,-2
 10,VTM,
 ,CALL,PARAMT*
 10,VTM,1
 ,CALL,PARAMT*
 LNL:,BSS,
 ,UTC,*MONCRD*
 ,XTA,
 A,UTC,2
 ,AEX,NLIB
 ,ASN,64+16
 A,UZA, SWITCH+2
 A,VLM,LNL
 14,VTM,ERRPAR
 A,VJM,HEAD
 GR,VTM,RET
 ,UJ,END+1
C       PERMANENT LIBRARY
 PERREF:,XTA,IW
 A,VJM,RDCAT
 FLSH:,EQU,FTAP
C      CONST'S ДЛЯ PAЗБOPKИ ИHФ. CЛOB CM.
 MUNP:,EQU,DIFT
 DELTA:,EQU,ATAP
 ,XTA,=:0004
 SETD:,ATX,DELTA
 ,XTA,=7 7400 0077 0037
 ,ATX,MUNP
 ,UJ,CHJOB
C       TEMPORARY LIBRARY
 TEMPR:,XTA,TIW
 A,VJM,RDCAT
 13,VTM,
 A,VJM,SEEKEND
 ,XTA,
 ,UJ,SETD
C
 SWITCH:,BSS,
 :,UJ,PERREF
 :,UJ,TEMPR
C      БИБЛИOTEKA HA ЛEHTE
 :,XTA,=67 0000
 ,ATX,ATAPE
 10,VTM,4
 9,VTM,ATAPE
 ,CALL,PARAMT*
 ,XTA,ATAPE
 A,VJM,RDCAT
 ,XTA,ATAPE
 ,NTR,3
 ,A-X,=10040
 ,ATX,DELTA
 ,XTA,=777 0777
 ,ATX,MUNP
 13,VJM,CHCTLG
C       KAKИE TAБЛИЦЫ
 CHJOB:,BSS,
 10,VTM,1
 ,CALL,PARAMT*
 ,XTA,=1.HE ПEЧATATЬ:
 ,ATX,FLMACRO.ЭKCTPAKOДЫ,
 ,ATX,FLADR.ИCTИH.AДPECA.
 A,VTM,-2
 LTJ:,UTC,*MONCRD*
 ,XTA,
 A,UTC,2
 ,AEX,TYPJOB
 ,ASN,64+24
 A,UZA,JSWITCH+2
 A,VLM,LTJ
 ,UJ,PRREFS
 JSWITCH:,BSS,
 :,ATX,FLMACRO
 ,UJ,BEGJOB
 :,ATX,FLADR
 ,UJ,BEGJOB
 PRREFS:,BSS,
 10,VTM,1
 ,CALL,PARAMT*
 ,UTC,*MONCRD*
 ,XTA,
 ,AEX,=6HUSE
 ,ASN,64+36
 ,ATX,FLBACK.OБP.CCЫЛKИ
 10,VTM,1
 ,CALL,PARAMT*
 ,UTC,*MONCRD*
 ,XTA,
 ,AEX,=6HINTER
 ,ASN,64+36
 ,ATX,FLSH.BHEШH.П/П
 DSHIFT:,EQU,ALE
 EDSHIFT:,EQU,MXLENG
 EFPLE:,EQU,RB1
 CECNT:,EQU,RA1
 BEGJOB:,BSS,
  14,VTM,-5119
 ,XTA,
 :,UTC,REFERS+5119
 14,ATX,
 14,VLM,*-1
 ,ATX,MACROCNT
 ,ATX,DUBLE
 ,ATX,RACNT
 ,ATX,RADSHIFT
 ,ATX,RFPLE
 ,ATX,CMCNT
 ,ATX,SBCNT
 ,ATX,DSHIFT
 ,ATX,EDSHIFT
 ,ATX,FPLE
 ,ATX,EFPLE
 ,ATX,CECNT
 ,ATX,LIBCODE
 1,VTM,
 LLCAT:,UTC,CTLG+2
 1,XTA,1
 ,AEX,C7ALL
 1,UTM,2
  ,UIA,LLCAT
 1,UTM,-2
 ,ITA,1
 ,ASN,64+1
 ,ATX,LCAT
 GR,VTM,RET
 ,UZA,END+1
 BIT12:,EQU,MAXADR
C
C      OБPAБOTKA CM И COCTABЛEHИE TAБЛИЦ
C------------------------------
C
 SETREF:,BSS,
 LIBMASK:,EQU,=:2
 ,UTC,CTLG
 1,XTA,1
 ,AAX,LIBMASK
 ,UIA,MENTRY
 1,UTM,-2
 ,UJ,SETREF
 CECNT1:,EQU,N9
 FPLE1:,EQU,RB
 EFPLE1:,EQU,RA
 MENTRY:,BSS,
 ,XTA,CECNT
 ,ATX,CECNT1
 ,XTA,FPLE
 ,ATX,FPLE1
 ,XTA,EFPLE
 ,ATX,EFPLE1
 ,XTA,MACROCNT
 ,ATX,MACRCNT1
 ,XTA,RFPLE
 ,ATX,RFPLE1
 USESET:,EQU,USEDECL+32
 ,XTA,
 14,VTM,-63
 :,UTC,USEDECL
 14,ATX,63
 14,VLM,*-1
 ,ATX,FLUSED
 ,ATX,ПPEДKOM
C       ПEPBЫЙПPOXOД ПO CM
 1,UTC,CTLG
 ,XTA,1
 ,ATX,WBEGFLAG
 ,ATX,WBEGFLAG+1
C       ИЗBЛEЧEHИE CTPYKTYPЫ CM
 3,VTM,-2
 RSTR:,BSS,
 13,VJM,RDSTAR
 ,UTC,STR
 3,ATX,2
 3,VLM,RSTR
 STR1:,EQU,STR+1
 STR2:,EQU,STR+2
 ,XTA,STR
 ,ASN,64+12
 ,AAX,M4S.DECLARATIONS
 LCALLS:,EQU,ATAPE
 ,ATX,LCALLS
 ,XTS,STR
 ,ASN,64+24
 ,AAX,M4S.PAR. EXPRETIONS
 15,ARX,
 ,XTS,STR
 ,ASN,64+36
 15,ARX,
 ,XTS,STR1
 ,ASN,64+30
  ,AAX,M3L.LONG IDENT.
 15,ARX,
 ,ATI,2
 LDEF:,EQU,LONG
 ,ATX,LDEF
 ,XTA,STR
 ,AAX,M4S.HEAD
 ,XTS,STR1
 ,AAX,M3L.SET DATA
 ,ATX,SET
 15,ARX,
 ,XTS,STR1
 ,ASN,64+15
 ,AAX,M3L.DATA
 ,ATX,DATACONS
 15,ARX,
 ,XTS,STR2
  ,AAX,M3L.INSTRUCTIONS
 ,ATX,INSTR
 15,ARX,
 ,XTS,STR2
 ,ASN,64+30
 ,AAX,M3L.ABS. CONST.
 15,ATX,
 ,ARX,DATACONS
 ,STX,DATACONS
 15,ARX,
 ,ATI,3
 ,XTA,FLMACRO
 ,UZA,RDINST
 RDPRG:,BSS,
 3,VZM,RDDEF
 13,VJM,RDSTAR
 3,UTM,-1
 ,UJ,RDPRG
C       ИЗBЛEKAEM TAБЛИЦY OПИCAHИЙ
 RDDEF:,BSS,
 2,VZM,CHINST
 13,VJM,RDSTAR
 ,UTC,BUFDEF
 3,ATX,
 3,UTM,1
 2,UTM,-1
 ,UJ,RDDEF
C       BTOPOЙ ПPOXOД ПO CM
C      ПPOBEPKA HAЛИЧИЯ ИCTИHHЫX AДPECOB
C      И CCЫЛOK HA TAБЛИЦY OПИCAHИЙ.
 CHINST:,BSS,
 1,UTC,CTLG
 ,XTA,1
 ,ATX,WBEGFLAG
 ,ATX,WBEGFLAG+1
 ,NTR,3
 3,VTM,-2
 :13,VJM,RDSTAR
 :3,VLM,*-1
 RDINST:,BSS,
 ,XTA,INSTR
 ,ATI,3
 LCHINST:3,VZM,SETDATA
 13,VJM,RDSTAR
 ,ATX,CЛCM
 ,ASN,64+24
 ,ATX,WORD
 CHWORD:,BSS,
 ,XTA,WORD
 ,UZA,ECHWORD
 ,ASN,64+19
 ,AAX,=1.0-KOPOTKAЯ CTP.
 ,ATI,14
 ,XTA,WORD
 14,APX,MP1
 14,AUX,MU2
 ,ATX,LOC.KOMAHДA
 ,XTA,FLMACRO.ИЩEM
 ,UZA,CHMACRO.ЭKCTPAKOДOB
 ,XTA,WORD
 14,AAX,M4S
 ,ATX,ADR.AДPEC
 14,A-X,MAXADR
 ,U1A,NODECL
 10,VTM,USEDECL
 11,VJM,CCHATO
 NODECL:,BSS,
 ,XTA,FLADR.ИCT.AДPECA
 ,U1A,ECHWORD.HE HYЖHЫ.
C      HE MOДИФИЦИPYETCЯ ЛИ ПPEДЫД. KOMAHДOЙ
 ,XTA,ПPEДKOM
 ,AAX,=0370 0000
 ,AEX,=0220 0000.UTC
 ,UZA,CHMADR
 ,AEX,BIT16.WTC
 ,U1A,NONMOD.HE MOД-CЯ
 CHMADR:,BSS,
 ,XTA,ПPEДKOM
 ,AAX,=7407 7777
 ,U1A,ECHWORD.MOД-CЯ
 NONMOD:,BSS,
 ,XTA,LOC
 ,AAX,=37
 ,ATI,13
 ,XTA,LOC
 ,ASN,64+5
 ,ATI,12
 12,XTA,MEMREFTB
 13,ASN,64
 ,AAX,=1
 ,UZA,ECHWORD
C      AДPECYETCЯ ПAMЯTЬ.
 ,XTA,WORD
 ,AAX,=7400 0000.ИMEETCЯ
 ,U1A,ECHWORD.ИHД. PEГИCTP
 ,XTA,ADR
 ,U1A,NOZADR
C        ПPOBEPKA HA ЭKCTPAKOД
 ,XTA,LOC
 ,A-X,=50
 ,U1A,ECHWORD
 ,A-X,=30
 ,UZA,ECHWORD
 NOZADR:,XTA,ADR
 14,A-X,MAXADR
 ,U1A,NOREF
 ,ATI,11
 ,UTC,BUFDEF
 11,XTA,-1
 ,ATX,INFW
 ,AAX,=4000 0000.B24
 ,UZA,ECHWORD
 ,XTA,INFW
 ,AAX,=1700 0000
 ,U1A,ECHWORD
 ,XTA,INFW
 ,AAX,M3L
 ,UJ,WRADR
 NOREF:,XTA,ADR
 14,A-X,MAXRADR
 ,UZA,ECHWORD
 ,XTA,ADR
 WRADR:,BSS,
 ,ASN,64-7
 ,AOX,LOC
 ,ATX,RADR
 ,XTA,RACNT
 ,ATI,10
 11,VTM,
C      ПPOBEPKA HAЛИЧИЯ B RACAT
 CHRACAT:10,VZM,ECHRCAT
 ,UTC,RACAT
 11,XTA,
 ,AEX,RADR
 ,UZA,SETRA
 10,UTM,-1
 11,UTM,1
 ,UJ,CHRACAT
C      ЗAHOCИM B RACAT
 ECHRCAT:,XTA,RADR
 ,UTC,RACAT
 11,ATX,
 ,XTA,RACNT
 ,ARX,=1
 ,ATX,RACNT
 SETRA:,ITA,11
 9,VTM,RAECAT
 10,VTM,RADSHIFT
 11,VTM,RFPLE
 13,VJM,WRECAT
 ECHWORD:,BSS,
 ,XTA,WORD
 ,ATX,ПPEДKOM
 ,XTA,CЛCM
 ,UZA,ERDPRG
 ,ATX,WORD
 ,XTA,
 ,ATX,CЛCM
 ,UJ,CHWORD
 ERDPRG:,BSS,
 3,UTM,-1
 ,UJ,LCHINST
 CCHATO:,BSS,
 15,ATX,
 ,AAX,=37
 ,X-A,
 ,STI,13.B L. CДBИГA
 ,ASN,64+5
 ,AAX,=37
 ,ATI,12.N CЛOBA
 ,XTA,=1
 13,ASN,64
 10,UTC,
 12,AOX,
 10,UTC,
 12,ATX,
 11,UJ,
 CCИЗTO:15,ATX,
 ,AAX,=37
 ,STI,13
 ,ASN,64+5
 ,AAX,=37
 ,ATI,12
 CCИЗTO1:,BSS,
 10,UTC,
 12,XTA,
 13,ASN,64-11
 ,AAX,BIT12
 11,UJ,
C       CБOP ИCПOЛЬЗYEMЫX ЭKCTPAKOДOB
 CHMACRO:,BSS,
 ,XTA,LOC
 ,A-X,=50
 ,U1A,ECHWORD
 ,A-X,=30
 ,UZA,ECHWORD
 ,ITA,3.AДPEC
 ,X-A,INSTR.KOMAHДЫ.
 ,XTS,LOC.KOMAHДA.
 ,ASN,64-15
 15,AOX,
 ,WTC,MACROCNT
 ,UTC,MACROTB
 ,ATX,
 ,XTA,MACROCNT
 ,ARX,=1
 ,ATX,MACROCNT
 ,UJ,ECHWORD
C        ПPOПYCK DATA И KOHCTAHT
 SETDATA:,BSS,
C       ДЛЯ ЭKCTPAKOДOB И ИCT.AДPECOB BЫXOД ИЗ CM.
 ,XTA,FLMACRO
 ,UZA,ENDCALL
 ,XTA,FLADR
 ,UZA,ENDCALL
 ,XTA,SET
 ,UZA,REFUPP
 ,ATI,3
 ,XTA,DATACONS
 ,ATI,2
 :2,VZM,LSCHSET
 13,VJM,RDSTAR
 2,UTM,-1
 ,UJ,*-1
C       PAЗБOPKA SET DATA
 LSCHSET:,BSS,
 3,VZM,REFUPP
 13,VJM,RDSTAR
 ,AAX,M4S
 ,A-X,MAXADR
 ,U1A,ELSCHS
 10,VTM,USESET
 11,VJM,CCHATO
 ELSCHS:3,UTM,-1
 ,UJ,LSCHSET
C       CCЫЛKИ BBEPX B TO
 REFUPP:,BSS,
 ,WTC,LCALLS
 3,VTM,
 LCDEF:,BSS,
 3,VZM,SETCALL
 ,ITA,3
 10,VTM,USEDECL
 11,VJM,CCИЗTO
 ,ATX,FLUSED
 10,VTM,USESET
 11,VJM,CCИЗTO1
 ,ATX,SET
 ,AOX,FLUSED
 ,UZA,ECDEF
 ,UTC,BUFDEF
 3,XTA,-1
 ,ATX,INFW
 ,AAX,=4000 0000.B24
 ,UZA,SETLADR
 ,XTA,INFW
 ,AAX,=1700 0000
 ,UZA,ECDEF.ИCTИHHOE ЗHAЧ
 ,AEX,=100 0000.B19
 ,UZA,ECDEF.OTH. AДPEC
 ,AEX,=1000 0000.B22
 ,UZA,ECDEF.OTH. AДP. WEQ
 ,AEX,=300 0000
 ,UZA,SETBREF.ECTЬ 20,22,24 P
 ,AAX,=700 0000
 ,AEX,=500 0000
 ,U1A,ECDEF.MACCИBЫ
C       OCTAЛCЯ COMMON
 SETBREF:,BSS,
 ,XTA,INFW
 ,AAX,BIT16
 ,UZA,ECDEF
 ,XTA,FLUSED
 ,UZA,SETREFS
 ,XTA,INFW
 10,VTM,USEDECL
 11,VJM,CCHATO
 SETREFS:,XTA,SET
 ,UZA,ECDEF
 ,XTA,INFW
 10,VTM,USESET
 11,VJM,CCHATO
 ECDEF:,BSS,
 3,UTM,-1
 ,UJ,LCDEF
 SETLADR:,XTA,INFW
 ,ASN,64+24
 ,ATX,WORD
 ,XTA,FLUSED
 ,UZA,SETLADRS
 ,XTA,WORD
 10,VTM,USEDECL
 11,VJM,CCHATO
 SETLADRS:,XTA,SET
 ,UZA,SETBREF
 ,XTA,WORD
 10,VTM,USESET
 11,VTM,SETBREF
 ,UJ,CCHATO
 SETCALL:,XTA,LCALLS
 ,ATI,3
 2,VTM,
C       PAЗБOPKA CCЫЛOK
 CHDEF:,BSS,
 3,VZM,ENDCALL
 2,UTM,1
C      1 B FLUSED,ECЛИ OБ'EKT ИCПOЛЗOBAH B П/П
 ,ITA,2
 10,VTM,USEDECL
 11,VJM,CCИЗTO
 ,ATX,FLUSED
 ,ITA,2
 10,VTM,USESET
 11,VJM,CCИЗTO
 ,ASN,64+1
 ,ATX,SET
 3,UTM,-1
 2,UTC,BUFDEF
 9,VTM,-1
 9,XTA,
 ,ATX,INFW
 ,AAX,=1700 0000
 ,ATX,B19/22
 ,AEX,=200 0000
 ,UZA,SUB
 ,AEX,=100 0000
 ,UZA,SUB
 ,AEX,=400 0000
 ,AAX,=700 0000
 ,U1A,CHDEF
 13,VJM,ID.COMMON
 ,XTA,CMCNT
 ,ATI,10
 11,VTM,
C       ПPOBEPKA HAЛИЧИЯ B COMCAT
 CHCOM:,BSS,
 10,VZM,ENDCCAT
 ,UTC,COMCAT
 11,XTA,
 ,AEX,WORD
 ,UZA,SETINF
 10,UTM,-1
 11,UTM,1
 ,UJ,CHCOM
C       ЗAHOCИM B COMCAT
 ENDCCAT:,XTA,WORD
 ,UTC,COMCAT
 11,ATX,
 ,XTA,CMCNT
 ,ARX,=1
 ,ATX,CMCNT
 SETINF:,XTA,INFW
 ,AAX,BIT16
 ,U1A,CLEADR
 ,XTA,INFW
 SETINF1:,BSS,
 ,ASN,64-15
 ,ITS,11
 15,AOX,
 ,AOX,FLUSED
 ,AOX,SET
 ,WTC,CECNT
 ,UTC,CECAT
 ,ATX,
 ,XTA,CECNT
 ,ARX,=1
 ,ATX,CECNT
 ,UJ,CHDEF
C      ДЛИHA OПPEДEЛEHA HEЯBHO
 CLEADR:,BSS,
 ,XTA,INFW
 ,ASN,64+15
 ,ASN,64-15
 ,UJ,SETINF1
C       ПOMEЧAEM CCЫЛKИ BHИЗ B TO
C
 SUB:13,VJM,ID.SUBPROGRAM
 13,VJM,SCHC
 ,UZA,INCAT
C       BHEШHЯЯ ПOДПPOГPAMMA
 ,XTA,SBCNT
 ,ATI,10
 11,VTM,
C ПPOBEPKA HAЛИЧИЯ B SEXTCAT
 CHSB:10,VZM,ENDSBCAT
 ,UTC,SEXTCAT
 11,XTA,
 ,AEX,WORD
 ,UZA,SETSUB.ECTЬ
 10,UTM,-1
 11,UTM,1
 ,UJ,CHSB
C       ЗAHOCИM B SEXTCAT
 ENDSBCAT:,XTA,WORD
 ,UTC,SEXTCAT
 11,ATX,
 ,XTA,SBCNT
 ,ARX,=1
 ,ATX,SBCNT
C       CCЫЛKY B SEXECAT
 SETSUB:,ITA,11
 9,VTM,SEXECAT
 10,VTM,EDSHIFT
 11,VTM,EFPLE
 13,VTM,CHDEF
 ,UJ,WRECAT
C       ПOИCK П/П B KATAЛOГE
 SCHC:,XTA,LCAT
 ,ATI,10
 9,VTM,
 LSCH:,UTC,CTLG+2
 9,XTA,
 ,AEX,WORD
 13,UZA,
 10,UTM,-1
 9,UTM,2
 10,VIM,LSCH
 13,UJ,
C       ЗAHECEHИE N CCЫЛAEMOЙ П/П B E KATAЛOГ
 WRECAT:10,WTC,
 12,VTM,
 ,AOX,FLUSED
 12,ASN,64
 11,XTS,
 15,ATX,
 ,ARX,=1
 11,STX,
 ,ASN,64+2
 ,STI,14
 9,J+M,14
 14,AOX,
 14,ATX,
 ,ITA,12
  ,AEX,=77734
 ,UZA,RETWRE
 12,UTM,-12
 ,ITA,12
 RETWRE:10,ATX,
 13,UJ,
C       ИЗBЛEЧEHИE HAЗBAHИЯ
 ID:9,XTA,
 ,ASN,64-25
 ,ARX,
 ,UZA,D1
 9,XTA,
 ,AAX,=:7777 7777
 D2:,ATX,WORD
 13,UJ,
 D1:9,XTA,
 ,ASN,64+24
 ,ATI,9
 9,UTM,-4001B
 ,UTC,BUFDEF
 9,XTA,
 ,UJ,D2
C       CCЫЛKA HA БИБЛИOT. П/П
 INCAT:,ITA,9
 9,VTM,SECAT
 10,VTM,DSHIFT
 11,VTM,FPLE
 13,VTM,CHDEF
 ,UJ,WRECAT
C       CCЫЛKИ HA ЭKCTPAKOДЫ.
 TORMTB:,XTA,MACROCNT
 ,A-X,MACRCNT1
 ,ASN,64-15
 ,UZA,ENDCH
 ,AOX,MACRCNT1.AДPEC
 ,UTC,RMACRTB
 9,ATX,
 ,UJ,ENDCH
C       KOЛ-BO И AДPECA CCЫЛOK B R KATAЛOГИ
 ENDCALL:,NTR,3
 ,ITA,1
 ,ASN,64+1
 ,ATI,9
 9,UTM,-1
 ,XTA,FLMACRO
 ,UZA,TORMTB
C      ПPИЗHAK SET DATA
 ,XTA,SET
 ,UZA,NOSET
 ,XTA,BIT48
 NOSET:,BSS,
C      CCЫЛKИ HA ИCTИHHЫE AДPECA
 ,XTS,RFPLE
 ,A-X,RFPLE1
 ,AAX,M11B
 ,ASN,64-12
 ,AOX,RFPLE1
 ,ASN,64-24
 15,AOX,
 ,UTC,CRCAT
 9,ATX,
 ,XTA,FLADR
 ,UZA,ENDCH
C       CCЫЛKИ HA COMMON'Ы
 ,XTA,CECNT
 ,A-X,CECNT1
 ,AAX,M3L
 ,UZA,CHESUB.HET CCЫЛOK
 ,ASN,64-12
 ,AOX,CECNT1
 ,UTC,CRCAT
 9,AOX,
 ,UTC,CRCAT
 9,ATX,
 CHESUB:,BSS,
C       CCЫЛKИ HA BHEШHИE П/П
 ,XTA,EFPLE
 ,A-X,EFPLE1
 ,AAX,M3L
 ,UZA,CHSUB
 ,ASN,64-12
 ,AOX,EFPLE1
 ,ASN,64-24
 ,UTC,SRCAT
 9,ATX,
C      CCЫЛKИ HA БИБЛИOT. П/П
 CHSUB:,XTA,FPLE
 ,A-X,FPLE1
 ,AAX,M3L
 ,UZA,ENDCH
 ,ASN,64-12
 ,AOX,FPLE1
 9,UTM,SRCAT
 9,AOX,
 9 ,ATX,
 ENDCH:,BSS,
 1,UTM,-2
 1,V1M,SETREF
C
C       HAЧИHAEM ПEЧATATЬ
C------------------------------
C
 BUF:,EQU,PRINT+5
 TOPR:C,VTM,1
 13,VJM,BORDE.YПOPЯД. KATAЛOГA
 ,XTA,FLADR
 ,UZA,BEGPR5TB
 ,XTA,FLMACRO
 ,UZA,BEGPR6TB
 ,XTA,FLBACK
 ,UZA,BEGPR2TB
C
C       ПEЧATЬ 1OЙ TAБЛИЦЫ
C------------------------------
C
 R,VTM,REF1
 GR,VJM,PRHEAD
 C,VTM,2
 R,VTM,COMNT1
 GR,VJM,PRCOMNT
 PR1TB:,BSS,
 ,NTR,3
 NP,WTC,ORDER
 C,VTM,
 C,XTA,
 12,VTM,
 13,VJM,TOISO
 C,XTA,1
 ,AAX,=:2
 GR,VTM,ENDSTR
 ,UZA,ENTRY
C       ГЛABHЫЙ BXOД
 F,VTM,
 C,UTC,
 14,VTM,-CTLG-2
 ,ITA,14.N BXOДA B KAT.
 ,ASN,64+1
 ,ATI,A
C       ПPOBEPKA HA SET DATA
 ,UTC,CRCAT
 A,XTA,
 ,ARX,
 ,U1A,NOPRSETD
 ,XTA,BUF+1
 ,AAX,=:7777 7777
 ,AOX,=6H''''''D
 ,ATX,BUF+1
 NOPRSETD:,BSS,
 ,UTC,SRCAT
 A,XTA,
 13,VTM,ENDSTR
 ,UZA,PRSTR.CCЫЛOK HET
 ,ATX,WORD
 14,VTM,CTLG+2
 9,VTM,SECAT
 R,VJM,SUMREF
 ,XTA,FLSH
 ,UZA,PRREF
C       CБOP BHEШHИX П/П
 ,UTC,SRCAT
 A,XTA,
 ,ASN,64+24
 ,UZA,PRREF.HET CCЫЛOK
 ,ATX,WORD
 9,VTM,SEXECAT
 14,VTM,SEXTCAT
 R,VTM,PRREF
 ,UJ,SUMREF
C       ПEЧATЬ ЗAГOЛOBKA
 PRHEAD:,BSS,
 ,XTA,
 ,CALL,DATAPR*
 R,MTJ,14
 ,ITS,14
 14,UTM,4
 A,VJM,HEAD+1
 GR,MTJ,13
 NP,VTM,
 ,UJ,BEGPR
C       ENTRY-BXOД
 ENTRY:,BSS,
 C,XTA,
 ,ASN,64-36
 ,AUX,MASK
 ,ATX,BUF+1
 SCHM:C,XTA,-1
 ,AAX,=:2
 C,UTM,-2
 ,UIA,MENT
 ,UJ,SCHM
 MENT:C,XTA,
 ,AUX,=37477
 ,AOX,BUF+1
 R,VJM,TRNSL
 ,ARX,=2000 000
 ,ATX,BUF+1
 C,XTA,
 ,ASN,64-12
 ,AUX,MASK
 R,VJM,TRNSL
 ,ATX,BUF+2
 ,XTA,=6H)
 ,ATX,BUF+3
 13,VJM,PRSTR
 GR,UJ,
C       ПEPEBOД ИЗ TEXT B ISO ЗAПИCЬ B BUF
 TOISO:,BSS,
 15,ATX,
 ,AUX,MASK
 R,VJM,TRNSL
 12,STX,BUF
 ,ASN,64-36
 ,AUX,MASK
 R,VJM,TRNSL
 12,ATX,BUF+1
 13,UJ,
C       BЫДAЧA CCЫЛKИ ИЗ EKATAЛOГA
 UNPREF:,BSS,
 15,ATX,
 ,ASN,64+2
 ,STI,10
 ,AAX,=3
 ,ATI,14
 12,VTM,
 :14,VZM,TKW
 14,UTM,-1
 12,UTM,12
 ,UJ,*-1
 TKW:9,UTC,
 10,XTA,
 12,ASN,64
 15,ATX,
 ,AAX,BIT12
 ,ASN,64-36
 ,STX,FLUSED
 ,AAX,M11B
 13,UJ,
C       CБOP CCЫЛOK HA П/П
 SUMREF:,ITA,14
 ,ATX,LOC.AДP. KATAЛOГA
 ,XTA,WORD.CЛOBO ИЗ RCAT
 ,ASN,64+12
 ,AAX,M4S
 R,UZA,
 ,ATI,GR.KOЛ-BO
 ,XTA,WORD
 ,AAX,M4S
 ,ATI,S.AДPEC
  LTREF:,ITA,S
 13,VJM,UNPREF
 ,ATI,14
 ,WTC,LOC
 14,UTM,
 ,ITA,14
 ,AOX,FLUSED
 ,UTC,BUFFER
 F,ATX,
 F,UTM,1
 S,UTM,1
 GR,UTM,-1
 GR,VIM,LTREF
 R,UJ,
C       YПOPЯДOЧИBAHИE CCЫЛOK HA П/П
 PRREF:13,VTM,ENDSTR
 F,VZM,PRSTR
 F,MTJ,9
 10,VTM,BUFFER
 A,VTM,
 13,VJM,YPOR
C       ПEЧATЬ CCЫЛOK
 LPRREF:A,WTC,BUFFER
 ,XTA,
 12,VTM,2
 13,VJM,TOISO
C       HE BHEШHЯЯ ЛИ
 ,NTR,3
 14,VTM,SEXTCAT
 ,ITA,14
 A,UTC,BUFFER
 ,XTS,
 ,AAX,M3L
 15,A-X,
 ,U1A,ECHEXT
 ,X-A,=2000
 ,U1A,ECHEXT
 ,XTA,BUF+1
 ,AAX,=:7777 7777 776
 ,AOX,=6H''''''''E
 ,ATX,BUF+1
 ECHEXT:,BSS,
 A,UTM,1
 13,VJM,PRSTR
 F,UTM,-1
 F,VIM,LPRREF
C       CЛEДYЮЩИЙ BXOД
 ENDSTR:NP,UTM,1
 ,ITA,NP
 ,AEX,LCAT
 ,UIA,PR1TB
 13,VJM,ENDPR
C
C       ПEЧATЬ 2 OЙ TAБЛИЦЫ
C------------------------------
C
 BEGPR2TB:,BSS,
 R,VTM,REF2
 GR,VJM,PRHEAD
 C,VTM,3
 R,VTM,COMNT2
 GR,VJM,PRCOMNT
 A,VTM,PRHEИCП
 R,VTM,HEИCП
 PRSUBH:14,VTM,-3
 SETSH:R,UTC,
 14,XTA,3
 14,ATX,BUF+3
 14,VLM,SETSH
 13,VJM,PRSTR
 A,MTJ,13
 ,UJ,PRSTR
 COMBCNT:,EQU,STR
 FLEXT:,EQU,STR+1
 CNTW:,EQU,STR+2
 PRHEИCП:,XTA,
 ,ATX,COMBCNT
 ,XTA,=1
 ,ATX,FLEXT
C       PAЗДEЛEHИE BXOДOB HA ИCПOЛЬЗ. И HEИCПOЛЬЗ.
 LPRHEИCП:NP,WTC,ORDER
 14,VTM,-CTLG-2
 ,ITA,14
 ,UJ,SCHREF
C       ЗAHECEHИE ИCПOЛЬЗ. BXOДOB
 TOCOMBOR:NP,UTC,ORDER
 ,XTA,
 ,WTC,COMBCNT
 ,UTC,COMBORD
 ,ATX,
 ,XTA,COMBCNT
 ,ARX,=1
 ,ATX,COMBCNT
 ,UJ,ENDLPHП
C       ПPOBEPKA COBПAДEHИЯ C HAЗBAHИEM COMMONA
 CHEQCOM:,XTA,CMCNT
 13,UZA,
 ,ATI,14
 LCHEQ:,UTC,COMCAT
 14,XTA,-1
 ,AEX,WORD
 ,UZA,EQCOM
 14,UTM,-1
 14,V1M,LCHEQ
 13,UJ,
 EQCOM:,XTA,BUF+1
 ,AAX,=:7777 7777
 ,AOX,=6H''''''C
 ,ATX,BUF+1
 13,UJ,
C
 SETENT:,U1A,TOCOMBOR.ИCПOЛЬЗ-CЯ
 NP,WTC,ORDER
 ,XTA,
 ,ATX,WORD
 12,VTM,
 13,VJM,TOISO
 13,VJM,CHEQCOM
 13,VJM,PRSTR
 ENDLPHП:NP,UTM,1
 ,ITA,NP
 ,AEX,LCAT
 ,U1A,LPRHEИCП
 R,VTM,PRИCП
 13,VJM,PRSTR
 NEWPAGE:,BSS,
 ,NTR,3
 ,XTA,MXLENG
 ,A-X,ALE
 ,A-X,=24
 R,MTJ,13
 ,UZA,PRSTR
C       BЫBOД HA HAЧAЛO CTPAHИЦЫ
 :13,VJM,PRSTR
 ,XTA,ALE
 ,U1A,*-1
 R,UJ,
 PRИCП:,BSS,
 ,XTA,COMBCNT
 ,ATI,9
 ,XTA,FLSH
 ,UZA,ENDRW
 ,XTA,SBCNT
 ,ATI,12
C       ПEPEПИCЬ BHEШHИX П/П
 LRW:12,VZM,ENDRW
 12,UTC,SEXTCAT
 14,VTM,-1
 ,ITA,14
 ,UTC,COMBORD
 9,ATX,
 9,UTM,1
 12,UTM,-1
 ,UJ,LRW
 ENDRW:,BSS,
 ,ITA,9
 ,ATX,COMBCNT
 ,UZA,BEGPR3TB
 NP,VTM,
 ,XTA,FLSH
 ,UZA,TOPRSH
 10,VTM,COMBORD
 13,VJM,YPOR
 TOPRSH:,BSS,
 R,VTM,ИCП
 A,VJM,PRSUBH
 ,XTA,
 ,ATX,FLEXT
 PR2TB:NP,WTC,COMBORD
 C,VTM,
 C,XTA,
 ,ATX,WORD
 12,VTM,
 13,VJM,TOISO
 F,VTM,
 ,XTA,FLSH
 ,UZA,NOEXT
C       OПPEДEЛЯEM BHEШHЮЮ П/П
 ,NTR,3
 14,VTM,SEXTCAT
 ,ITA,14
 NP,UTC,COMBORD
 ,X-A,
 ,U1A,NOEXT
 ,X-A,=2000
 ,U1A,NOEXT
 ,XTS,BUF+1
 ,AAX,=:7777 7777
 ,AOX,=6H''''''E
 ,STX,BUF+1
 ,X-A,=2000
 9,VTM,SEXECAT
 C,VTM,24
 ,UJ,SCHREFE
 NOEXT:,BSS,
 C,UTM,-CTLG-2
 ,ITA,C
 SCHREF:,BSS,
 9,VTM,SECAT
 C,VTM,
 SCHREFE:,BSS,
 ,ATX,LOC.N BXOДA
 A,VTM,
C       ПOИCK CCЫЛAEMЫX ПPOГPAMM
 CHREF:A,WTC,ORDER
 14,VTM,-CTLG-2
 ,ITA,14
 ,ASN,64+1
 ,ATI,14
 ,UTC,SRCAT
 14,XTA,
 ,UZA,CHNW
 C,ASN,64+12
 ,AAX,M4S
 ,UZA,CHNW.HET CCЫЛOK
 ,ATI,GR.KOЛ-BO
 ,UTC,SRCAT
 14,XTA,
 C,ASN,64
 ,AAX,M4S
 ,ATI,S.AДPEC
 LTREF2:,ITA,S
 13,VJM,UNPREF
 ,AEX,LOC
 ,U1A,NEXTREF
 ,XTA,FLEXT
 ,U1A,SETENT
 ,UJ,TOTAB
 NEXTREF:,BSS,
 S,UTM,1
 GR,UTM,-1
 GR,VIM,LTREF2
 CHNW:A,UTM,1
 ,ITA,A
 ,AEX,LCAT
 ,UIA,CHREF
 ,WTC,FLEXT
 14,VTM,
 14,V1M,SETENT
 13,VTM,ENDSTR2
 F,VZM,PRSTR
 13,VJM,CHEQCOM
 A,VTM,
C       ПEЧATЬ CПИCKA CCЫЛAEMЫX ПPГPAMM
 LPR2:A,WTC,BUFFER
 ,XTA,
 12,VTM,2
 13,VJM,TOISO
C      N - П/П OПИCAHA HO HE ИCПOЛЬЗYETCЯ
 A,UTC,BUFFER
 ,XTA,
 ,ARX,
 ,UZA,USED
 ,XTA,BUF+1
 ,AAX,=:7777 7777 776
 ,AOX,=6H''''''''N
 ,ATX,BUF+1
 USED:,BSS,
 A,UTM,1
 13,VJM,PRSTR
 F,UTM,-1
 F,VIM,LPR2
 ENDSTR2:NP,UTM,1
 ,ITA,NP
 ,AEX,COMBCNT
 ,UIA,PR2TB
 13,VJM,ENDPR
C
C       ПEЧATЬ  3 EЙ TAБЛИЦЫ
C------------------------------
C
 BEGPR3TB:,BSS,
 ,XTA,CMCNT
 ,UZA,RET.COMMON'OB HET
 ,XTA,FLBACK
 ,UZA,BEGPR4TB
 R,VTM,REF3
 GR,VJM,PRHEAD
 C,VTM,6
 R,VTM,COMNT3
 GR,VJM,PRCOMNT
 PR3TB1:,BSS,
 NP,WTC,ORDER
 C,VTM,
 12,VTM,
 C,XTA,
 13,VJM,TOISO
 C,XTA,1
 ,AAX,=:2
 ,ATX,RA1
 ,U1A,CHCREF
 C,MTJ,13
C       ИЩEM ГЛABHЫЙ BXOД
 :C,UTM,-2
 C,XTA,1
 ,AAX,=:2
 ,UZA,*-1
 CHCREF:C,UTC,
 14,VTM,-CTLG-2
 ,ITA,14
 ,ASN,64+1
 ,ATI,14
 ,UTC,CRCAT
 14,XTA,
 ,AAX,B24/13
 ,UZA,EPR3TB1.HET CCЫЛKИ
 ,XTA,RA1
 ,U1A,PRMENT
 GR,VTM,EPR3TB1
 13,MTJ,C
 ,UJ,ENTRY
C       ГЛABHЫЙ BXOД
 PRMENT:,BSS,
 ,UTC,CRCAT
 14,XTA,
 ,AAX,M4S
 ,ATI,10
 ,UTC,CRCAT
 14,XTA,
 ,ASN,64+12
 ,AAX,M4S
 ,ATI,9
 9,MTJ,A
 11,VTM,
 F,VTM,
 10,UTM,CECAT
 COMORD:,EQU,SRCAT
C       CБOP CCЫЛOK
 SETCOM:10,XTA,
 ,AAX,M10B
 ,ATI,14
 10,XTA,
 ,ASN,64+15
 10,XTS,
 ,AAX,=6000
 ,ASN,64-36
 15,AOX,
 ,UTC,BUFFER
 F,ATX,1
 ,UTC,COMCAT
 14,XTA,
 ,UTC,BUFFER
 F,ATX,
 F,UTC,BUFFER
 14,VTM,
 ,ITA,14
  ,UTC,COMORD
 11,ATX,
 F,UTM,2
 10,UTM,1
 11,UTM,1
 9,UTM,-1
 9,V1M,SETCOM
 ,ITA,11
 ,ATX,COMBCNT
 10,VTM,COMORD
 A,MTJ,9
 13,VJM,YPOR
C       ПEЧATЬ CCЫЛOK
 ,XTA,
 ,ATX,CNTW
 PRCOM:,WTC,CNTW
 ,WTC,COMORD
 A,VTM,-BUFFER
 F,VTM,2
 C,VJM,LPRCOM
 ,XTA,CNTW
 ,ARX,=1
 ,ATX,CNTW
 ,AEX,COMBCNT
 ,U1A,PRCOM
 EPR3TB1:,BSS,
 NP,UTM,1
 ,ITA,NP
 ,AEX,LCAT
 ,U1A,PR3TB1
 13,VJM,ENDPR
C
C       ПEЧATЩ 4-OЙ TAБЛИЦЫ
C------------------------------
C
 BEGPR4TB:,BSS,
 R,VTM,REF4
 GR,VJM,PRHEAD
 C,VTM,7
 R,VTM,COMNT4
 GR,VJM,PRCOMNT
    ,WTC,CMCNT
 9,VTM,
 9,MTJ,A
 10,VTM,
C       YПOPЯДOЧИBAHИE COMMON'OB
 SETADR:10,UTC,COMCAT
 14,VTM,
 ,ITA,14
 ,UTC,COMORD
 10,ATX,
 A,UTM,-1
 10,UTM,1
 A,VIM,SETADR
 10,VTM,COMORD
 13,VJM,YPOR
 PR3TB:NP,WTC,COMORD
 C,VTM,
 C,XTA,
 12,VTM,
 ,ATX,WORD
 13,VJM,TOISO
 F,VTM,
 13,VJM,SCHC
 ,U1A,NODATA
C       ИMEETCЯ BLOCK DATA
 ,XTA,BUF+1
 ,AAX,=:7777 7777 776
 ,AOX,=6H''''''''E
 ,ATX,BUF+1
 NODATA:,BSS,
 C,UTM,-COMCAT
 ,ITA,C
     ,ATX,LOC
 ,XTA,
 ,ATX,LONG
 A,VTM,
C       CБOP CCЫЛAEMЫX П/П
 CHECOM:A,WTC,ORDER
 C,VTM,
 C,UTC,-CTLG-2
 14,VTM,
 ,ITA,14
 ,ASN,64+1
 ,ATI,14
 ,UTC,CRCAT
 14,XTA,
 ,AAX,B24/13
 ,UZA,ECHCOM.HET CCЫЛKИ
 ,ASN,64+12
 ,ATI,GR.KOЛ-BO
 ,UTC,CRCAT
 14,XTA,
 ,AAX,M4S
 ,ATI,S.AДPEC
 S,UTM,CECAT
 LTREF3:S,XTA,
 ,AEX,LOC
 ,AAX,M10B
 ,UZA,FIX.ECTЬ CCЫЛKA
 S,UTM,1
 GR,UTM,-1
 GR,VIM,LTREF3
 ECHCOM: A,UTM,1
 ,ITA,A
 ,AEX,LCAT
 ,UIA,CHECOM
 ,XTA,LONG.MAX. ДЛИHA
 ,AAX,BIT16
 13,VTM,SETMLONG
 ,U1A,NOLONG
 ,XTA,LONG
 ,CALL,OCTTDEC
 ,ASN,64-8
 ,AOX,=40
 SETMLONG:,BSS,
 ,ATX,BUF+2
 13,VJM,PRSTR.ПEЧATЬ COMMON'A
 C,VTM,EPRCOM
 A,VTM,
C       ПEЧATЬ CCЫЛOK
 LPRCOM:,UTC,BUFFER
 A,XTA,
 12,VTM,3
 13,VJM,TOISO
 ,XTA,=6H'''''' ''''
 ,ATX,WORD
 ,UTC,BUFFER
 A,XTA,1
 ,ATX,LONG
 ,ASN,64-1
 ,ARX,
 ,U1A,CHNOUSE
 ,XTA,=6H''''''D''''
 ,UJ,TOWORD
 CHNOUSE:,XTA,LONG
 ,ARX,
 ,UZA,CUSED
 ,XTA,=6H''''''N''''
 TOWORD:,BSS,
 ,ATX,WORD
 ,XTA,LONG
 CUSED:,AAX,BIT16
 13,VTM,SETLONG
 ,U1A,NOLONG
 ,XTA,LONG
 ,AAX,M3L
 ,CALL,OCTTDEC
 ,ASN,64-8
 ,AOX,=40
 SETLONG:,BSS,
  ,ATX,BUF+2
 14,VTM,=6H''''''''S .    S
 ,XTA,LONG
 ,AAX,=100 00000
 ,UIA,TYPEC.CEKTOPHЫЙ
 14,VTM,=6H''''''''P .    P
 ,XTA,LONG
 ,AAX,=4 00000
 ,UIA,TYPEC.CTPAHИЧHЫЙ
 14,VTM,=6H''''''''L .    L
 TYPEC:,BSS,
 ,XTA,BUF+1
 ,AAX,=:7777 7777
 14,AOX,
 ,AOX,WORD
 ,ATX,BUF+1
 A,UTM,2
 13,VJM,PRSTR
 F,UTM,-2
 F,VIM,LPRCOM
 C,UJ,
C
C      ДЛИHA COMMON'A OПPEДEЛEHA HEЯBHO
 NOLONG:,BSS,
 ,XTA,=6H*****
 13,UJ,
C
 EPRCOM:,BSS,
 NP,UTM,1
 ,ITA,NP
 ,AEX,CMCNT
 ,UIA,PR3TB
 13,VJM,ENDPR
C
C      ПEЧATЬ 5-OЙ TAБЛИЦЫ.
C------------------------------
C
 BEGPR5TB:,BSS,
 ,XTA,RACNT
 ,UZA,RET
 ,ATI,9
 11,VTM,
 R,VTM,REF5
 GR,VJM,PRHEAD
 10,VTM,
 14,VTM,RACAT
 ,NTR,3
 SCH*63:,BSS,
 14,XTA,
 ,AAX,=177
 ,A-X,=50
 ,U1A,NO*63
 ,A-X,=30
 ,UZA,NO*63
C      AДPEC B *63
 ,ITA,14
 ,UTC,COMBORD
 10,ATX,
 10,UTM,1
 ESCH*63:14,UTM,1
 9,UTM,-1
 9,V1M,SCH*63
 N*63:,EQU,WBEGFLAG
 NMATR:,EQU,WBEGFLAG+1
 ,ITA,11
 ,ATX,NMATR
 ,ITA,10
 ,ATX,N*63
 ,UZA,PRMATR
 10,MTJ,9
 10,VTM,COMBORD
 ,ITA,10
 ,ATX,ADR
 ,ATX,FPLE
 13,VJM,SORT
 R,VTM,ИCП*63
 A,VJM,PRSUBH
C      ПEЧATЬ OДHOЙ ПOДTAБЛЦЫ
 PR5TB1:,BSS,
 ,XTA,BIT48
 ,ATX,INSTR
 LPR5TB1:,BSS,
 ,WTC,ADR
 NP,WTC,
 A,VTM,
 A,XTA,
 ,AEX,INSTR
  ,ASN,64+7
 ,UZA,NOPRADR.TOT ЖE AДPEC
 A,XTA,
 ,ASN,64-26
 ,AUX,=6H'7''7''7''7''7'''
 ,AOX,=6H00000
 ,ATX,BUF
 NOPRADR:,BSS,
 A,XTA,
 ,ASN,64+6
 ,AAX,=1
 ,ATI,14
 A,XTA,
 14,APX,MU2
 14,AUX,MU1
 14,AOX,MO
 ,ATX,BUF+1
 A,UTC,
 14,VTM,-RACAT
 ,ITA,14
 ,ATX,LOC.N B RACAT
 A,VTM,
 F,VTM,
 9,VTM,RAECAT
C      ПOИCK CCЫЛAЮЩИXCЯ П/П
 SCHRRA:,BSS,
 A,WTC,ORDER
 14,VTM,-CTLG-2
 ,ITA,14
 ,ASN,64+1
 ,ATI,14
 ,UTC,CRCAT
 14,XTA,
 ,ASN,64+24
 ,AAX,M4S
 ,ATI,S.AДPEC B REACAT
 ,UTC,CRCAT
 14,XTA,
 ,ASN,64+36
 ,AAX,M11B
 ,ATI,GR.KOЛ-BO
 ,UZA,ESCHRA
 LTAKERA:,BSS,
 ,ITA,S
 13,VJM,UNPREF
 ,AEX,LOC
 ,U1A,ELTAK
 A,UTC,ORDER
 ,XTA,
 ,UTC,BUFFER
 F,ATX,
 F,UTM,1
 ,UJ,ESCHRA
 ELTAK:S,UTM,1
 GR,UTM,-1
 GR,V1M,LTAKERA
 ESCHRA:A,UTM,1
 ,ITA,A
 ,AEX,LCAT
 ,U1A,SCHRRA
 13,VTM,EPR5
 F,VZM,PRSTR
C      ПEЧATЬ CCЫЛOK HA OДИH AДPEC
 A,VTM,
 LPR5:A,WTC,BUFFER
 ,XTA,
 12,VTM,2
 13,VJM,TOISO
 A,UTM,1
 13,VJM,PRSTR
 F,UTM,-1
 F,V1M,LPR5
 EPR5:NP,UTM,1
 ,ITA,NP
 ,AEX,N*63
 ,U1A,LPR5TB1
 13,VTM,RET
 ,XTA,FPLE
 ,UZA,ENDPR
 NP,VTM,
C      ПEЧATЬ ИCTИHHЫX MAT. AДPECOB.
 PRMATR:,BSS,
 13,VTM,RET
 ,XTA,NMATR
 ,UZA,ENDPR
 ,XTA,N*63
 ,UZA,SETCNTS
 R,VJM,NEWPAGE
 SETCNTS:10,VTM,COMORD
 ,ITA,10
 ,ATX,ADR
 ,XTA,NMATR
 ,ATX,N*63
 ,ATI,9
 13,VJM,SORT
 R,VTM,ИCПNO*63
 A,VJM,PRSUBH
 ,XTA,
 ,ATX,FPLE
 ,UJ,PR5TB1
 NO*63:,ITA,14
 ,UTC,COMORD
 11,ATX,
 11,UTM,1
 ,UJ,ESCH*63
C
C      YПOPЯДOЧИBAHИE ИCTИHHЫX AДPECOB
 SORT:,BSS,
 ,NTR,3
 YПП:9,MTJ,12
 12,UTM,-1
 13,UTC,
 12,VZM,
 10,J+M,12.AДPEC
 ,ITA,12.ПPEДПOCЛEДHEГO
 ,ATX,N9.ЭЛEMEHTA.
 10,MTJ,11
 LYПП:11,WTC,
 ,XTA,
 11,WTC,1
 ,A-X,
 ,U1A,EYПП
 11,XTA,
 11,XTS,1
 11,STX,
 11,ATX,1
 EYПП:11,UTM,1
 ,ITA,11
 ,AEX,N9
 ,U1A,LYПП
 9,UTM,-1
 ,UJ,YПП
 PRCOMNT:12,VTM,-4
 13,VJM,PRSTR
 LPRCMNT:R,UTC,
 12,XTA,4
 ,UTC,BUF
 12,ATX,4
 12,VLM,LPRCMNT
 13,VJM,PRSTR
 R,UTM,5
 12,VTM,-4
 C,UTM,-1
 C,V1M,LPRCMNT
 GR,MTJ,13
 ,UJ,PRSTR
C
C       ПEЧATЬ 6-OЙTAБЛИЦЫ.
C---------------------------------
C
 BEGPR6TB:,BSS,
 ,XTA,MACROCNT
 ,UZA,RET
 R,VTM,REF6
 GR,VJM,PRHEAD
 PR6TB1:,BSS,
 ,XTA,
 ,ATX,INSTR
 NP,WTC,ORDER
 C,VTM,
 12,VTM,
 C,XTA,
 13,VJM,TOISO
 C,XTA,1
 ,AAX,=:2
 ,ATX,RA1
 ,U1A,MENTFM
 C,MTJ,13
 :C,UTM,-2
 C,XTA,1
 ,AAX,=:2
 ,UZA,*-1
 MENTFM:C,UTC,
 14,VTM,-CTLG-2
 ,ITA,14
 ,ASN,64+1
 ,ATI,14
 14,UTM,RMACRTB
 14,XTA,
 ,UZA,EPR6TB1.HET CCЫЛKИ
 ,XTA,RA1
 ,U1A,PRMENT6
 GR,VTM,EPR6TB1
 13,MTJ,C
 ,UJ,ENTRY
 PRMENT6:,BSS,
 14,XTA,
 ,ATI,A.AДPEC
 ,ASN,64+15
 ,ATI,F.KOЛ-BO
 A,UTM,MACROTB
 F,MTJ,14
 13,VTM,COMBORD
 SETMADR:,ITA,A
 13,ATX,
 A,UTM,1
 13,UTM,1
 14,UTM,-1
 14,V1M,SETMADR
 10,VTM,COMBORD
 F,MTJ,9
 13,VJM,SORT
 A,VTM,
 LPR6TB:,BSS,
 A,WTC,COMBORD
 14,VTM,
 14,XTA,
 ,ASN,64-33
 ,AUX,=6H'7''7''7''7''7'''
 ,AOX,=6H00000
  ,ATX,BUF+2.AДPEC ЭKCTP.
 14,XTA,
 ,AEX,INSTR
 ,AAX,=77 00000
 ,UZA,NPRINST
 14,XTA,
 ,ATX,INSTR
 ,ASN,64-27
 ,AUX,MU1
 ,AOX,MO
 ,ATX,BUF+3.ЭKCTPAKOД
 NPRINST:,BSS,
 A,UTM,1
 13,VJM,PRSTR
 F,UTM,-1
 F,V1M,LPR6TB
 EPR6TB1:,BSS,
 NP,UTM,1
 ,ITA,NP
 ,AEX,LCAT
 ,U1A,PR6TB1
 13,VTM,RET
 ,UJ,ENDPR
C      CCЫЛ EMЫE ПPOГP. B  BUFFER
 TOTAB:,BSS,
 A,UTC,ORDER
 ,XTA,
 ,AOX,FLUSED
 ,UTC,BUFFER
 F,ATX,
 F,UTM,1
 ,UJ,CHNW
C       ЗAHECEHИE CCЫЛOK HA COMMON'Ы
 FIX:C,XTA,
 ,UTC,BUFFER
 F,ATX,
 S,XTA,
 ,ASN,64+15
 S,XTS,
 ,AAX,=6000
 ,ASN,64-36
 15,AOX,
 ,UTC,BUFFER
 F,ATX,1
 ,AAX,=1 77777
 ,ATX,WORD
 ,NTR,3
C       OПPДEЛЯEM MAX. ДЛИHY COMMON'A
 ,A-X,LONG
 ,UIA,LEST
 ,XTA,WORD
 ,ATX,LONG
 LEST:F,UTM,2
 ,UJ,ECHCOM
C      HAЧAЛЬHAЯ YCTAHOBKA ДЛЯ ПEЧATИ
 BEGPR:,ITA,13
 ,ATX,I13
 TWOSTR:14,VTM,SPACE
 ,ITS,14
 ,ITS,14
 ,XTS,
 ,CALL,PRINT8//
 ,XTA,PL3*5.63 CTPOKИ
 ,ATX,MXLENG
 ,ATX,WC1
 ,XTA,
 ,ATX,ALE
 ,UJ,RETPR
C'       ПEЧATЬ OДHOЙ CTPOKИ
 PRSTR:,ITA,13
 ,ATX,I13
 ,XTA,WC1
 ,UZA,REALPR
 14,VTM,-4
 LTRNF:,UTC,BUF
 14,XTA,4
 ,WTC,ALE
 ,UTC,PRBUF
 14,ATX,4
 14,VLM,LTRNF
 ,XTA,ALE
 ,ARX,=5
 ,ATX,ALE
 ,AEX,MXLENG
 ,UIA,RETPR
 ,ATX,ALE
 ,ATX,WC1
 RETPR:14,VTM,-4
 ,XTA,SPACE
 :,UTC,BUF
 14,ATX,4
 14,VLM,*-1
 ,WTC,I13
 ,UJ,
 REALPR:14,VTM,-4
 BTRN:,WTC,ALE
 ,UTC,PRBUF
 14,XTA,4
 ,UTC,PRINT
 14,ATX,4
 14,VLM,BTRN
 14,VTM,PRINT
 ,ITS,14
 14,UTM,10
 ,ITS,14
 14,VTM,3
 ,ITS,14
 ,CALL,PRINT8//
 ,XTA,ALE
 ,ARX,=5
 ,ATX,ALE
 ,AEX,MXLENG
 ,UIA,RETPR
 ,ATX,ALE
 ,XTA,MXLENG
 ,ATX,WC1
 ,AEX,PL*5.66 CTPOK
 ,UZA,RETPR
 ,XTA,MXLENG
 ,AEX,PL1*5.65 CTPOK
  ,UIA,CH63
 ,XTA,PL*5
 CHNMXL:,ATX,MXLENG
 ,UJ,RETPR
 CH63:,XTA,PL1*5
 ,UJ,CHNMXL
C       ДOПEЧATЫBAHИE БYФEPA
 ENDPR:,ITA,13
 ,XTS,WC1
 ,UZA,PRPRBUF
 ,XTA,ALE
 ,UIA,RWCNT
C       БYФEP ПYCT
 RETEPR:,UTC,1
 ,CALL,PRINT8//
 15,WTC,
 ,UJ,
 PRPRBUF:,WTC,ALE
 ,UTC,PRBUF
 14,VTM,
 ,ITS,14
 14,UTM,4
 ,ITS,14
 14,VTM,3
 ,ITS,14
 ,CALL,PRINT8//
 ,XTA,ALE
 ,ARX,=5
 ,ATX,ALE
 ,AEX,MXLENG
 ,UIA,PRPRBUF
 ,UJ,RETEPR
 RWCNT:,XTA,ALE
 ,ATX,MXLENG
 ,XTA,
 ,ATX,ALE
 ,UJ,PRPRBUF
C ЧTEHИE И ПEPEДЧA CЛOB CM
 RDSTAR:,BSS,
 ,XTA,WBEGFLAG
 ,UZA,READWORD.ПPOДOЛЖEHИE
C       ПEPBOE OБPAЩEHИE ЗA П/П
 ,ITA,
 ,ATX,WBEGFLAG
 ,XTA,MUNP
 ,AEX,=777 0777
 ,ATX,FLTAPE.0 - C MЛ
 ,XTA,WBEGFLAG+1
 ,AEX,DUBLE
 ,AOX,FLTAPE
 ,ATX,FLDBL
 ,XTA,WBEGFLAG+1
 ,ATX,DUBLE
 ,XTA,FLDBL
 ,UZA,RDR
 ,XTA,WBEGFLAG+1
 ,AAX,MESD
 ,AEX,LIBCODE
 ,UZA,RSVP
 ,AEX,LIBCODE
 ,ATX,LIBCODE
 12,VJM,LIBR1
 RETLIBR:,BSS,
 ,XTA,WBEGFLAG+1
 ,AAX,MSEC
 ,ASN,64+36
 NEWZON:,ARX,ABUF
 ,ATX,ADRWORD
 READWORD:,XTA,ADRWORD
 ,AEX,ABUF
 ,UZA,EXIT
 ,AAX,=1777
 ,UZA,READ1
 EXIT:,XTA,ADRWORD
 ,ARX,=1
 ,ATX,ADRWORD
 ,WTC,ADRWORD
 ,XTA,-1
 13,UJ,
 RDR:,XTA,NTRAK
 ,ATX,NTRAK1
 ,UZA,RETLIBR
 ,AOX,NMБ
 12,VJM,*70
 ,XTA,
 ,ATX,NTRAK
 RDR1:,AOX,CRED
 ,AOX,NMБ
 12,VTM,RETLIBR
 ,UJ,*70
 RSVP:,BSS,
 ,XTA,NTRAK1
 ,UZA,RETLIBR
 ,AEX,NTRAK
 ,XTS,
 ,STX,NTRAK
 ,UZA,RETLIBR
 ,XTA,NTRAK1
 ,UJ,RDR1
 READ1:,BSS,
 ,XTA,FLDBL
 ,UZA,READ1D
 ,XTA,LIBCODE
 ,ARX,=1 00000 00000
 12,VJM,LIBR
 ,XTA,
 ,UJ,NEWZON
 READ1D:,XTA,NTRAK
 ,ARX,=1
 ,ATX,NTRAK
 ,AOX,NMБ
 ,AOX,CRED
 12,VJM,*70
 ,XTA,
 ,UJ,NEWZON
 LIBR:,ATX,LIBCODE
 ,XTA,FLTAPE
 ,U1A,LIBR1
 ,XTA,NTRAK
 15,ATX,
 ,ARX,=1
 ,STX,NTRAK
 ,AOX,NMБ
 12,MTJ,11
 12,VJM,*70
 11,MTJ,12
 LIBR1:,BSS,
 ,XTA,LIBCODE
 ,AUX,MUNP
 ,AOX,CRED
 ,ARX,DELTA
 *70:,BSS,
 ,XTS,ABUF
 ,ASN,64-20
 15,AOX,
 ,ATX,AEX
 ,*70,AEX
 12,UJ,
 T:00,LOG,00.BLANK
 01,LOG,77
 02,LOG,46.Б
 03,LOG,60.Ц
 04,LOG,50.Д
 05,LOG,57.Ф
 06,LOG,47.Г
 07,LOG,53.И
 10,LOG,77
 11,LOG,77
 12,LOG,00.*
 13,LOG,54.И
 14,LOG,55.Л
 15,LOG,70.Я
 16,LOG,51.Ж
 17,LOG,01./
 20,LOG,02.0
 21,LOG,03.1
 22,LOG,04.2
 23,LOG,05.3
 24,LOG,06.4
 25,LOG,07.5
 26,LOG,10.6
 27,LOG,11.7
 30,LOG,12.8
 31,LOG,13.9
 32,LOG,65.Ь
 33,LOG,77
 34,LOG,56.П
 35,LOG,77.-
 36,LOG,77.+
 37,LOG,64.Ы
 40,LOG,52.З
 41,LOG,14.A
 42,LOG,15.B
 43,LOG,16.C
 44,LOG,17.D
 45,LOG,20.E
 46,LOG,21.F
 47,LOG,22.G
 50,LOG,23.H
 51,LOG,24.I
 52,LOG,25.J
 53,LOG,26.K
 54,LOG,27.L
 55,LOG,30.M
 56,LOG,31.N
 57,LOG,32.0
 60,LOG,33.P
 61,LOG,34.Q
 62,LOG,35.R
 63,LOG,36.S
 64,LOG,37.T
 65,LOG,40.U
 66,LOG,41.V
 67,LOG,42.W
 70,LOG,43.X
 71,LOG,44.Y
 72,LOG,45.Z
 73,LOG,62.Ш
 74,LOG,66.Э
 75,LOG,63.Щ
 76,LOG,61.Ц
 77,LOG,67.Ю
 TAB:,ISO,4HЦБ
 ,ISO,4HИГФД
  ,ISO,4HЙ*)(
 ,ISO,4H/ЖЯЛ
 ,ISO,4H3210
 ,ISO,4H7654
 ,ISO,4H Ь98
 ,ISO,4HЫ+-П
 ,ISO,4HCBAЗ
 ,ISO,4HGFED
 ,ISO,4HKJIH
 ,ISO,4HONML
 ,ISO,4HSRQP
 ,ISO,4HWVUT
 ,ISO,4HШZYX
 ,ISO,4HЮЧЩЭ
C
C--- ЧИCЛO CTPOK B CTPAHИЦE ---
C
 PL :,EQU,66
 PL1:,EQU,PL-1
 PL3:,EQU,PL-3
 PLX5:,P*P,(PL)5
 PL1X5:,P*P,(PL1)5
 PL3X5:,P*P,(PL3)5
 PL*5:,,
 ,Z00,PLX5
 PL1*5:,,
 ,Z00,PL1X5
 PL3*5:,,
 ,Z00,PL3X5
 MXL:,,
 ,Z00,4-PL
 AIII:,Z00,ESC
 ,Z00,PL-4
 ESC:,Z00,ESC+1
 ,Z00,PL1
 ,Z00,ESC+1
 ,Z00,PL
 ABUF:,,
 ,Z00,BUFFER
 MASK:,LOG,1763747717637477
 TR:,LOG,10020040 10020040
 TR8:,TEXT,8H0000 00
 TR8CBT:,TEXT,8H00  000
 TR8CB:,LOG,0401 0020 0401 0020
 MASK81:,OCT,0307 0707 0003 07
 MASK82:,LOG,01603403 01601407
 MASK8:,LOG,01603407 01603407
 TR8M:,OCT,04010020 04010000
 C7ALL:,LOG,7777 7777 7777 7777
 M4S:,LOG,7777
 M3L:,LOG,77777
  MU1:,ISO,6H'''''7''7'''''
 ,ISO,6H'''''3''7'''''
 MO:,ISO,6H 000
 ,ISO,6H  00
 MU2:,LOG,77
 ,LOG,117
 MP1:,LOG,0077 0000
 ,LOG,037 00000
 MAXADR:,LOG,4000
 ,LOG,74000
 MAXRADR:,LOG,4000
 ,LOG,40000
 BIT48:,OCT,4
 BIT16:,LOG,1 00000
 B24/13:,LOG,7777 0000
 M11B:,LOG,3777
 M10B:,LOG,1777
 MEMREFTB:,BSS,
 ,LOG,000 7777 7773
 ,LOG,677 7777 7400
 ,LOG,000 0013 1714
 NMБ:,LOG,16 0000
 LIBCODE:,OCT,
 MSEC:,OCT,174
 MESD:,OCT,0037 77
 CRED:,OCT,001
 AEX:,OCT,
 ADRWORD:,OCT,
 TW:,010,NLC
 ,,
 TAPLIB:,ISO,18HPERSONAL LIBRARY
 TEMPLIB:,ISO,18H TEMPORARY LIBRARY
  PERLIB:,ISO,18HPERMANENT LIBRARY
 REF1:,ISO,30HCCЫЛKИ HA ПOДПPOГPAMMЫ
 REF2:,ISO,30HИCПOЛЬЗOBAHИE ПOДПPOГPAMM
 REF3:,ISO,30HCCЫЛKИ HA COMMON'47'Ы
  REF4:,ISO,30HИCПOЛЬЗOBAHИE COMMON'47'OB
 REF5:,ISO,30HИCПOЛЬЗOBAHИE ИCTИHHЫX AДPECOB
 REF6:,ISO,30HИCПOЛЬЗOBAHИE ЭKCTPAKOДOB
 HEИCП:,ISO,24HHEИCПOЛЬЗYEMЫE BXOДЫ
     ИCП:,ISO,24HИCПOЛЬЗYEMЫE BXOДЫ
 ИCП*63:,ISO,24HB ЭKCTPAKOДAX
 ИCПNO*63:,ISO,24HB OCTAЛЬHЫX KOMAHДAX
 COMNT1:,ISO,30HD - PACCЫЛKA ДAHHЫX
 COMNT2:,ISO,30HE - BHEШHЯЯ П/П
 ,ISO,30HN - OПИCAHA, HE ИCПOЛЬЗYETCЯ
 ,ISO,30HC - ECTЬ OДHOИMEHHЫЙ COMMON
 COMNT4:,ISO,30HE - ECTЬ OДHOИMEHHAЯ П/П
 COMNT3:,ISO,30HD - PACCЫЛKA ДAHHЫX
 ,ISO,30HN - OПИCAH, HE ИCПOЛЬЗYETCЯ
 ,ISO,30HP - CTPAHИЧHЫЙ COMMON
 ,ISO,30HS - CEKTOPHЫЙ COMMON
 ,ISO,30HL - ПPOCTOЙ COMMON
 ,ISO,30H***** - ДЛИHA OПИCAHA HEЯBHO
 ERRCAT:,ISO,18HИCПOPЧEH KATAЛOГ
 ERRPAR:,ISO,18HOШИБOЧHЫЙ ПAPAMETP
 NLIB:,ISO,6HPERM
 ,CONT,6HTEMP
 ,CONT,6HTAPE
 TYPJOB:,BSS,
 ,ISO,6HMACROS
 ,ISO,6HRADDRE
 ,ISO,6HREFERS
 SPACE:,BSS,
 ,ISO,6H
 CBHO:,ISO,6HCBOБOД
 ,BSS,2
 HO:,ISO,6HHO
 DIFT:,BSS,1
 ATAP:,BSS,1
 FTAP:,BSS,1
 ATAPE:,BSS,1
 LONG:,BSS,1
 LOC:,BSS,1
 N9:,BSS,1
 XX:,BSS,1
 RA:,BSS,1
 RB:,BSS,1
 I13:,BSS,1
 RA1:,BSS,1
 RB1:,BSS,1
 CATLENG:,BSS,1
 MXLENG:,BSS,1
 ALE:,BSS,1
 WC:,BSS,1
 ,BSS,1
 CMCNT:,BSS,1
 SBCNT:,BSS,1
 WBEGFLAG:,BSS,2
 STR:,BSS,3
 WORD:,BSS,1
 LCAT:,BSS,1
 FPLE:,BSS,1
 FLMACRO:,BSS,1
 FLADR:,BSS,1
 MACROCNT:,BSS,1
 MACRCNT1:,BSS,1
 SET:,BSS,1
 INSTR:,BSS,1
 ADR:,BSS,1
 FLUSED:,BSS,1
 RACNT:,BSS,1
 RADSHIFT:,BSS,1
 RFPLE:,BSS,1
 RFPLE1:,BSS,1
 RADR:,BSS,1
 B19/22:,BSS,1
 CЛCM:,BSS,1
 ПPEДKOM:,BSS,1
 NTRAK1:,BSS,1
 NTRAK:,BSS,1
 FLDBL:,BSS,1
 FLTAPE:,BSS,1
 DUBLE:,BSS,1
 INFW:,BSS,1
 DATACONS:,BSS,1
 USEDECL:,BSS,64
 WC1:,BSS,1
 FLBACK:,BSS,1
 PRINT:,BSS,10
 ,END,
  TELOUT*:,NAME,
  NAMEJOB*:,SUBP,
  TT:,ITA,14
  ,FUN,76B
  13,UJ,
  TTTITLE:,ENTRY,
  14,VTM,NAMEJOB*
  ,UJ,TT
  ,END,
 TEMFILE:,NAME,.ПOЧTИ UPPFILE
C*
C*     U S E R   ДЛЯ  ПOЛYЧEHИЯ  BXOДHOГO
C*  Y П П - ФAЙЛA   M C   T E M П.
C*
C*========================================
C*      З A M E H A  C И M B O Л O B     I
C*                                       I
C*---------------------------------------I
C*        O Д И H  B  O Д И H            I
C*.......................................I
C*  CИMBOЛ VT-340   I     CИMBOЛ YПП     I
C*..................I....................I
C* ["]          042 I OTKP.KABЫЧKA   032 I
C* MOPДA        100 I CTPEЛKA BBEPX  021 I
C* ДИEЗ         043 I HEPABEHCTBO    034 I
C* OБPATH.CЛЭШ  134 I KOHEЦ CTPOKИ   175 I
C*---------------------------------------I
C*        Д B A  B  O Д И H              I
C*      ( ЧEPEЗ  <<ESCAPE>> )            I
C*.......................................I
C*  CИMBOЛ VT-340   I    CИMBOЛ YПП      I
C*..................I....................I
C* [.]          056 I ПOДCTP.ДECЯTKA 020 I
C* [*]          052 I YMHOЖEHИE      024 I
C* [<]          074 I .LE.           116 I
C* [>]          076 I .GE.           117 I
C* [)]          051 I ИMПЛИKAЦИЯ     122 I
C* BOCKЛ. ЗHAK  041 I HE ЛOГИЧECKOE  123 I
C* [:]          072 I ЦEЛOE ДEЛEHИE  124 I
C* [=]          075 I TOЖДECTBO      125 I
C* MИHYC        055 I ГOPИЗ.ЧEPTA    131 I
C* [Ь]          170 I TBEPДЫЙ ЗHAK   135 I
C* [V]          126 I ЛOГ. ИЛИ       120 I
C*========================================
C*  CИMBOЛ  <<ESCAPE>> - OБP. CЛЭШ
C*........................................
C*
C*    ПOЯCHEHИE.
C*    BBEДEH ПPOГPAMMHЫЙ ESCAPE - PEГИCTP.
C*  CИMBOЛ ESCAPE BKЛЮЧAET ЭTOT PEГИCTP, И
C*  CЛEДYЮЩИЙ CИMBOЛ ЗAMEHЯETCЯ ЧEPEЗ TAБ-
C*  ЛИЦY "ДBA B OДИH".
C*    ECЛИ CИMBOЛ HE HAЙДEH  B  ЭTOЙ  TAБ-
C*  ЛИЦE, TO OH OБPAБATЫBAETCЯ KAK OБЫЧHЫЙ
C*  (B ЧACTHOCTИ ЗAMEHЯETCЯ ЧEPEЗ TAБЛИЦY
C*  "OДИH B OДИH").
C*    PEГИCTP  <<ESCAPE>>  PAБOTAET TOЛЬKO
C*  HA  CЛEДYЮЩИЙ  CИMBOЛ  ЗA  CИMBOЛOM
C*  <<ESCAPE>>.
C*
C*     BO BCE KAPTЫ, CTOЯЩИE MEЖДY ДBYMЯ
C*  KAPTAMИ, C ПEPBOЙ ПOЗ. KOTOPЫX HAБИ-
C*  TЫ TPИ OБP.CЛЭШA, ДOБABЛЯETCЯ CИMBOЛ
C*  <KOHEЦ CTPOKИ> ('175'). ECЛИ B 4-Й
C*  ПOЗИЦИИ ПEPBOЙ ИЗ ЭTИX KAPT CTOИT
C*  CИMBOЛ 'L', TO MOЖHO ИCПOЛЬЗOBATЬ
C*  80 ПOЗИЦИЙ HA CTPOKE (ИHAЧE 72).
C*
C*  YПП-ФAЙЛ ЗAПИCЫBAETCЯ HA MБ 06
C*
 TAB/ITG*:,SUBP,
 ESC:,EQU,134B
 /362/:,PC,1024
 NP:,P/P,(/362/)16
 *C:7,MTJ,14.
 7,BASE,*C
 ,STX,SERV.
 ,ATX,CARD.
 ,ITA,14
 ,ITS,13
 ,ITS,5.
 ,XTS,I5.
 ,ATI,5.ПEPEYCTAHOBKA
 ,ITA,
 ,ATX,ESC/FL
 ,WTC,CARD.
 14,VTM,.
 14,XTA,.
 ,AEX,=5H*READ
 ,UIA,CON1.
 14,XTA,1
 ,AEX,=3HOLD
 ,UZA,FIN.
 CON1:,BSS,.
 14,XTA,.
 ,AEX,=5H*CALL
 ,UZA,CALL.
 14,XTA,
 ,AEX,=3H'134''134''134'
 ,AAX,=:7777 7777
 ,UZA,REGIM
 ,XTA,FIRST.
 ,UIA,JUM1.
 12,VTM,-13
 S1:12,UTC,BЫKA+13
 ,XTA,.
 13,VJM,WRWORD.
 12,VLM,S1.
 ,ATX,FIRST.
 JUM1:,BSS,.
 ,XTA,L/FL
 ,UZA,L72
C    PAЗБИPAEM BCE 80 ПOЗИЦИЙ
 8,VTM,13
 9,VTM,-16
 12,VTM,-79
 ,UJ,R/R
 L72:,BSS,
 8,VTM,11.
 9,VTM,-48.
 12,VTM,-71.
C    ПOИCK "XBOCTA" KAPTЫ
 R/R:,BSS,.
 ,WTC,CARD.
 8,XTA,.
 9,ASN,64+48.
 ,AAX,=177
 9,UTM,8.
 9,VIM,JUM2.
 8,UTM,-1.
 9,VTM,-48
 JUM2:,BSS,.
 ,AEX,=40.
 ,UIA,XBOCT.
 12,VLM,R/R.
 ,UJ,ПYCK.
 XBOCT:,BSS,
 ,MTJ,8.
 ,MTJ,10.
 9,VTM,48.
 11,VTM,-40.
 ,UJ,DO.
 R/C:,BSS,.
 ,WTC,CARD.
 8,XTA,.
 9,ASN,64-8
 ,AAX,=177.
 ,ATI,14.
 ,XTA,ESC/FL
 ,U1A,ESCAPE
C     ЗAMEHA 'OДИH B OДИH'
 14,UTM,-ESC
 14,VZM,GIVESC.ПPИШEЛ ESCAPE
 14,UTM,ESC
 FIFTY:,BSS,
 14,UTM,-42B
 14,VZM,*042
 14,UTM,42B-100B
 14,VZM,*100
 14,UTM,100B-43B
 14,VZM,*043
 14,UTM,43B-134B
 14,VZM,*134
 ,UTC,TAB/ITG*
 14,XTA,134B
 ,AAX,=177
 WRS:,UTC,-1
 NO WR:14,VTM,
 9,UTM,-8.
 9,VIM,*2.
 9,VTM,48.
 8,UTM,1.
 13,UJ,.
 ESCAPE:,BSS,
 ,ITA,
 ,ATX,ESC/FL
C     ЗAMEHA 'ДBA B OДИH'
 14,UTM,-56B.TOЧKA
 14,VZM,*056.
 14,UTM,56B-52B.*
 14,VZM,*052.
 14,UTM,52B-74B.<
 14,VZM,*074.
 14,UTM,74B-76B.>
 14,VZM,*076.
 14,UTM,76B-51B.)
 14,VZM,*051.
 14,UTM,51B-41B.BOCKЛ. ЗHAK
 14,VZM,*041.
 14,UTM,41B-72B.:
 14,VZM,*072.
 14,UTM,72B-75B.=
 14,VZM,*075.
 14,UTM,75B-55B.-
 14,VZM,*055.
 14,UTM,55B-170B.Ь
 14,VZM,*170.
 14,UTM,170B-126B.V
 14,VZM,*126.
 14,UTM,126B
 ,UJ,FIFTY
C
C     Д B A   B   O Д И H
C
 *126:,XTA,=120
 ,UJ,WRS
 *056:,XTA,=20
 ,UJ,WRS
 *052:,XTA,=24
 ,UJ,WRS
 *074:,XTA,=116
 ,UJ,WRS
 *076:,XTA,=117
 ,UJ,WRS
 *051:,XTA,=122
 ,UJ,WRS
 *041:,XTA,=123
 ,UJ,WRS
 *072:,XTA,=124
 ,UJ,WRS
 *075:,XTA,=125
 ,UJ,WRS
 *055:,XTA,=131
 ,UJ,WRS
 *170:,XTA,=135
 ,UJ,WRS
C     O Д И H   B   O Д И H
 *042:,XTA,=32
 ,UJ,WRS
 *100:,XTA,=21
 ,UJ,WRS
 *043:,XTA,=34
 ,UJ,WRS
 *134:,XTA,=175
 ,UJ,WRS
C-----------------------------------------
 GIVESC:,BSS,
 ,XTA,=1
 ,ATX,ESC/FL
 ,UJ,NO WR
C     ДOФOPMИPOBAHИE HEЧETA
 *1:,BSS,.
 ,ATX,W
 ,ACX,=1
 ,AAX,=1
 ,ASN,64-7.
 ,AOX,W
 11,ASN,64+8
 10,AOX,BIN CA+1
 10,ATX,BIN CA+1.
 11,UTM,8
 11,VIM,*2.
 11,VTM,-40
 10,UTM,1.
 *2:13,UJ,.
 DO:,BSS,.
 13,VJM,R/C.
 14,VZM,NEXT
 13,VJM,*1.
 NEXT:,BSS,
 12,VLM,DO.
 ,XTA,DOLCAR.
 ,UZA,AГA.
 ,XTA,W
 ,AEX,=175
 ,UZA,AГA.KOHEЦ YЖE ECTЬ
 ,XTA,=175
 13,VJM,*1.
C     ПEPEПИCЬ BINARY KAPTЫ
 AГA:,BSS,.
 ,XTA,BIN CA
 13,VJM,WRWORD.
 12,VTM,-23
 *3:12,UTC,BIN CA+24
 ,XTA,
 13,VJM,WRWORD.
 ,XTA,.
 12,UTC,BIN CA+24.
 ,ATX,.
 12,VLM,*3.
 ПYCK:,BSS,.
 ,XTS,CARD.
 ,WTC,SERV.
 13,VJM,.
 OUTSUBP:,BSS,.
 ,ITA,5.
 ,STX,I5
 ,STI,5.
 ,STI,13
 ,STI,7.
 ,NTR,6
 13,UJ,.
 REGIM:,BSS,
 ,XTA,DOLCAR
 ,AEX,=1
 ,ATX,DOLCAR
 ,ATX,L/FL
 ,UZA,OUTSUBP
 14,XTA,
 ,AEX,=6H''''''L''''
 ,AAX,=6H'''''''177'''''
 ,UZA,OUTSUBP.80 ПOЗ.
 ,XTA,
 ,ATX,L/FL
 ,UJ,OUTSUBP
 TWRBINC:,ENTRY,.ДOП. BXOД
 ,ATI,8.
 ,ITA,7.
 7,VTM,*C.
 ,ITS,13.
 ,ITS,5
 ,XTS,I5.
 ,ATI,5.
 9,VTM,-24
 MTW:9,UTC,24
 8,XTA,.
 13,VJM,WRWORD.
 9,VLM,MTW.
 ,UJ,OUTSUBP.
 WRWORD:,BSS,.ПOCЛOBHAЯ            ЗAПИCЬ
 5,VZM,MDW.
 5,UTC,/362/+1024.
 ,ATX,
 5,UTM,1
 13,UJ,.
 MDW:,BSS,.
 ,ITS,.
 ,*70,IW.
 ,XTA,IW.
 ,ARX,=7741
 ,AOX,=7740
 ,AEX,=7740
 ,ATX,IW.
 ,STI,.
 5,VTM,-1024.
 ,UJ,WRWORD.
 FIN:,BSS,.
 8,VTM,-1.
 9,VTM,*5.
 FIN/:,BSS,.
 12,VTM,-12
 *4:,WTC,CARD.
 12,XTA,12.
 13,VJM,WRWORD.
 12,VLM,*4.
 ,WTC,CARD.
 ,XTA,13.
 ,AOX,=377
 ,AEX,=365
 13,VJM,WRWORD.
 ,ATX,FIRST.
 9,UJ,
 *5:,BSS,.
 8,VLM,FIN/.
 13,VTM,ПYCK
 ,UJ,MDW.
 CALL:,BSS,.
 9,VTM,ПYCK.
 ,MTJ,8
 ,UJ,FIN/.
 IW:,,NP.
 ,006,.
 FIRST:,OCT,.
 BЫKA:,ISO,83H*CALL TEMP//
 ,CONT,1H'12'
 BIN CA:,LOG,77751
 ,BSS,24
 I5:,INT,-1024.
 ESC/FL:,LOG,
 L/FL:,LOG,
 W:,BSS,1
 CARD:,BSS,1
 SERV:,BSS,1.
 DOLCAR:,BSS,1.
 ,END,UPPFILE .
  TEMP//:,NAME,
C     BЫЗOB MC TEMП
  KCOUNT*:LC,BLOCK,KC(4)
  SCEM:,EQU,83
 REGSCAL:,EQU,100B
  *40000:B,BLOCK,PAGE
  NP:,P/P,(PAGE)16
  READY:,BSS,
C     YCTAHOBKA PEЖИMA ЧTEHИЯ KAPT
C     C MБ BBOДA ДЛЯ ЭK.60
 ,CALL,Э60/MБ
  ,*50,70100B+SCEM
 ,AOX,=:7777 7774
  ,*57,5
C......................................
C
 10,VTM,
 ,CALL,PARAMT*
 11,VZM,NO PAR
 10,VTM,2
 ,XTA,=6HDIALOG
 ,CALL,PARAMT*
 ,U1A,NO PAR
 ,XTA,=:0004.B39
 ,*50,74100B+REGSCAL
 NO PAR:,BSS,
 ,*76,
 ,*64,
  ,*72,=:11 4037 77
 ,*70,INF
  ,*50,75B
  ,XTA,
  14,VTM,-12
  IZERO:,BSS,
  14,ATI,1+12
  14,VLM,IZERO
  15,VTM,
  ,*67,DEBUG
C=====================
  DEBUG:,,E/DEB
  9,,
C=====================
  E/DEB:,BSS,
  14,VTM,134B
  ,UJ,40150B
  INF:,010,NP
  ,030,176B
  ,END,
 TEMP/SI:,NAME,.COДPAHO C ALGOL/SI
 ,CALL,MAKFIL/
 ,CALL,TEMP//
 ,SJ,
 ,END,
 TEXISO:,NAME,.
C
C******   ПEPEBOД ИДEHTИФИKATOPA B ISO
C
 C:,STI,14.KYДA
 ,ATI,12.OTKYДA
C......   PEГИCTPЫ B MAГAЗИH
 ,ITA,13.
 ,ITS,11.
 ,ITS,10.
 ,ITS,9.
C......
 13,BASE,C.
 12,XTS,.TEXT-CЛOBO
 12,VTM,1-12.ЦИKЛ : TEXT/12
 11,VTM,1-6.ЦИKЛ : ISO/6
 L1:,ASN,64-6.
 ,ATX,R WOR.OCTATOK
 ,YTA,.
 15,ATX,.TEXT-CИMBOЛ
 ,ASN,64+2.
 ,ATI,10.AДPEC ПO TAБЛ.
 ,ASN,64-2.
 15,AEX,.
 ,ASN,64-3.
 ,ATI,9. CДBИГ
 10,XTA,TABLE.
 9,ASN,64+16.
 ,AAX,C377.ISO-CИMBOЛ
 14,XTS,.
 ,ASN,64-8.
 15,AOX,.
 14,ATX,.ЗAП.CИMBOЛA
 11,VLM,L2.
 11,VTM,1-6.
 14,UTM,1.
 L2:,XTA,R WOR.
 12,VLM,L1.
C......   BOCCTAHOBЛEHИE ИP
 ,STX,.
 ,STI,9.
 ,STI,10.
 ,STI,11.
 ,STI,13.
 13,UJ,.
C......
 C377:,LOG,377.
 R WOR:,BSS,1.
 TABLE:,BSS,.
 00,ISO,6HЦБ
 04,ISO,6HИГФД
 10,ISO,6HЙ* .
 14,ISO,6H/ЖЯЛ
 20,ISO,6H3210
 24,ISO,6H7654
 30,ISO,6H:Ь98
 34,ISO,6HЫ+-П
 40,ISO,6HCBAЗ
 44,ISO,6HGFED
 50,ISO,6HKJIH
 54,ISO,6HONML
 60,ISO,6HSRQP
 64,ISO,6HWVUT
 70,ISO,6HШZYX
 74,ISO,6HЮЧЩЭ
 ,END,.
 TRANSFIO:,NAME,:13/02/81
 ,ITS,13.A=6 CИMBOЛOB ГOCT
 15,ATX,
 11,VTM,-48
 12,BASE,*
 ,XTA,
 ,ATX,ISO
 15,XTA,-2.=A
 L6:,ASN,64-8
 15,ATX,-2
 ,YTA,
 15,ATX,
 ,ASN,64+2
 ,STI,14.HOMEP CЛ B TAБЛ
 ,AAX,=3
 ,ATI,13.MECTO B ''0123''
 GOST/ISO:,SUBP,
 ,UTC,GOST/ISO
 14,XTA,
 13,UTM,-3
 SH:,ASN,64+8.HOPMAЛ.BПPABO
 13,VLM,SH
 ,AAX,=377
C  CИMBOЛ YПAKYEM BЛEBO
 11,ASN,64+8
 ,AOX,ISO
 ,ATX,ISO
 15,XTA,-2.OCTATOK
 11,UTM,8
 11,V1M,L6
 ,STX,
 ,STI,13
 ,XTA,ISO
 13,UJ,
 ISO:,BSS,1
 ,END,
 TRNS10T8:  ,NAME,
 C       :  ,BSS,
          14,VTM,C
          14,ATX,INPNUM-C
          12,VTM,48
          14,XTA,=I0-C
 LOOP    :14,A*X,=R10.-C
          14,XTS,INPNUM-C
          12,ASN,64-8
          14,AAX,=17-C
          14,AOX,=I0-C
          15,A+X,
          12,UTM,-8
          12,V1M,LOOP
            ,NTR,3
          14,A+X,=I0-C
            ,NTR,2
          13, UJ,
 INPNUM  :  ,BSS,1
            ,END,
      TXTGST     :,NAME,
            12 ,  VTM,   *00023
                 ,UTC,
 *00001    :     ,STI,  8
                 ,ATI,  9
                 ,ITA,  13
                 ,UTC,   BSS+ 0002B
 *00003    :     ,ATX,
            13 ,  VTM,   *00003
             9 ,  XTA,
            13 ,  ATX,  63B
             9 ,  VTM,  77777B
                 ,UTC,
 *00006    :13 ,  AUX,  60B
            14 ,  VTM,  77773B
 *00007    :     ,ASN,  110B
            13 ,  ATX,  64B
                 ,YTA,
            13 ,  AUX,  61B
                 ,ATI,  10
                 ,ASN,  117B
                 ,ATI,  11
            12 ,  UTC,
            11 ,  XTA,
            10 ,  ASN,  50B
            13 ,  AAX,  62B
            13 ,  AOX,  64B
            14 ,  VLM,   *00007
             8 ,  ATX,
             8 ,  UTM,  1
            13 ,  XTA,  63B
                 ,ASN,  34B
             9 ,  VLM,   *00006
                 ,STI,
                 ,NTR,  6
                 ,WTC,   BSS+ 0002B
                 , UJ,
      TXTISO    :,ENTRY,
            12 ,  VTM,   *00043
                 , UJ,   *00001
 *00023    :,     LOG,  15420416 03607417.
            ,     LOG,  12021464 11007417.
            ,     LOG,  12214423 04407417.
            ,     LOG,  03023076 12607417.
            ,     LOG,  00601001 00007417.
            ,     LOG,  01603005 01007417.
            ,     LOG,  03235411 02007417.
            ,     LOG,  16405013 13607417.
            ,     LOG,  14221040 11607417.
            ,     LOG,  20240045 17607417.
            ,     LOG,  12441502 13207417.
            ,     LOG,  13442454 21007417.
            ,     LOG,  22043506 14007417.
            ,     LOG,  22645111 14407417.
            ,     LOG,  16046063 15207417.
            ,     LOG,  17233471 17007417.
 *00043    :,     LOG,  30661056 10020040.
            ,     LOG,  32263546 31020040.
            ,     LOG,  32425051 12020040.
            ,     LOG,  13673161 33020040.
            ,     LOG,  14631061 14020040.
            ,     LOG,  15633065 15020040.
            ,     LOG,  13074071 16020040.
            ,     LOG,  36225455 34020040.
            ,     LOG,  20641101 36420040.
            ,     LOG,  21643105 21020040.
            ,     LOG,  22645111 22020040.
            ,     LOG,  23647115 23020040.
            ,     LOG,  24651121 24020040.
            ,     LOG,  25653125 25020040.
            ,     LOG,  36655131 26020040.
            ,     LOG,  30077175 37020040.
            ,     LOG,  17637477 17637477.
            ,     LOG,  00000000 07700030.
            ,     LOG,  77600000 00000000.
 BSS       :     ,BSS,  3
      ,END,
  TYDA*:,NAME,
  B:12,VTM,RASPAK
  ,UJ,C
  OTTYD*:,ENTRY,
  12,VTM,ZAPAK
  C:8,BASE,B
  9,VTM,77655B
  15,WTC,
  10,VTM,
  ,AAX,=77777
  ,AOX,=I0
  ,A*X,=:4154
  ,ITS,13
  ,STX,RET
  12,UJ,
  RASPAK:,ATX,W
  ,CALL,RASPAK8
  ,AAX,=77777
  10,ATX,
  10,UTM,1
  ,XTA,W
  ,A+X,=:405
  9,VLM,RASPAK
  END:,WTC,RET
  ,UJ,
  ZAPAK:,ATX,W
  10,XTS,
  ,XTS,W
  ,CALL,ZAPAK8
  10,UTM,1
  ,XTA,W
  ,A+X,=:405
  9,VLM,ZAPAK
  ,UJ,END
  W:,LOG,1
  RET:,INT,13
  ,END,
 UPPFILE:,NAME,.CAXAPOB
 TABLE2*:,SUBP,
 /362/:,PC,1024
 NP:,P/P,(/362/)16
 *C:7,MTJ,14.
 7,BASE,*C
 ,STX,SERV.
 ,ATX,CARD.
 ,ITA,14
 ,ITS,13
 ,ITS,5.
 ,XTS,I5.
 ,ATI,5.ПEPEYCTAHOBKA
 ,WTC,CARD.
 14,VTM,.
 14,XTA,.
 ,AEX,=5H*READ
 ,UIA,CON1.
 14,XTA,1
 ,AEX,=3HOLD
 ,UZA,FIN.
 CON1:,BSS,.
 14,XTA,.
 ,AEX,=5H*CALL
 ,UZA,CALL.
 ,ASN,64+40.
 ,AEX,=16
 ,ATX,DOLCAR.
 ,XTA,FIRST.
 ,UIA,JUM1.
 12,VTM,-13
 S1:12,UTC,BЫKA+13
 ,XTA,.
 13,VJM,WRWORD.
 12,VLM,S1.
 ,ATX,FIRST.
 JUM1:,BSS,.
 8,VTM,11.
 9,VTM,-48.
 12,VTM,-71.
C    ПOИCK "XBOCTA" KAPTЫ
 R/R:,BSS,.
 ,WTC,CARD.
 8,XTA,.
 9,ASN,64+48.
 ,AAX,=177
 9,UTM,8.
 9,VIM,JUM2.
 8,UTM,-1.
 9,VTM,-48
 JUM2:,BSS,.
 ,AEX,=40.
 ,UIA,XBOCT.
 12,VLM,R/R.
 ,UJ,ПYCK.
 XBOCT:,BSS,
 ,MTJ,8.
 ,MTJ,10.
 9,VTM,48.
 11,VTM,-40.
 ,UJ,DO.
 R/C:,BSS,.
 ,WTC,CARD.
 8,XTA,.
 9,ASN,64-8
 ,AAX,=177.
 ,ATI,14.
C---------------------------------------
C  ЗAMEHЫ CИMBOЛOB HA "XИTPЫE"   :
C  % - ДEЛ. ЦEЛOE
C  OБP. CЛЭШ - OП. 10
C  XEШ - HEPAB.
C  MOPДA - BOЗB. B CTEП.
C  AND - ЛOГ. YMH
C   ?  - ЛOГ. CЛOЖ.
C   *  - YMH.
C---------------------------------------
 14,UTM,-47B.
C     ЗДECЬ MOЖHO BCTABИTЬ CПEЦ.ПEPEBOД
 14,VIM,HE KAB.
 KBЧK:,BSS,.
 ,XTA,=33.
 ,AEX,=1
 ,ATX,=33.
 ,UJ,ЗAП KA.
C     ПEPEBOД ISO B GOST
 HE KAB:,BSS,.
C     PЯД ПPOBEPOK ПO ПOBOДY TЮPИHA
 14,UTM,47B-100B.
 14,VIM,NO POW.
 ,XTA,=21.
 ,UJ,ЗAП KA.
 NO POW:14,UTM,100B-52B
 14,VIM,NO MUL.
 ,XTA,=24
 ,UJ,ЗAП KA
 NO MUL:14,UTM,52B-43B.
 14,VIM,NO XEШ.
 ,XTA,=34.
 ,UJ,ЗAП KA.
 NO XEШ:14,UTM,43B-42B.
 14,VZM,KBЧK.
 NO AP*:14,UTM,42B-77B.
 14,VIM,NO ПAЛ.
 ,XTA,=120
 ,UJ,ЗAП KA.
 NO ПAЛ:14,UTM,77B-41B.
 14,VIM,NO ЧEP.
 ,XTA,=130.
 ,UJ,ЗAП KA
 NO ЧEP:,BSS,.
 14,UTM,41B-134B
 14,VIM,NO 10
 ,XTA,=20
 ,UJ,ЗAПKA.
 NO 10: 14,UTM,134B-45B
 14,VIM,NO DV.
 ,XTA,=124
 ,UJ,ЗAПKA
 NO DV:,BSS,.
 14,UTM,45B-30B
 14,VIM,TRISS.
 ,XTA,=31
 ,UJ,ЗAПKA.
 TRISS:,BSS,.
 14,VTM,-134B.
 SS1:14,UTC,TABLE2*+134B.
 ,AEX,.
 ,UZA,TYT.
 14,UTC,TABLE2*+134B.
 ,AEX,.
 14,VLM,SS1.
 14,VTM,17B-134B.
 TYT:,BSS,.
 14,UTM,134B.
 ,ITA,14.
 ЗAП KA:,BSS,.
 9,UTM,-8.
 9,VIM,*2.
 9,VTM,48.
 8,UTM,1.
 13,UJ,.
C     ДOФOPMИPOBAHИE HEЧETA
 *1:,BSS,.
 15,ATX,.
 ,ACX,=1
 ,AAX,=1
 ,ASN,64-7.
 15,AOX,.
 11,ASN,64+8
 10,AOX,BIN CA+1
 10,ATX,BIN CA+1.
 11,UTM,8
 11,VIM,*2.
 11,VTM,-40
 10,UTM,1.
 *2:13,UJ,.
 DO:,BSS,.
 13,VJM,R/C.
 13,VJM,*1.
 12,VLM,DO.
 ,XTA,DOLCAR.
 ,UZA,AГA.
 ,XTA,=17
 13,VJM,*1.
 ,XTA,=174.
 13,VJM,*1.
C     ПEPEПИCЬ BINARY KAPTЫ
 AГA:,BSS,.
 ,XTA,BIN CA
 13,VJM,WRWORD.
 12,VTM,-23
 *3:12,UTC,BIN CA+24
 ,XTA,
 13,VJM,WRWORD.
 ,XTA,.
 12,UTC,BIN CA+24.
 ,ATX,.
 12,VLM,*3.
 ПYCK:,BSS,.
 ,XTS,CARD.
 ,WTC,SERV.
 13,VJM,.
 OUTSUBP:,BSS,.
 ,ITA,5.
 ,STX,I5
 ,STI,5.
 ,STI,13
 ,STI,7.
 ,NTR,6
 13,UJ,.
 WRIBINC:,ENTRY,.ДOП. BXOД
 ,ATI,8.
 ,ITA,7.
 7,VTM,*C.
 ,ITS,13.
 ,ITS,5
 ,XTS,I5.
 ,ATI,5.
 9,VTM,-24
 MTW:9,UTC,24
 8,XTA,.
 13,VJM,WRWORD.
 9,VLM,MTW.
 ,UJ,OUTSUBP.
 WRWORD:,BSS,.ПOCЛOBHAЯ            ЗAПИCЬ
 5,VZM,MDW.
 5,UTC,/362/+1024.
 ,ATX,
 5,UTM,1
 13,UJ,.
 MDW:,BSS,.
 ,ITS,.
 ,*70,IW.
 ,XTA,IW.
 ,ARX,=7741
 ,AOX,=7740
 ,AEX,=7740
 ,ATX,IW.
 ,STI,.
 5,VTM,-1024.
 ,UJ,WRWORD.
 FIN:,BSS,.
 8,VTM,-1.
 9,VTM,*5.
 FIN/:,BSS,.
 12,VTM,-12
 *4:,WTC,CARD.
 12,XTA,12.
 13,VJM,WRWORD.
 12,VLM,*4.
 ,WTC,CARD.
 ,XTA,13.
 ,AOX,=377
 ,AEX,=365
 13,VJM,WRWORD.
 ,ATX,FIRST.
 9,UJ,
 *5:,BSS,.
 8,VLM,FIN/.
 13,VTM,ПYCK
 ,UJ,MDW.
 CALL:,BSS,.
 9,VTM,ПYCK.
 ,MTJ,8
 ,UJ,FIN/.
 IW:,,NP.
 ,006,.
 FIRST:,OCT,.
 BЫKA:,ISO,83H*CALL ALGOL//
 ,CONT,1H'12'
 BIN CA:,LOG,77751
 ,BSS,24
 I5:,INT,-1024.
 CARD:,BSS,1
 SERV:,BSS,1.
 DOLCAR:,BSS,1.
 ,END,UPPFILE .
  WAIT:,NAME,
C*
C**** 25.12.84.   A.П.CAПOЖHИKOB
C*
C*     SUBROUTINE WAIT(TIM)
C*
C*     ЗAДEPЖKA HA  TIM  CEKYHД.
C*     TIM - BEЩECTBEHHOE ЧИCЛO.
C*
C*     ПPИMEP:  CALL WAIT(0.8)
C*     - ПAYЗA ДЛИHOЙ 0.8 CEK.
C*
  ,ATI,14
  14,XTA,
  ,*57,13B
  ,STI,
  13,UJ,
  ,END,
 WRITE1  :  ,NAME, . 10.04.73
 '
 '       WRITING   1   CARD
 '
 PAGBUFER:  , PP,1024
 WRITMAR*:  , LC,1
 LIMWR*:LC,BLOCK,(1),GAK/4
 ISOTCOSY:,SUBP,
 C       :  ,BSS,
 BEGIN   : 12,  VTM  ,C                   .
 12,BAS,C.
 11,VTM,SAVCARD.
             ,  NTR  ,6                   .
             ,  UZA  ,MAG                 .
             ,  ITS  ,13                  .
           12,  STX  ,I13-C               .
 15,ATX,
 ,XTS,ADRSC
 13,VJM,ISOTCOSY
 ,AAX,Z15/1
 ,AOX,ADRSC.
             ,  NTR  ,10                  .
             ,  U1A  ,FILE                .
 CYCLM:13,VTM,-13
 ,XTA,SAVCARD.
 ,AEX,=6H*READ'201'
 ,U1A,CYCL
 ,XTA,SAVCARD+1
 ,AEX,=3HOLD
 ,ASN,64+24
 ,U1A,CYCL.ПPOCTAЯ
 ,XTA,=5H*READ
 ,ATX,SAVCARD
 CYCL    : 12,  XTA  ,WORD-C              .
           12,  ARX  ,CWORD-C             .
           12,  ATX  ,WORD-C              .
             ,  UZA  ,MAG                 .
           11,  XTA  ,                    .
             ,  WTC  ,WORD                .
             ,  ATX  ,-1                  .
 12,AAX,=377-C
 12,AEX,=12-C
 ,UZA,ECYCL
           11,  UTM  ,1                   .
           13,  VLM  ,CYCL                .
 ECYCL:,BSS,
           12,  XTA  ,I13-C               .
             ,  STI  ,13                  .
           13,  UJ   ,                    .
 MAG     :  ,BSS,
            ,UTC,WRITMAR*
            ,XTS,
            ,U1A,NOMAG
           12,  070  ,INFMAG-C            .
 NOMAG   :  ,BSS,
 ,UTC,LIMWR*.
 ,XTA,
 ,ARX,=1
 ,UTC,LIMWR*
 ,ATX,
         : 12,  XTA  ,BUFER-C             .
           12,  ATX  ,WORD-C              .
 'INSTRUCTION'                              WRITE               PAGE 3
            ,ITA,13
          12,XTS,INFMAG-C
            ,CALL,DRUMTAP*
          12,STX,INFMAG-C
            ,STI,13
             ,  U1A  ,CYCL                .
             ,  STX  ,                    .
           13,  UJ   ,                    .
 FILE    :   ,  ASN  ,64-9                .
           12,  AUX  ,MASKMAG-C           .
           12,  XTS  ,BUFER-C             .
             ,  ASN  ,64-20               .
           12,  AEX  ,UN40-C              .
           15,  AOX  ,                    .
           12,  ATX  ,INFMAG-C            .
           12,  XTA  ,BUFER-C             .
           12,  ATX  ,WORD-C              .
 ,UJ,CYCLM
 BUFER   :  3,  37   ,''70000''           .
             ,  20   ,PAGBUFER            .
 ADRSC:,,
 ,Z00,SAVCARD.
 Z15/1:,OCT,7777 7777 777
 CWORD   :   ,  OCT  ,0001 0000 0000 0001 .
  UN1    :   ,  OCT  ,0000 0000 0000 0001 .
 MASKMAG :   ,  OCT  ,0000 0000 0077 0777 .
 UN40    :   ,  OCT  ,001                 .
 WORD    :  ,BSS,1
 INFMAG  :  ,BSS,1
 I13     :  ,BSS,1
 SAVCARD:,BSS,14.
            ,END,
      YESLIS     :,NAME,
C*
C*    SUBROUTINE NOTLIS - OTMEHA ЛИCTOBAHИЯ
C*    SUBROUTINE YESLIS - BOCCT. ЛИCTOBAHИЯ
C*    SUBROUTINE NEWLIS - BЫBOД HA HOB.ЛИCT
C*    SUBROUTINE KILNUMPG - OTMEHA HYMEPAЦИИ
C*        CTPAHИЦ ПPИ ПEЧATИ BCEГO ЛИCTИHГA.
C*
                :,UTC,  1-IWNL
      NEWLIS:,ENTRY,
                :,UTC,IWNL
      NOTLIS    :,ENTRY,
                :, *64,
            13 ,   UJ,
  KILNUMPG:,ENTRY,
  :,*63,704B. =:02
  ,*50,74100B+571B.STAT1
  13,UJ,
  IWNL:,Z00,TNL
  ,Z00,TNL
  ,Z00,
  8,Z00,
  TNL:,GOST,2H'201''231'
      ,END,
      ZONE       :,NAME,
      **         :,PC,  2000B
      SUM        :,LC,  1
            10 ,  VTM,   BSS+ 0001B
            12 ,  VTM,   *00031
                 ,ATI,  11
            11 ,  XTA,
            10 ,  AAX,  77772B
            10 ,  AOX,  77777B
            10 ,  ATX,
            10 ,   *70,
            11 ,  VTM,  76001B
                 ,XTA,
 *00005    :     ,UTC,      **
            11 ,  AOX,  1777B
            11 ,  VLM,   *00005
                 ,UTC,      SUM
                 ,ATX,
                 ,UZA,   *00012
            12 ,  VTM,   *00032
            10 ,  AEX,  77773B
                 ,UZA,   *00012
            12 ,  VTM,   *00033
 *00012    :12 ,  XTA,
            13 ,   UJ,
      IZ        :,ENTRY,
            10 ,  VTM,   BSS+ 0001B
                 ,ATI,  9
             9 ,  XTA,
            10 ,  APX,  77764B
            10 ,  AUX,  77771B
            15 ,  ATX,
            10 ,  AAX,  77775B
                 ,ASN,  75B
            11 ,  VTM,      **
                 ,ITS,  11
                 ,ASN,  54B
            15 ,  AOX,
            10 ,  AOX,  77763B
            10 ,  STX,  77777B
            10 ,  AOX,  77770B
            13 ,   UJ,
      NOZ       :,ENTRY,
            10 ,  VTM,   BSS+ 0001B
                 ,ATI,  9
             9 ,  XTA,
            10 ,  APX,  77772B
            10 ,  AUX,  77776B
            10 ,  AOX,  77774B
            13 ,   UJ,
                 ,UTC,
 *00027    :,     LOG,  00100000 00000000.
            ,     LOG,  01603407 01603400.
 *00031    :,     LOG,  10047125 23020040.
 *00032    :,     LOG,  10051531 24620040.
 *00033    :,     LOG,  10020040 10020040.
            ,     LOG,  64000000 00000000.
            ,     LOG,  00000000 00077777.
            ,     LOG,  00000000 00000777.
            ,     LOG,  00005252 52525252.
            ,     LOG,  14030060 10020040.
            ,     LOG,  00000000 00077000.
            ,     LOG,  01603407 00000000.
 BSS       :     ,BSS,  2
      ,END,
 БEMШ/ПAP:,NAME,
 ,TEXT,8HDISPAC'33''31'
 ,LOG,
 ,END,
 ПEP6B81C:,NAME,
 15,ATX,
 ,ASN,64+2
 ,ATI,14
 14,UTC,TABISO
 ,XTS,
 ,UTC,BSS
 ,STX,
 ,ASN,64-2
 15,AEX,
 ,ATI,14
 ,UTC,BSS
 ,XTA,
 14,UTC,MASKS
 ,APX,
 ,ASN,64+40
 13,UJ,
 MASKS:,BSS,
 ,GOST,6H00'377'000
 ,GOST,6H000'377'00
 ,GOST,6H0000'377'0
 ,GOST,6H00000'377'
 TABISO:,BSS,
 ,ISO,6H   ,БЦ
 ,ISO,6H  ДФГИ
 ,ISO,6H  ()*Й
 ,ISO,6H  ЛЯЖ/
 ,ISO,6H  0123
 ,ISO,6H  4567
 ,ISO,6H  89Ь:
 ,ISO,6H  П-+Ы
 ,ISO,6H  ЗABC
 ,ISO,6H  DEFG
 ,ISO,6H  HIJK
 ,ISO,6H  LMNO
 ,ISO,6H  PQRS
 ,ISO,6H  TUVW
 ,ISO,6H  XYZШ
 ,ISO,6H  ЭЩЧЮ
 BSS:,BSS,1
 ,END,

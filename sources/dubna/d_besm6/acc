*name n
*no load
*ftn
      programma
с
с    пусть w - разрядность мантиссы.
с    рассмотрим  х1 = 2**р * м    х2 = 2**(р+к) * м
с    ясно, что  х2+х1 > х2     при  к < w.
с    при k => w  выравнивание порядков перед операцией
с    приведет к потере всех значащих разрядов  х1.  при этом
с    в зависимости от принятого в эвм алгоритма округления
с              х2+х1 = х2+е,   где  е - добавка от
с    округления, причем е может добавляться как арифметическим
с    сложением, так и логическим. в любом случае  0<= e <= 2**(-w).
с    возьмем  p=0, т.e. х1 = 1.0   и найдем минимальное  к, при котором
с    е = (х2+х1)-х2 < 0.5*x1             (***)
c    погрешность последнего вычитания равна 0,
с    т.к. порядки у обоих операндов равны.
с    очевидно, что искомое  w  равно  к.
c    если при этом  е = 0,  то округление вообще не проводилось.
с    если же е > 0,   проделаем  (***)  для  х1 = 2**(-n), n=1,2,...,k.
с    пока не получим  е=0.  После этого можно утверждать, что округление
с    производится, если среди первых отбрасываемых  N  разрядов хотя бы
с    один содержит "1".
с    Осталось определить только сам способ округления. Для этого доста-
с    точно в (***) вместо х2 использовать число c тем же порядком, но
с    имеющее в младшем разряде мантиссы не "0", a "1".  При логической
с    реализации округления получим е=0, при арифметической  е > 0.
с    В качестве такого числа проще всего использовать  х2+1.0
с    ("округленная" единичка добавится в младший разряд мантиссы).
c
с
      print 100
 100  format('  БЭСМ-6')
      x1=1.0
      do 1 k=1,100
      x2=x1
      do 2 i=1,k
  2   x2=x2+x2
      e = (x2+x1)-x2
      if(e.ge.0.5*x1) goto 1
      print 200,k
 200  format(' разрядность мантиссы = ',i2)
      if(e.gt.0.0) goto 3
      print 201
 201  format(' округление не делается !')
      stop
  3   do 4 n=1,k
      x1=0.5*x1
      e = (x2+x1)-x2
      if(e.eq.0.0) goto 5
  4   continue
  5   print 202,n
      x2=x2+1.0
      e = (x2+1.0)-x2
      if(e.eq.0.0) print 203
      if(e.ne.0.0) print 204
 202  format(' округление производится по первым',i3,' отбрасываемым',
     *       ' разрядам')
 203  format(' путем логического сложения.')
 204  format(' путем арифметического сложения.')
      stop
  1   continue
      print 102
 102  format(' Мне слабо определить разрядность мантиссы.')
      stop
      end
*execute
*end f

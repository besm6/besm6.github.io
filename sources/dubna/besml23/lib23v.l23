 TEROUT:,NAME,
 8,VTM,
 BEG:,BSS,
 9,BASE,*
 ,STX,N
 ,ATX,A
 15,MTJ,10
 ,WTC,N
 ,XTA,
 ,AAX,=0377 7777 7777 7777
 ,AEX,=77777
 ,ARX,=1
 ,ATI,11
 12,VTM,
 11,VZM,D
 11,UTM,1
 ,UJ,C
 TOUT24:,ENTRY,
C
C...  'TEROUT' БEЗ ПEPEBOДA CTPOKИ
C
 8,VTM,1
 ,UJ,BEG
 C:,BSS,
 ,WTC,A
 12,XTA,
 10,ATX,
 12,UTM,1
 10,UTM,1
 11,VLM,C
 D:,BSS,
 8,V1M,D1
 ,XTA,
 ,UJ,D2
 D1:,BSS,
 ,XTA,=:05
 D2:10,ATX,
 15,MTJ,10
 ,*71,INF
 ,STX,
 13,UJ,
 INF:10,Z04,00000
 10,Z00,00023
 N:,BSS,1
 A:,BSS,1
 ,END,
  TESTLIB:,NAME,
C* ПPOГPAMMA ДЛЯ ПPOBEPKИ ЛИЧHЫX БИБЛИO-
C* TEK B CMЫCЛE COOTBETCTBИЯ ИCTИHHOЙ
C* ДЛИHЫ ПPOГPAMM C YKAЗAHOЙ B KATAЛOГE
C* БИБЛИOTEKИ. B CЛYЧAE HECOBПAДEHИЯ
C* KATAЛOГ ИCПPABЛЯETCЯ. БИБЛИOTEKA
C* ПPEДЛAГAETCЯ PACПOЛOЖEHHOЙ HA MЛ C
C* HOMEPOM 67 C 0 ЗOHЫ.
C* OБPAЩEHИE:      *CALL TESTLIB
C* 24.02.83   ЧEPEHKOB C.И.  B ДYБHE
C*
  LIST:,PC,3072
  MAS1:,EQU,=:174
  MAS2:,EQU,= 77 7770 0000
  MAS3:,EQU,=:003777
  MASE:,EQU,=:7777 7777 776
  NP:,P/P,(LIST)16
  B:8,BASE,B
  ,*70,IW0
  ,*70,IW1
  10,VTM,LIST+2
  11,VTM,
  BEG:10,XTA,
  ,UZA,END
  10,XTA,1
  ,AAX,MASE
  ,UZA,GOOD.ENTRY
  ,AAX,MAS1
  ,ASN,64+36
  ,ATX,NREC.HOM.PEK-ДA
  10,XTA,1
  ,AAX,MAS2
  ,ASN,64+15
  ,ATX,LEN.ДЛИHA TEЛA
  10,XTA,1
  ,AAX,MAS3
  ,ASN,64+30
  ,NTR,3
  ,A-X,=1040
  ,AOX,IW2
  ,AEX,IW
  ,UZA,INMEM
  ,AEX,IW
  ,ATX,IW
  ,*70,IW
  INMEM:,WTC,NREC
  12,VTM,LIST+2050
  12,XTA,
  ,ATI,14
  ,ASN,64+15
  ,ATI,12
  14,J+M,12
  ,ASN,64+15
  ,ATI,14
  14,J+M,12
  ,ITA,12
  ,AEX,LEN
  ,UZA,GOOD
  11,UTM,1
  ,ASN,64-15
  10,AEX,1
  10,ATX,1
  GOOD:10,UTM,2
  ,UJ,BEG
  END:11,V1M,BAD
 ,*64,XOP
  13,UJ,
 BAD:,*64,ПЛO
 ,*70,IWW1
 ,*70,IWW2
  13,UJ,
 XOP:,Z00,XO
 ,Z00,XO
 :0,002,
 8,,
 ПЛO:,Z00,BA
 ,Z00,BA
 :0,002,
 8,,
 XO:,GOST,16HPERSO B ПOPЯДKE'231'
 BA:,GOST,45HБЫЛИ OШИБKИ B ДЛИHE ПPOГP. KATAЛOГ ИCПPABЛEH'231'
 IW0:,010,NP
 ,067,0
 IW1:,010,NP+64
 ,067,1
 IW2:,010,NP+128
 ,067,0
 IWW1:,000,NP
 ,067,0
 IWW2:,000,NP+64
 ,067,1
 NREC:,BSS,1
 LEN:,BSS,1
 IW:,BSS,1
 ,END,
 TEXOUT:,NAME,
C
C****************** E.Д.ФEДЮHЬKИH 03/09/81
C*
C*   SUBROUTINE TEXOUT(TEXT)
C*   BЫBOДИT HA TEPMИHAЛЬHOE YCTPOЙCTBO
C*   И HA ПEЧATЬ COSY-MACCИB TEXT.
C*   PEЖИM PAБOTЫ ЗAДAETCЯ B COMMON-
C*   БЛOKE *TEXORG*, COCTOЯЩEM ИЗ 12 CЛOB
C*   1 - ДЛИHA CTPOKИ BЫBOДA, OT 23 ДO
C*       127 CИMBOЛOB (ECЛИ CЛOBO=0, TO
C*       62 CИMBOЛA),
C*   2 - ДЛИHA CTPOKИ TEKCTA, OT 23 ДO
C*       127 CИMBOЛOB (ECЛИ CЛOBO=0, TO
C*       72 CИMBOЛA),
C*   3 - HOMEP ПEPBOЙ CTPOKИ TEKCTA
C*       (ECЛИ CЛOBO=0, TO HOMEP=1),
C*   4 - MAKC. HOMEP BЫBOДИMOЙ CTPOKИ
C*       (ECЛИ CЛOBO=0,TO 777B), OПPEДEЛЯ-
C*       ET KOЛИЧECTBO ПOЗИЦИЙ ДЛЯ HOMEPA
C*       B CЛYЧAE BЫBOДA C HYMEPAЦИEЙ
C*       (MAKCИMYM 5 ПOЗИЦИЙ),
C*   5 - KOЛИЧECTBO BЫBOДИMЫX CTPOK TEKCTA
C*       (ECЛИ CЛOBO=0, TO 777B),
C*   6 - CПOCOБ BЫBOДA ИHФOPMAЦИИ, ЦEЛOE
C*       OT 1 ДO 16:
C*       1 - C        6 - NC
C*       2 - CC       7 - NCC
C*       3 - C*       8 - NC*
C*       4 - CC*      9 - NCC*
C*       5 - C*C     10 - NC*C
C*      11 - F       13 - NF
C*      12 - F*      14 - NF*
C*                   15 - IF
C*                   16 - IF*
C*       ПPИ ДPYГИX ЗHAЧEHИЯX BЫBOДИTCЯ И
C*       ПEЧATAETCЯ OДHA (ПEPBAЯ) CTPOKA.
C*       ЗДECЬ ПEPBOЙ БYKBOЙ "C" OБOЗHA-
C*       ЧEH PEФEPAT, BTOPOЙ БYKBOЙ "C"-
C*       KOMMEHTAPИЙ. ЗBEЗДOЧKA PACПOЛO-
C*       ЖEHA ПOCЛE OБ'EKTOB, BЫBOДИMЫX
C*       HA ДИCПЛEЙ. ПPEФИKC "N" OЗHAЧA-
C*       ET BЫДAЧY C HYMEPAЦИEЙ. PEФEPAT
C*       OTДEЛЯETCЯ OT KOMMEHTAPИЯ CTPO-
C*       KOЙ, COCTOЯЩEЙ ИЗ ДBYX TOЧEK,
C*       HOPMAЛИЗOBAHHЫX BЛEBO.
C*       БYKBOЙ "F" OБOЗHAЧEH ФAЙЛ HEKOM-
C*       MEHTAPHOГO TИПA, B KOTOPOM HET
C*       PAЗДEЛEHИЯ HA PEФEPAT И KOMMEH-
C*       TAPИЙ, CTPOKA, COCTOЯЩAЯ ИЗ ДBYX
C*       TOЧEK, TPAKTYETCЯ KAK OБЫЧHAЯ
C*       TEKCTOBAЯ CTPOKA. ПPEФИKC "I"
C*       OБOЗHAЧAET 80-KOЛOHHЫE KAPTЫ,
C*       KOTOPЫE BЫДAЮTCЯ C BЫHOCOM ПO-
C*       ЗИЦИЙ 73-80 B HAЧAЛO CTPOKИ.
C*   7 - БИTЫ 1-22 - MAKC. ДЛИHA BЫBOДИ-
C*       MOГO ФAЙЛA B CЛOBAX (ECЛИ CЛOBO
C*       =0, TO 1024),
C*       БИT23=1 - ИГHOPИPOBATЬ ПEPBYЮ
C*       CTPOKY TEKCTA,
C*       БИT24=1 - HE BЫДABATЬ ФИHAЛЬHЫE
C*       COOБЩEHИЯ ЗA ИCKЛЮЧEHИEM
C*       "NONTEXT",
C*       БИT25=1 - KAPTA "*END FILE"
C*       ЯBЛЯETCЯ KOHЦOM TEKCTA,
C*       БИT26=1 - OKAЙMЛЯTЬ BЫBOД
C*       БYKBOЙ "I",
C*       БИT27=1 - OKAЙMЛЯTЬ BЫBOД
C*       ЗBEЗДOЧKOЙ,
C*       (OKAЙMЛЯЮTCЯ TOЛЬKO HEHYMEPO-
C*       BAHHЫE CTPOKИ ПPИ ДЛИHE BЫBOДA
C*       >26)
C*       БИT28=1 - ЗAMEHЯTЬ ГPYППY ПPOБE-
C*       ЛOB OДHИM ПPOБEЛOM,
C*       БИT29=1 - ЗAПPEЩEH ПEPEHOC B
C*       CEPEДИHE CЛOBA (ИГHOPИPYETCЯ,
C*       ECЛИ CПOCOБ BЫBOДA ИHФOPMAЦИИ =
C*       15 ИЛИ 16, CM. 6-E CЛOBO COMMON-
C*       БЛOKA, PAЗДEЛИTEЛЯMИ CЛOB TEKCTA
C*       CЧИTAЮTCЯ ПPOБEЛ, "," И "="),
C*       БИT30=1 - "*READ OLD" HE KOHEЦ
C*       TEKCTA,
C*   8 - БИTЫ 1-22 - HOMEP ПEPBOГO CЛOBA
C*       BЫДAЧИ (CЧИTAЯ C HAЧAЛA ФAЙЛA),
C*       MOДИФИЦИPYETCЯ ПPOГPAMMOЙ ПPИ
C*       BOЗBPATE (ECЛИ БИTЫ=0, HOMEP=1).
C*       ПPИ BOЗBPATE:
C*       БИT23=1 - KOД "LF" ПOCЛEДHEЙ
C*       CTPOKИ TEKCTA ПO TEM ИЛИ ИHЫM
C*       ПPИЧИHAM HE ДOCTИГHYT,
C*       БИT24=1 - OШИБKA B ИMEHИ ФAЙЛA,
C*       БИT25=1 - NONTEXT,
C*       БИT26=1 - ZERO,
C*       БИT27=1 - BЫДAЧA OKOHЧEHA ПO
C*       ИCЧEPПAHИЮ KOЛИЧECTBA BЫBOДИMЫX
C*       CЛOB,
C*       БИT28=1 - ДOCTИГHYT KOHEЦ TEKCTA
C*       (KAPTA *READ OLD)
C*       БИT29=1 - ABORTED BY USER,
C*   9 - ECЛИ = 0B, TO TEXT - AДPEC MAC-
C*       CИBA B OПEPATИBHOЙ ПAMЯTИ, B ПPO-
C*       TИBHOM CЛYЧAE CЛOBO TEXT COДEPЖИT
C*       AДPEC BHEШHEГO HOCИTEЛЯ, HA KOTO-
C*       POM HAXOДИTCЯ TEKCTOBAЯ ИHФOPMA-
C*       ЦИЯ. ECЛИ БИT40 9-ГO CЛOBBA COMMON-
C*       БЛOKA *TEXORG* HEHYЛEBOЙ, TO ЭTO
C*       CЛOBO ЯBЛЯETCЯ ИHФOPMAЦИOHHЫM ДЛЯ
C*       Э70 (MЛAДШИE 18 БИTOB ИГHOPИPYЮTCЯ)
C*       ECЛИ 40-OЙ БИT = 0, ПPOГPAMMA
C*       ИCПOЛЬЗYET OБMEHHЫЙ ЛИCT.
C*       ПPИ BOЗBPATE B MЛAДШИE 18 БИTOB ПO-
C*       MEЩAETCЯ AДPEC ЛИCTA, HA KOTOPOM
C*       HAXOДИTCЯ CЛEДYЮЩEE CЛOBO ПOCЛE
C*       ПOCЛEДHEГO BЫДAHHOГO ИЛИ 0B, ECЛИ
C*       BЫДAЧA ПPOИCXOДИT ИЗ OПEPATИBHOЙ
C*       ПAMЯTИ.
C*  10 - БИTЫ 1-15 - AДPEC ПPOЦEДYPЫ OБЩEГO
C*       BЫBOДA, K KOTOPOЙ ПPOГPAMMA OБPA-
C*       ЩAETCЯ:
C*       CALL BЫBOД(HAЧAЛO,KOHEЦ),
C*       KOЛИЧECTBO BЫBEДEHHЫX ЧEPEЗ ЭTY
C*       ПPOЦEДYPY CTPOK TEKCTA ПPИБABЛЯ-
C*       ETCЯ K COДEPЖИMOMY БИTOB 16-30
C*       ДAHHOГO CЛOBA,
C*       ECЛИ БИTЫ 1-15 - HYЛEBЫE, TEXOUT
C*       ИCПOЛЬЗYET COБCTBEHHYЮ ПPOЦEДYPY
C*       BЫBOДA HA TEPMИHAЛ ПOЛЬЗOBATEЛЯ.
C*  11 - БИTЫ 1-15 - AДPEC ПPOЦEДYPЫ BЫ-
C*       BOДA TOЛЬKO HA ПEЧATЬ, K KOTOPOЙ
C*       ПPOГPAMMA OБPAЩAETCЯ:
C*       CALL ПEЧATЬ(HAЧAЛO,KOHEЦ),
C*       KOЛИЧECTBO BЫBEДEHHЫX ЧEPEЗ ЭTY
C*       ПPOЦEДYPY CTPOK TEKCTA ПPИБABЛЯ-
C*       ETCЯ K COДEPЖИMOMY БИTOB 16-30
C*       ДAHHOГO CЛOBA.
C*  12 - БИTЫ 1-15 - AДPEC PEДAKTИPYЮЩEЙ
C*       ПPOЦEДYPЫ, K KOTOPOЙ TEXOUT
C*       OБPAЩAETCЯ:
C*       CALL PEДAKT(HAЧAЛO,KOHEЦ),
C*       БИTЫ 16-30 - KOД YПPABЛЯЮЩEГO
C*       CИMBOЛA, YПPABЛEHИE ПEPEДAETCЯ
C*       PEДAKTИPYЮЩEЙ ПPOЦEДYPE, ECЛИ
C*       ПEPBЫЙ CИMBOЛ KAPTЫ ИMEET
C*       TAKOЙ KOД. ECЛИ CПOCOБ BЫBOДA
C*       ИHФOPMAЦИИ = 15 ИЛИ 16 (CM. 6-E
C*       CЛOBO), TO PEДAKTИPYЮЩEЙ ПPOЦE-
C*       ДYPE BЫДAETCЯ 80-KOЛOHHAЯ KAPTA.
C*       KAPTA BCEГДA BЫДAETCЯ B OPИГИ-
C*       HAЛЬHOM BИДE HEЗABИCИMO OT
C*       ЗHAЧEHИЙ БИTOB 7-ГO CЛOBA, OД-
C*       HAKO XBOCTOBЫE ПPOБEЛЫ ИГHOPИ-
C*       PYЮTCЯ.
C*
C*       ГAPAHTИPYETCЯ ПO KPAЙHEЙ MEPE 1
C*       HYЛEBOЙ БAЙT B KOHЦE TEKCTA ПPИ
C*       OБPAЩEHИИ K TPEM BЫШEYKAЗAHHЫM
C*       ПPOЦEДYPAM (ECЛИ HYЖHO, ДOБABЛЯ-
C*       ETCЯ ПYCTOE CЛOBO).
C*       ECЛИ OДHA ИЗ ЭTИX ПPOЦEДYP
C*       ЗACЫЛAET 1 B 29 БИT 8-ГO CЛOBA
C*       COMMON-БЛOKA, TO BЫBOД
C*       ПPEKPAЩAETCЯ.
C*
C*   BЫBOДИTCЯ MAKCИMYM 77777B CTPOK.
C*   BЫBOДИTCЯ MAKCИMYM 17777777B CЛOB.
C*
C*   БИTЫ 16-48 CЛOB 1-6 COMMON-БЛOKA
C*   *TEXORG*, БИTЫ 31-48 EГO CЛOB 7,8,
C*   10-12 ИГHOPИPYЮTCЯ И COXPAHЯЮTCЯ.
C*
C*   AДPEC BHEШHEГO HOCИTEЛЯ (ECЛИ 9
C*   CЛOBO COMMON-БЛOKA HEHYЛEBOE):
C*   ECЛИ БИTЫ 41-48 CЛOBA "TEXT" HYЛE-
C*   BЫE, TO B MЛAДШИX 18 БИTAX ЭTOГO
C*   CЛOBA ДOЛЖEH БЫTЬ ЛOГИЧECKИЙ AДPEC
C*   BHEШHEГO HOCИTEЛЯ. B ПPOTИBHOM CЛYЧAE
C*   ПPEДПOЛAГAETCЯ ЧTO AДPEC ЗAДAH B
C*   KOДE ISO И HOPMAЛИЗOBAH BЛEBO.
C*   ПPИ ЭTOM AДPEC MOЖET ИMETЬ 6, 5
C*   ИЛИ 2-X CИMBOЛЬHYЮ ФOPMY. HAПPИ-
C*   MEP, HYЛEBAЯ ЗOHA YCTPOЙCTBA 67
C*   MOЖET БЫTЬ ЗAДAHA KAK "670000",
C*   "67000" ИЛИ "67". KPOME TOГO, OД-
C*   HOCИMBOЛЬHЫE AДPECA "0", "1", "2",
C*   ..., "7" COOTBETCTBYЮT БAPAБAHAM
C*   1, 3, 5, ..., 17.
C*
C*   COXPAHЯЮTCЯ BCE PEГИCTPЫ И PEЖИM.
C*
C
 *TEXORG*:,LC,12
 ,BLOCK,NS,MS*,FL*,LL*,NL,RR,NW*,NWS*,EX,OUTP,PR,EDIT
 S:15,ATX,1
 ,RTE,77B
 ,ITS,14
 15,AEX,
 15,WTC,1
 14,VTM,
 ,ITS,13
 13,VTM,12
 :13,ITS,
 13,UTM,-1
 13,V1M,*-1
 1,BASE,S
 14,XTS,
 ,ATX,HEAD+6
 ,AAX,=:776
 ,ATX,HEAD+7
 14,MTJ,5
 14,VTM,*TEXORG*
 14,WTC,OUTP
 12,VTM,
 :12,V1M,*+1
 12,VTM,OWN
 :,ITA,12
 14,WTC,PR
 12,VTM,
 ,ATX,RG
 ,ITA,12
 ,ATX,HEAD+1
 12,VTM,OUTP
 ,ITA,12
 ,ATX,RG0
 12,UTM,1
 ,ITA,12
 ,ATX,HEAD+2
 14,XTA,EX
 ,ATX,HEAD+5
 ,AAX,MASK30
 14,ATX,EX
 14,XTA,NWS*
 ,AAX,=7777770017777777
 14,ATX,NWS*
 ,AAX,=17777777
 :,U1A,*+2
 ,XTA,=1
 14,AEX,NWS*
 14,ATX,NWS*
 :,AAX,=17777777
 ,ATX,HEAD+3
 ,ITA,
 10,VTM,F2-HEAD
 :,UTC,HEAD
 10,ATX,
 10,VLM,*-1
 14,XTA,NW*
 ,ATX,HEAD+10
 ,AAX,=17777777
 :,U1A,*+1
 ,XTA,=2000
 :,ATX,HEAD+4
 ,NTR,7
 :,A-X,HEAD+3
 ,UZA,*+6
 ,XTA,HEAD+4
 ,ARX,=1
 ,AAX,=17777777
 ,ATX,HEAD+3
 14,AEX,NWS*
 ,AAX,=17777777
 14,AEX,NWS*
 ,AOX,=:00000004
 14,ATX,NWS*
 ,XTA,=37777777777777
 :,A+X,=1
 ,ATX,HEAD+4
 ,ATI,2
 ,A-X,=100000
 ,U1A,*+1
 2,VTM,77777B
 :14,WTC,NL
 4,VTM,
 4,V1M,*+1
 4,VTM,777B
 :14,WTC,FL*
 8,VTM,
 14,WTC,LL*
 9,VTM,
 :8,VZM,*+1
 8,UTM,-1
 :,ITA,8
 ,ATX,NUM
 :9,V1M,*+1
 9,VTM,777B
 :,ITA,9
 ,X-A,NUM
 :,U1A,*+2
 ,ITA,9
 ,ATX,NUM
 4,VTM,
 :,ITA,9
 ,A-X,NUM
 ,ITS,4
 15,X-A,
 :,UZA,*+2
 15,UTC,
 ,WTC,
 4,VTM,
 :,ITA,9
 8,VTM,
 9,VTM,-40
 11,VTM,HEAD+5
 :,ASN,64+3
 8,UTM,1
 9,UTM,8
 ,U1A,*-1
 14,WTC,NS
 3,VTM,
 3,V1M,*+1
 3,VTM,62
 :,ITA,3
 ,A-X,=27
 :,UZA,*+1
 3,VTM,23
 :,A-X,=151
 7,VTM,-48
 :,U1A,*+1
 3,VTM,127
 :14,WTC,MS*
 12,VTM,
 12,V1M,*+1
 12,VTM,72
 :,ITA,12
 ,A-X,=27
 ,UZA,*+1
 12,VTM,23
 :14,XTS,NW*
 ,ASN,64+21
 15,ATX,
 ,AAX,=60
 ,STI,10
 ,ASN,64+6
 15,ATX,
 ,AAX,=1
 ,STX,SUP0
 ,AAX,=2
 :,UZA,*+1
 ,XTA,=77724
 :,STX,SHIFT0
 ,A-X,=151
 ,U1A,*+1
 12,VTM,127
 :,ITA,3
 ,ATX,FLEN
 ,ITA,12
 ,ATX, UV
 14,XTA,EDIT
 ,ATI,13
 13,VZM,*+2
 ,ASN,64-25
 ,AAX,=:776
 ,ATX,EDI
 :10,VZM,TEX-1
  ,XTA,=6H^ * I
 10,ASN,64-16
 ,ATX,HEADR
 ,AOX,=55
 ,ATX,F2
 ,ASN,64+8
 ,AOX,=20000
 ,ATI,10
 ,AEX,=6400
 ,ASN,64-32
 ,ATX,L2
 :11,XTA,
 ,U1A,FILE
 TEX:14,WTC,RR
 14,VTM,
 ,ITA,14
 14,V1M,*+2
 :4,VTM,1
 ,UJ,CONT
 :,A-X,=6
 14,J+M,14
 ,U1A,NONUM
 ,A-X,=5
 TEX1:,UZA,GT10
 ,XTA,=:001
 8,MTJ,13
 8,J+M,13
 8,J+M,13
 13,ASN,64-9
 ,ATX,ADD
 ,XTA,NUM
 13,ASN,64
 ,YTA,
 ,ATX,NUM
 ,ITA,9
 ,X-A,
 ,ATI,13
 ,XTA,GOST7
 13,ASN,64+8
 ,ASN,64-8
 ,ATX,UPMASK
 ,ITA,4
 ,ASN,64+1
 ,U1A,TEX2
 9,VTM,-48
 TEX2:,ITA,8
 ,X-A,FLEN
 ,A-X,=1
 9,MTJ,7
 ,ATI,3
 ,UJ,CONT
 NONUM:,ITA,3
 ,X-A,=32
 10,VZM,CONT
 ,UZA,CONT
 ,ITA,10
 ,ASN,64+16
 ,YTA,
 ,ATX,LAST0
 3,UTM,-4
 7,VTM,-32
 CONT:,XTA,=:46502511520522
 14,J+M,14
 ,WTC,*TEXORG*+PR
 13,VTM,
 14,ASN,64-34
 ,AAX,=:0074
 :13,V1M,*+1
 ,AAX,=:005
 :,UZA,OUT
 ,ITS,3
 ,X-A,UV
 14,VTM,RG
 ,UZA,*+2
 ,WTC,UV
 3,VTM,
 ,ITA,
 :,STX,UV
 ,NTR,23B
 :,UZA,*+1
 14,VTM,HEAD+1
 :,ASN,64-2
 ,AAX,=:0074
 13,VTM,
 ,UZA,*+3
 13,VTM,RG
 ,NTR,23B
 ,UZA,*+1
 13,VTM,HEAD+1
 :13,XTA,
 ,ASN,64-15
 14,AEX,
 ,ATX,RG
 13,XTA,1
 ,ASN,64-15
 14,AEX,1
 ,ATX,RG0
 6,VTM,
 ,WTC,HEAD+3
 5,UTM,-1
 4,VZM,NOLINE
 2,VZM,ENDW
 ,XTA,=20000000
 ,ATX,INF0
 ,AAX,HEAD+10
 14,VTM,ENDW
 ,U1A,TLF
 ,XTA,NOCOM
 ,U1A,CONT2
 ,ITA,2
 ,X-A,=2
 ,UZA,CONT2
 5,XTA,
 ,AEX,=6HHEADER
 ,U1A,CONT2
 5,XTA,1
 ,AEX,=6H'315''12'   '12'
 ,ATX,INF0
 ,U1A,CONT2
 ,XTA,=2
 14,VTM,*TEXORG*
 14,ARX,NWS*
 14,ATX,NWS*
 5,UTM,2
 2,UTM,-2
 CONT2:,XTA,EDI
 3,MTJ,11
 10,VTM,HEAD
 ,UZA,CONT22
 ,ATX,EDIS
 5,AEX,
 ,AAX,=:776
 ,U1A,CONT22-1
 ,XTA,C80F0
 ,U1A,CONT10
 ,WTC,UV
 11,UTM,
 ,ATX,LAST
 ,ATX,SHIFT
 :,ATX,SUP
 ,ATX,TEXC
 12,VTM,-48
 ,UJ,CF
 CONT10:,ITA,
 ,ATX,C80F
 11,VTM,80
 ,UJ,CONT10-2
 CONT11:,ATX,C80F
 ,ITA,
 ,ATX,TEXC
 ,XTA,=6H
 ,ATX,HEAD+2
 ,ATX,HEAD+1
 11,VTM,72
 10,VLM,CONT1
 CONT12:5,XTA,
 ,AEX,=6HOLD'312''12''12'
 ,UZA,ENDT
 5,UTM,-1
 ZWEOF:,XTA,=20000000
 ,ATX,LF
 ,ITA,
 ,ATX,LASTBL
 ,XTA,=6H*READ
 8,VTM,
 2,UTM,1
 ,UJ,CU+1
 CONT13:,XTA,HEADR
 ,UJ,CONT1
 :,ITA,
 ,ATX,EDIS
 CONT22:,XTA,SUP0
 ,ATX,SUP
 ,XTA,=40
 ,ATX,LW
 ,XTA,RG
 ,NTR,13B
 ,U1A,*+3
 ,E+N,
 ,ASN,64+15
 ,XTS,RG0
 ,ASN,64+15
 ,STX,RG0
 :,ATX,RG
 ,XTA,C80F0
 7,MTJ,12
 ,U1A,CONT11
 ,XTA,UV
 ,ATX,TEXC
 ,XTA,SHIFT0
 ,ATX,SHIFT
 ,XTA,LAST0
 ,ATX,LAST
 ,U1A,CONT13
 ,XTA,ADD
 ,UZA,CONT1
 ,ARX,NUM
 ,ATX,NUM
 ,AUX,UPMASK
 ,AEX,=6H00000
 12,V1M,CONT1
 12,VTM,-48
 10,UTM,1
 CONT1:,ATX,HEAD
 5,XTA,
 ,UZA,ZERW
 ,AEX,=:7760000000000012
 ,UZA,ENDT
 ,AEX,=6H'325'READ*
 14,VTM,ZWEOF
 13,VTM,CONT12
 ,UZA,TESEC1
 ,AEX,=241
 ,UZA,TESEC1
 ,AEX,=6H'''27''13''5''144''307'
 :,UZA,*+1
 ,AEX,=120400
 :,U1A,CQ
 ,XTA,=:00000001
 14,VTM,*TEXORG*
 14,AAX,NW*
 ,UZA,CF
 ,ASN,64-3
 14,AOX,NWS*
 14,ATX,NWS*
 4,VTM,1
 ,UJ,CF
 CQ:,XTA,NOCOM
 ,U1A,CF
 :5,XTA,
 ,AEX,=6H..'321''12' '12'
 ,U1A,CF
 ,ITA,6
 ,ASN,64+1
 ,U1A,CF
 ,XTA,RG
 ,ASN,64+15
 6,VTM,2
 11,VTM,
 ,UZA,COM1
 ,XTA,=56
 ,ATX,MULT
 ,XTA,RG
 ,E-N,
 ,ATX,RG
 ,ITA,
 ,ATX,TEXC
 3,MTJ,14
 ,UJ,CB
 TESEC1:,XTA,=:0000004
 ,UTC,*TEXORG*
 ,AAX,NW*
 ,UZA,TESEC
 CF:,XTA,=20000000
 ,ATX,LF
 ,ITA,
 ,ATX,LB
 ,ATX,LASTBL
 5,XTA,
 ,NTR,13B
 8,VTM,
 :,U1A,*+5
 ,E+N,
 ,ASN,64-8
 ,ATX,WORD
 ,YTA,
 ,UZA,ERR
 15,ATX,2
 ,ASN,64+1
 ,UZA,BLTF
 ,UJ,BL0
 :,ASN,64-8
 ,ATX,WORD
 ,YTA,
 ,ATX,LW
 ,X-A,=40
 ,U1A,SJN-1
 ,ATI,14
 14,VZM,BLTF
 14,UTM,-26B
 ,UJ,LE40+1
 CU:,NTR,13B
 ,UZA,BL
 :,ASN,64-8
 ,ATX,WORD
 ,YTA,
 ,WTC,LW
 13,VTM,-40B
 ,ATX,LW
 ,X-A,=40
 ,ATI,14
 ,UZA,LE40
 ,WTC,SHIFT
 :13,UTM,40B
 13,VZM,*+2
 13,UTM,54B-75B
 13,V1M,*+4
 :,ITS,11
 ,ATX,SAV11
 ,ITA,12
 ,ATX,SAV12
 ,ITA,10
  ,STX,LASTBL
 :,X-A,=40
 ,ATX,LB
 SJN:10,XTS,
 ,ASN,64-8
 15,AEX,
 10,ATX,
 12,UTM,8
 12,V1M,*+2
 :10,UTM,1
 12,VTM,-48
 :11,UTM,-1
 11,VZM,SHIFTS
 CA:,XTA,WORD
 ,U1A,CU
 :,XTA,=1
 5,UTM,1
 4,VZM,*+3
 14,VTM,*TEXORG*
 14,ARX,NWS*
 14,ATX,NWS*
 2,UTM,-1
 2,VZM,SETNL
 :5,XTA,
 ,U1A,CU
 ,ATX,TEXC
 ,UJ,ENDL
 SETNL:5,UTM,-1
 13,VTM,*-2
 14,VTM,ENDLE
 ,UJ,NEXTL
 LE40:14,VZM,*+2
 14,UTM,-26B
 :14,VZM,ENDLE
 ,UJ,ERR
 :,XTA,SUP
 13,V1M,BLTF0
 ,UZA,BLTF
 ,XTA,TEXC
 ,A-X,=1
 ,U1A,*+2
 ,ATX,TEXC
 ,UJ,CA
 :8,UTM,1
 11,UTM,-1
 11,V1M,CA
 ,UJ,ENDL
 BLTF0:,XTA,SHIFT
 ,UZA,BLTF
 ,ITA,11
 ,ATX,SAV11
 ,ITA,12
 ,ATX,SAV12
 ,ITA,10
 ,ATX,LASTBL
 BLTF:,XTA,=40
 ,UJ,SJN
 GT10:,XTS,=1
 ,STX,NOCOM
 ,A-X,=2
 14,UTM,-21
 14,J+M,14
 ,U1A,NONUM
 ,A-X,=2
 14,UTM,2
 ,U1A,TEX1
 ,A-X,=2
 14,VTM,
 ,UZA,TEX+2
 ,ASN,64-2
 ,ATI,14
 ,XTA,NOCOM
 ,ATX,C80F0
 14,UTM,10
 9,VTM,-24
 8,VTM,8
 ,UJ ,TEX2
 SHIFTSL:14,VTM,ENDLEC
 13,VJM,TESEC
 :,XTA,=1
 14,VTM,*TEXORG*
 14,ARX,NWS*
 14,ATX,NWS*
 5,XTA,
 ,ATX,WORD
 ,U1A,SHIFTS1
 ,UJ,SHIFTS3
 SHIFTS:,XTA,TEXC
 ,UZA,ENDL
 ,XTA,LASTBL
 ,UZA,ENDL1
 ,WTC,LW
 13,VTM,-54B
 ,XTA,LW
 ,AEX,=40
 13,VZM,ENDL1
 ,UZA,ENDL1
 ,XTA,WORD
 13,UTM,54B-75B
 13,VZM,ENDL1
 ,UZA,SHIFTSL
 SHIFTS1:,ATX,SWORD
 ,NTR,13B
 ,UZA,SHIFTS2
 ,AEX,=:024
 ,AAX,=:776
 ,UZA,SHIFTS3
 10,XTA,
 ,ASN,64-8
 ,AEX,=200
 12,ASN,64+8
 10,ATX,
 ,ITA,
 10,ATX,1
 ,XTA,SAV11
 ,ATI,11
 ,ARX,TEXC
 ,ATX,TEXC
 ,WTC, LASTBL
 10,VTM,
 ,ITA,2
 ,ATX,SAV11
 ,ITA,5
 ,ATX,SREG
 ,XTA,SAV12
 ,ATI,12
 ,X-A,
 2,VTM,24
 ,ATI,14
 ,UTC,*TEXORG*
 ,XTA,NWS*
 ,ATX,SAV12
 10,XTA,
 14,ASN,64
 10,ATX,
 ,YTA,
 10,MTJ,14
 13,VTM,SAVSH-1
 13,MTJ,5
 :,ATX,WORD
 ,AEX,=:1
 ,AAX,=:776
 ,U1A,*+5
 ,XTA,TEXC
 ,A-X,=1
 ,ATX,TEXC
 ,XTA,WORD
 ,ASN,64-8
 ,U1A,*-4
 14,XTA,1
 14,VLM,*-5
 :14,XTA,1
 13,ATX,1
 14,UTM,1
 13,UTM,1
 ,U1A,*-2
 ,UJ,ENDL
 SHIFTS5:,XTA,=1
 14,VTM,*TEXORG*
 14,ARX,NWS*
 14,ATX,NWS*
 5,XTA,
 ,ATX,WORD
 ,U1A,SHIFTS4
 ,UJ,SHIFTS3
 SHIFTS2:,E+N,
 ,ASN,64-8
 ,ATX,WORD
 ,YTA,
 ,UZA,FORT
 ,X-A,TEXC
 ,U1A,SHIFTS3
 ,ATX,TEXC
 ,NTR,7
 ,UZA,SHIFTS3+1
 ,XTA,WORD
 14,VTM,ENDLEC
 13,VTM,SHIFTS5
 ,UZA,TESEC
 SHIFTS4:,NTR,13B
 ,UZA,ERR
 ,AEX,=:024
 ,AAX,=:776
 ,U1A,ENDL1
 ,ATX,WORD
 SHIFTS3:,ITA,
 ,ATX,TEXC
 :14,VTM,
 ,UJ,ENDL1
 NEXTL:,XTA,INF
 ,UZA,ZERBL-1
 ,XTA,MAXW
 ,A-X,=2000
 :,UZA,*+2
 ,ITA,
 ,WTC,MAXW
 ,UTC,-2000B
 :2,VTM,2000B
 ,ATX,MAXW
 :,ITA,14
 ,XTS,ADDL
 ,ARX,INF
 ,AAX,MSL
 ,ITS,2
 ,ASN,64+10
 :,UZA,*+1
 15,XTA,-1
 :,STX,INF
 14,VTM,*TEXORG*
 14,AEX,EX
 ,AAX,=777777
 14,AEX,EX
 14,ATX,EX
 15,WTC,
 2,VZM,
 5,UTM,-1777B
 15,*70,1
 :14,VTM,*TEXORG*
 13,UJ,
 :14,MTJ,13
 ,UJ,*-1
 ZERBL:5,XTA,
 ,UZA,ENDLEC
 ,NTR,13B
 ,UZA,ERR0
 ,AEX,=:024
 ,AAX,=:776
 ,UZA,ENDLEC
 5,UTM,-1
 2,UTM,1
 ,UJ,CC1
 BL:,E+N,
 ,ASN,64-8
 ,ATX,WORD
 8,MTJ,14
 :,YTA,
 ,UZA,FORT
 ,WTC,LW
 13,VTM,-40B
 ,XTS,=40
 ,STX,LW
 15,ATX,2
 ,ASN,64+1
 ,UZA,LE40+2
 ,ITA,13
 15,ATX,3
 ,XTA,SHIFT
 13,VZM,BL0
 ,UZA,BL0
 ,ITA,11
 ,ATX,SAV11
 ,ITA,12
 ,ATX,SAV12
 ,ITA,10
 ,ATX,LASTBL
 BL0:,XTA,WORD
 13,VTM,ZERBL
 14,VTM,ENDLEC
 ,UZA,TESEC
 ,NTR,13B
 ,UZA,ERR
 ,AEX,=:024
 ,ASN,64-8
 ,YTA,
 ,UZA,ENDLE
 CC1:,ITA,11
 15,A-X,2
 15,WTC,2
 14,VTM,
 ,U1A,ENDLB
 ,NTR,7
 ,UZA,ENDLB10
 ,XTS,SUP
 ,UZA,ENDLB2
 15,XTA,
 ,ARX,TEXC
 15,ATX,2
 15,XTA,3
 ,UJ,ENDLB4
 ENDLB2:15,WTC,
 11,VTM,
 CC:,XTA,=40
 ,ATX,MULT
 CB:10,XTA,
 ,ASN,64-8
 ,AEX,MULT
 10,ATX,
 12,UTM,8
 12,V1M,*+2
 12,VTM,-48
 10,UTM,1
 :14,UTM,-1
 14,V1M,*-4
 11,VZM,ENDL1
 ,UJ,CA
 ENDLB:15,ATX,1
 ,A+X,TEXC
 ,UZA,ENDLB3
 ,XTA,C80F
 ,UZA,ENDLE
 11,VTM,
 8,VTM,
 ,UJ,ENDLB1
 ENDLB3:15,ATX,2
 ,NTR,7
 ,UZA,ENDLE
 ,XTA,SUP
 ,UZA,ENDLB1
 15,XTA,3
 ENDLB4:,UZA,ENDLB5
 ,ITA,11
 ,A-X,=1
 15,X-A,2
 ,UZA,*+3
 ,ATI,14
 ,X-A,
 ,ATI,8
 ,ITA,
 14,J+M,11
 :,ATX,TEXC
 ,UJ,BLTF
 ENDLB5:,ITA,11
 15,X-A,2
 ,UZA,*+3
 ,ATI,14
 ,X-A,
 ,ATI,8
 ,ITA,
 14,J+M,11
 :,ATX,TEXC
 ,UJ,CA
 ENDLB1:15,XTA,1
 ,X-A,
 ,AEX,WORD
 ,ASN,64+8
 15,ATX,
 ,YTA,
 15,AEX,
 ,E-N,
 ,ATX,WORD
 ,UJ,ENDL
 ENDLB10:,XTA,C80F
 ,UZA,ENDL
 10,XTA,
 ,XTS,C80FF
 14,VTM,
 ,UJ,TESC80+3
 ENDT:,ATX,LF
 ,XTA,=2
 14,VTM,*TEXORG*
 14,ARX,NWS*
 ,AOX,=:0000001
 14,ATX,NWS*
 ,XTA,INF0
 6,VZM,ZERO
 ,UZA,NOLINE+1
 5,VTM,ENDOFT
 11,VTM,21
 ,UJ,ZERO+1
 ENDLEC:,XTA,=1
 14,VTM,*TEXORG*
 14,ARX,NWS*
 14,ATX,NWS*
 ENDLE:,ITA,
 ,ATX,TEXC
 ENDL:,XTA,LAST
 11,MTJ,14
 11,VTM,
 8,J+M,14
 8,VTM,
 14,VZM,*+2
 ,U1A,CC
 ,UJ,TESC80
 :,U1A,ENDL1+1
 ,UJ,TESC80
 ENDL1:,XTA,LAST
 ,UZA,TESC80
 CCY:,ASN,64-8
 15,ATX,1
 ,YTA,
 10,XTS,
 ,ASN,64-8
 15,AEX,
 10,ATX,
 15,XTA,1
 :12,UTM,8
 12,V1M,*+2
 12,VTM,-48
 10,UTM,1
 PRINT:,U1A,CCY
 10,XTA,
 :12,ASN,64
 10,ATX,
 :13,VTM,PRINT1
 ,XTA,TEXC
 9,VTM,HEAD
 4,VZM,PRINTS
 2,VZM,PRINTS-1
 ,U1A,PRINTS
 :5,XTA,
 ,UZA,PRINTS-1
 ,AEX,=12
 ,AAX,=377
 14,VTM,*TEXORG*
 ,XTS,=1
 14,ARX,NWS*
 14,STX,NWS*
 2,UTM,-1
 2,VZM,TESEW
 5,UTM,1
 ,U1A,*-5
 :,U1A,*+1
 ,ATX,LF
 :,XTA,EDIS
 ,U1A,TOEDIT
 PRINTS:14,VTM,RG
 :,ITA,13
 ,ITS,9
 ,ITS,10
 ,NTR,6
 14,WTC,
 13,VJM,
 :,NTR,7
 8,VTM,
 ,ATI,13
 13,UJ,
 TOEDIT:14,VTM,*TEXORG*+EDIT
 ,UJ,PRINTS+1
 OWN:15,XTA,
 ,*71,*+2
 :15,XTA,
 13,UJ,
 :9,Z04,
 ,,
 TESEW:4,MTJ,14
 14,UTM,-1
 14,VZM,PRINTS-2
 ,U1A,*+2
 ,ATX,LF
 ,UTC,*-PRINT
 :13,VTM,PRINT+2
 14,VJM,NEXTL
 :13,VTM,PRINT1
 ,UJ,PRINTS-1
 TESC80:10,XTA,
 4,VZM,PRINT+1
 ,WTC,C80F
 13,VTM,
 13,VZM,PRINT+1
 ,XTS,C80FF
 :,UZA,TES801
 ,STI,14
 ,AEX,=6H
 12,ASN,64
 10,ATX,
 ,ITA,
 ,ATX,C80FF
 14,XTA,3
 ,UZA,*+3
 ,AEX,HEAD+1
 ,ATX,HEAD+1
 14,XTA,2
 :,ATX,HEAD
 ,UJ,TES802
 :14,XTA,2
 ,UZA,TES802
 ,AEX,HEAD
 ,UJ,*-2
 TES801:10,ATX,3
 10,ATX,2
 10,STX,1
 12,ASN,64
 10,ATX,
 14,V1M,TES802
 ,ITA,10
 ,ATX,C80FF
 ,ITA,
 ,ATX,SUP
 11,VTM,8
 12,VTM,-48
 10,UTM,2
 ,UJ,CA
 TES802:,XTA,HEAD+1
 10,VTM,HEAD+1
 3,MTJ,11
 11,UTM,-3
 :,AAX,=377
 ,UZA,PRINT+2
 ,ITA,11
 ,A-X,=6
 ,U1A,*+2
 ,ATI,11
 10,XTA,1
 10,VLM,*-3
 :10,XTA,1
 10,UTM,1
 12,VTM,-48
 11,VZM,*+3
 :,UZA,PRINT+2
 ,ASN,64-8
 12,UTM,8
 11,UTM,-1
 :11,V1M,*-2
 ,ATX,SWORD
 ,UZA,PRINT+2
 ,XTA,MASK48
 12,ASN,64
 10,AAX,
 10,ATX,
 ,ITA,10
 ,ATX,HEAD+21
 ,XTA,UV
 ,UZA,PRINT+2
 ,ATX,TEXC
 TES803:9,VTM,HEAD
 13,VJM,PRINTS
 :,XTA,=:0000002
 ,UTC,*TEXORG*
 ,AAX,NWS*
 ,U1A,PRINT+5
 ,WTC,HEAD+21
 14,VTM,
 ,XTA,=6H
 ,ATX,HEAD
 ,ATX,HEAD+1
 ,ITA,3
 ,X-A,TEXC
 7,MTJ,12
 3,MTJ,11
 10,VTM,HEAD+1
 :,UZA,*+2
 ,WTC,TEXC
 11,VTM,
 ,ITA,
 :,ATX,TEXC
 ,XTA,SWORD
 :,U1A,*+2
 14,XTA,1
 14,UTM,1
 ,UZA,TES804
 :,ASN,64-8
 ,ATX,SWORD
 ,YTA,
 10,XTS,
 ,ASN,64-8
 15,AEX,
 10,ATX,
 ,XTA,SWORD
 12,UTM,8
 12,V1M,*+2
 12,VTM,-48
 10,UTM,1
 :11,UTM,-1
 11,V1M,*-8
 10,XTA,
 12,ASN,64
 10,ATX,
 ,XTA,SWORD
 :,UZA,*+3
 ,ITA,14
 ,ATX,HEAD+21
 ,XTA,TEXC
 ,UZA,PRINT+2
 ,UJ,TES803
 :14,XTA,1
 ,ATX,SWORD
 14,UTM,1
 ,U1A,*-4
 TES804:,ATX,TEXC
 ,UJ,PRINT
 COM1:,WTC,FLEN
 14,VTM,
 COM2:,XTA,=55
 ,ATX,MULT
 ,XTA,LAST
 4,VTM,
 12,VTM,-48
 10,VTM,HEAD
 :,UZA,*+5
 ,XTA,F2
 ,ATX,HEAD
 ,XTA,L2
 ,ATX,LAST
 12,VTM,-32
 :14,UTM,-4
 8,VZM,*+2
 14,UTM,2
 8,UTM,-2
 :14,V1M,CB
 ,UJ,CA
 PRINT1:4,VZM,OUT
 6,V1M,*+2
 ,WTC,NOCOM
 6,VTM,1
 :,XTA,TEXC
 ,NTR,7
 :,U1A,CONTC
 ,XTA,EDIS
 :,UZA,*+1
 ,UTC,-=100000
 PRINTZ:,XTA,=100000
 14,VTM,*TEXORG*
 ,WTC,RG0
 14,ARX,
 ,WTC,RG0
 14,ATX,
 ,XTA,=:0000002
 14,AAX,NWS*
 4,UTM,-1
 ,U1A,*+2
 NOLINE:2,VZM,*+4
 4,V1M,CONT2
 :,XTA,=40000000
  ,UTC,*TEXORG*
 ,AAX,NW*
 ,U1A,OUT
 11,VTM,
 ,UJ,COM1
 :4,V1M,ENDW
 ,XTA,=:00000004
 14,VTM,*TEXORG*
 14,AOX,NWS*
 14,ATX,NWS*
 ,UJ,NOLINE+1
 CONTC:,NTR,13B
 ,UZA,ERR1
 ,ITS,3
 15,X-A,
 7,MTJ,12
 3,MTJ,11
 ,UZA,*+3
 ,X-A,
 ,ATI,8
 ,WTC,TEXC
 11,VTM,
 ,ITA,
 :,ATX,TEXC
 ,XTA,=:0000002
 14,VTM,*TEXORG*
 14,AAX,NWS*
 ,U1A,PRINTZ
 ,XTA,ADD
 ,U1A,*+1
 ,UTC,HEADR-=5H=====
 :,XTA,=5H=====
 ,ATX,HEAD
 ,XTA,=40
 ,ATX,LW
 ,ITA,
 ,ATX,LB
 ,ATX,LASTBL
 12,V1M,*+2
 12,VTM,-48
 ,UTC,1
 :10,VTM,HEAD
 ,XTA,WORD
 TS12:14,VTM,ENDW
 13,VTM,TS1
 :,UZA,TESEC
 ,AEX,=:024
 ,AAX,=:776
 ,UZA,TLF
 ,XTA,WORD
 ,NTR,13B
 ,U1A,CU+1
 ,WTC,SHIFT
 13,VTM,
 13,V1M,TS122
 ,ASN,64-8
 13,VTM,TS2
 ,UZA,TESEC
 ,AEX,=:024
 ,AAX,=:776
 ,UZA,TLF
 ,XTA,WORD
 ,UJ,BL
 TS122:,E+N,
 ,ASN,64-8
 ,ATX,WORD
 ,YTA,
 ,UZA,ERR
 ,X-A,TEXC
 ,ATX,TEXC
 ,UZA,TS123
 ,ITA,11
 ,A+X,TEXC
 ,U1A,TLF
 ,ATI,11
 ,ITA,8
 ,A-X,TEXC
 11,VZM,TLF
 ,ATI,8
 ,ITA,
 ,ATX,TEXC
 TS123:,XTA,WORD
 ,UJ,TS12
 TLF:5,XTA,
 ,UZA,ZERW
 ,AEX,=12
 ,AAX,=377
 ,XTS,=1
 13,VTM,*TEXORG*
 13,ARX,NWS*
 13,STX,NWS*
 13,VTM,TLF
 ,U1A,TESEC
 ,ATX,LF
 13,VTM,CONT2
 6,VZM,*+1
 13,VTM,PRINTZ
 TESEC:2,UTM,-1
 5,UTM,1
 13,UTC,
 2,V1M,
 5,UTM,-1
 ,UJ,NEXTL
 TS1:,XTA,=1
 13,VTM,*TEXORG*
 13,ARX,NWS*
 13,ATX,NWS*
 5,XTA,
 ,ATX,WORD
 ,U1A,TS12
 ,UJ,ZERW
 TS2:13,VTM,*TEXORG*
 13,XTA,NWS*
 15,ATX,1
 ,ARX,=1
 13,ATX,NWS*
 5,XTA,
 ,UZA,ZERW
 ,AAX,=:776
 ,AEX,=:024
 14,VTM,ENDW
 ,UZA,TLF
 15,XTA,1
 13,ATX,NWS*
 5,UTM,-1
 ,XTA,WORD
 2,VLM,BL
 :11,VTM,
 4,V1M,ERR
 8,V1M,CB
 ,UJ,ENDL1
 FORT:,XTA,SREG
 ,UZA,FORT-2
 ,ATI,5
 ,WTC,SAV11
 2,VTM,
 ,XTA,SAV12
 ,UTC,*TEXORG*
 ,ATX,NWS*
 ,ITA,
 ,ATX,SREG
 ,XTA,SWORD
 ,UJ,CU+1
 ERR0:,XTA,=1
 14,VTM,*TEXORG*
 14,ARX,NWS*
 14,ATX,NWS*
 ERR:,XTA,=:00000001
 14,VTM,*TEXORG*
 14,AOX,NWS*
 14,ATX,NWS*
 ,XTA,LB
 ,UZA,ERR1-1
 ,XTA,TEXC
 ,E-N,
 ,ATX,TEXC
 ,UJ,ENDL
 :,ITA,
 ,ATX,LF
 ERR1:5,XTA,
 ,ATX,LW
 ,XTA,INF
 14,VTM,*TEXORG*
 ,U1A,NOMEM
 14,XTA,NWS*
 ,ASN,64-33
 ,AUX,GOST7
 ,AEX,=6H00000>
 ,ATX,HEAD
 ,ITA,5
 ,ASN,64-33
 ,AUX,GOST7
 ,UJ,ERR2
 NOMEM:14,XTA,EX
 ,ASN,64-30
 ,AUX,GOST7
 ,AEX,=6H000000
 ,ATX,HEAD
 ,ITA,5
 ,ASN,64-38
 ,ASN,64+5
 ,AUX,GOST7
 ,UTC,=6H>0000:-=6H00000:
 ERR2:,AEX,=6H00000:
 ,ATX,HEAD+1
 ,ITA,
 ,ATX,HEAD+2
 ,ITA,3
 ,A-X,=37
 9,VTM,HEAD
 10,VTM,HEAD+2
 :,UZA,*+2
 13,VJM,PRINTS
 :9,VTM,HEAD
 9,MTJ,10
 :,XTA,LW
 ,AUX,GS2
 ,AEX,=6H 0000
 10,ATX,
 ,XTA,LW
 ,ASN,64-12
 ,ATX,LW
 ,AUX,GS3
 ,AEX,=6H0000 0
 10,ATX,1
 ,XTA,LW
 ,ASN,64-15
 ,ATX,LW
 ,AUX,GS4
 ,AEX,=6H000 00
 10,ATX,2
 ,XTA,LW
 ,ASN,64-15
 ,AUX,GOST7
 ,AEX,=6H00''''''''
 10,ATX,3
 10,UTM,3
 5,VTM,ERRT
 13,VJM,PRINTS
 :11,VTM,13
 ,UJ,ZERO+3
 ZERW:5,VTM,ZERWT
 ,XTA,=:00000002
 14,VTM,*TEXORG*
 ,UJ,ENDW+2
 ENDW:5,VTM,ENDBLT
 ,XTA,=:00000004
 14,VTM,*TEXORG*
 ,UTC,16
 :11,VTM,7
 14,AOX,NWS*
 14,ATX,NWS*
 ,UJ,ZERO+1
 ZERO:5,VTM,ZERT
 11,VTM,9
 :,XTA,=40000000
 ,UTC,*TEXORG*
 ,AAX,NW*
 ,U1A,OUT
 :5,XTA,
 ,ATX,WORD
 ,ITA,11
 ,X-A,FLEN
 15,ATX,
 ,ASN,64+1
 ,ATI,14
 15,X-A,
 ,ATI,8
 11,VLM,COM2
 OUT:,XTA,LF
 14,VTM,*TEXORG*
 14,AEX,NWS*
 14,ATX,NWS*
 ,XTA,REST
 13,VTM,OUT+2
 ,UZA,OUT1
 ,*70,REST
 OUT1:,XTA,INF
 ,UZA,*+2
 2,V1M,*+1
 14,VJM,NEXTL+5
 :15,XTA,
 14,VTM,1-13
 :14,STI,13
 14,VLM,*
  ,STI,14
 15,XTR,1
 13,UJ,
 FILE:,AAX,=:001
 ,U1A,*+3
 11,VTM,45B
 ,*70,46B
 ,XTA,45B
 ,ATX,REST
 :,XTA,HEAD+4
 ,A-X,=2000
 14,VTM,=777777
 ,U1A,*+2
 ,ATX,MAXW
 2,VTM,2000B
 :,XTA,HEAD+7
 ,U1A,FILE2
 ,XTA,HEAD+6
 ,AAX,=777777
 FILE1:,XTS,=1
 ,ATX,ADDL
 ,XTA,MASK48
 ,ATX,MSL
 ,XTA,HEAD+3
 ,A-X,=1
 15,ATX,
 ,AAX,=1777
 ,STX,HEAD+3
 ,ASN,64+10
 ,STX,WORD
 ,ASN,64+12
 ,X-A,=27
 15,UTM,1
 ,UZA,DRUM
 ,XTA,WORD
 15,ARX,
 11,XTS,
 RDR:,AAX,MASK30
 15,AEX,-1
 ,ATX,INF
 ,ASN,64+20
 ,STI,5
 ,ASN,64+12
 ,X-A,=27
 ,UZA,*+3
 ,ATI,13
 ,*57,4040B
 :13,ASN,64+1
 ,NTR,13B
 :,U1A,NOF
 ,*70,INF
 :,XTA,HEAD+3
 ,A+X,=1
 15,UTM,1
 ,STX,HEAD+3
 14,VTM,*TEXORG*
 14,AEX,EX
 14,ATX,EX
 ,ITA,2
 ,ASN,64+10
 ,U1A,TEX
 ,ATX,INF
 ,UJ,TEX
 DRUM:,X-A,=27
 ,ASN,64-12
 ,STX,SWORD
 ,AEX,SWORD
 ,ARX,WORD
 ,ATX,WORD
 ,ASN,64+5
 ,ASN,64-5
 15,ATX,
 ,AEX,WORD
 ,AEX,SWORD
 ,STX,SWORD
 ,ASN,64-7
 ,ARX,SWORD
 15,ATX,
 ,ASN,64+12
 ,X-A,=27
 ,U1A,ERRDR
 ,ASN,64-15
 ,AEX,=77777
 ,ITS,2
 ,ARX,MAXW
 15,X-A,
 ,UZA,*+5
 ,A+X,MAXW
 ,ATX,MAXW
 ,UZA,*+3
 ,ITS,2
 15,A+X,
 ,ATI,2
 ,ITA,
 ,ATX,MAXW
 :,XTA,=7741
 ,ATX,ADDL
 ,XTA,=7777777777770037
 ,ATX,MSL
 11,XTA,
 ,UJ,RDR
 FILE2:,XTA,HEAD+6
 ,AOX,=6H
 ,AEX,=6H
 15,ATX,
 ,ARX,MASK47
 15,AEX,
 ,ASN,64+3
 ,AAX,=6H
 15,ATX,1
 ,ASN,64+1
 15,AEX,1
 ,AOX,HEAD+6
 ,AEX,=6H000000
 ,AAX,MASK370
 ,U1A,ERRF
 ,XTA,HEAD+6
 15,AOX,1
 15,AEX,1
 15,ATX,2
 ,ASN,64-8
 ,UZA,FILE3
 ,ASN,64-8
 ,UZA,*+3
 ,ASN,64-16
 ,UZA,ERRF
 ,ASN,64-8
 ,U1A,*+1
 14,VTM,=770777
 :15,XTA,2
 ,APX,GOST7
 14,AUX,
 ,UJ,FILE1
 FILE3:15,XTA,2
 ,ASN,64-3
 ,E+N,2
 ,ASN,64-18
 ,YTA,
 ,UJ,FILE1
 NOF:,XTA,INF
 ,ASN,64+12
 ,ASN,64-42
 ,AUX,GOST7
 ,AEX,=6H00"''''''
 ,ATX,WORD
 9,VTM,NOFF
 ,UJ,ERRF+2
 ERRDR:15,XTA,
 ,ASN,64+12
 ,ASN,64-42
 ,AUX,GOST7
 ,AEX,=6H00"''''''
 ,ATX,SWORD
 9,VTM,NODR
 ,UJ,ERRF+2
 ERRF:,XTA,HEAD+6
 15,AOX,1
 ,ATX,INF0
 9,VTM,ERRFT
 :13 ,VTM,OUT
 9,MTJ,10
 :10,XTA,
 ,AAX,=377
 ,UZA,PRINTS
 10,VLM,*-1
 ENDOFT:,ISO,24HE-N-D---O-F---T-E-X-T'200'''''
 ZERWT:,ISO,12HZ-E-R-O'200'''''''''
 ENDBLT:,ISO,24HE-N-D---O-F---B-L-O-C-K'200'
 ZERT:,ISO,12HП-Y-C-T-O'200'''''
 ERRT:,ISO,18HN-O-N-T-E-X-T'200'''''''''
 ERRFT:,ISO,12HOШ. ФAЙЛ:  "
 INF0:,OCT,
 ,OCT,104
 NOFF:,ISO,12HHET ФAЙЛA: "
 WORD:,OCT,
 NODR:,ISO,12HOШ.БAPAБAH:"
 SWORD:,OCT,
 MASK47:,LOG,7777777777777776
 MASK30:,OCT,7777777777
 GOST7:,GOST,6H777777
 GS2:,GOST,6H077770
 GS3:,GOST,6H777707
 GS4:,GOST,6H777077
 MASK370:,ISO,6H'370''370''370''370''370''370'
 MASK48:,OCT,7777777777777777
 LW:,BSS,1
 LB:,BSS,1
 EDI:,BSS,1
 EDIS:,BSS,1
 ADDL:,BSS,1
 MSL:,BSS,1
 SUP0:,BSS,1
 SUP:,BSS,1
 SHIFT0:,BSS,1
 MULT:,BSS,1
 SAV11:,BSS,1
 SAV12:,BSS,1
 LASTBL:,BSS,1
 LF:,BSS,1
 FLEN:,BSS,1
 RG:,BSS,1
 RG0:,BSS,1
 F2:,BSS,1
 L2:,BSS,1
 C80F0:,BSS,1
 C80F:,BSS,1
 SHIFT:,BSS,1
 MAXW:,BSS,1
 REST:,BSS,1
 UV:,BSS,1
 TEXC:,BSS,1
 NOCOM:,BSS,1
 C80FF:,BSS,1
 SREG:,BSS,1
 INF:,BSS,1
 ADD:,BSS,1
 NUM:,BSS,1
 UPMASK:,BSS,1
 LAST0:,BSS,1
 LAST:,BSS,1
 HEADR:,BSS,1
 HEAD:,BSS,23
 SAVSH:,BSS,22
 ,END,
 TINGR1:,NAME,
C*
C*   SUBROUTINE TINGR1(A,N,CLF)
C*   BBOД ЭKPAHA B PEЖИME "SEND"
C*   N - ЧИCЛO ПPИHЯTЫX CTPOK.
C*   KAЖДAЯ CTPOKA ЗAHИMAET POBHO
C*   14 CЛOB B MACCИBE A ( < 225 )
C*   XBOCTЫ CTPOK ЗAПOЛHЯЮTCЯ ПPOБEЛAMИ
C*   CLF - CИMBOЛ, KOTOPЫM ЗAMEHЯETC "LF".
C*   48-41 P. CLF ПOMEЩAЮTCЯ B MЛ.PAЗPЯДЫ
C*   CЛOBA KAЖДOЙ CTPOKИ, COДEPЖABШEГO "LF".
C*
 *BUFSEN*:,SC,225
 8,BASE,*
 ,STI,10
 ,STI,11
 ,ATI,12
 10,XTA,
 ,ASN,64+40
 ,ATX,CLF
 10,VTM,-224
 9,VTM,*BUFSEN*
 ,ITA,
 POC:9,UTC,
 10,ATX,224
 10,VLM,POC
 ,*71,INF
 BEG:14,VTM,-13
 9,XTA,
 ,UZA,FIN
 10,UTM,1
 LOOP1:9,XTA,
 12,ATX,
 ,AAX,=377
 9,UTM,1
 12,UTM,1
 ,UZA,END ST
 14,VLM,LOOP1
 END ST:,BSS,
 9,XTA,-1
 ,ARX,=7777 7777 7777 7400
 ,AAX,=6H'200''200''200''200''200''0'
 ,ASN,64+2
 9,AOX,-1
 ,AOX,CLF
 12,ATX,-1
 14,VZM,BEG
 14,UTM,1
 ,XTA,BLAN
 LOOP2:,BSS,
 12,ATX,
 9,UTM,1
 12,UTM,1
 14,VLM,LOOP2
 ,UJ,BEG.ЦИKЛ ПO CTPOKAM
 FIN:,BSS,
 ,ITA,10
 ,AOX,=:64
 11,STX,
 13,UJ,
 INF:,Z06,*BUFSEN*
 4,Z00,
 BLAN:,ISO,6H
 CLF:,BSS,1
 ,END,
  TRACER:,NAME,
C*
C*    11.10.79  A.П.CAПOЖHИKOB
C*
C*   TPACCИPOBKA ЭKCTPAKOДOB.
C*   OПPEДEЛEHИE BPEMEHИ OTPAБOTKИ
C*   И KOЛИЧECTBA OБMEHOB C MЛ/MБ
C*   HA ЗAДAHHOM YЧACTKE ПPOГPAMMЫ.
C*
C*    B HAЧAЛE ИCCЛEДYEMOГO YЧACTKA
C*   ПPOГPAMMЫ CTABИTCЯ BЫЗOB :
C*       CALL TRACER(N) ,  ГДE
C*   N = 50,51,...,76  - BOCЬMEPИЧHЫЙ
C*   HOMEP ЭKCTPAKOДA, ПOДЛEЖAЩEГO
C*   TPACCИPOBKE,  ЛИБO  N = 1 ,
C*   ECЛИ TPACCИPOBKA HE HYЖHA.
C*    B KOHЦE ИCCЛEДYEMOГO YЧACTKA
C*   CTABИTCЯ BЫЗOB: CALL TRACER(0).
C*
C*   ПPOГPAMMA ИCПOЛЬЗYET 8-14 И.P.
C*   A TAKЖE ПOCЛEДHИE 20 CЛOB MAГAЗИHA.
C*
  SCEM:,EQU,83
C---
  B:,STI,11
  11,XTA,
  ,ATI,11
  9,VTM,ETEX
 :11,VZM,*+1
  9,VTM,BTEX
   :8,BASE,B
  ,ITA,13
  ,ASN,64-30
  ,AUX,GOST7
  ,AOX,TWOP
  9,ATX,2
  11,VZM,ETRA
  ,FUN,70100B+SCEM
  ,ATX,SCEMC.ЗAПOMHИM
  ,ITA,15
  ,AAX,=77400
  ,AOX,=1 00354
  ,ATI,10
  ,FUN,50B
  ,XTA,JUMP
  10,CTX,
  ,XTA,=:4
  ,FUN,52B
  ,XTA,THET
  11,UTM,-1
  11,VZM,TOPRIN
  11,UTM,1
  ,XTA,=:4
  11,ASN,64-50B
  ,AOX,=:0000 0001
  ,*57,5
  ,ITA,11
  ,ASN,64-33
  ,AUX,GOST7
  ,AOX,TEX
  TO PRIN:9,ATX,5
  ,UJ,PRIN
C---
  ETRA:,BSS,
  13,MTJ,10
  ,FUN,51B
  ,XTA,COUNT
  ,CALL,OCTTDEC
  10,MTJ,13
  ,AEX,=6H000000
  9,ATX,5
  ,XTA,SCEMC
  ,*57,5
C---
  PRIN:,BSS,
  ,FUN,71B
  ,PRINT,IP
  ,XTA,
  ,ATX,COUNT
      13,UJ,
C----
  END EX:14,BASE,B
  ,XTA,=1
  ,ARX,COUNT
  ,ATX,COUNT
  ,XTA,
  ,FUN,60B
  JUMP:,UJ,END EX
  IP:9,Z00,
  9,Z00,
  ,002,
  9,,
  GOST7:,GOST,6H777777
  TWOP :,GOST,6H:00000
  THET :,GOST,6HИ HET'231'
  TEX  :,GOST,6HA:Э00'231'
  BTEX :,GOST,18H<<<OT AДPECA
  ,GOST,18H  TPACCИPOBK
  ETEX:,GOST,18H>>>ДO AДPECA
  ,GOST,24H    OБMEHOB:       '231'
  SCEMC:,BSS,1
  COUNT:,BSS,1
  ,END,
  V MEMOR :,NAME,
C*
C*********  20.10.82.  A.П.CAПOЖHИKOB
C*
C*       ПAKET ПPOГPAMM ДЛЯ PAБOTЫ
C*         C BИPTYAЛЬHOЙ ПAMЯTЬЮ
C*
C*       SUBROUTINE  VMEMOR(LUN)
C*       SUBROUTINE  VMWRIT(MAC,VADR,LEN)
C*       SUBROUTINE  VMREAD(MAC,VADR,LEN)
C*       SUBROUTINE  VMSET(WORD,VADR,LEN)
C*       SUBROUTINE  WRVMEM(WORD,VADR)
C*       SUBROUTINE  LANDVM(WORD,VADR)
C*       SUBROUTINE  LORVM (WORD,VADR)
C*       FUNCTION    RDVMEM(VADR)
C*       FUNCTION    MEMVRD(VADR)
C*       SUBROUTINE  VMCOPY(MT,N,IRW)
C*       SUBROUTINE  VMSAVE(NSWIN)
C*       SUBROUTINE  VMCLOS
C*
C*   BИPTYAЛЬHAЯ ПAMЯTЬ ПPEДCTABЛЯET COБOЙ
C*   MHOЖECTBO CЛOB, AДPECYEMЫX ЦEЛOЧИCЛEH-
C*   HOЙ ПEPEMEHHOЙ  VADR, ПPИHИMAЮЩEЙ ЗHA-
C*   ЧEHИЯ :  0,1,2, ... , 524287.
C*   TAKИM OБPAЗOM, OБЬEM BИPT.ПAMЯTИ PABEH
C*   512K CЛOB, ЧTO ЭKBИBAЛEHTHO 1000(8)
C*   ЗOHAM MAГHИTHOЙ ЛEHTЫ (ФAЙЛA HA ДИCKE)
C*
C*    VMEMOR - ИHИЦИИPYЮЩEE OБPAЩEHИE
C*    LUN - YKAЗATEЛЬ ФAЙЛA, ГДE БYДET
C*    PAЗMEЩATЬCЯ V-ПAMЯTЬ, HAПPИMEP:
C*        CALL VMEMOR(410000B)
C*        CALL VMEMOR(670240B)
C*    ECЛИ  LUN=0 , A TAKЖE ПPИ OTCYTCTBИИ
C*    ИHИЦИИPYЮЩEГO OБPAЩEHИЯ,  V-ПAMЯTЬ
C*    PAЗMEЩAETCЯ HA  MБ 03-06,10-17,24-27,
C*    T.E. ЯBЛЯETCЯ BPEMEHHOЙ PAБOЧEЙ
C*    ПAMЯTЬЮ.
C*
C*    VMWRIT, VMREAD - ЗAПИCЬ/ЧTEHИE
C*    ПEPBЫX  LEN  CЛOB MACCИBA  MAC  B V-
C*    ПAMЯTЬ, HAЧИHAЯ C AДPECA  VADR.
C*    CALL VMWRIT(M,100,8) - ЗAПИCЬ 8 CЛOB
C*    MACCИBA M B 100-107 ЯЧEЙKИ V-ПAMЯTИ.
C*    CALL VMREAD(M,100,8) - ЧTEHИE B MAC-
C*    CИB M ИЗ 100-107 ЯЧEEK V-ПAMЯTИ.
C*
C*    VMSET  -  POCПИCЬ LEN CЛOB V-ПAMЯTИ,
C*    HAЧИHAЯ C AДPECA VADR,  KOДOM WORD
C*    ИЗ MOЗY.  ЗAMEЧAHИE:  LEN < 32000 !
C*
C*    VMCOPY - CTPAHИЧHAЯ ПEPEПИCЬ MEЖДY
C*    OБЫЧHOЙ BHEШHEЙ И  V-ПAMЯTЬЮ.  ЗДECЬ
C*    MT - OБЫЧHЫЙ YKAЗATEЛЬ  BИДA  NNZZZZ,
C*    N  - ЧИCЛO ПEPEПИCЫBAEMЫX CTPAHИЦ,
C*    IRW - HAПPABЛEHИE ПEPEПИCИ ( 0 : ИЗ
C*        V-ПAMЯTИ HA <MT>,  1 : HAOБOPOT )
C*    ПEPEПИCЬ HAЧИHAETCЯ C 0-ГO V-ЛИCTA.
C*
C*    WRVMEM, RDVMEM, MEMVRD - BXOДЫ ДЛЯ
C*    ПOCЛOBHOЙ PAБOTЫ. PAБOTAЮT B 2-3 PAЗA
C*    БЫCTPEE, ЧEM VWREAD,VMWRIT ПPИ LEN=1.
C*    CALL WRVMEM(WORD,VADR) - ЗAПИCЬ B
C*    V-ПAMЯTЬ CЛOBA WORD ПO AДPECY VADR.
C*    WORD=RDVMEM(VADR) - ЧTEHИE ИЗ ЯЧEЙKИ
C*    V-ПAMЯTИ C AДPECOM VADR.
C*    MEMVRD - TO ЖE, ЧTO И RDVMEM, HO
C*    PEЗYЛЬTAT ИMEET TИП "INTEGER".
C*
C*    LANDVM  (LORVM)  PEAЛИЗYЮT OПEPAЦИИ
C*    "ЛOГИЧECKOE YMHOЖEHИE (CЛOЖEHИE)"
C*    MEЖДY CЛOBOM ИЗ V-ПAMЯTИ C AДPECOM
C*    VADR  И CЛOBOM  WORD  ИЗ MOЗY.
C*    PEЗYЛЬTAT ЗAПИCЫBAETCЯ OБPATHO B
C*    V-ПAMЯTЬ ПO AДPECY  VADR.
C*
C*    B KAЧECTBE OБMEHHЫX БYФEPOB ИCПOЛЬЗY-
C*    ETCЯ BCE CBOБOДHOE ПPOCTPAHCTBO MOЗY,
C*    OCTABШEECЯ ПOCЛE ЗAГPYЗKИ ПPOГPAMMЫ
C*    ПOЛЬЗOBATEЛЯ. ПPИMEHЯETCЯ AЛГOPИTM
C*    ЗAMEЩEHИЯ C БPOHИPOBAHИEM OДHOЙ CTPA-
C*    HИЦЫ БYФEPHOЙ ПAMЯTИ, ПOЭTOMY ДЛЯ
C*    БYФEPHOЙ OБЛACTИ TPEБYETCЯ HE MEHEE
C*    2-X CTPAHИЦ.
C*
C*    CALL VMSAVE(NSWIN) - ПPИHYДИTEЛЬHЫЙ
C*    ДOCБPOC OБMEHHЫX БYФEPOB B V-ПAMЯTЬ.
C*    ПPИ ЭTOM B NSWIN ЗAПИCЫBAETCЯ ЦEЛOE
C*    ЧИCЛO = 32768 * NCP + NEX,    ГДE
C*    NCP - ЧИCЛO ПEPECЧETOB ПPИOPИTETOB
C*    CTPAHИЦ, ПPOИЗBEДEHHOE C HAЧAЛA PA-
C*    БOTЫ ПPOГPAMMЫ;
C*    NEX - ЧИCЛO OБMEHOB C BHEШHEЙ ПAMЯTЬЮ.
C*
C*    VMCLOS - ДOCБPOC OБMEHHЫX БYФEPOB И
C*    ЗABEPШEHИE PAБOTЫ C V-ПAMЯTЬЮ.
C*    ПOCЛE OБPAЩEHИЯ K VMCLOS MOЖHO
C*    OПPEДEЛИTЬ V-ПAMЯTЬ B HOBOM ФAЙЛE,
C*    ПOBTOPHO OБPATИBШИCЬ K VMEMOR.
C*
C*    ДOCБPOC OБMEHHЫX БYФEPOB ПPOИЗBOДИTCЯ
C*    ABTOMATИЧECKИ ПO KOHЦY ЗAДAЧИ   И ПPИ
C*    KAЖДOM OБPAЩEHИИ K ПPOГPAMME  LOADGO.
C*
C*          Д И A Г H O C T И K A :
C*
C*  1. "OШ.ПAPAMETP" - HEBEPHO ЗAДAHO LUN
C*     ПPИ ИHИЦИИPYЮЩEM OБPAЩEHИИ.
C*  2. "MAЛO ПAMЯTИ" - ПOCЛE ЗAГPYЗKИ ПPO-
C*     ГPAMMЫ OCTAЛOCЬ MEHEE 2 ЛИCTOB CBO-
C*     БOДHOЙ ПAMЯTИ ДЛЯ OБMEHHЫX БYФEPOB.
C*  3. "OШ. V-AДPEC" - ЗHAЧEHИE ПEPEMEHHOЙ
C*     VADR  ПPEBЫШAET 524288.
C*
C*     BMECTE C ДИAГHOCTИKOЙ ПEЧATAETCЯ
C*     AДPEC B ПPOГPAMME ПOЛЬЗOBATEЛЯ, ГДE
C*     БЫЛO OШИБOЧHOE OБPAЩEHИE K VMEMOR.
C*     ПOCЛE ПEЧATИ ДИAГHOCTИKИ PAБOTA
C*     ПPOГPAMMЫ ПPEKPAЩAETCЯ.
C*
  MONITOR*:,SUBP,
  LOADANGO:,SUBP,
  A/I0:,LC,1
  LAST:,EQU,16B
  FREE:,EQU,17B
  ABOCT:,EQU,20B
  LVP:,EQU,512.ЧИCЛO V-ЛИCTOB
  CONTROL:,EQU,=1777 7777 7000
  CON DRU:,EQU,=:0757 7417.MБ:03-06,10-17,24-27
C...   COXPAHЯEMЫE PEГИCTPЫ :
  E:,EQU,13.BOЗBPAT
  T:,EQU,1.TИП:R/W
  M:,EQU,2.AДPEC MOЗY
  L:,EQU,3.ЧИCЛO CЛOB
C...   HECOXPAHЯEMЫE PEГИCTPЫ :
  B:,EQU,8.БAЗA
  V:,EQU,9.V-AДPEC
  P:,EQU,10.V-ЛИCT
  A:,EQU,11.AДPEC ПO БYФEPY
  R:,EQU,12.PAБOЧИЙ
  W:,EQU,14.PAБOЧИЙ
C
C------------------------------
C
  BEG:B,BASE,BEG
  ,STI,R
  R,XTA,
  ,AAX,=0377 7777 7700 6000
  ,U1A,ER LUN
  ,ATX,RPBOSS
  ,XTA,GO OPEN
  ,CTX,GETV
  R,XTA,
  ,AAX,=77 7777
  ,ATX,ABUF
   ,UZA,SPEC
  ,ARX,=10 0000
  ,AAX,=40 0000
  E,U1A,
  ,UJ,ER LUN
 SPEC:R,XTA,
  ,AAX,=:2
  ,ATX,VTYP
  E,UJ,
C
   VMCOPY:,ENTRY,
  ,STI,R
  ,STI,P
  ,ATI,A
  ,ITA,M
  ,ITS,T
  ,ITS,L
  V,VTM,VADR
  R,XTS,
  ,ATI,T.ЧT/ЗП
  A,MTJ,M.YK.MЛ
  P,WTC,
  L,VTM,.N ZON
  T,VZM,COPY1
  T,VTM,=:001
C---
  COPY1:V,ATX,.BHAЧAЛE<1000
  R,VJM,GETV
  L,VZM,RETRAN
  T,XTA,
  ,ASN,64-7
  P,AOX,
  P,ATX,
  ,AAX,=:0000 37
 :M,AOX,
  T,AOX,
  V,XTS,
  ,ASN,64+10
  ,AAX,=77777
  15,ARX,
  ,ATX,WS
  ,*70,WS
  L,UTM,-1
  V,XTA,
  ,ARX,=2000
  ,UJ,COPY1
C
C      *********************
C
C        OCHOBHЫE TAБЛИЦЫ :
C
C   1. BTAB - TAБЛИЦA БYФEPHЫX CTPAHИЦ
C      YПOPЯДOЧEHA ПO YБЫBAHИЮ ПPИOPИ-
C      TETOB. KOHЧAETCЯ ПYCTЫM CЛOBOM.
C       1-15  : HOMEP БYФ.CTPAHИЦЫ
C      16-30  : HOMEP V-CTPAHИЦЫ + 1
C               ИЛИ 0, ECЛИ CBOБ.БYФEP
C
C   2. VTAB - TAБЛИЦA BИPTYAЛЬHЫX CTPAHИЦ
C      48     : ЗAKPЫTA ПO OБPAЩEHИЮ
C      47     : БЫЛA ЗAПИCЬ B CTPAHИЦY
C      35-31  : HOMEP ЛИCTA ПPИПИCKИ ИЛИ 0
C      30-11  : KOД ДЛЯ ПPИПИCKИ CTPAHИЦЫ
C      10-1   : BCEГДA "0"
C
C   3. KBUF - ЧИCЛO БYФEPHЫX CTPAHИЦ-1
C      RP   - HOMEP PEЗEPBHOЙ CTPAHИЦЫ
C             (BCEГДA ПOCЛEДHЯЯ B BTAB)
C      NC   - HOMEP CTPAHИЦЫ, Y KOTOPOЙ
C             ПOCЛEДHИЙ PAЗ ПEPECЧИTЫ-
C             BAЛИ ПPИOPИTET.
C
C      *********************
C
  WRVMEM:,ENTRY,
  ,ATI,V
  R,VJM,GETV
  :15,WTC,
  ,XTA,
  WRA:A,ATX,
  ,XTA,=:2.БЫЛA
  P,AOX,.ЗAПИCЬ
  P,STX,
  E,UJ,
C
C      *********************
C
  LAND VM:,ENTRY,
  ,ATI,V
  R,VJM,GETV
   15,WTC,
   ,AAX,
  ,UJ,WRA
C
C      *********************
C
  LOR VM:,ENTRY,
  ,ATI,V
  R,VJM,GETV
   15,WTC,
   ,AOX,
  ,UJ,WRA
C
C     **********************
C
  RDVMEM:,ENTRY,
  MEMVRD:,ENTRY,
  ,ATI,V
  E,MTJ,R
C
C      OCYЩECTBЛEHИE ПPИПИCKИ
C
  GETV:B,VTM,BEG
  ,UJ,OPEN.ЗATEM : V,XTA,
 :,ASN,64+10
  ,ATI,P.V-ЛИCT
  ,AAX,CONTROL
  ,U1A,ER ADR.OШИБKA
  P,UTM,VTAB
  P,XTA,
  ,ARX,
  ,UZA,P/CLOS.ЗAKPЫT
  GETV1:V,AEX,.ПPИПИCKA
  ,ATI,A.K БYФEPY
  A,XTA,
  R,UJ,
C
C      ***********************
C
  VM SET:,ENTRY,
  ,UTC,VSET-TRANW
  VM WRIT:,ENTRY,
  ,UTC,TRANW-TRANR
  VM READ:,ENTRY,
  W,VTM,
  ,STI,R
  ,STI,V.V-ADRES
  ,ATI,A
  ,ITA,M
  ,ITS,T
  ,ITS,L
  R,XTS,
  ,ATI,L.ЧИCЛO CЛOB
  A,MTJ,M.MOЗY-AДPEC
  V,VIM,NEXTV
  V,VTM,A/I0.ДЛЯ ЛEHTЯEB
  NEXTV:L,VZM,RETRAN
  V,VIM,FIRST
  T,MTJ,W
  P,UTM,1
  ,ITA,P
  ,ASN,64-10
  V,VTM,VADR
  V,ATX,
  FIRST:W,MTJ,T.R/W
  R,VJM,GETV
  :T,VZM,TRAN
  ,XTA,=:2.БЫЛA
  P,AOX,.ЗAПИCЬ
  P,ATX,
  TRAN:,ITA,A
  ,AOX,=76000
  ,ATI,V.-OCTATOK ЛИCTA
  ,ITA,L
  ,ASN,64+11
  ,U1A,LONG
  V,MTJ,W
  L,J+M,W
  W,UTM,-1
  ,ITA,W
  ,AAX,=40 000
  ,U1A,SHORT
C...  БYДET ПEPEXOД HA CЛEДYЮЩИЙ V-ЛИCT
  LONG:V,J+M,L
  V,UTM,1
  P,UTM,-VTAB
  T,UJ,TRANR
C...  B ПPEДEЛAX TEKYЩEГO V-ЛИCTA
  SHORT:,ITA,L
  ,AEX,=77777
  ,ATI,V
  V,UTM,2
  L,VTM,
  T,UJ,TRANR
C...   ЦИKЛЫ ПEPEПИCИ :
  TRANR:A,XTA,
  A,UTM,1
  M,ATX,
  M,UTM,1
 V,VLM,*-2
  ,UJ,NEXTV
  TRANW:M,XTA,
  M,UTM,1
  A,ATX,
  A,UTM,1
  V,VLM,*-2
  ,UJ,NEXTV
   V SET:M,XTA,
  :A,ATX,
  A,UTM,1
  V,VLM,*-1
  ,UJ,NEXTV
  RETRAN:15,XTA,
  ,STI,L
  ,STI,T
  ,STI,M
  E,UJ,
C
C
C...   BXOД ПPИ ПEPBOM OБPAЩEHИИ :
C      OTKPЫTИE БYФEPHOГO ПYЛA
C
  OPEN:15,MTJ,A
  15,VTM,VTAB
  ,XTA,=:4
  W,VTM,1-LVP
  :15,ATX,
  W,VLM,*
  ,WTC,LAST
  W,VTM,-1777B
  15,VTM,NC.NC=B48
  ,ITS,W
  ,ASN,64+10.ПOCЛEДHИЙ
  ,WTC,FREE
  W,VTM,1777B
  ,ITS,W
  ,ASN,64+10.ПEPBЫЙ
  W,ATX,-1777B.ДЛЯ LOADGO
  ,ATI,W
  ,NTR,3
  15,A-X,
  ,UZA,ERBUF. < 2 ЛИCTOB
  ,ATI,P
  ,X-A,
  ,ATX,KBUF
  ,NTR,6
  :,ITA,W.HAЧAЛЬHOE
  15,ATX,.COCTOЯHИE
  W,UTM,1.BTAB
  P,VLM,*-1
  ,ATX,RP
  ,XTA,
  ,ATX,RPBOSS. - HИЧEЙ !
  ,XTS,LOADANGO
  ,AEX,GO LOAD. ПEPEXBATЫ
  ,UZA,OWN REAC. - YЖE БЫЛИ
  ,XTA,LOADANGO
  ,ATX,LOAD
  ,XTA,GO LOAD
  ,CTX,LOADANGO
  ,XTA,GO STOP
  ,CTX,MONITOR*+4
  ,XTA,ABOCT
  ,AEX,J STOP
  ,ASN,64+24
  ,U1A,OWN REAC.IFOVFL
  ,XTA,GO STOP
  ,CTX,ABOCT
  OWN REAC:,XTA,GO WORK
  ,CTX,GETV
  A,MTJ,15
  ,UJ,GETV
C
C...  ПEPEMEHHЫE KOMAHДЫ :
C
  GO STOP :W,VTM,JSTOP
  ,UJ,CLOSE
  GO LOAD:W,VTM,LOADANGO
  ,UJ,CLOSE
  GO OPEN:B,VTM,BEG
  ,UJ,OPEN
  GO WORK:B,VTM,BEG
  V,XTA,
C
C
C      *************************
C
  VM CLOS:,ENTRY,
  E,MTJ,W.ПOЛHOCTЬЮ
  ,UJ,CLOSE
C
  VM SAVE:,ENTRY,
  B,BASE,BEG
  ,STI,W
  ,XTA,SWCNT
  W,ATX,
  ,XTA,ABUF
  E,UZA,
  E,MTJ,W
  E,VTM,.TOЛЬKO ДOCБPOC
C
C
C...   CПACEHИE БYФEPHOГO ПYЛA :
C
  CLOSE:,ITS,W
  ,ITS,B
  ,ITS,T
  ,ITS,P
  B,VTM,BEG
  T,VTM,BTAB-1
     ,XTS,GO OPEN
  ,ATX,RPBOSS.HИЧEЙ
  E,VZM,C1
  ,CTX,GETV
  ,XTA,LOAD
  ,CTX,LOADANGO
  ,XTA,JSTOP
  ,CTX,MONITOR*+4
   C1:T,UTM,1
  T,XTA,
  ,UZA,C2
  ,ASN,64+15
  ,ATI,P.V-ЛИCT + 1
  P,VZM,C1.CBOБOДEH
  P,UTM,VTAB-1
  W,VTM,C1
C
C...      БЛOKИ OБMEHOB
C      P - AДPEC CTPOKИ VTAB
C
  EXCH/W:,ITA,P
  ,ATX,RPBOSS
  P,XTA,
  ,AAX,=:2
  W,UZA,.HE MEHЯЛCЯ
  P,AEX,
  P,ATX,
  ,UJ,EXCH
  EXCH/R:,ITA,P
  ,AOX,VTYP.0 ИЛИ B47
  ,AEX,RPBOSS
  W,UZA,
  ,XTA,VTYP
  P,AOX,
  P,ATX,
  ,XTA,=:001
  EXCH:P,AOX,
  ,AAX,=:0010 37
  ,ATX,WS.ЛИCT
  ,XTA,=1
  ,ARX,SWCNT
  ,ATX,SWCNT
  ,ITA,W
  P,MTJ,W
  W,UTM,-VTAB
  ,ITS,W.V-ЛИCT
  ,XTS,ABUF
  ,U1A,TAPE
C...   V-ПAMЯTЬ HA MБ
  ,ITA,W
  ,ASN,64+5
  ,ATI,W
  ,XTA,=:4
  W,ASN,64
  ,AUX,CONDRU
  ,ANX,=77777
  ,ASN,64-12
C---
  TAPE:15,ARX,
  ,AOX,WS
  ,ATX,WS
  ,*70,WS
  15,WTC,
  ,UJ,
C---
 C2 :,STX,RPBOSS.HИЧEЙ
  ,STI,P
  ,STI,T
  ,STI,B
  ,STI,W
  W,UJ,
C
C
C...   PEAKЦИЯ HA ЗAKPЫTЫЙ ЛИCT :
C
  P/CLOS:,AAX,=:0000 37.ПPИПИCAH
  ,U1A,CALCPRT.- ДA
  ,XTA,RP
  ,ASN,64-30.B CTEK
  P,UTM,-VTAB
  ,ITS,P
  P,UTM,VTAB
  ,AEX,RP
  ,ASN,64-10. ПPИПИCKA
  15,AOX,-1.P  K  RP
  P,ATX,
  W,VJM,EXCH/R.ЧTEHИE P
  ,XTA,
  ,ATX,NC.RP-MAXPRT
  P,UTM,1-VTAB
  ,ITA,P
  ,ASN,64-15
  ,AOX,RP
C...   CTAPЫЙ RP - ПOCЛEДHИЙ B BTAB
  ,WTC,KBUF
  A,VTM,BTAB-2
  A,ATX,2
C...   HOBЫЙ RP - ПEPEД CTAPЫM
  A,XTA,1
  ,ASN,64+15
  ,ATI,P.EГO XOЗЯИH
  A,XTA,1
  ,AAX,=77777
  A,ATX,1.OCBOБOЖДEHИE
  ,ATX,RP.B BTAB
  P,VZM,CLCP.И БЫЛ CBOБOДEH
  P,UTM,VTAB-1
  W,VTM,CLCP
  ,UJ,EXCH/W.YKAЧKA
C...  ЗAKPЫTИE БYФEPOB ПO ПPИOPИTETY
  CLCP1:A,UTM,-1
  ,ASN,64+15
  ,ATI,W
  W,VZM,CLCP.CBOБOДEH
  W,UTM,VTAB-1
  ,XTA,=:4
  W,AOX,
  W,ATX,
  CLCP:A,XTA,
  ,U1A,CLCP1
  ,XTA,=:4
  P,STX,.CYMM=OLDRP
C
C...   ПEPECЧET ПPИOPИTETOB
C
  CALC PRT:,ASN,64+30
  ,ATX,WS
  A,VTM,BTAB
  ,XTA,=10 0000
  ,ARX,SWCNT
  ,ATX,SWCNT
  SP:A,XTA,
  ,UZA,ERBUF.OШИБKA
  ,AEX,WS
  ,AAX,=77777
  ,UZA,CP
  A,VLM,SP
  CP:A,XTA,
  ,ATX,WS
  SHIFT:A,UTM,-1
  A,XTA,.CДBИГ
  A,ATX,1.BHИЗ
  ,AEX,NC.BCEX
  ,AAX,=77777.ДO NC
  ,U1A,SHIFT
  ,XTA,WS
  A,ATX,1
  ,ATX,NC
  ,ASN,64+15
  ,ATI,P
  P,VZM,ER BUF.OШИБKA
  P,UTM,VTAB-1
  P,XTA,
  ,AOX,=:4.OTKPЫTИE
  ,AEX,=:4.CTPAHИЦЫ
  P,ATX,
  ,UJ,GETV1
C
C...     Д И A Г H O C T И K A :
C
  ER LUN:,UTC,TERL-TERB
  ER BUF:,UTC,TERB-TERA
  ER ADR:W,VTM,TERA
  ,ITA,E
  ,ASN,64-33
  ,AUX,=6H'7''7''7''7''7'''
  ,AOX,=17
  ,ATX,DIAGN+2
  W,XTA,
  ,ATX,DIAGN+3
  W,XTA,1
  ,ATX,DIAGN+4
  ,*64,PRIN
 JSTOP:,SJ,
  PRIN:,Z00,DIAGN
  ,Z00,DIAGN
  ,004,
  9,,
  TERA:,GOST,12HOШ. V-AДPEC'231'
  TERB:,GOST,12HMAЛO ПAMЯTИ'231'
  TERL:,GOST,12HOШ.ПAPAMETP'231'
  DIAGN:,GOST,12HVMEMOR  ИЗ
C
C---------
C
  SWCNT:,INT,0
  RPBOSS:,INT,-1
  LOAD:,BSS,1
  RP  :,BSS,1
  WS  :,BSS,1
  ABUF:,BSS,1
  VTYP:,BSS,1
  KBUF:,BSS,1
  VADR:,BSS,1
  NC:,BSS,1.ПEPEД BTAB !
  BTAB:,BSS,32
  VTAB:,BSS,LVP
  ,END,
  WRAUTOR:,NAME,
C*
C*****  01.03.82   A.П.CAПOЖHИKOB
C*
C*    ПPOГPAMMA ДЛЯ BЫДAЧИ ИHФOPMAЦИИ
C*    O TOM, KTO, KOГДA, И HA KAKOM
C*    YCTPOЙCTBE ПPOИЗBEЛ ПOCЛEДHЮЮ
C*    ЗAПИCЬ B ЗAДAHHЫE ЗOHЫ ЛEHTЫ
C*    ИЛИ ФAЙЛA HA ДИCKE.
C*    ПPИMEP OБPAЩEHИЯ :
C*    *CALL WRAUTOR
C*    L67    -HOMEP ЛEHTЫ/ФAЙЛA
C*    0      -HAЧ.ЗOHA
C*    137    -KOH.ЗOHA (BOCЬMEP.ЧИCЛA)
C*
  *PROTO*:LC,BLOCK,(2),NL(23),L(40)
  *PASS* :PC,BLOCK,P(1024),F(2048),EX(1024)
  TABLE2*:,SUBP,
  NP:,P/P,(P)16
  WRAUTR:,SUBP,
  ,XTA,=I16
  ,ATX,NL
  ,XTA,=I2
  ,ATX,L
  ,ITA,13
  ,ASN,64+3
  ,U1A,MAT
  ,XTA,=I14
  ,ATX,NL
  ,XTA,=I0
  :13,VZM,*+1
  ,AOX,=I2
  :,ATX,L
  13,VJM,MAT
  ,SJ,
  MAT:8,VTM,-2
  ,*63,564B
  ,ATI,14.*USESTA
  14,*63,2
  ,AOX,INFR
  ,ATX,L+1
  RT:,*70,L+1
  ,XTA,=100 0000 0001
  ,ARX,L+1
  ,ATX,L+1
  8,VLM,RT
  ,XTA,=:0004
  ,FUN,74200B.REGSCAL
  14,VTM,EX
  ,ITA,14
  ,ASN,64-20
  ,AOX,=:001
  ,ATX,L+1
  ,UJ,WRAUTR
  INFR:,014,NP
  ,000,
  GSTISO:,ENTRY,
  ,ATI,14
  14,XTA,
  ,AAX,=377
  ,ATI,14
  ,UTC,TABLE2*
  14,XTA,
  ,ASN,64-40
  13,UJ,
  ,END,
 WRITPL:,NAME,.7.08.81
C**********  7.08.81 **** ДABЫДOB A.Л.***
C*     10.10.84 ПPИЛOЖИЛ PYKY И.P.PЫБAKOB
C*          ( 'MЯГKИЙ' BBOД MACCИBA C ПЛ,
C*    PEAЛИЗOBAHHЫЙ M.ГYPEBИЧEM B ,*60,).
C*
C*  ПPOГPAMMA ДЛЯ BBOДA C BOCЬMИPAЗPЯДHOЙ
C*  ПEPФOЛEHTЫ И  БYФEPИЗAЦИИ  BBEДEHHOГO
C*  MACCИBA HA  MЛ ЮЗEPA.
C*     БYФEPИЗAЦИЯ - ПO  6 CTPOK  П/Л  B
C*  CЛOBO БЭCM-6, HAЧИHAЯ CO CTAPШИX PAЗ-
C*  PЯДOB, ПO 8 PAЗPЯДOB HA CTPOKY.
C*
C*       O Б P A Щ E H И E :
C*  *CALL WRITPL:<YK.MЛ>,<OГP.>
C*   <YK.MЛ>   - YKAЗATEЛЬ MЛ БYФEPИЗAЦИИ
C*   <OГP.>    - OГPAHИЧИTEЛЬ ПO MЛ
C*
C*      CTAHДAPTHO:
C*   <YK.MЛ>   = 37000
C*   <OГP.>    = 1000
C*
C*      ПPИMEP OБPAЩEHИЯ:
C*  *CALL WRITPL:41245,36
C*
C*     OPГAHИЗYETCЯ BBOД MACCИBA CИMBOЛOB
C*     ДO ПPИKAЗA 'KПЛ',
C*     B HAЧAЛE И B KOHЦE БYДET CKOЛЬKO-TO
C*     ЛИШHИX HYЛEЙ.
C*     БYФEP - BECЬ 'XBOCT' ПAMЯTИ,HAЧИHAЯ
C*     C ПEPBOГO CBOБOДHOГO ЛИCTA.
C*
C*     ECЛИ ПOCЛE <OГP.> CTOИT HE ПPOБEЛ,
C*     A CИMBOЛ-PAЗДEЛИTEЛЬ (HAПP. TOЧKA),
C*     TO BBEДEHHЫE CИMBOЛЫ PACПEЧATЫBAЮTCЯ.
C****************************************
 *MONCRD*:,LC,12
 8,BASE,*
 ,ITA,13
 ,ATX,RET
 10,VTM,
 ,CALL,PARAMT*
 11,VZM,BEG
 10,VTM,4.YK.MЛ
 9,VTM,YK
 ,CALL,PARAMT*
 14,VZM,ERRET.OШИБKИ
 11,VZM,BEG
 10,VTM,5.OГP.
 9,VTM,OГP
 ,CALL,PARAMT*
 14,VZM,ERRET.OШИБKИ
 ,ITA,11.
C  ECЛИ ПOCЛE <OГP> HE ПPOБEЛЫ,TO HAПEЧATAEM BBEДEHHOE
 ,ATX,BLP.=0-ПEЧATЬ БЛOKИP.
 BEG:,BSS,
 ,WTC,17B.FREE
 11,VTM,-1
 ,ITA,11
 ,ASN,64+10
 ,ARX,=1
 ,ASN,64-10
 ,ATI,11.HAЧ.AДP.ПAM
 ,ASN,64-20
 ,ATX,NZ.ЛИCT+D NZ
 RPL:11,*60,.BBOД MACCИBA
 ,,1 . ДO 'KПЛ'
 ,ASN,64+15
 ,AAX,=77777
 ,UZA,RPL.PAHO ДAH 'KПЛ'
 ,ATX,NKODWR
 ,ATX,NKOD.ПPИHЯTO CЛOB
 ,ATI,14
 14,UTM,-1
 ,ITA,14
 ,ASN,64+10
 ,ARX,=1
 ,ATX,NZEND.ПPИHЯTO ЗOH
 11,J+M,14
 9,VTM,1024
 ,ITA,9
 ,ATX,LPR.
C  POCПИCЬ HYЛЯMИ OCTATKA ЛИCTA
 LZER:,XTA,
 14,ATX,1
 14,UTM,1
 ,ITA,14
 ,AAX,=1777
 ,U1A,LZER
C  ЗAПИCЬ BBEДEHHOГO HA MЛ
 LWR:,BSS,
 ,XTA,NZ
 ,ARX,YK
 9,VTM,LPR
 ,*70,
 ,XTA,BLP
 ,UZA,NEWZ
 ,XTA,NZ
 ,ARX,=1
 ,AAX,=3777
 ,AEX,NZEND
 ,U1A,ADR
 ,XTA,NKOD
 ,AAX,=1777
 ,UZA,ADR
 ,ATX,LPR.NON-FULL PAGE
 ADR:,XTS,NZ
 ,ASN,64+20.ADR IN MEMOR
 ,ITS,9
 ,CALL,PRIBYT
 NEWZ:,BSS,
 ,XTA,NZ
 ,ARX,=100 00000001.PAGE+1,NZ+1
 ,ATX,NZ
 ,AAX,=3777
 ,AEX,NZEND
 ,UZA,END WR
 ,AEX,NZEND
 ,AEX,OГP
 ,U1A,LWR
C    --- CPAБOTAЛ OГPAHИЧИTEЛЬ ---
 ,AEX,OГP
 ,ASN,64-10
 ,ATX,NKODWR
 14,VTM,ET1
 ,ITA,14
 ,CALL,PRINT8/M
C-----
 END WR:,BSS,
 ,XTA,NKOD
 ,CALL,OCTTDEC
 ,AOX,TZ1
 ,ATX,T+2
 ,XTA,NKODWR
 ,CALL,OCTTDEC
 ,AOX,TZ1
 ,ATX,T+6
 ,XTA,NZ
 ,ASN,64-36
 ,AUX,=6H'''''7''7''7''7'
 ,AOX,TZ2
 ,ATX,T+8
 14,VTM,T
 RETURN:,ITA,14
 ,WTC,RET
 13,VTM,
 ,UJ,PRINT8/M
 ERRET:14,VTM,ERTEXT
 ,UJ,RETURN
C
C    KOHCTAHTЫ  И  PAБ. ЯЧEЙKИ
C
 RET:,BSS,1
 LPR:,BSS,1.ДЛИHA ПEЧ.
 YK:,LOG,37 0000
 OГP:,LOG,1000
 BLP:,LOG,56
 NKOD:,LOG,
 NKODWR:,LOG,
 NZ:,LOG,
 NZEND:,LOG,0
 ERTEXT:,ISO,23H OШИБKИ, BBOДA HE БЫЛO''
 TZ1:,ISO,6H000000
 TZ2:,ISO,6H (0000
 ET1:,ISO,18H   ***MAЛO MЛ!***''
 T:,ISO,60H   BBEДEHO  000000 CЛOB,  ЗAПИCAHO  000000 CЛOB  (0000 ЗOH)''
  ,END,
  ИCKATEЛЬ:,NAME,
C*
C*****  19.11.82   A.П.CAПOЖHИKOB
C*
C*     ПPOCMOTP COДEPЖИMOГO ЛEHT/ФAЙЛOB
C*
C*     OБPAЩEHИE:  *CALL ИCKATEЛЬ
C*
C*   ДAЛEE, ECЛИ ЗAДAЧA ЗAПYЩEHA C
C*   TEPMИHAЛA, PAБOTA ПPOИCXOДИT B
C*   ДИAЛOГE C ПOЛЬЗOBATEЛEM.
C*   ПPOГPAMMA BЫДAET ЗAПPOC TOMA.
C*   OTBEЧAЙTE:  <OПИCAHИE TOMA>(NNNN)
C*   ГДE NNNN - ЧИCЛO ЗOH HA ЛEHTE, A
C*   ДЛЯ ДИCKOB NNNN MOЖHO HE ЗAДABATЬ.
C*   ПPИMEP:  T12/MYTAPE(1200)
C*            D40/OWN,BILL,FILE1
C*   ПPOCMOTP HAЧИHAETCЯ C 0 ЗOHЫ.
C*   ECЛИ TPEБYETCЯ ПPOCMOTP HE C HAЧAЛA,
C*   YKAЖИTE ПAPAMETP ZONE=<HOMEP ЗOHЫ>.
C*   ПPИMEP:  T12/MYTAPE(1200)  ZONE=40
C*
C*   ПPOГPAMMA PEAГИPYET HA PEЖИM ПEЧATИ
C*   (*NO LIST, *STAND, *FULL)
C*   B PEЖИME *NO LIST BЫДAЮTCЯ TOЛЬKO
C*   YПPABЛЯЮЩИE KAPTЫ M.C."ДYБHA",
C*   OБHAPYЖEHHЫE HA ЛEHTE/ФAЙЛE.
C*   B PEЖИME *STANDARD LIST BЫДAETCЯ
C*   KPOME TOГO  HEAD LIST TEKCTOBЫX
C*   ФAЙЛOB И KATAЛOГИ БИБЛИOTEK.
C*   B PEЖИME *FULL LIST TEKCTOBЫE
C*   ФAЙЛЫ BЫДAЮTCЯ ПOЛHOCTЬЮ.
C*
C*   ПPOГPAMMA ДOПYCKAET CMEHY PEЖИMA
C*   B ПPOЦECCE PAБOTЫ.  ПO ЗAПOЛHEHИЮ
C*   ЭKPAHA TEPMИHAЛA ПPOИCXOДИT OЖИ-
C*   ДAHИE ДИPEKTИBЫ ПOЛЬЗOBATEЛЯ.
C*   BOЗMOЖHЫE ДИPEKTИBЫ :
C*    1.  EXIT     - KOHEЦ PAБOTЫ
C*    2.  NEW      - ПEPEXOД HA HOBЫЙ TOM
C*    3.  REGIM=*NO LIST, *STAND ИЛИ *FULL
C*                 - CMEHA PEЖИMA
C*    4.  ZONE=<HOMEP ЗOHЫ>  - ПEPEXOД
C*                   HA HOBYЮ ЗOHY
C*   KOДЫ ДИPEKTИB MOЖHO COKPAЩATЬ
C*   BПЛOTЬ ДO 1 CИMBOЛA (Z BMECTO ZONE)
C*
C*
C*   ECЛИ ПPOГPAMMA ЗAПYЩEHA HE C
C*   TEPMИHAЛA, TO HA П/K ДAHHЫX
C*   ДOЛЖHO HAXOДИTЬCЯ OПИCAHИE TOMA
C*   KPOME TOГO, OПEPATИBHOE YПPABЛE-
C*   HИE PEЖИMOM OTCYTCTBYET.
C*   PEЖИM ПEЧATИ HA AЦПY OПPEДEЛЯ-
C*   ETCЯ KAPTAMИ *NO LIST, .. И T.П.
C*
C*   ПPИMEP:  *TAPE:12/MYTAPE,67,R
C*            *LIBRA:23
C*            *FULL LIST
C*            *CALL ИCKATEЛЬ
C*            L67(1200) ZONE=100
C*            *END FILE
C*   ПPOИЗBOДИTCЯ ПPOCMOTP COДEPЖИMOГO
C*   100-1177 ЗOH ЛEHTЫ C MAT.HOMEPOM
C*   67.  PEЖИM ПEЧATИ = *FULL LIST
C*
  BTLOOK:,SUBP,
  *P*:,PC,2048
  14,VTM,=2
  :,ITS,14
  ,CALL,DIALOP
C...  +2 : BXOД ИЗ БCЗ : БEЗ ПPOTOKOЛA
  :,CALL,BTLOOK
  14,VTM,TEXT
  ,ITS,14
  14,VTM,=I12
  ,ITS,14
  14,VTM,=I-1
  ,ITS,14
  ,CALL,DIALOG
  ,ITS,
  ,CALL,DIALOP
  ,SJ,
  TEXT:,ISO,12HДO CBИДAHИЯ
  ,END,
   ЖИPПEЧ:,NAME,
C*
C***     29.12.83    A.П.CAПOЖHИKOB
C*
C*    SUBROUTINE ЖИPПEЧ(STR,L,IPOZ)
C*    ПEЧATЬ L CИMBOЛOB TEKCTOBOЙ CTPOKИ STR,
C*    HAЧИHAЯ C ПOЗИЦИИ IPOZ=1,2,...,127.
C*
C*    STR COДEPЖИT CИMBOЛЫ B KOДE ISO,
C*    ПPИЧEM HEKOTOPЫE CИMBOЛЫ MOГYT БЫTЬ
C*    ПOMEЧEHЫ БИTOM ЖИPHOCTИ - "1" 8-ГO БИTA.
C*    TAKИE CИMBOЛЫ ПEЧATAЮTCЯ C
C*    TPOЙHЫM HAЛOЖEHИEM.
C*
C*    BAЖHOE ЗAMEЧAHИE:  B OTЛИЧИE OT OБЫЧHOГO
C*      PRINT-A,  ДEЛAЮЩEГO ПPOTЯЖKY CTPOKИ
C*      ПOCЛE  ПEЧATИ,  ЖИPПEЧ ДEЛAET ПPOTЯЖKY
C*      ПEPEД  ПEЧATЬЮ !
C*      KPOME TOГO, ПPOГPAMMA "KOПИT" ПPOTЯЖKИ,
C*      T.E. ДЛЯ CTPOK ИЗ ПPOБEЛOB ПEЧATЬ HE
C*      ПPOИЗBOДИTCЯ, A YBEЛИЧИBAETCЯ CЧETЧИK
C*      ПPOTЯЖEK ДЛЯ CЛEДYЮЩEЙ ПEЧATИ (OH XPA-
C*      HИTCЯ B   COMMON /ЖПPOT/ NSKIP  ).
C*      ПOЭTOMY ПPИ PAБOTE C  ЖИPПEЧ  BЫBOД
C*      HA HOBЫЙ ЛИCT PEKOMEHДYETCЯ ДEЛATЬ C
C*      ПOMOЩЬЮ П/П  ЖИPЛИC ( CALL ЖИPЛИC ).
C*
C
C...  ЧИCЛO HAЛOЖEHИЙ (HE MEHEE 2)
  KREP:,EQU,3
C
C...  AДPEC B ДИCПETЧEPE TAБЛ. ISO-->GOST
  ATABL:,EQU,526B
C
C...  ПPOTЯЖKИ ПEPEД ПEЧATЬЮ
  *ЖПPOT*:LC,BLOCK,NSKIP
C
C...   P E Г И C T P Ы :
  *1:B,BLOCK,CNT.CЧ-K CИMBOЛOB
  ,CONT,C1.ПPOБEЛЫ 1
  ,CONT,C2.ПPOБEЛЫ 2
  ,CONT,W1.TEK.CЛOBO 1
  ,CONT,W2.TEK.CЛOBO 2
  ,CONT,B1.TEK.БAЙT 1
  ,CONT,B2.TEK.БAЙT 2
  ,CONT,S .CИMBOЛ
  ,CONT,BASE.БAЗ.PEГИCTP
  ,CONT,ISO.BXOДH.CTPOKA
  ,CONT,KS.EE ДЛИHA
  ,CONT,POZ.HAЧ.ПOЗИЦИЯ
  ,CONT,R.BOЗBPAT
  ,CONT,RR.PAБ.PEГИCTP
C
C---
C
  BEG:,STI,POZ
  ,STI,KS
  POZ,WTC,
  POZ,VTM,
  KS,WTC,
  KS,VTM,
C
C--- BXOД +3 : KS И POZ HA PEГИCTPAX
C
  :,STI,ISO
  RR,VTM,-6
  :RR,ITS,7
  RR,VLM,*
  BASE,BASE,BEG
  CNT,VTM,
  W1,VTM,STR1
  W2,VTM,STR2
  B1,VTM,-5
  B2,VTM,-5
  C2,VTM,
C
C---   ПPOTЯЖKИ B HAЧAЛO CTPOKИ
C
  ,UTC,NSKIP
  ,XTS,
  RR,VJM,PUT1
  ,XTA,
  RR,VJM,PUT2
  C1,VTM,
   ,*65,ATABL
  ,ATX,GOST
C
C---   ECЛИ POZ.NE.1 - ЛEBЫE ПPOБEЛЫ
C
  POZ,MTJ,S
  S,UTM,-1
  S,VZM,NEXTWORD
  ,ITA,S
  S,UTM,128
 :,ASN,64+7
  ,UZA,*+2
  POZ,VTM,1
  ,UJ,NEXTWORD
 :,ITA,S
  RR,VJM,PUT1
  ,ITA,S
  RR,VJM,PUT2
C
C---   OCH.ЦИKЛ ПEPEBOДA B COSY-GOST
C
  NEXTWORD:ISO,XTA,
  ,UZA,END
  NEXTSYM:,ASN,64-8
  ,ATX,WORD
  ,YTA,
  ,ATX,SYM
  ,AAX,=177
  ,ATI,S
  S,UTM,-40B
  S,VZM,BLAN1
  ,WTC,GOST
  S,*65,40B
  ,ASN,64-8
  ,ASN,64-8
  ,YTA,
  ,ATI,S
  ,AEX,=17
  ,UZA,BLAN1
C
C---  CБPOC ПPOБEЛOB И CИMBOЛA B STR1
C
  :C1,VZM,PUTSTR1
  C1,UTM,127
  ,ITA,C1
  RR,VJM,PUT1
  PUTSTR1:,ITA,S
  RR,VJM,PUT1
  C1,VTM,
C
C---   ПPOБA БИTA ЖИPHOCTИ
C
  ,XTA,SYM
  ,AAX,=200
  ,UZA,BLAN2
C
C---   CБPOC ПPOБEЛOB И CИMBOЛA B STR2
C
  :C2,VZM,PUTSTR2
  C2,UTM,127
  ,ITA,C2
  RR,VJM,PUT2
  PUTSTR2:,ITA,S
  RR,VJM,PUT2
  C2,VTM,
  ,UJ,CHEND
C
C---   HAKOПЛEHИE COSY-ПPOБEЛOB
C
  BLAN1:C1,UTM,1
  BLAN2:C2,UTM,1
C
C---   ПPOБA KOHЦA CTPOKИ
C
  CHEND:CNT,UTM,1
  KS,UTM,-1
  KS,VZM,END
  ,ITA,POZ
  ,AAX,=200
  ,U1A,END
  POZ,UTM,1
  ,XTA,WORD
  ,U1A,NEXTSYM
  ISO,VLM,NEXTWORD
C
C---   ПPOБA ПYCTOЙ CTPOKИ
C
  END:,WTC,NSKIP
  KS,VTM,1
  ,ITA,CNT
  ,ITS,C1
 15,AEX,
  ,UZA,EXIT
C
C---   ДOOФOPMЛEHИE STR1 И ПEЧATЬ
C
  S,VTM,STR1
  B1,MTJ,C1
  :,XTA,=176
  RR,VJM,PUT1
  C1,VLM,*-1
   ,*64,PRIN
  KS,VTM,1
C
C---   ПPOБA HAЛOЖEHИЯ
C
  ,ITA,CNT
  ,ITS,C2
  15,AEX,
  ,UZA,EXIT
C
C---   ДOOФOPMЛEHИE STR2 И ПEЧATЬ
C
  B2,MTJ,C2
  :,XTA,=176
  RR,VJM,PUT2
  C2,VLM,*-1
  C2,VTM,2-KREP
  REPEAT:S,VTM,STR2
   ,*64,PRIN
  C2,VLM,REPEAT
  ,UJ,EXIT
C
C---   YПAKOBKA BЫXOДHЫX CTPOK
C
  PUT1:W1,XTS,
  ,ASN,64-8
  15,AOX,
  W1,ATX,
  B1,VLM,PRET
  B1,VTM,-5
  W1,VLM,PRET
C-
  PUT2:W2,XTS,
  ,ASN,64-8
  15,AOX,
  W2,ATX,
  B2,VLM,PRET
  B2,VTM,-5
  W2,UTM,1
  PRET:RR,UJ,
C
C---   ЗAПOMИHAHИE ПPOTЯЖEK И BЫXOД
C
  EXIT:,ITA,KS
  ,UTC,NSKIP
  ,STX,
  RR,VTM,7
  :RR,STI,
  RR,UTM,-1
  RR,VIM,*-1
   R,UJ,
C
C---------------
C
  ЖИPЛИC:,ENTRY,
C
C---   BЫBOД HA HOBЫЙ ЛИCT
C      C OБHYЛEHИEM ПPOTЯЖEK
C
  BASE,BASE,BEG
  ,*64,NEWLIST
  ,XTA,=I0
  ,UTC,NSKIP
  ,ATX,
  R,UJ,
  NEWLIST:,Z00,TNL
  ,Z00,TNL
  ,Z00,
  8,Z00,
  TNL:,GOST,2H'201''231'
  PRIN:S,Z10,
  S,Z00,
  GOST:,BSS,1
  WORD:,BSS,1
  SYM :,BSS,1
  STR1:,BSS,23
  STR2:,BSS,23
  ,END,
 ЗAПAKN:,NAME,.
C
C*****   ЗAПAKOBKA HOMEPA-OБPAЗA
C
 *NUMS*:LC,BLOCK,NN0,NUM(35),MAS(35),NR(12)
 *8:,BLOCK,B,IN,T,I1,I2
 BAS:B,BASE,*.
 ,UTC,NN0.
 ,XTA,.
 ,ATI,T.
 T,UTM,NUM-1.
 ,XTA,.
 T,ATX,.
 I1,VTM,1-6.
 I2,VTM,-48.
 PAC 1:I1,UTC,NR+5.
 ,XTA,.
 ,UZA,PAC 2.
 ,AAX,C377.
 I2,UTM,8.
 I2,ASN,64.
 T,AOX,.
 T,ATX,.
 I1,VLM,PAC 1.
 PAC 2:,XTA,FULW.
 I2,ASN,64+48.
 ,AEX,FULW.
 T,ATX,MAS-NUM.
 13,UJ,.
 EQUNUM:,ENTRY,.
C
C*****   ПPOBEPKA HOMEPA HA ЭTAЛOHЫ
C
 B,BASE,BAS.
 ,ATI,IN.
 ,UTC,NN0.
 ,XTA,.
 ,ATI,I1.
 T,VTM,NUM.
 CHE:13,UTC,.
 I1,VZM,.
 IN,XTA,.
 T,AEX,.
 T,AAX,MAS-NUM.
 13,UZA,.
 I1,UTM,-1.
 T,VLM,CHE.
 YПAKN:,ENTRY,.
C
C*****   YПAKOBKA HOMEPA-PEЗYЛЬTATA
C
 :B,BASE,BAS.
 ,ATI,IN.
 I1,VTM,1-2.
 T,VTM,NR.
 PAC 3:I2,VTM,1-6.
 PAC 4:,ASN,64-8.
 T,XTS,.
 ,AAX,C377.
 15,AOX,.
 T,UTM,1.
 I2,VLM,PAC 4.
 I1,UTC,1.
 IN,ATX,.
 I1,VLM,PAC 3.
 ,STX,.
 13,UJ,.
 FULW:,LOG,7777 7777 7777 7777.
 C377:,LOG,377.
 ,END,.

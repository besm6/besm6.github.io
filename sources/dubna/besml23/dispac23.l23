 INTER:,NAME,
C
C**************** E.d.fEd`HxKiH 20/06/84
C*
C*   FUNCTION   INTER(L)
C*
C*   zHA~EHiE fYHKcii i L = 0, ECli
C*   zAdA~A HE qBlqETCq TEPMiHAlxHOj.
C*   B pPOTiBHOM ClY~AE: L = dliHE CTPO-
C*   Ki TEPMiHAlA; INTER = +L, ECli TEP-
C*   MiHAl - diCplEj, INTER = -L, ECli
C*   TEPMiHAl HE diCplEj. B COMMON-blOK
C*   *INTER* zACylAETCq:
C*   *INTER*  = iHfOPMAciOHHOE ClOBO dlq
C*              OpPOCA COCTOqHiq TEPMi-
C*              HAlA ili HYlx, ECli TEP-
C*              MiHAl HE pOdKl`~EH.
C*   *INTER*+1= PEzYlxTAT OpPOCA COCTOq-
C*              Hiq.
C*   *INTER*+2= B PAzPqdAX 13-18 - lOgi-
C*              ~ECKij HOMEP TEPMiHAlA,
C*              OCTAlxHyE PAzPqdy HYlE-
C*              ByE.
C*   *INTER*+3= 0B.
C*
C*   OpEPAciOHHAq CiCTEMA - diCpAK.
C*
C*   ObPA}EHiE - CTAHdAPTHOE.
C*
C
 *INFTER*:,LC,5
 S:11,VTM,*INFTER*
 ,ATI,12
 11,XTA,
 10,BASE,S
 :,U1A,L2
 ,*71,MASK
 :,UZA,OUT
 ,ANX,=:012
 11,ATX,
 ,ASN,64-12
 11,ATX,2
 11,*71,
 :,XTA,INF0
 11,AEX,2
 15,ATX,1
 15,*71,1
 :,UZA,S+2
 11,*71,
 11,ATX,1
 ,ASN,64-2
 ,NTR,13B
 ,U1A,OUT0
 ,XTA,INF
 11,AEX,2
 15,ATX,1
 15,*71,1
 L0:,UZA,S+2
 11,*71,
 L1:11,ATX,1
 ,AAX,=:00000003
 ,AEX,=:00000003
 ,UZA,OUT
 OUT0:11,XTA,1
 10,UTM,1
 :11,ATX,1
 ,ASN,64-3
 ,YTA,
 ,ATI,14
 ,ITA,
 11,ATX,3
 10,UTC,BAS-S
 14,XTA,
 15,ATX,
 ,NTR,7
 15,AVX,-1
 12,STX,
 ,NTR,6
 13,UJ,
 L2:11,XTA,1
 ,U1A,L1
 ,XTA,MASK
 ,UJ,L0
 OUT:11,ATX,
 ,UJ,OUT0+1
 INF0:10,020,MASK-S
 10,Z10,MASK-S
 INF:10,021,ERASE-S
 10,Z10,ERASE-S
 MASK:,OCT,7777777777777777
 ERASE:,GOST,6H/:0000
 BAS:,INT,0
 ,INT,-68
 ,INT,80
 ,INT,-78
 ,INT,80
 ,INT,-68
 ,INT,80
 ,END,
 QIN:,NAME,
C
C**************** E.d.fEd`HxKiH 15/06/84
C*
C*   SUBROUTINE QIN(TEXT,L)
C*
C*   BBOdiT B MACCiB TEXT CTPOKY C |KPA-
C*   HA diCplEq. B L zACylAETCq ~iClO
C*   BBEdEHHyX CiMBOlOB. CTPOKA B MACCi-
C*   BE TEXT dOpOlHqETCq pPObElAMi dO
C*   cElOgO ~iClA ClOB. L < 79.
C*   BBEdEHHyj TEKCT - B KOdE ISO.
C*
C*   SUBROUTINE STER(H,NH,TEXT,L,IT)
C*
C*   BBOd C pOdCKAzKOj. pOdCKAzKA H
C*   dliHOj NH CiMBOlOB. TEXT i L iME-
C*   `T TOT vE CMyCl, ~TO i B QIN.
C*   IT=0 - BBOd C zACEKPE~iBAHiEM,
C*   IT=1 - BBOd bEz zACEKPE~iBAHiq.
C*
C*   OpEPAciOHHAq CiCTEMA - diCpAK.
C*
C*   ObPA}EHiE CTAHdAPTHOE. iCpOlxzY`T-
C*   Cq bEz BOCCTAHOBlEHiq PEgiCTPy 8-12
C*   i 14. PEviM HOPMAlizAcii i OKPYglE-
C*   Hiq HE MEHqETCq. HA ByXOdE - lOgi-
C*   ~ECKAq gPYppA.
C*
C*   iCpOlxzY`TCq pOdpPOgPAMMy TABLE2*
C*   i ISTG.
C*
C
 *INFTER*:,LC,5
 TABLE2*:,SUBP,
 8,VTM,INF
 ,STI,12
 ,ATI,11
 S:9,VTM,*INFTER*
 9,XTA,1
 ,U1A,L1
 9,*71,
 L1:9,XTA,2
 8,AEX,
 :9,ATX,1
 9,*71,1
 :10,BASE,INF
 9,*71,
 :9,ATX,1
 ,AAX,=:00000003
 ,AEX,=:00000001
 ,UZA,L1
 ,XTA,=:776
 15,ATX,13
 10,XTA,1
 11,ATX,
 :15,MTJ,14
 9,VTM,
 CY:14,XTA,
 8,VTM,-5
 :,ASN,64-8
 15,ATX,
 ,YTA,
 ,AEX,=377
 ,UZA,CE
 ,AEX,=377
 11,STX,
 11,WTC,
 ,UTC,TABLE2*
 ,AEX,
 9,UTM,1
 8,VLM,*-5
 11,ATX,
 11,UTM,1
 14,VLM,CY
 CE:15,XTA,
 8,UTM,5
 8,VZM,FULL
 ,AEX,=40
 8,UTM,-5
 ,UJ,*+2
 :,ASN,64-8
 ,AEX,=40
 :8,VLM,*-1
 11,ATX,
 FULL:,ITA,9
 10,AEX,2
 12,STX,
 13,UJ,
 STER:,ENTRY,
 ,STI,10
 ,STI,12
 ,STI,11
 ,STI,14
 ,ATI,9
 ,ITA,13
 8,VTM,INFT0
 14,WTC,
 13,VTM,
 13,V1M,*+2
 :9,VTM,=:132
 13,VTM,1
 :10,WTC,
 10,VTM,
 10,VZM,*+1
 ,UTC,1
 :8,XTS,
 8,STX,INFT-INFT0
 15,MTJ,10
 8,STX,SAV13-INFT0
 :9,XTS,
 8,VTM,-5
 :,ASN,64-8
 15,ATX,
 ,YTA,
 13,VZM,CL
 ,UZA,CL
 14,CALL,ISTG
 15,AEX,
 13,UTM,-1
 8,VLM,*-4
 9,VLM,*-5
 CL:,UTC,=377
 ,XTA,
 ,WTC,SAV13
 13,VTM,
 15,AEX,
 ,UTC,8
 :,ASN,64-8
 8,VLM,*
 15,ATX,
 10,MTJ,15
 8,VTM,INFT
 ,UJ,S
 INFT0:15,036,
 15,Z10,12
 :15,034,
 15,Z10,12
 INF:15,030,
 15,Z10,12
 :,ISO,6H
 ,OCT,64
 INFT:,BSS,1
 SAV13:,BSS,1
 ,END,
 QOUT:,NAME,
C
C**************** E.d.fEd`HxKiH 05/06/84
C*
C*   SUBROUTINE QOUT(TEXT,L)
C*
C*   ByBOdiT HA diCplEj L CiMBOlOB MAC-
C*   CiBA TEXT. XBOCTOByE pPObEly igHO-
C*   PiPY`TCq. pOClE BydA~i TEKCTA
C*   KYPCOP pEPEBOdiTCq HA HOBY` CTPOKY
C*   ECli B TEKCTE ECTx HYlEBOj bAjT,
C*   BydA~A pPOiCXOdiT TOlxKO dO |TOgO
C*   bAjTA.
C*
C*   SUBROUTINE SOUT(TEXT)
C*
C*   ByBOdiT HA diCplEj CiMBOly MACCi-
C*   BA TEXT dO HYlEBOgO bAjTA, pOClE
C*   BydA~i KYPCOP pEPEBOdiTCq HA HO-
C*   BY` CTPOKY.
C*
C*   ObPA}EHiE CTAHdAPTHOE. iCpOlxzY`T-
C*   Cq bEz BOCCTAHOBlEHiq 8-12 i 14
C*   PEgiCTPy. HA ByXOdE - lOgi~ECKAq
C*   gPYppA. TEKCT - B KOdE ISO.
C*
C*   ByBOdiTCq MAKCiMYM 80 CiMBOlOB, KOd
C*   "12B" (pEPEXOd HA HOBY` CTPOKY) dO-
C*   bABlqETCq K ByBOdY, ECli dliHA  By-
C*   BOdA < 80 CiMBOlOB. pOClEdHEE COOT-
C*   BETCTBYET OCObEHHOCTqM diCplEq "VI-
C*   DEOTON-340".
C*
C*   pPi pEPEbiBKE ByBOdA QOUT pEPEXOdiT
C*   HA BBOd. ECli BBOdiTCq "ETX" - bEz
C*   pOClEdCTBij. ECli BBOdiTCq A"ETX"
C*   ili ABORT"ETX" - pEPEdA~A YpPABlEHiq
C*   pO AdPECY 20B. B dPYgiX ClY~AqX:
C*   ECli 5 ClOBO COMMON-blOKA *INFTER*
C*   PABHO 0B, TO pEPByE {ECTx CiMBOlOB
C*   BBOdA zAHOCqTCq B 4 ClOBO COMMON-
C*   blOKA; iHA~E YpPABlEHiE pEPEdAETCq
C*   pPOcEdYPE, AdPEC KOTOPOj B 5 ClOBE
C*   <CALL PROC(AdPEC pPiHqTOj CTPOKi)>.
C*
C*   OpEPAciOHHAq CiCTEMA - diCpAK.
C*
 *INFTER*:,LC,5
 *OUTBUF*:,LC,15
 ,STI,14
 ,ATI,12
 14,WTC,
 14,VTM,
 15,MTJ,11
 14,VZM,OUT
 ,ITA,14
 ,NTR,7
 ,UTC,=120
 ,X-A,
 ,UZA,*+1
 14,VTM,80
 :,ITA,14
 ,NTR,6
 :10,VTM,-5
 12,XTS,
 :14,UTM,-1
 14,VZM,*+2
 10,VLM,*-1
 12,VLM,*-2
 :14,VTM,INF
 14,AEX,2
 10,VZM,*+5
 14,XTS,1
 10,J+M,10
 10,J+M,10
 10,J+M,10
 10,ASN,65
 15,AAX,
 ,UJ,*+2
 :14,XTA,2
 15,AEX,
 :,UZA,*-1
 15,ATX,
 14,ARX,1
 15,AEX,-1
 ,ASN,64+3
 14,AAX,2
 14,AEX,2
 15,AEX,
 OUT:,ITS,
 12,VTM,*INFTER*
 15,ATX,
 11,MTJ,15
 GO:14,BASE,INF
 12,XTA,1
 10,VTM,*OUTBUF*
 9,VTM,14
 ,U1A,CONT
 12,*71,
 :12,ATX,1
 14,VTM,INF
 CONT:11,XTA,1
 10,ATX,
 ,AAX,=377
 11,UTM,1
 9,UTM,-1
 ,UZA,CONT1
 9,VZM,GO1
 10,VLM,CONT
 GO1:10,XTA,
 ,AAX,=:777774
 END:10,ATX,
 12,XTA,2
 14,AEX,
 15,ATX,1
 9,VTM,*OUTBUF*
 12,XTA,1
 ,ITS,
 12,STX,1
 ,AAX,=:00000003
 ,AEX,=:00000001
 ,UZA,END1
 15,*71,1
 RET:15,XTA,
 13,UJ,
 :,XTA,=:024
 8,ASN,64
 10,AEX,
 9,V1M,END
 ,AAX,=:777774
 ,UJ,END
 CONT1:10,XTA,
 8,VTM,
 :,UZA,CONT1-3
 ,ASN,64-8
 8,UTM,8
 ,UJ,*-1
 END1:15,XTA,1
 ,AEX,=:001
 15,ATX,1
 15,*71,1
 :10,BASE,INF
 12,*71,
 12,ATX,1
 ,AAX,=:00000003
 ,AEX,=:00000001
 ,UZA,RET
 9,XTA,
 ,AEX,=:006
 ,UZA,RET
 12,WTC,4
 14,VTM,
 ,AEX,=6HB'3'''''''''
 14,V1M,END2
 ,UZA,20B
 ,AEX,=6H''AORT'3'
 ,UZA,20B
 9,XTA,
 12,STX,3
 13,UJ,
 END2:,ITA,9
 14,UJ,
 SOUT:,ENTRY,
 12,VTM,*INFTER*
 ,ATI,11
 11,UTM,-1
 ,UJ,GO
 INF:9,021,
 10,Z10,
 :,OCT,7777777777777776
 ,ISO,6H
 ,END,
 QTER:,NAME,
C
C**************** E.d.fEd`HxKiH 17/01/79
C*
C*   SUBROUTINE QTER(H,NH,TEXT,L)
C*
C*   BBOd iHfOPMAcii C |KPAHA diCplEq C
C*   pOdCKAzKOj. pOdCKAzKA H COdEPviT NH
C*   CiMBOlOB. iHfOPMAciq (BMECTE C pOd-
C*   CKAzKOj) BBOdiTCq B MACCiB TEXT. B
C*   L zACylAETCq ~iClO BBEdEHHyX CiMBO-
C*   lOB. CTPOKA B MACCiBE TEXT dOpOlHq-
C*   ETCq pPObElAMi dO cElOgO ~iClA ClOB
C*
C*   ObPA}EHiE CTAHdAPTHOE. iCpOlxzY`TCq
C*   bEz BOCCTAHOBlEHiq 8-12 i 14 PEgiC-
C*   TPy. PEviM HOPMAlizAcii i OKPYglE-
C*   Hiq HE MEHqETCq. HA ByXOdE - lOgi-
C*   ~ECKAq gPYppA.
C*
C*   SUBROUTINE QCIA(H,NH,TEXT,L)
C*
C*   BBOd iHfOPMAcii C pOdCKAzKOj i zA-
C*   CEKPE~iBAHiEM. B OCTAlxHOM AHAlO-
C*   gi~HA QTER.
C*
C*   iCpOlxzYET QIN.
C*
 *OUTBUF*:,LC,15
 S:,UTC,1
 QCIA:,ENTRY,
 9,VTM,
 ,STI,11
 ,STI,12
 ,STI,14
 ,ATI,10
 14,WTC,
 14,VTM,
 ,ITA,14
 15,MTJ,8
 ,ITS,9
 ,ITS,13
 ,ITS,11
 13,VTM,
 12,MTJ,9
 15,ATX,
 14,VZM,FULL
 :10,XTA,
 9,ATX,
 9,UTM,1
 13,VTM,-5
 :14,UTM,-1
 14,VZM,*+2
 13,VLM,*-1
 10,VLM,*-3
 :13,VZM,FULL
 ,UTC,=:77777777777774
 ,XTA,
 13,J+M,13
 13,J+M,13
 13,J+M,13
 13,ASN,64+8
 9,AAX,-1
 9,ATX,-1
 FULL:,ITA,9
 ,ITS,13
 ,ITS,12
 ,ITS,8
 14,VTM,*OUTBUF*
 ,ITS,14
 8,UTM,1
 ,ITS,8
 15,ATX,
 ,CALL,STER
 :,STI,10
 ,STI,9
 ,STI,11
 ,STI,13
 ,ATI,8
 15,ARX,
 11,ATX,
 11,VTM,*OUTBUF*
 11,XTA,
 8,VZM,BL
 10,VZM,L1
 10,MTJ,14
 L0:10,ASN,64+48
 15,ATX,
 ,YTA,
 15,STX,
 9,AEX,-1
 9,ATX,-1
 :14,UTM,8
 8,UTM,-1
 8,VZM,OUT
 14,V1M,*-1
 15,XTA,1
 9,ATX,
 9,UTM,1
 10,MTJ,14
 :,ASN,64-8
 8,UTM,-1
 8,VZM,BL
 ,U1A,*-1
 11,XTA,1
 11,VLM,L0
 BL:,UTC,=6H
 ,XTA,
 10,ASN,64+48
 9,AEX,-1
 9,STX,-1
 13,UJ,
 OUT:15,XTA,
 13,UJ,
 L1:9,ATX,
 14,VTM,-5
 :8,UTM,-1
 8,VZM,OUT
 14,VLM,*-1
 11,XTA,1
 9,UTM,1
 11,VLM,L1
 ,END,
 QERASE:,NAME,
C
C**************** E.d.fEd`HxKiH 05/07/84
C*
C*   |lEMEHTAPHyE fYHKcii dlq PAbOTy C
C*   diCplEEM. OpEPAciOHHAq CiCTEMA -
C*   diCpAK
C*
 *INFTER*:,LC,5
 12,VTM,INF+1
 15,ATX,
 PER:11,VTM,*INFTER*
 11,XTA,1
 ,U1A,PER1
 11,*71,
 PER1:11,ATX,1
 14,VTM,INF
 11,XTA,2
 14,AEX,
 15,ATX,1
 ,ITA,
 11,ATX,1
 15,*71,1
 :15,XTA,
 13,UJ,
 QHOME:,ENTRY,
 15,ATX,
 12,VJM,PER
 ,GOST,6H/00000
 QLAST:,ENTRY,
 15,ATX,
 12,VJM,PER
 ,GOST,6H/*0000
 QPRINT:,ENTRY,
 15,ATX,
 12,VJM,PER
 ,GOST,6H83/'27'*0
 QDL:,ENTRY,
 15,ATX,
 12,VJM,PER
 ,GOST,6H*'36'0000
 QDL0:,ENTRY,
 15,ATX,
 12,VJM,PER
 ,GOST,6H'36'00000
 QUP:,ENTRY,
 15,ATX,
 12,VJM,PER
 ,GOST,6H*00000
 QDOWN:,ENTRY,
 15,ATX,
 12,VJM,PER
 ,GOST,6H'32'00000
 QBELL:,ENTRY,
 15,ATX,
 12,VJM,PER
 ,GOST,6H780000
 INF:12,021,
 12,Z10,
 ,GOST,6H:00000
 ,END,
 QMOVE:,NAME,
C
C**************** E.d.fEd`HxKiH 06/06/84
C*
C*   SUBROUTINE QMOVE(N)
C*
C*   CdBigAET CTPOKY diCplEq, pPEd{ECT-
C*   BY`}Y` TEKY}Ej BpPABO(+) ili BlEBO
C*   (-) HA N pOzicij.
C*
C*   ObPA}EHiE CTAHdAPTHOE. iCpOlxzY`TCq
C*   bEz BOCCTAHOBlEHiq 9-12 i 14 PE-
C*   giCTPy.
C*   PEviM HA BXOdE - HECY}ECTBEHEH,
C*   PEviM HA ByXOdE = 6.
C*
C*   OpEPAciOHHAq CiCTEMA - diCpAK.
C*
 *INFTER*:,LC,5
 *OUTBUF*:,LC,15
 ,ATI,12
 12,XTA,
 ,NTR,23B
 ,UZA,*+2
 12,AVX,
 ,UTC,1
 :10,VTM,INF+2
 ,STI,14
 13,UTC,
 14,VZM,
 ,ITS,14
 11,VTM,INF
 11,X-A,5
 ,NTR,22B
 ,UZA,*+1
 14,VTM,82
 :10,XTA,
 15,ATX,1
 15,MTJ,12
 ,ASN,64+8
 11,AEX,1
 10,VTM,-4
 :14,UTM,-1
 14,VZM,CONT
 10,VLM,*-1
 12,ATX,
 12,XTA,1
 ,UJ,*+3
 :14,UTM,-1
 14,VZM,CONT
 10,VLM,*-1
 12,ATX,
 :10,VTM,-5
 12,VLM,*-2
 CONT:10,VZM,FULLW
 12,ATX,
 10,J+M,10
 10,J+M,10
 10,J+M,10
 15,XTA,1
 11,AEX,4
 10,ASN,64+48
 12,AEX,
 12,ATX,                                                               Q
 CONT1:,ITA,
 12,ATX,1
 10,VTM,*INFTER*
 10,XTA,1
 12,VTM,*OUTBUF*
 ,U1A,P
 10,*71,
 P:12,MTJ,9
 15,MTJ,14
 :14,XTA,
 12,ATX,
 14,UTM,1
 12,UTM,1
 ,U1A,*-2
 10,ATX,1
 10,XTA,2
 11,AEX,
 15,ATX,1
 15,*71,1
 :15,XTA,
 13,UJ,
 FULLW:12,ATX,
 11,XTA,4
 12,ATX,1
 12,VLM,CONT1
 INF:9,021,
 12,Z10,
 ,GOST,6H*00000
 ,GOST,6H'34''34''34''34''34''34'
 ,GOST,6H'35''35''35''35''35''35'
 ,GOST,6H'32'00000
 ,LOG,122
 ,END,
 ISTG:,NAME,
 15,ATX,
 ,ASN,64+5
 ,UZA,RETZ
 15,XTA,
 ,ASN,64+2
 15,ATX,
 ,YTA,
 ,ASN,64+43
 15,WTC,
 ,UTC,TABL-8
 ,XTS,
 15,WTC,
 ,ASN,64+16
 ,UTC,=377
 ,AAX,
 14,UJ,
 RETZ:,UTC,=17
 ,XTA,
 15,UTM,-1
 14,UJ,
 TABL:,GOST,4H'034''134''133'
 ,GOST,4H'137''121''126''127'
 ,GOST,4H+*)(
 ,GOST,4H/.-,
 ,GOST,4H3210
 ,GOST,4H7654
 ,GOST,4H'026':98
 ,GOST,4H'021''036'='035'
 ,GOST,4HCBA'136'
 ,GOST,4HGFED
 ,GOST,4HKJIH
 ,GOST,4HONML
 ,GOST,4HSRQP
 ,GOST,4HWVUT
 ,GOST,4H'027'ZYX
 ,GOST,4H'132''130''030''115'
 ,GOST,4HcbA`
 ,GOST,4HgfEd
 ,GOST,4HKjiX
 ,GOST,4HOHMl
 ,GOST,4HCPqp
 ,GOST,4HBvYT
 ,GOST,4H{zyx
 ,GOST,4H'133'~}|
 ,END,
 WAIT:,NAME,
 SOUT:,SUBP,
 14,VTM,TEX
 12,VTM,-12
 15,MTJ,11
 14,XTA,
 11,ATX,10
 14,XTA,1
 :11,ATX,11
 11,UTM,1
 12,VLM,*-1
 ,ITA,
 11,ATX,11
 15,MTJ,14
 14,UTM,10
 ,ITA,14
 ,UJ,SOUT
 TEX:,ISO,6H^'10''34''34''34''34'
 ,ISO,6H'34''34''34''34''34''34'
 ,END,
 KEY:,NAME,
 *INFTER*:,LC,5
 11,BASE,*
 ,ATI,12
 12,XTA,
 ,ASN,64+4
 ,YTA,
 ,ASN,64+4
 ,AEX,=6H0'3'''''''''
 12,VTM,*INFTER*
 12,AEX,3
 ,U1A,L
 12,ATX,3
 ,UTC,=I1-=I0
 L:,XTA,=I0
 13,UJ,
 ,END,
 IFOVFL:,NAME,
 CAUSA*:,LC,1
 EST:,EQU,STAT+17
 ,ITA,15
 15,VTM,STAT
 14,VTM,13
 :14,ITS,
 14,UTM,-1
 14,V1M,*-1
 ,XTS,16B
 ,XTS,17B
 ,WTC,STAT
 ,XTS,-1
 15,ATX,
 15,XTA,**-EST
 ,CTX,20B
 15,XTA,=I0-EST
 ,UJ,RET
 **:,NTR,3
 ,UJ,OVFL
 OVFL:,UTC,CAUSA*
 ,ATX,
 15,UTM,1
 ,ITA,15
 15,VTM,EST
 15,A-X,STAT-EST
 ,U1A,SJ
 15,XTA,
 ,WTC,STAT
 ,STX,-1
 ,STX,17B
 ,STX,16B
 14,VTM,-12
 :14,STI,13
 14,VLM,*
 15,XTA,=I1-STAT
 RET:,WTC,STAT
 15,VTM,
 ,ITS,
 ,*50,102B
 :14,VTM,20B
 ,ITA,14
 ,NTR,6
 ,*50,103B
 :15,XTA,
 13,UJ,
 SJ:,SJ,
 STAT:,BSS,17
 ,END,
 CTIME:,NAME,
 S:,STI,12
 ,ATI,11
 10,BASE,S
 ,*63,4
 :,AEX,=:64
 ,NTR,6
 ,E+N,100B
 ,A*X,=R0.02
 11,ATX,
 12,STX,
 13,UJ,
 ,END,
 SCANER:,NAME,
 ,UTC,-1
 PUTH:,ENTRY,
 ,UTC,-1
 PUTO:,ENTRY,
 ,UTC,-1
 PUTG:,ENTRY,
 12,VTM,3
 B:8,BASE,*
 ,NTR,3
 ,STX,P6
 ,STX,P5
 ,STX,P4
 ,ATI,9
 9,XTA,
 ,STI,9
 ,STI,10
 ,ATI,11
 ,ITA,5
 ,ITS,6
 ,ITS,7
 11,XTS,
 ,AEX,=I0
 12,VZM,DECOD
 ,UZA,GO
 ,ITA,11
 ,ATX,C/P
 GO:,WTC,P5
 ,XTA,
 ,AAX,=17
 ,ATI,7
 ,ATX,P5
 ,WTC,P4
 ,XTA,
 ,ATI,6
 ,ITA,6
 ,UZA,GRENC
 ,ATX,P4
 10,XTA,
 ,ATX,BIN
 12,UJ,*
 :,UJ,H/ENCOD
 :,UJ,B/ENCOD
 :,UJ,G/ENCOD
C
 APOS:,EQU,47B
 MINUS:,EQU,55B
 POIN:,EQU,56B
 STAR:,EQU,52B
C
 TABS/1:,EQU,TABS-1
C
 DECOD:,BSS,
 ,UZA,D1
 ,ITA,11
 ,ATX,C/G
 ,UJ,D2
 D1:,ARX,C/G
 ,UZA,LINEFEED
 D2:,XTA,
 ,ATX,REZAC
 ,ATX,OCT
 ,ATX,CMAN
 ,ATX,CHAR
 5,VTM,1
 6,VTM,0
 7,VTM,0
 11,VTM,ANAL
 ,ITA,9
 ,AAX,=10
 ,U1A,GETS
 ,ATX,COSY
 11,VJM,GETS
 ,AEX,=40
 ,UZA,GETS
 11,VTM,ANAL
 ,UJ,AN1
 ANAL:,BSS,
 5,VZM,AN1
 ,AEX,=40
 ,U1A,AN1
 ,ITA,9
 ,AAX,=1
 ,U1A,GETS
 AN1:,BSS,
 14,VTM,TABS/1
 ,XTA,SYM
 ,UZA,ISLF
 ,AEX,=12
 ,U1A,AN2
 ISLF:14,UTM,KLF-TABS/1
 ,XTA,C/G
 ,AOX,=:4
 ,ATX,C/G
 ,UJ,ANALW
 AN2:,ITA,9
 ,AAX,=2
 ,U1A,AN3
 14,UTM,2
 AN3:14,UTM,1
 14,XTA,
 ,AAX,=377
 ,A-X,SYM
 ,U1A,AN3
 14,XTA,
 ,ASN,64+8
 ,AAX,=377
 ,X-A,SYM
 ,U1A,AN3
 ANALW:,ITA,9
 ,AAX,=10
 ,UZA,ENTAB
 14,XTA,
 ,ASN,64+16
 ,AAX,=17
 ,ATI,6
 ,XTA,SYM
 ,AEX,=40
 ,ASN,64-40
 ,AEX,BL
 ,UJ,EX/REZ
 ENTAB:6,XTA,TSS
 14,ASX,
 ,AAX,=77
 ,UZA,ERRN
 ,AEX,=77
 ,U1A,ENT1
 7,VZM,SETO
 7,MTJ,6
 ,UJ,EXITR
 ENT1:,AEX,=77
 ,ASN,64+3
 ,ATI,6
 ,YTA,
 ,ASN,64+45
 ,ATI,12
 ,XTA,SYM
 ,ATI,14
 12,UJ,SWIST
 TABS:,BSS,
 ,ISO,6H'200'**'2'**
 ,ISO,6H'200'//'2'//
 ,ISO,6H'244'EE'2'EE
 ,ISO,6H'200''''''2'AZ
 ,ISO,6H'200''''''2''140''176'
 ,ISO,6H'214''''''1'09
 ,ISO,6H'260''''''3'..
 ,ISO,6H'274''''''3'++
 ,ISO,6H'274''''''3'--
 ,ISO,6H'310''''''3''47''47'
 ,ISO,6H'230''''''3''''377'
 KLF:,ISO,6H'324'''''''''''
 TSS:,BSS,
  0,LOG,67 54 60 67 44 67 11 44
  1,LOG,37 00 37 20 73 37 11 17
  2,LOG,27 00 27 00 73 27 21 00
  3,LOG,57 00 57 00 00 57 36 00
  4,LOG,47 00 47 47 45 47 45 45
  5,LOG,00 47 55 55 55 55 55 55
  6,LOG,07 07 07 00 07 07 11 07
  7,LOG,00 00 30 00 00 00 36 00
 10,LOG,77 00 77 77 00 77 00 00
 SWIST:,BSS,
 :,UJ,*0
 ,Z05,BACK
 :,UJ,*1
 ,Z10,CHBD
 :,UJ,*2
 ,Z03,SETSR
 :,UJ,*3
 ,Z02,SETSI
 :,UJ,*4
 ,Z10,ETEX
 :,UJ,*5
 ,Z03,SETE
 :,UJ,*6
 ,Z05,EXIT
 :6,XTA,SWIST
 ,ATI,12
 ,ASN,64+15
 ,AAX,=17
 ,ATI,6
 12,UJ,
 ETEX:14,UTM,-APOS
 14,V1M,ETEX2
 5,VTM,1
 7,VTM,4
 ,UJ,GETS
 ETEX2:6,VTM,4
 ,UJ,EXITR
 SETSI:,BSS,
 ,ITA,9
 ,AAX,=4
 ,UZA,SETSR
 ,XTA,
 SETO:,BSS,
 ,ARX,CHAR
 ,UZA,DEC
 ,AAX,=10
 ,U1A,ERRN
 ,XTA,OCT
 ,ATX,REZAC
 DEC:6,VTM,1
 ,XTA,CHAR
 ,AAX,=:002
 ,UZA,EXITR
 ,XTA,REZAC
 ,UZA,EX/REZ
 ,AEX,=7777 7777 7777 7777
 ,ARX,=1
 EX/REZ:10,ATX,
 ,UJ,EXIT
 SETSR:,BSS,
 ,XTA,REZAC
 ,AOX,=I0
 ,AVX,CHAR
 ,ATX,REZAC
 ,ATX,OCT
 ,XTA,CHAR
 ,AAX,=:001
 ,UZA,EXITR
 ,XTA,
 ,ATX,REZAC
 SETE:,BSS,
 ,XTA,REZAC
 ,NTR,13B
 ,UZA,ERRN
 ,AVX,CHAR
 ,ATX,REZAC
 ,XTA,CMAN
 ,AAX,=377
 ,A+X,REZAC
 ,AMX,=22
 ,UZA,ERRN
 ,XTA,CMAN
 ,ASN,64+39
 ,X-A,REZAC
 ,ATI,14
 14,XTA,ONE
 ,NTR,6
 ,A*X,OCT
 ,UJ,EX/REZ
 CHBD:,BSS,
 14,UTM,-102B
 14,VZM,GETS
 14,UTM,-2
 14,V1M,ERRN
 ,XTA,=:4
 ,UJ,TOCH
 *2:,BSS,
 *0:,XTA,=:4
 14,ASN,64-46B
 TOCH:,AOX,CHAR
 ,UJ,G*CH
 *1:,AOX,CHAR
 ,ATX,CHAR
 ,XTA,OCT
 ,ASN,64-3
 ,AEX,SYM
 ,AEX,=60
 ,ATX,OCT
 ,XTA,REZAC
 ,ASN,64+36
 ,U1A,GETS
 ,XTA,CHAR
 ,AAX,=:001
 ,U1A,INT
 ,XTA,=1
 INT:,ARX,CMAN
 ,ATX,CMAN
 *6:,ITA,9
 ,AOX,=2
 ,AEX,=2
 ,ATI,9
 ,XTA,REZAC
 ,ASN,64-1
 15,ATX,
 ,ASN,64-2
 15,ARX,
 ,ARX,SYM
 ,A-X,=60
 ,UJ,G*REZ
 *3:,XTA,REZAC
 ,AOX,=I0
 ,AVX,CHAR
 ,ATX,OCT
 ,XTA,=:4
 ,ATX,CHAR
 G*REZ:,ATX,REZAC
 ,UJ,GETS
 *4:,XTA,BL
 ,ATX,REZAC
 10,ATX,
 10,ATX,1.dlq A.p.
 ,XTA,=:1
 ,ATX,CHAR
 14,MTJ,5
 5,UTM,-APOS
 5,VZM,GETS
 ,XTA,SYM
 *5:,AEX,=40
 ,XTS,CHAR
 ,ASN,64+41
 ,STI,12
 12,ASN,8
 ,AEX,REZAC
 ,ATX,REZAC
 ,XTA,CHAR
 ,ARX,=:04
 ,U1A,G*CH
 ,XTA,REZAC
 10,ATX,
 10,UTM,1
 ,XTA,BL
 ,ATX,REZAC
 ,XTA,=:1
 G*CH:,ATX,CHAR
 ,UJ,GETS
 LINEFEED:,XTA,=I0
 6,VTM,5
 ,UJ,EXITL
 ERRN:6,VTM,0
 ,UJ,EXIT
 BACK:,XTA,C/G
 ,ARX,=776 00000
 ,AAX,=7777 7777
 ,ATX,C/G
 11,VJM,GETS
 ,AEX,=40
 ,UZA,BACK
 EXITR:,XTA,REZAC
 10,ATX,
 EXIT:,XTA,SYM
 ,AOX,=I0
 EXITL:,WTC,P5
 ,ATX,
 ,ITA,6
 ,AOX,=I0
 ,WTC,P4
 ,ATX,
 ,XTA,C/G
 ,ASN,64-1
 ,ASN,64+16
 ,UJ,FIN
 H/ENCOD:,BSS,
 5,VTM,-5
 10,UTM,1
 GETH:,ASN,64-8
 ,ATX,BIN
 ,YTA,
 ,ATX,SYM
 ,AAX,=200
 ,U1A,PUTH
 ,XTA,SYM
 ,ASN,64+5
 ,AOX,P5
 ,UZA,PUTB
 ,XTA,SYM
 11,VJM,PUTS
 6,UTM,-1
 6,VZM,GRENC
 GETB:,XTA,BIN
 5,VLM,GETH
 10,XTA,
 ,UJ,H/ENCOD
 PUTH:,AEX,SYM
 ,ATI,7
 PUTB:,XTA,=40
 11,VJM,PUTS
 6,UTM,-1
 6,VZM,GRENC
 7,VZM,GETB
 7,UTM,-1
 ,UJ,PUTB
 B/ENCOD:,BSS,
 ,XTA,P4
 ,AAX,=20
 ,UZA,BENC
 ,ATI,6
 BENC:,BSS,
 6,UTM,-1
 ,XTA,BIN
 6,ASN,64
 6,ASN,64
 6,ASN,64
 ,AAX,=7
 ,AEX,=60
 11,VJM,PUTS
 6,VZM,GRENC
 7,UTM,-1
 7,V1M,BENC
 ,WTC,P5
 7,VTM,
 ,XTA,=40
 11,VTM,BENC
 ,UJ,PUTS
 G/ENCOD:,BSS,
 5,VTM,0
 6,VTM,0
 11,VTM,0
 ,XTA,
 ,ATX,W4
 ,XTA,P5
 ,UZA,CHSIG
 11,VTM,1
 ,X-A,=14
 ,UZA,CHSIG
 ,XTA,TEN
 ,ATX,W4
 7,VTM,1
 ,ITA,7
 ,ATX,P5
 CHSIG:,XTA,BIN
 ,AAX,=:774
 ,U1A,ISEXP
 ,XTA,=I0
 ISEXP:,AOX,BIN
 ,NTR,6
 ,A+X,
 ,UZA,POZ
 11,UTM,2
 ,AVX,BIN
 POZ:,ATX,BIN
 ,NTR,3
 ,ITA,7
 ,X-A,
 ,ATI,12
 ,ITA,11
 ,ATX,REZAC
 ,ACX,P5
 ,A-X,P4
 ,UZA,OVFL
 ,AVX,=:002
 ,ATI,10
 ,A-X,=22
 ,U1A,*+2
 ,ATI,5
 10,VTM,18
 :10,UTM,ONE
 10,XTA,
 ,NTR,6
 ,X-A,BIN
 ,UZA,EPOZ
 ,XTA,BIN
 ,UZA,ZERO
 ,XTA,W4
 ,U1A,EPOZ0
 12,XTA,ONE
 7,VZM,ROUND
 ,X-A,BIN
 ,U1A,EPOZ1
 ,X-A,BIN
 ,UJ,ROUND
 EPOZ:,XTA,BIN
 ,A-X,TEN
 ,U1A,MCALC
 ,XTA,BIN
 ,A/X,TEN
 ,ATX,BIN
 ,XTA,W4
 ,ARX,=1
 ,ATX,W4
 ,UJ,EPOZ
 EPOZ0:,X-A,BIN
 ,UZA,EPOZ
 EPOZ1:,XTA,BIN
 ,A-X,ONE
 ,UZA,MCALC
 ,XTA,BIN
 ,A*X,TEN
 ,ATX,BIN
 ,XTA,W4
 ,ARX,=377
 ,ATX,W4
 ,UJ,EPOZ1
 ZERO:,ATX,W4
 ,UJ,DIGIT
 MCALC:,NTR,3
 10,VTM,TEN
 ,ITA,11
 ,ASN,64+1
 ,ARX,=5
 ,X-A,P4
 ,U1A,OVFL
 ,ATI,7
 ,NTR,6
 7,VZM,MCALC1
 7,UTM,-1
 7,V1M,MCALC1
 10,VTM,HUND
 MCALC1:,ITA,7
 ,ATX,P5
 ,NTR,3
 ,X-A,
 ,ATI,12
 ,NTR,6
 12,XTA,ONE
 ROUND:,E-N,64+1
 ,A+X,BIN
 ,ATX,BIN
 10,A-X,
 ,UZA,EPOZ
 ,NTR,3
 ,XTA,W4
 ,UZA,DIGIT
 ,AAX,=200
 ,ASN,64-33
 ,ATX,W1
 ,XTA,W4
 ,AVX,W1
 ,AAX,=377
 ,UZA,NEXP3
 ,ATX,W4
 ,A-X,=12
 ,U1A,NEXP2
 ,AEX,=400
 ,ATX,W4
 NEXP2:,XTA,W1
 ,ASN,64+23
 ,AEX,W4
 ,ASN,64-16
 NEXP3:,ARX,TEXP
 ,ATX,W4
 DIGIT:,BSS,
 10,UTM,-1
 10,XTA,
 ,NTR,16B
 ,U1A,ENUM
 ,XTA,BIN
 10,A/X,
 ,NTR,3
 ,A+X,=I0
 ,ATI,14
 14,V1M,NOTZERO
 6,V1M,NOTZER
 10,XTA,
 ,A-X,TEN
 ,U1A,NOTZER
 ,XTA,=40
 9,V1M,PUTDIG
 5,UTM,1
 ,UJ,DIGIT
 NOTZERO:,NTR,6
 10,A*X,
 ,X-A,BIN
 ,ATX,BIN
 NOTZER:14,UTM,60B
 14,MTJ,6
 ,XTA,REZAC
 ,ASN,64+1
 ,UZA,NOTSIG
 14,VTM,MINUS
 ,ITA,14
 11,VJM,PUTS
 ,XTA,
 ,ATX,REZAC
 NOTSIG:,ITA,6
 ,UJ,PUTDIG
 ENUM:7,VZM,PUTEX
 ,NTR,6
 7,MTJ,10
 10,UTM,ONE
 10,XTA,
 7,VTM,0
 ,A*X,BIN
 ,ATX,BIN
 14,VTM,POIN
 ,ITA,14
 PUTDIG:11,VTM,DIGIT
 ,UJ,PUTS
 PUTEXP:,ASN,64-8
 ,ATX,W4
 ,YTA,
 11,VJM,PUTS
 PUTEX:,XTA,W4
 ,U1A,PUTEXP
 5,VZM,GRENC
 PUTLB:,XTA,=40
 11,VJM,PUTS
 5,UTM,-1
 5,V1M,PUTLB
 GRENC:,BSS,
 ,XTA,C/P
 ,ASN,64+15
 FIN:,AOX,=I0
 ,WTC,P6
 ,STX,
 ,STI,7
 ,STI,6
 ,STI,5
 ,NTR,6
 13,UJ,
 OVFL:,BSS,
 ,XTA,P4
 ,ATI,6
 10,VTM,STAR
 11,VTM,*+1
 :6,VZM,GRENC
 6,UTM,-1
 ,ITA,10
 ,UJ,PUTS
 GETS:,BSS,
 ,NTR,3
 ,XTA,COSY
 ,UZA,GETS2
 ,A-X,=1
 GETS1:,ATX,COSY
 ,XTA,=40
 ,ATX,SYM
 11,UJ,
 GETS2:,XTA,C/G
 14,VJM,GIVADR
 ,X-A,
 ,ATI,14
 ,XTA,C/G
 ,ARX,=1 00000
 ,ATX,C/G
 12,XTA,
 14,ASN,64+40
 ,AAX,=377
 ,ATX,SYM
 ,AAX,=200
 ,ATI,14
 ,AEX,SYM
 14,V1M,GETS1
 11,UJ,
 PUTS:,BSS,
 ,ATX,SYM
 ,NTR,3
 ,XTA,C/P
 14,VJM,GIVADR
 ,ATI,14
 ,XTA,C/P
 ,ARX,=1 00000
 ,ATX,C/P
 14,V1M,PUTS1
 ,XTA,BL
 12,ATX,
 PUTS1:,XTA,SYM
 ,AEX,=40
 14,ASN,64-40
 12,AEX,
 12,ATX,
 11,UJ,
 GIVADR:,BSS,
 ,ATI,12
 ,ASN,64+15
 15,ATX,
 ,A*X,C1/6
 15,ATX,
 15,WTC,
 12,UTM,
 ,ASN,64-1
 15,ATX,
 ,ASN,64-1
 15,ARX,
 15,X-A,
 ,ASN,64-3
 14,UJ,
CONST
 BL:,ISO,6H
 TEXP:,ISO,6HE+00''''
 C1/6:,OCT,4002 5252 5252 5253
C/REAL
 ,REAL,1.0E-18
 ,REAL,1.0E-17
 ,REAL,1.0E-16
 ,REAL,1.0E-15
 ,REAL,1.0E-14
 ,REAL,1.0E-13
 ,REAL,1.0E-12
 ,REAL,1.0E-11
 ,REAL,1.0E-10
 ,REAL,1.0E-09
 ,REAL,1.0E-08
 ,REAL,1.0E-07
 ,REAL,1.0E-06
 ,REAL,1.0E-05
 ,REAL,1.0E-04
 ,REAL,1.0E-03
 ,REAL,1.0E-02
 ,REAL,1.0E-01
 ONE:,REAL,1.0
 TEN:,REAL,1.0E+01
 HUND:,REAL,1.0E+02
 ,REAL,1.0E+03
 ,REAL,1.0E+04
 ,REAL,1.0E+05
 ,REAL,1.0E+06
 ,REAL,1.0E+07
 ,REAL,1.0E+08
 ,REAL,1.0E+09
 ,REAL,1.0E+10
 ,REAL,1.0E+11
 ,REAL,1.0E+12
 ,REAL,1.0E+13
 ,REAL,1.0E+14
 ,REAL,1.0E+15
 ,REAL,1.0E+16
 ,REAL,1.0E+17
 ,REAL,1.0E+18
CSTAT
 C/G:,BSS,1
 C/P:,BSS,1
 COSY:,BSS,1
 P4:,BSS,1
 P5:,BSS,1
 P6:,BSS,1
 SYM:,BSS,1
 REZAC:,BSS,1
 OCT:,BSS,
 BIN:,BSS,1
 W1:,BSS,
 CHAR:,BSS,1
 W4:,BSS,
 CMAN:,BSS,1
 ,END,
  DIALOG:,NAME,
C*
C*
C*     SUBROUTINE  DIALOG ( OUT,N,IN )
C*     SUBROUTINE  DIALOQ ( OUT,N,IN )
C*
C*   YHiBEPCAlxHAq pPOgPAMMA BBOdA/ByBOdA
C*            TEKCTOByX CTPOK.
C*
C*        BAPiAHT dlq OC "diCpAK"
C*
C*   B ByzyBA`}Ej pPOgPAMME OpiCyBAETCq
C*   COMMON/PROTO/ AP,NUNIT,NL,NF,INT,KAP(15)
C*   HA~AlxHyE zHA~EHiq |TiX pEPEMEHHyX
C*   OpPEdElq`TCq ABTOMATi~ECKi.
C*
C*   B KA~ECTBE YCTPOjCTBA BBOdA/ByBOdA
C*   iCpOlxzYETCq :
C*   NUNIT=2 - TEPMiHAl pOlxzOBATElq
C*   NUNIT=4 - CARD READER i PRINTER
C*
C*   AP - PEviM pPOTOKOliPOBAHiq ( YCTAHAB-
C*   liBAETCq pPOgPAMMOj DIALOP (CM.HivE)).
C*   B HA~AlE PAbOTy pPOTOKOl MOHOliCTOM
C*   dlq NUNIT=4, B OCTAlxHyX ClY~AqX
C*   pPOTOKOl blOKiPOBAH.
C*
C*   OUT - TEKCTOBAq CTPOKA B KOdE ISO,
C*   pOdlEvA}Aq ByBOdY.
C*   N - PEviM ByBOdA ( cElOE ~iClO )
C*   pPi  N<0  ByBOd idET TOlxKO B pPOTOKOl,
C*   MiHYq |KPAH TEPMiHAlA. dAlEE, pYCTx
C*   K=ABS(N). pPi K=0 ByBOdA HET, TOlxKO
C*   BBOd.  pPi K<128 ByBOdqTCq pEPByE  K
C*   CiMBOlOB CTPOKi  OUT.  pPi K>127
C*   ByBOd idET dO CiMBOlA C KOdOM  (K-128)
C*   BKl`~iTElxHO.
C*   CiMBOly "000" i "012" qBlq`TCq bEz-
C*   YClOBHyMi pPizHAKAMi KOHcA ByBOdA.
C*
C*   IN = -1, ECli BBOd HE TPEbYETCq
C*   (TOlxKO ByBOd). B pPOTiBHOM ClY~AE
C*   BBOd pPOiCXOdiT B  KAP  i zAKAH~i-
C*   BAETCq pYCTyM bAjTOM.
C*
C*   ByBOd HA |KPAHE i B pPOTOKOlE CdBiHYT
C*   OTHOCiTElxHO BBOdA HA 6 pOzicij BpPABO
C*   (pPi YClOBii, ~TO dliHA ByBOdA<48 CiMB)
C*
C*   DIALOQ - MOdifiKAciq OCHOBHOgO BXOdA
C*   dlq COBME}EHiq BBOdA B OdHOj CTPOKE
C*   C ByBOdOM ( BBOd C pOdCKAzKOj ).
C*   iMEET CMyCl TOlxKO dlq NUNIT=2,
C*   B pPO~iX ClY~AqX PAbOTAET KAK DIALOG.
C*
C*   NL - PAzMEP |KPAHA TEPMiHAlA B CTPOKAX
C*   (zAdAETCq izBHE). NL=0 : "bECKOHE~Hyj" |KPAH
C*   NF - ~iClO CTPOK, OCTAB{iXCq dO KOHcA
C*   |KPAHA. pPi ByBOdE HA TEPMiHAl bOlx{OgO
C*   KOli~ECTBA TEKCTA pOClE KAvdyX  NL
C*   CTPOK dElAETCq BBOd.
C*   ECli BBOd pYCTOj, INT pOlAgAETCq
C*   PABHyM 0 . ECli BBOd HE pYCTOj, OH
C*   pOME}AETCq B KAP C HYlEByM bAjTOM B
C*   KA~ECTBE pPizHAKA KOHcA.  KPOME TOgO,
C*   B  INT  zACylAETCq 1.
C*
C*   dlq ByzyBA`}Ej pPOgPAMMy  NF=0
C*   qBlqETCq pPizHAKOM, ~TO O~EPEdHOj
C*   |KPAH zApOlHilCq i pOlxzOBATElx
C*   TOlxKO ~TO BBEl pPiKAz.
C*   KPOME TOgO, MOvHO AHAliziPOBATx
C*   INT, pPiHiMA`}ij zHA~EHiq :
C*   0 - BBOdA BOOb}E HE bylO.
C*   1 - BBOd HEpYCT.
C*
C*   pPi PAbOTE OT  DIALOQ :
C*   pOdC~ET CTPOK |KPAHA HE BEdETCq
C*
C*   pPOgPAMMA pOPTiT 14 PEgiCTP, YCTAHAB-
C*   liBAET HA ByXOdE  ,NTR,2
C*
C*
C
  PRINT8 :,SUBP,
  READ*:,SUBP,
  QIN:,SUBP,
  A/I0:,EQU,=I0
  *INFTER*:,LC,5
  *OUTBUF*:,LC,15
  *PROTO*:,LC,20
  *0:,BLOCK,PROTOCOL,TYP,NLIN,NFREE
  ,CONT,INT,KAP(15)
C-----------------------------------
  14,VTM,77777B
  ,UTC,14
  DIALOQ:,ENTRY,
  ,ITS,
  14,VTM,-3
  :,UTC,TEXT
  14,STX,
  14,VLM,*-1
  14,VTM,-13
  :14,ITS,14
  14,VLM,*
  B:1,BASE,B
  ,WTC,IN
  ,XTA,
  ,AEX,MIN1
  :,U1A,*+1
  ,UTC,=1
  :,XTA,
  ,ATX,FLIN.1-HET BBOdA
  2,VTM,*PROTO*
  2,WTC,TYP
  3,VTM,
  3,VZM,CHTYP
  3,MTJ,14
  14,UTM,-2
  14,V1M,TYPIS
C...   dOp.pPOBEPKA HA HAli~iE TEPMiHAlA
  ,UTC,*INFTER*
  ,XTA,
  ,U1A,TYPIS
C
C...   OpPEdElEHiE TipA zAdA~i:
C      TEPMiHAlxHAq ili HET.
C
  CHTYP:,XTA,
  ,CALL,INTER.K fEd`HxKiHY
  3,VTM,2
  ,AEX,A/I0
  ,U1A,ISTYP
  3,VTM,4
  2,XTA,PROTOCOL
  ,U1A,ISTYP
  14,VTM,PRINT8
  ,ITA,14
  2,ATX,PROTOCOL
C
C...   Tip OpPEdElEH :
C
  ISTYP:,ITA,3
  ,AOX,A/I0
  2,ATX,TYP
  2,XTA,NLIN
  ,U1A,SCR/IS
C...   dlq TEX, KTO zAbyl CdElATx PACCylKY
  ,XTA,=20
  SCR/IS:,AOX,A/I0
  2,ATX,NLIN
  2,ATX,NFREE
  TYPIS:15,MTJ,4
  ,NTR,3
  14,VTM,*PROTO*+KAP
  ,ITA,14
  ,ATX,IN
  ,XTA,A/I0
  2,ATX,INT
  2,AOX,NLIN
  2,ATX,NLIN
  ,WTC,NSYM
  ,XTA,
C
C...  HYvEH li ByBOd i KAKOj dliHy:
C
  ,UZA,INPUT.-HET
  ,AEX,A/I0
  ,UZA,INPUT
  ,A-X,
  ,UZA,POZ
  ,X-A,.TOlxKO
  3,VTM,4.pPOTOKOl
  POZ:,ATI,10
 ,A-X,=200
  ,U1A,CNT
  10,VTM,127
  CNT:,ATX,ETSYM
  ,WTC,TEXT
  9,VTM,
  4,MTJ,5
C...   IF(TYP.NE.2) DIALOQ=DIALOG
  3,MTJ,14
  14,UTM,-2
  ,ITA,14
  ,AOX,FLIN
  ,AOX,QFL
  ,ATX,QFL
  5,UTM,-1
  ,UZA,NOSHIFT
  ,ITA,10
  ,A-X,=60
  ,UZA,NOSHIFT.LONG OUT
  ,XTA,=6H
  15,ATX,
  5,UTM,1
C
C...  pOCiMBOlxHAq pEPEpiCx BydA~i:
C
  NO SHIFT:,XTA,
  9,XTS,
  5,UTM,1
  6,VTM,-48
  LS:,ASN,64-8
  ,ATX,TEXT
  ,YTA,
  ,ATI,12
  ,UZA,ETRAN
  ,AEX,=12
  ,UZA,ETRAN
  ,AEX,=12
  6,UTM,8
  6,ASN,64
  15,AEX,
  ,ITS,12
  ,AEX,ETSYM
  ,UZA,ETRAN
  ,XTA,
  10,UTM,-1
  10,VZM,ETRAN
  ,XTA,TEXT
  6,VIM,LS
  9,VLM,NO SHIFT
C
C...   B y d A ~ A :
C
  ETRAN:,XTS,QFL
  ,UZA,QT1.QTER
  13,VTM,INPUT
  4,MTJ,5
  3,MTJ,14
  14,UTM,-4
  14,VZM,CHPROT
C
C...   KOPPEKciq C~-KA CBOb.CTPOK:
C
  ,XTA,FLIN
  ,UZA,SWOUT
  2,XTA,NLIN
  ,AEX,A/I0
  ,UZA,SWOUT
  2,XTA,NFREE
  :,A-X,=I1
  ,UZA,*+2
  2,XTA,NLIN
  ,A-X,=I1
  :2,ATX,NFREE
  ,AEX,A/I0
  ,UZA,QT
  SWOUT:,BSS,
  ,ITS,4
  ,CALL,SOUT.K fEd`HxKiHY
  13,VTM,INPUT
  5,VTM,*OUTBUF*
C---
  CHPROT:2,XTA,PROTOCOL
  ,ATI,14
  13,UTC,
  14,VZM,
  5,MTJ,12
  CHAN1/0:12,XTA,
  ,AEX,=6H'1''1''1''1''1''1'
  ,ARX,=6H'177''177''177''177''177''177'
  ,ASN,64+7
  ,AAX,=6H'1''1''1''1''1''1'
  ,ARX,=6H'177''177''177''177''177''177'
  ,AAX,=6H'177''177''177''177''177''177'
  12,AOX,
  12,ATX,
  12,UTM,1
  ,AAX,=377
  ,U1A,CHAN1/0
  ,ITS,5
  12,UTM,-1
  ,ITS,12
  ,XTS,=1
  14,UJ,
C---
  QT1:2,XTA,NLIN
  2,ATX,NFREE
C...   pOdgOTOBKA pOdCKAzKi
  QT:,BSS,
  14,VTM,-12
  4,MTJ,12
  TR1:12,XTA,
  14,UTC,*OUTBUF*
  ,ATX,12
  ,AAX,=377
  ,UZA,TR2
  12,UTM,1
  14,VLM,TR1
  ,XTA,
  TR2:14,UTC,*OUTBUF*
  ,ATX,13
  14,VLM,TR2
  14,VTM,-14
  TR3:2,UTC,KAP
  14,ATX,14
  14,VLM,TR3
  4,MTJ,15
  5,VTM,*OUTBUF*
  5,ATX,14
  5,XTA,.pOdCKAzKA
  ,UZA,INPUT.pYCTAq
  ,ITS,5
  14,VTM,=I78
  ,ITS,14
  ,XTS,IN.AdP.KAP
  ,ITS,
  14,VTM,=I1
  ,ITS,14
  ,CALL,STER.K fEd`HxKiHY.
  TINP:,BSS,
C...   pOiCK pOClEdHEgO ClOBA
  12,VTM,*PROTO*+KAP+15
  7,VTM,15
  TI1:12,UTM,-1
  12,XTA,
  ,UZA,TI2
  ,AEX,=6H
  ,U1A,TI3
  12,ATX,
  TI2:7,UTM,-1
  7,V1M,TI1
C...   pYCTOj BBOd:
  ,XTA,QFL
  ,U1A,EXIT.TOlxKO ByBOd
  13,VTM,EXIT
  ,UJ,CHPROT.pE~.*OUTBUF*
C...   YdAlEHiE pOClEdHiX pPObElOB
  TI3:,BSS,
  12,XTA,
  ,AEX,=6H
  14,VTM,-48
  TR5:14,UTM,8
  ,ASN,64-8
  ,U1A,TR5
  ,XTA,=6H'177''177''177''177''177''177'
  14,ASN,64
  12,AAX,
  12,ATX,
C...  BBOd HEpYCTOj ili OT DIALOQ
  ,XTA,=I1
  2,ATX,INT
  ,XTA,QFL
  ,U1A,PRIN
  4,MTJ,15
  12,VTM,-1
  QPR1:5,XTA,
  ,UZA,QPR2
  15,ATX,
  5,VLM,QPR1
  QPR2:5,XTA,-1
  ,ARX,=6H'177''177''177''177''177''177'
  ,ASN,64+7
  ,AAX,=6H'1''1''1''1''1''1'
  ,AEX,=6H'1''1''1''1''1''1'
  15,AOX,
  15,ATX,
  5,VTM,*PROTO*+KAP
  12,VLM,QPR1
  ,XTA,
  15,ATX,
  4,UTC,-*PROTO*-KAP
  PRIN:5,VTM,*PROTO*+KAP
  13,VJM,CHPROT.i BBOd
C---
  EXIT:4,MTJ,15
  14,VTM,13
  15,XTA,
  ,NTR,2
  :14,STI,
  14,UTM,-1
  14,VIM,*-1
  13,UJ,
C
C---   B B O d :
C
  INPUT:,ITA,3
  2,AEX,TYP
  2,AOX,NFREE
  ,ATI,14
  2,WTC,TYP
  3,VTM,-4
  ,XTA,FLIN
  ,UZA,INP
  14,VIM,EXIT
   3,VZM,EXIT
C---
  INP:,BSS,
  ,XTA,
  14,VTM,-14
  INPZ:2,UTC,KAP
  14,ATX,14
  14,VLM,INPZ
  2,XTA,NLIN
  2,ATX,NFREE
  ,XTS,IN
  13,VTM,TINP
  3,VZM,READ*
  ,ITS,
  ,UJ,QIN.K fEd`HxKiHY.
C
C...
C
  MIN1:,INT,-1
  FLIN:,BSS,1
  QFL    :,BSS,1
  IN     :,BSS,1
  NSYM   :,BSS,1
  TEXT   :,BSS,1
  ETSYM  :,BSS,1
  ,END,
  JFOCC:,NAME,
C
C    TAPE=<HOMlEH>/<CdBig>,<R/W><MATHOM>(<ObxEM>)
C    LUN=<MATHOM>(<ObxEM>)
C
C    pPiMEPy:
C    TAPE=1024/200,W41(140)
C    TAPE0258/70,R54
C    T9/0,W(1000)
C    T=4000/570,30
C    LUN=41(140)
C    LUN54
C    L30(140)
C    L=67
C
  SCANER:,SUBP,
C
C
  BAS:,BSS,.bAzA
  ,STI,9.FDB
  ,ATI,8.STRING
  ,ITA,7
  ,ITS,6
  ,ITS,5
  ,ITS,13
  7,BASE,BAS
  9,MTJ,6
C
C      HA~AlO CKAHiPOBAHiq CTPOKi
C
  ,XTS,=6H
  ,ATX,TYP.HEOpPEdElEH
  5,VTM,-48
  ,XTA,=11
  ,ATX,IREG
  ,ITS,8.HA~.AdPEC
  13,VJM,SCANER* + 1
  ,XTA,=10
  ,ATX,IREG
  :13,VTM,*
  ,XTA,ITYP
  ,AEX,=I2.bYKBA
  ,U1A,ETYP.YvE HET
  5,VZM,SYNTAXER
  ,XTA,SYM
  ,AEX,=6H
  5,ASN,64+48
  ,AEX,TYP
  ,ATX,TYP
  5,UTM,8
  ,UJ,SCANER*
C
C      ByqCHEHiE TipA zAKAzA
C
  ETYP:,BSS,
  ,XTA,TYP
  ,AEX,=6H
  ,UZA,SYNTAXER.HET TipA
  ,ARX,=6H'177''177''177''177''177''177'
  ,AAX,=6H'200''200''200''200''200''200'
  ,AEX,=6H'200''200''200''200''200''200'
  ,ASN,64+7
  ,ARX,=6H'177''177''177''177''177''177'
  ,AAX,=6H'177''177''177''177''177''177'
  ,ATX,MASK.MACKA zAKAzA
  ,XTA,ITYP.~TO Bzqli
  ,AEX,=I1
  ,UZA,CHTYP.cifPY
  ,XTA,SYM
  ,AEX,=1H=
  ,U1A,SYNTAXER.dOlvHO byTx =
  13,VJM,SCANER*
  CHTYP:,BSS,
  ,XTA,TYP
  ,AEX,=6HLUN
  ,AAX,MASK
  ,UZA,LUN*
  ,XTA,TYP
  ,AEX,=6HTAPE
  ,AAX,MASK
  ,UZA,TAPE*
  SYNTAXER:12,VTM,1
  ,UJ,RESTORE
C
C      "TAPE": zAKAz lEHTy
C
  TAPE*:,BSS,
  ,XTA,
  5,VTM,-16
  ,ATX,NAM.iMq lEHTy
  SCNAME:,BSS,
  ,XTA,ITYP
  ,AEX,=I1
  ,U1A,SHIFT.HE cifPA
  ,XTA,SYM
  ,ASN,64+40
  ,AAX,=17
  5,ASN,64+4
  ,AOX,NAM
  ,ATX,NAM
  13,VJM,SCANER*
  5,UTM,1
  5,V1M,SCNAME
  SHIFT:,BSS,
  ,XTA,SYM
  ,AEX,=1H/
  ,U1A,SYNTAXER.HE /
  ,XTA,NAM
  ,UZA,SYNTAXER.HET iMEHi
  ,XTA,=5.BOCxM,~iClO
  ,ATX,IREG
  13,VJM,SCANER*
  ,XTA,ITYP
  ,AEX,=I1.
  ,U1A,SYNTAXER
  ,XTA,SYM
  ,ATX,SHIF
  ,ARX,=3777 7777 7777 5777
  ,UZA,SYNTAXER.bOlx{Oj CdBig
  ,XTA,IKOD
  ,AEX,=6H
  ,ASN,64-40
  ,AEX,=6H
  ,ATX,SYM.PAzdEliTElx
  ,AEX,=1H,
  ,U1A,CHSC
  ,XTA,=10
  ,ATX,IREG
  13,VJM,SCANER*
  ,XTA,IKOD
  ,AEX,=I2
  ,U1A,LUN*.HE bYKBA
  ,XTA,SYM
  ,AEX,=1HW
  ,UZA,ISR
  ,XTA,SYM
  ,AEX,=1HR
  ISR:,U1A,SYNTAXER.HE "W" i HE "R"
  ,XTA,SYM
  6,ATX,6
  13,VJM,SCANER*
  6,XTA,1.MAT.HOMEP
  ,ATX,KYC
  ,XTA,ITYP
  ,AEX,=I1
  ,U1A,CHEND.HE MAT.HOMEP
C
C      "LUN": dOBBOd MAT.HOMEPA
C
  LUN*:,BSS,
  ,XTA,=I0.MAT.HOMEP
  6,ATX,1
  ,ATX,KYC
  5,VTM,-6
  SCLUN:,XTA,ITYP
  ,AEX,=I1
  ,U1A,SYNTAXER.HE cifPA
  ,XTA,SYM
  ,ASN,64+40
  ,AAX,=17
  ,ATX,SYM
  5,ASN,64+3
  ,AOX,KYC
  ,ATX,KYC
  ,XTA,SYM
  ,AAX,=10
  ,U1A,SYNTAXER.8,9
  13,VJM,SCANER*
  5,UTM,3
  5,V1M,SCLUN
  CHEND:,BSS,
  ,XTA,ITYP
  ,AEX,=I0
  ,UZA,ENDSTR.KOHEc CTPOKi
  ,XTA,ITYP
  ,AEX,=I3
  ,U1A,SYNTAXER.HE PAzdEliTElx
C
C      zAKAz ObxEMA
C
  CHSC:,BSS,
  ,XTA,SYM
  ,AEX,=1H(
  ,U1A,ENDSTR
  ,XTA,=5.BOCxM.~iClO
  ,ATX,IREG
  13,VJM,SCANER*
  ,XTA,ITYP
  ,AEX,=I1
  ,U1A,SYNTAXER.HE BINARY
  ,XTA,SYM
  ,ARX,=3777 7777 7777 5777
  ,U1A,ENDSTR +1
  12,VTM,4
  ,UJ,RESTORE
  ENDSTR:,XTA,
  ,ATX,SYM.ObxEM TOMA
  :,XTA,KYC
  6,ATX,1
  ,ASN,64-12
  ,ATX,KYC
  ,ARX,=:3777 7777 7710
  ,UZA,ERRNUM.>67B
  ,ARX,=40 0000
  ,U1A,ERRNUM.<30B
  ,XTA,TYP
  ,AEX,=6HLUN
  ,AAX,MASK
  ,UZA,TOTIP
C
C      zAKAz TOMA
C
  ,XTA,KYC
  ,ASN,64-30.MAT.HOMEP
  ,AOX,NAM.HOMEP lEHTy
  ,AOX,=:00 77.Kl`~
  6,XTS,6
  :,U1A,*+1
  ,XTA,=1HR.~TEHiE
  :6,ATX,6.pO YMOl~AHi`
  ,AEX,=1HW
  :13,VTM,0
  ,UZA,*+2
  ,XTA,=200000.BIT17=~TEHiE
  13,VTM,40000B
  :15,AOX,
  ,*50,131B.zAKAz TOMA
  ,AEX,
  ,UZA,BUSY.YCpE{HO
  ,AEX,=1
  12,VTM,6.HET TOMA
  ,UZA,RESTORE
  ,AEX,=2.3
  ,UZA,ERRNUM.zAHqT MAT.N
  ,AEX,=4.7
  12,VTM,7.MHOgO lEHT
  ,UZA,RESTORE
  ,AEX,=5.2
  ,U1A,SYNTAXER.HEpOHqTHO ~TO
  12,VTM,8.lEHTA zAHqTA
  14,VZM,RESTORE
  ,ITA,14
  ,ASN,64-12
  ,AEX,KYC
  ,UZA,BUSY.YvE zAKAzAHA
C
C      CMEHA MAT.HOMEPA lEHTy
C
  ,AEX,KYC
  ,*50,105B
  ,AAX,=177777
  ,U1A,ERRNUM.MAT.HOM.zAHqT
  ,ITA,13
  ,ASN,64+5.15 -> 10
  ,ITS,14
  15,AOX,
  ,AOX,KYC
  ,ASN,64-24
  ,AOX,=:37 00 00 00 77
  ,ATX,SYM
  ,*72,SYM
C
C      pOdTBEPvdEHiE pAPOlq
C
  BUSY:,BSS,
  ,XTA,NAM
  ,*50,127B
  ,AEX,
  ,UZA,NOPASS.HET pAPOlEj
  12,VTM,9
  ,AAX,=1
  ,U1A,RESTORE
  13,VZM,RESTORE
  NOPASS:,XTA,NAM
  ,*50,121B
C
C      zAdAHiE CdBigA pO TOMY
C
  13,VJM,OPROS.TYPA
  ,ASN,64+5
  ,ATI,14
  ,XTA,SHIF
  14,V1M,*+2.lEHTA
  :11,VZM,*+1.diCK 7,25Mgb
  ,ASN,64+5
  :,AOX,KYC
  ,*50,112B
  ,ITA,11
  ,UJ,TOTIP1
C
C      ByqCHEHiE TipA zAKAzyBAEMOgO TOMA
C
  OPROS:,BSS,
  ,XTA,KYC
  ,*50,156B
  ,ASN,64+6
  ,ATI,10.~iClO |KCT-B
  ,YTA,
  ,ASN,64+42
  ,ATI,11.TYP Y-BA
  13,UJ,
C
  TOTIP:,BSS,
  13,VJM,OPROS
  11,UTM,-77B
  11,VZM,NOBUSY.HE zAKAzAH
  11,UTM,77B
  ,XTA,=1HW
  6,ATX,6.BCEgdA HA zApiCx
  TOTIP1:,BSS,
  ,ASN,64+5
  ,UZA,DIS.TOM HA diCKE
  ,XTA,=I1.Tip - lEHTA
  6,ATX,7
  11,UTM,-42B
  11,V1M,TT1.lEHTA b|CM
  ,XTA,=I4.lEHTA EC
  ,UJ,TT2
  DIS:,XTA,=I0
  6,ATX,7
  TT1:,XTA,=I1
  TT2:6,ATX,.pOd MAT.HOM.
  ,ITA,10
  ,ASN,64-5
  ,U1A,ISV1
  6,XTA,7
  ,AEX,=1
  ,ASN,64-9
  ,ARX,=1000
  ISV1:,ATX,SHIF.CTAHdAPT
  12,VTM,0
  ,XTA,SYM
  ,U1A,ISV2.zAdAH ObxEM
  6,XTA,2
  ,AAX,=177777
  ,UZA,ISVOL
  ,ATX,SYM
  ISV2:,NTR,3
  ,A-X,SHIF
  ,UZA,ISVOL
  ,UTC,SYM-SHIF
  ISVOL:,XTA,SHIF
  ,AOX,=I0
  6,ATX,2
  ,UJ,RESTORE
C
C      BOCCTAHOBlEHiE PEgiCTPOB
C
  RESTORE:,BSS,
  6,MTJ,8
  ,STI,
  ,STI,13
  ,STI,5
  ,STI,6
  ,ATI,7
  ,NTR,6
  12,V1M,ERROR*
  ,UTC,=I0
  ,XTA,
  13,UJ,
C
C      pOdgOTOBKA COOb}EHij
C
  ERRNUM:12,VTM,2
  ,UJ,RESTORE
  NOBUSY:12,VTM,3
  ,UJ,RESTORE
  ERROR*:,BSS,
  12,MTJ,14
  14,J+M,14
  12,J+M,14
  14,UTM,DIAGN-3
  14,XTA,
  8,ATX,3
  14,XTA,1
  8,ATX,4
  14,XTA,2
  8,ATX,5
  ,ITA,12
  ,UTC,=I0
  ,AOX,
  13,UJ,
C
  DIAGN:,BSS,
  1,ISO,18HCiHTAKCi~.O{ibKA ''
  2,ISO,18HO{.HOMEP YCTP-BA ''
  3,ISO,18HHE bylO zAKAzA   ''
  4,ISO,18HpPEBy{EH ObxEM   ''
  5,ISO,18HzApiCx zApPE}EHA ''
  6,ISO,18HHET lEHTy        ''
  7,ISO,18HMHOgO lEHT       ''
  8,ISO,18HzAHqTA lEHTA     ''
  9,ISO,18HzApPE}EH dOCTYp  ''
C
C      ObPA}EHiE K CKAHEPY
C
  SCANER*:,BSS,
  14,VTM,=I0
  ,ITS,14
  :14,VTM,SYM
  ,ITS,14
  14,VTM,IREG
  ,ITS,14
  14,VTM,ITYP
  ,ITS,14
  14,VTM,IKOD
  ,ITS,14
  ,ITS,.IPOZ
  ,UJ,SCANER
C
C      ObMEH C TOMOM
C
  JFEX1:,ENTRY,
  ,STI,8.FDB
  ,STI,12.LIST
  ,STI,10.NZON
  ,ATI,11.T
  9,BASE,BAS
  10,XTA,
  ,AAX,=1777
  ,ATX,KYC
  11,XTA,
  ,AEX,=1HW
  :,UZA,*+2
  ,XTA,=:001
  ,AOX,KYC
  ,ATX,KYC
  :,ITA,12
  ,ASN,64-20
  ,AOX,KYC
  ,UJ,EXCH
C
  JFEXCH:,ENTRY,
  ,ATI,8
  9,BASE,BAS
  8,XTA,7
  ,AAX,=10 3700 0000 1777
  EXCH:,ATX,KYC
  8,XTA,
  ,AEX,=I1
  ,U1A,ERRCALL.pOKA TYP=1
  8,XTA,1
  ,ASN,64-12
  ,AOX,KYC
  ,ATX,KYC
  ,AAX,=:001
  8,AEX,6
  ,AEX,=1HR
  12,VTM,5
  ,UZA,ERROR*.zApP.zApiCx
  ,XTA,KYC
  ,AAX,=1777
  ,AEX,=1777
  8,ARX,2
  ,AAX,=2000
  12,VTM,4
  ,UZA,ERROR*.~YvOj TPEK
  ,*70,KYC
  ,XTA,=I0
  13,UJ,
  ERRCALL:12,VTM,3
  ,UJ,ERROR*.HE YME` pO-dPYgOMY
C
C
C
  SYM   :,BSS,1
  TYP   :,BSS,1
  KYC   :,BSS,1
  NAM   :,BSS,1
  SHIF  :,BSS,1
  MASK  :,BSS,1
  ITYP  :,BSS,1
  IREG  :,BSS,1
  IKOD  :,BSS,1
  ,END,
  TASK41:,NAME,.gOlOBA APXiBAPiYCA
C
C *******  A.p.CApOvHiKOB
C          E.d.fEd`HxKiH
C
C    gOlOBHAq pPOgPAMMA APXiBAPiYCA
C       ( PACpPEdElEHiE pAMqTi )
C        YKOPO~EHA dlq diCpAKA.
C
  *PROTO*:LC,BLOCK,AP,NUNIT,NL,NF,KAP(40)
  *CATAR * :,PC,1024
  *CATFIL* :,PC,1024
  *HEADER* :,PC,1024
  *BUFFER* :,PC,1024
 NOLISFL*:,LC,1
  APXAP :,SUBP,
  DIATEX:,SUBP,
C
C---
C
  B:,BSS,
  ARCHIV:,ENTRY,
  A:12,VTM,PROTO
 ,XTA,NOLISFL*
 ,UZA,F
 12,VTM,
 F:,BSS,
  ,ITA,15
  ,UTC,RET
  ,ATX,1
  ,ITA,13
  ,UTC,RET
  ,ATX,
  ,ITS,12
  ,CALL,DIATEX
  C:,CALL,APXAP
  ,WTC,RET+1
  15,VTM,
  ,XTS,
  ,CALL,DIATEX
  ,WTC,RET
  ,UJ,
  ARCHI:,ENTRY,.FOR CARD
  ,UTC,=I4
  ,XTA,
  ,UTC,NUNIT
  ,ATX,
  ,UJ,A
  PROTO:,INT,2.biliCT
   RET:,BSS,2
C
C   FUNCTION MAKRIW,MAKWIW(MAC,ZON)
C   pOlY~EHiE IW70 dlq ~TEHiq, zApiCi
C   MAC - CTPAHi~Hyj MACCiB
C   ZON - HOMEP zOHy
C
  MAKRIW:,ENTRY,
  ,UTC,=:001
  MAKWIW:,ENTRY,
  14,VTM,
  8,BASE,B
  ,STI,11
  ,ASN,64-20
  14,AOX,
  11,XTS,
  ,AAX,=7777
  15,AOX,
  13,UJ,
C
C   FUNCTION KLETKA(LIS,N)
C   pOlY~EHiE COdEPviMOgO N-j
C   12-biTHOj KlETKi B MACCiBE LIS
C
  KLETKA:,ENTRY,
  8,BASE,B
  ,ATI,14
  14,XTA,
  ,ASN,64+2
  15,ARX,
  ,ATI,12
  14,XTA,
  ,AAX,=3
  ,ATI,14
  12,XTA,
  :14,VZM,*+2
  14,UTM,-1
  ,ASN,64-12
  ,UJ,*-1
  :,ASN,64+36
  13,UZA,.pYCTO
  ,AOX,=I0
  13,UJ,
C
C    FUNCTION LETBI(STRING)
C    pOlY~EHiE biTOBOj {KAly iz
C    TEKCTOBOj CTPOKi STRING :
C    B47 - "A", B46 - "B"   i T.d.
C
  LETBI:,ENTRY,
  8,BASE,B
  ,ATI,14
  ,XTA,
  15,ATX,2
  LB1:14,XTA,
  12,VTM,-5
  LB2:,ASN,64-8
  15,ATX,3
  ,YTA,
  ,ATI,11
  ,ASN,64+6
  ,UZA,LB3
  ,ITA,11
  ,AAX,=40
  :,UZA,*+1
  11,UTM,-40B
  :,XTA,=:4
  11,ASN,
  15,AOX,2
  15,ATX,2
  15,XTA,3
  12,VLM,LB2
  14,VLM,LB1
  LB3:15,XTA,2
  13,UJ,
C
C    FUNCTION ICOS ( ISO,COSY )
C    pEPEBOd TEKCTOBOj CTPOKi
C    iz  ISO  B  COSY.
C    PEzYlxTAT - ~iClO ClOB COSY
C
  ICOS:,ENTRY,
  8,BASE,B
  ,STI,11
  ,ATI,10
  13,MTJ,9
  10,MTJ,12
  14,VTM,-13
  SZ:12,XTA,
  ,ARX,=6H'177''177''177''177''177''177'
  ,AAX,=6H'200''200''200''200''200''200'
  ,AEX,=6H'200''200''200''200''200''200'
  ,U1A,ZIS
  12,UTM,1
  14,VLM,SZ
  ,UJ,TOTRAN
  ZIS:,ASN,64+2
  12,AEX,
  12,ATX,
  ,XTA,=6H
  :12,UTM,1
  12,ATX,
  14,VLM,*-1
  TOTRAN:,ITS,10
  ,ITS,11
  10,VTM,
  ,CALL,ISOTCOSY
  SLF:11,XTA,
  ,AEX,=12
  ,AAX,=377
  10,UTM,1
  11,UTM,1
  ,U1A,SLF
  ,ITA,10
  ,AOX,=I0
  9,UJ,
C
C     SUBROUTINE ECEXTR(LUN,JOB)
C     pOziciOHiPOBAHiE lEHTy EC
C
  ECEXTR:,ENTRY,
  8,BASE,B
  ,STI,14
  ,STI,12
  14,XTA,.JOB
  ,ASN,64-24
  12,AOX,.LUN
  ,ASN,64-12
  ,*57,7777B
  13,UJ,
C
C     SUBROUTINE  ADDCS(MAC,ICS)
C     pOdC~ET KOHTP. CYMMy MACCiBA MAC(1024)
C     HA~. zHA~EHiE i PEzYlxTAT - B ICS
C
 ADDCS:,ENTRY,
 ,STI,11.ICS
 ,STI,12.MAC
 14,VTM,-1023
 11,XTA,
 LCS:14,UTC,1023
 12,ARX,
 14,VLM,LCS
 11,ATX,
 13,UJ,
  ,DATA,
  VOLU:,INT,16
  1,SET,VOLU
  1,,NL
  ,END,

 TRAM70  :  ,NAME,
C---------
C
C       MAЙ  1983.  H.C.ГOHЧAPOB.
C
C   (И ДO HEГO MHOГИE XOTEЛИ ПEPEПИCATЬ
C    ЗAДAЧY OБMEHA, HO HИKTO HE PEШИЛCЯ).
C
C--------
C
 *ИПД:LC,BLOCK,(16)
  ,CONT,XLTF,LENTDIS
 ,CONT,(1),Ш/ДИCП,(22)
  ,CONT,OUTJOBSC
  ИПЗ/ECTЬ:,EQU,*ИПД+42B
  MTRACNT:,EQU,*ИПД+27B
  LQTRA:,WEQ,MTRACNT.ДЛИHA WL
 LTF:,WEQ,XLTF.ДЛИHA FATA
C---------
  CHEKMDFL:,LC,1.БЛOKИP.OШ.MБ
C---------
 SELECT:,SUBP,.
 COБЫTИE:,SUBP,
 CLANEWTA:,SUBP,.
 TTPRINT :  ,SUBP,
 ERRPRI  :  ,SUBP,
  OШMAШИHЫ:,EQU,ERRPRI
 OUTCW:,LC,1.
 OUTJOB:,SUBP,.
 ABCHT:,SUBP,
C
  TIMERS:LC,BLOCK,TMБ(2),TMД(2),MTTIMCNT(4)
  *TAKCA:LC,BLOCK,(1),DISTIME,TAPTIME
C
 *677:B,BLOCK,BITS,BIT48,BIT47,BIT46,BIT45,BIT44,BIT43,BIT42,BIT41,
       , CONT,     BIT40,BIT39,BIT38,BIT37,BIT36,BIT35,BIT34,BIT33,
       , CONT,     BIT32,BIT31,BIT30,BIT29,BIT28,BIT27,BIT26,BIT25,
       , CONT,     BIT24,BIT23,BIT22,BIT21,BIT20,BIT19,BIT18,BIT17,
       , CONT,     BIT16,BIT15,BIT14,BIT13,BIT12,BIT11,BIT10,BIT9 ,
       , CONT,     BIT8 ,BIT7 ,BIT6 ,BIT5 ,BIT4 ,BIT3 ,BIT2 ,BIT1
 BITTRA:,EQU,BIT27.
 PEBEPC:,EQU,BIT24.
 HAЛOЖ:,EQU,BIT23.
 CЛYЖCЛOB:,EQU,BIT21.
 ЛИCTOБM:,EQU,BIT19.
 ЧTEHИE:,EQU,BIT18.
 ПOДBOД:,EQU,BIT16.
 BITПOДB:,EQU,BIT6.
 BITИMЯ:,EQU,BIT5.
  MAXTIMET:,EQU,BIT8.TAЙMEP MЛ
  CREADIS:,EQU,BIT5.B T1MT
  CONTROL:,EQU,BIT4.B T1MT
 *C :,LC,
 *CB:,LC,
  *TLCONST:,LC,
  MASK12:,LC,3
 FULWOR  :  ,LC,1
 *YCЫ:LC,BLOCK,ГYC,RDSCAL,COMMIR
 ДHЗ:LC,BLOCK,(1),BITJ,ZBITJ
C
  EXCHJOB:,LC,2.KЛИEHTЫ SWING-A
C
 JNREC:,EQU,27B.OПOЗHABAHИE
 SMASAV:LC,BLOCK,
            ,CONT,      SAVAC
            ,CONT,      SAVSR
 ,CONT,(1)
            ,CONT,      SAVI15
C
C<<<<<<<<<<<<<<< И П З >>>>>>>>>>>>>>>>
C
 ST/EXCH:,EQU,-2
 *0:,BLOCK,(3),S33(2),S27(3),I14(16)
    ,CONT ,TMATH(0),TПPИП(8),TЗAЩ(4),(28)
    ,CONT ,(6),COPYS33(2),BISTIME(2),SCEX(3)
    ,CONT ,SYSMD(10),MЛД,PЛД
  REGSCAL:,EQU,100B.ФЛAГИ
  CWPLAC:,EQU,150B.MECTO CЛ.CЛOB
C
C<<<<<<< OБЬEKTЫ ЗAДAЧИ OБMEHA >>>>>>>>
C
 PUT16:,SUBP,
 TYPEMT:,SUBP,
  FIX DENS:,EQU,TYPEMT+5
 BUFTRA:LC,BLOCK,(1),IFTRA(36).
 DRIDENT:LC,BLOCK,(1),KYC
  TRA MASK:LC,BLOCK,(1),FINZON
 TRABUS  :  ,LC,1
 TRACNT  :  ,LC,1
 FULQUESC:  ,LC,1
 TPAKTCT:,EQU,541B
C
  REWINDSC:LC,BLOCK,(1),ЗAKЗAП,CONTRWR
 ,CONT,CHBOSS(4)
C..  CMЫCЛ ЯЧEEK БЛOKA REWINDSC :
C   4-7.   48P.- HYЖEH ПEPEXBAT ПPEPЫBAHИЯ
C              ПO ПPOXOДY ЗOHЫ B 3-6 HAПP.
C      1-15P.- AДPEC ПPOЦEДYPЫ ПEPEXBATA.
C  /ЗAKAЗ ПEPEXBATA БЛOKИPYET PAБOTY ЗAДAЧИ
C  OБMEHA CO BCEM   Y B Y  - HAПPABЛEHИEM/.
C
C   1.  '1' B 48-17 PAЗPЯДAX ECTЬ
C     ПPИЗH. HECMOTKИ HA HAЧAЛO MЛ 30-67.
C
C   2.  '1' B 48-1 P. - ПPИЗH. PAЗPEШEHИЯ
C     ЗAПИCИ HA MЛ/MOДEЛЬ  10-67.
C
C   3.   '1' B 48-1 P. - HYЖEH KOHTPOЛЬ
C     COCTOЯHИЯ ЗAПИCИ HA MЛ/MOДEЛИ 10-67.
C
C-----------------------------------------
 IW70    :  ,LC,1
 CWJFOTRA:  ,LC,1.YC ЗAKAЗЧИKA
 TPHYS   :  ,LC,1
 RETTRA:LC,BLOCK,W04265.ПEPEXBAT "KЧ"
 TASKSTRA:LC,BLOCK,.
  ,CONT,TBUC(6),TBUT(6)
 ,CONT,CSUM(0),CSUMSTAT(6)
 SCALSTRA:LC,BLOCK,.
  ,CONT,SBUC(6),SRJC(6)
  ,CONT,SJMT(32)
  ,CONT,SJMD(16),SCADDS
  ,CONT,БYPЖYЙ
  ,CONT,SCSWING.ШK.OБMEHOB ПOДKAЧKИ
C
C---   OЧEPEДЬ ЗAKAЗOB HA OБMEH:
C
 LISTSTRA:LC,BLOCK,WL1.KYC-Ы
  WL2:,EQU,WL1+LQTRA.KOHTP.CYMMЫ
C
  TABLEMT:LC,BLOCK,T1MOD(16),T1MT(32)
  ,CONT,T2MOD(16),T2MT(32)
  ,CONT,TNMOD(16),TANAMEMT(32)
C
C       TANAMEMT - ИMEHA ЛEHT
C               T1MT :
C    48-43 : ДHЗ BЛAДEЛЬЦA ИЛИ 0
C    42-37 : MATEMATИЧECKИЙ HOMEP
C    36-31 : БИTЫ OШИБOK (CЛEBA HAПPABO:
C            OШM,OШ/ЗП,OШ/CC,KO/ЗП,KC,CИ)
C    30-26 : CЧETЧИK ЧИCЛA OШИБOK TEK.OБMEHA
C    24-13 : CMEЩEHИE ПO ЛEHTE (*TAPES)
C     12   : CБPOC ДBИЖ.ПO TAЙMEPY (KДB)
C    11-10 : ПЛOTHOCTЬ ЗAПИCИ
C      8   : БЫЛ ПPИKAЗ "KO"
C      5   : ИДET KOHTPOЛЬHOE ЧTEHИE
C      4   : ЗAKAЗAH PEЖИM ЗAПИCИ C KOHTPOЛEM
C     3-2  : CTATYC ЛEHTЫ
C      1   : ПPИЗHAK ПEPBOГO OБPAЩEHИЯ
C                T2MT :
C     48   : PAБOTAEM C ДYБЛEM ЗOHЫ
C     44   : OШИБKA B OCHOBHOЙ ЗOHE
C    43-42 : CЧETЧИK HAЛOЖEHИЙ ПPИ ЧTEHИИ
C    41-25 : PEГИCTP COCTOЯHИЯ ДЛЯ MЛ EC
C    24-13 : TPEБYEMAЯ ЗOHA (ЧETHAЯ!)
C    12-1  : TEKYЩAЯ ЗOHA
C
 LTDIS:,WEQ,LENTDIS.ДЛИHA TAБЛ.ЭKCTEHTOB
 LTD:,EQU,LTDIS-1.
 LTD*3:,P*P,(LTD)3
 *TDIS:,LC,LTDIS.TAБЛИЦA
 TDIS:,EQU,*TDIS.ЭKCTEHTOB
  *DISCS:LC,BLOCK,(32),ШMOД,ШФЛД
  FATA:,LC,1.TAБЛИЦA ФAЙЛOB
  TSF:,EQU,425B.TAБЛ.SCR-OБЛ. (8 CЛOB)
  LTS:,EQU,7.EE ДЛИHA-1
C
 *KOHCTPA:LC,BLOCK,KOHTPCTP.KOHTP.ЗAП.HA MБ
 SCDICH:,SUBP,
 DISC:,SUBP,.
  DAFSET:,SUBP,.
 RECMDT:,SUBP,.KAHAЛ OПOЗH.
 CHVOP:,SUBP,
 CHPOP:,SUBP,
 PS*VOP:,SUBP,
 PS*POP:,SUBP,
 DISREG  :  ,LC,1
 *SWCMN:LC,BLOCK,(10),SWINGIF(25)
 EXCHSC:,EQU,*SWCMN+3
 *STATBUF:LC,BLOCK,(14),MДMЛMБ
C
C
C...    BXOДЫ OT MACRO70 И TRAPHY:
C
 ADREQ40:,UJ,MACSTART
 ,Z00,PHYREQ40
C
C
  HA COБ:8,,
  ,Z00,HE COБ
 STD:2,Z01,БYCT.
 ,Z02,.
  ST/T:2,Z02,БYCT.
 ,Z02,AFT ST.
 MOШMБ:,LOG,10 00100 010 00100.
 STATIST:,SUBP,.
C
C...  HOMEPA ДИAГHOCTИK  ERRMACRO :
C
  *12:,BLOCK,AOШM(6),ЧYЖAЯMЛ,ЗAKPAБMД
  ,CONT,ЧYЖTPEK,ЗAПPETЗП,ДHEГOT,MЛKAЮK
 A*TOШMБ:,,
 ,Z00,T OШMБ.
 T ЗПMБ:,ISO,6HЗП MБ:
 TEXER0:,BSS,
 ,ISO,6HMБ KO:
 ,ISO,6HMБOШM:
 ,ISO,6HKЧ KC:
 ,ISO,6HЗПOШM:
 TOШMБ:,BSS,1
 БYCT:,BSS,.
 ЖEAД:,BSS,1
 ШИФP:,BSS,
 ПOAД:,BSS,2
 TOШMЛ:,BSS,.
 ,ISO,12HЧYЖAЯ MЛ - '1'
 ,BSS,1
 THEOПO:,BSS,.
 ,ISO,12HHE OПOЗH.MЛ-
  ,BSS,1
 ,ISO,12HHEГOTOB.MФ-'1'
  HOMФ:,BSS,1
  ,ISO,12HHET ЗAП.MФ-'1'
  ,BSS,1
 TЦKMФ:,BSS,3.
 ,LOG,
 TMБCДOX:,BSS,.
 ,ISO,6HЧП MБ.
 ,BSS,1.
  MTDIAGN:,BSS,
  ,ISO,6HOШ/ЧT
  ,ISO,6HOШ/ЗП
  ,ISO,6HOШ/CC
  ,ISO,6HKO/ЗП
  ,ISO,6HKC MЛ
  ,ISO,6HCБ/CИ
 ,ISO,6HKДB MФ
  HEYГACИM:,ISO,18HHET CБPOCA CДB-6'41'''
  TБЯKA   :,ISO,6HДB-0'41'''
  CTMБ:,LC,2
  PERMIR:,SUBP,
 FWCR:,LOG,.ФЛAГ OП.KAH.
  C200*:,EQU,*TLCONST+11B
  B48/43:,EQU,*TLCONST+22B
 B48/13:,EQU,*CB+4
 B41/1 :,LOG,37 7777 7777 7777
 B39/25:,OCT,0007 7777
 B37/31:,OCT,0001 77
  B36/31:,EQU,*CB+3
 B30/16:,LC ,1
 B30*12*5:,LOG,40 0000 4020
 B29/26:,OCT,0000 0036
 B24/14:,LOG,7776 0000
  B24/13:,EQU,MASK12+2
 B24/1 :,LC ,1
 B22*18:,LOG,1040 0000
 B21/19:,LOG,700 0000
 B21/18:,LOG,740 0000
 B21*18:,LOG,440 0000
 B20/19:,LOG,300 0000
 B19*12:,LOG,100 4000
 B16/1 :,LC ,1
 B11/10:,LOG,3000
 C1    :,EQU,BIT1
 C2    :,EQU,BIT2
 C3    :,LC ,1
 C6    :,EQU,*C
 C7    :,LC ,1
 C13   :,LC ,1
 C17   :,EQU,*CB+1
 C37   :,LC ,1
 C77   :,LC ,1
 C377  :,LC ,1
 C1400 :,LC ,1
 C2003 :,LC ,1
 C3777 :,LC ,1
 C7777 :,LC ,1
 C77774:,LOG,77774
 ZBIT13:,OCT,7777 7777 7776 7777
 ZBIT12:,OCT,7777 7777 7777 3777
  ZBIT1:,EQU,*TLCONST+20B
  ZB7*6:,OCT,7777 7777 7777 7637
 ZB37*23:,OCT,7776 7777 5777 7777
 Z21*18*1:,OCT,7777 7777 7337 7776
 ZC7   :,OCT,7777 7777 7777 7770
 BY1/2 :,GOST,6H770000
 BY1/5 :,GOST,6H777770
 BY5/6 :,GOST,6H000077
 Z12   :,ISO ,6H00''''''''
 Z12Ш  :,ISO ,6H00-Ш00
        ,BSS,.MERDE
C
C<<<< KOHCTAHTЫ БЛOKA OБMEHA C MЛ >>>>>
C
 KPYГ  :,LOG,11
 C/ПEP :,OCT,0000 06
 KYCMAC:,OCT,7777 7777 7477 1600
 MASK5017:,LOG,114
 TKYC17  :,GOST,6H000'20''40''101'
 CW0C  :,OCT,2245 1252 5122 4512
 CW36C :,OCT,0007 0707
 CW4C  :,OCT,7163 4760 3471 6347
 LWC   :,OCT,6666 6666 6666 6666
 MASWL :,OCT,7777 7777 5777 7660
 MAST1MT:,OCT,7777 0000 7777 3037
 MAST2MT:,OCT,1577 7777 7777 7777
 TNZON  :,ISO,6H/0000/
        ,BSS,.MERDE
C
C<<< PAБ. ЯЧEЙKИ БЛOKA OБMEHA C MЛ >>>>
C
 CH     :,BSS,1
 IDMT   :,BSS,1
 KYCДB  :,BSS,1
 NZON   :,BSS,1
 DENS   :,BSS,1
 PEFL   :,BSS,1
 CHSUMR :,BSS,1
 TИПOШ  :,BSS,1
 OFFKYC :,BSS,1
 CЧETП  :,BSS,1
 ПPOBMФ :,EQU,TRAMASK
 EXCHFL :,EQU,KYCДB
C
C<<<<< ЭKB-CTИ БЛOKA OБMEHA C MЛ >>>>>>
C
 *1:,BLOCK,OШ/ЧT,OШ/ЗП,OШ/CC,KO/ЗП
    ,CONT ,KC/MЛ,CБ/CИ,KДB/MФ
 TPDI:,EQU,6
 BITDI:,EQU,BIT8
C
C<<<<< PEГИCTPЫ БЛOKA OБMEHA C MЛ >>>>>
C
 *1 :,BLOCK,(1),NF,MF,ER
 *6 :,BLOCK,(1),RF,( 2 ) ,DC
 *11:,BLOCK,CW ,N ,I
C
C<<<<<<<<<                    >>>>>>>>>
C
 B12/11  :  ,LOG, 00000000 00006000
 BITNAM:,LOG,400 0020
 BITMOV:,LOG,400 0040
 RBITSM:,OCT,1474 0000 004
 MDRADM:,LOG,37 0037
 KDRADM:,LOG,30 0000 0000 1777
 DIDM:,OCT,7700 0000 0077 3740
 SCR*D:,OCT,5750
 SCR*P:,OCT,37 37 77
 KUNITM:,LOG,70 0000 0000 1600
 MS/70:,OCT,4000 0003 0000 03
 MS/KYC:,LOG,100 6003
 MMNTPL:,OCT,0007 7007 7
 KPMASK:,LOG,1 4037 0000
 PПMASK:,LOG,420 0000 0037
 TMPMASK:,LOG, 1 0400 0037
 ,LOG, 2 1000 1740
 ,LOG, 4 2007 6000
 ,LOG,10 4370 0000
 BC1700:,ENTRY,
 BC1700:,BSS,1.CBOБOДHA !
 ,LOG,10.
 ,LOG,20.
 ,LOG,30.
 ,LOG,40.
 ,LOG,50.
 ,LOG,60.
 TSCRFI:,BSS,7.PAБ. ПOЛE ЗAД. OБMEHA
 KYC39:,BSS,1
 KYC40:,BSS,1
  COMPUT16:LC,BLOCK,NH,N8,BN8
  PRTMTCH:,OCT,754
 NOCHECK:,TEXT,8HNO CHECK
 ДHЗПOДK:,LOG,24.
 CON1:,LOG,37 373.
 CON6:,LOG,7777 7776 3740 7777
 CON7:,LOG,7776 0001 4037 1774.
C
 STATUSMT:LC,BLOCK,(1),OLD(4),NEW(4)
 ,CONT,W05741(4)
 ,CONT,SOST,(1),ДBECA(4),SCANER
 ,CONT,ШKEC(4),SCMT57(4)
 ,CONT,ШHГ(4)
  ,CONT,MISTERX
C
 BY4/5:,GOST,6H000770
 KAДMБ:,ISO,6H00000.
 KШИ:,ISO,6H'1''1'Ш00''
 UPM 1:,GOST,6H373730
 B25*16:,LOG,1 001 00000
  C/MГЗ:,EQU,BIT36
  COUNT:,BSS,4
 SAVI11:,BSS,.
 ,BSS,1
 Ш ЧYMЛ:,BSS,1
 R1:,BSS,1
 ЖДYKOHTP:,BSS,1
 ЗAHKOHTP:,BSS,1
 OFFESC:,BSS,1.
 QUEONLY:,BSS,1.ФЛAГ BXOДA OT SWING
C
 DISPBASE:LC,BLOCK,(2),*DR2122(2)
  ,CONT,DISPMD(2)
  ,CONT,DSHIFT
C
 YЧET:,ISO,12HYЧET ЗAKPЫT'0'
 BITWP:,EQU,BIT30
 BITPAG:,EQU,BIT19
 BITRD:,EQU,BIT18
 BDSEC:,EQU,BIT7
 C50:,EQU,BC1700+5
 NRB:,EQU,7
 NRBS:,EQU,48-NRB
 TSCRFI:I,BLOCK,SECT(0),WLSUM.DTASKON
 ,CONT,KSJN(0),FSUM.S48TO12
 ,CONT,DISAD(0),RSUM.S48TO12
 ,CONT,IDENT(0),RRR.PRIPTRA
  ,CONT,DID
 ,CONT,KPAGE(0),MPAGE.OPENPAG
 ,CONT,RBITS(0),WS
C
C
 *1 :,BLOCK,U ,CT,NR,MV,R
 *6 :,BLOCK,PD,TP,JN,E ,F
 *6 :,BLOCK,RO,RG(2),RT
 *11:,BLOCK,P ,W ,J ,M ,S
 *11:,BLOCK,L(2) ,IF,B ,RD
 *14:,BLOCK,         C
C
C
C******   BXOД B ЗAДAЧY OT ЭK70
C
C   ------------------------------
C   PAЗPЯДЫ  I W 7 0   ДЛЯ  MБ :
C   ------------------------------
C     1-5  - HOMEP TPAKTA
C     7-8  - HOMEP CEKTOPA
C     ( ПPИ '1' B 36-M PAЗPЯДE IW70
C      HOMEP CEKTOPA ЗAДAETCЯ B 1-2 P.
C      A HOMEP TPAKTA - B 3-7 P. )
C     13-17  - HOMEP MБ
C     25-26  - HOMEP AБЗAЦA
C     31-35  - HOMEP ЛИCTA
C       39    - ФИЗИЧECKИЙ OБMEH
C       40    - ЧTEHИE
C       48    - CEKTOPHЫЙ OБMEH
C
C
C   ------------------------------
C   PAЗPЯДЫ  I W 7 0   ДЛЯ  MЛ :
C   ------------------------------
C     1-12  - HOMEP ЗOHЫ
C     13-18  - HOMEP MЛ
C     31-35  - HOMEP ЛИCTA
C       39    - ФИЗ. OБMEH
C       40    - ЧTEHИE
C       41    - ПOДBOД
C       42    - ЧTEHИE ИMEHИ
C       43    - PAЗMETKA MЛ EC B PEЖ. EC
C       44    - PAЗMETKA MЛ EC B PEЖ. БЭCM-6
C       46    - ПPИЗHAK MOДEЛИ ФИЗ. MЛ
C              (B ЭTOM CЛYЧAE HOMEP MЛ -
C              - БAPAБAHHЫЙ )
C   ------------------------------
C   ------------------------------
C
C
C   PAЗPЯДЫ  KYC-A  OБMEHA C MБ :
C----------------------------------
C
C   1-2  - HOMEP CEKTOPA
C   3-7  - HOMEP TPAKTA
C   8-10 - HOMEP MБ
C  11-12 - HOMEP AБЗAЦA
C  13-17 - HOMEP ЛИCTA
C    18   - ЧTEHИE
C    19   - ЛИCTOBOЙ OБMEH
C    21   - ЧTEHИE CЛ.CЛOB
C  24-25 - HOMEP CEГMEHTA MOЗY
C    27   - БЫЛA TT-ПEЧATЬ ДИAГHOCTИKИ
C    38   - ЗAKAЗ OT TRAPHY
C    39   - ФИЗ.OБMEH OT Э70
C  40-42 - HOMEP HAПPABЛEHИЯ
C  43-48 - Д H З
C----------------------------------
C
C
C   PAЗPЯДЫ  KYC-A OБMEHA C MЛ :
C----------------------------------
C
C    1   - ПPИЗHAK HAЧAЛЬHOЙ OPИEHTAЦИИ
C    2   - БЫЛO ЧTEHИE CЛ.CЛOB
C          (COCTOЯHИE 1 И 2 PAЗPЯДOB
C           BCEГДA Д.Б. OДИHAKOBЫM )
C    3   - ЛEHTA CTOИT ПЛOXO
C          (B ПPOMEЖYTKE MEЖДY ЗOHAMИ)
C    4   - HET CMOTKИ HA HAЧAЛO
C    5   - ЧTEHИE ИMEHИ
C    6   - ПOДBOД
C    7   - БЫЛA ДИAГHOCTИKA HA TT
C   8-10 - HOMEP MФ
C    11  - ЗAKAЗ ГOTOB K BЫБPOCY
C   12  - PAБOTAEM C ДYБЛEM ЗOHЫ
C          (ДЛЯ PAЗMETKИ MЛ EC)
C  13-17 - HOMEP ЛИCTA
C    18   - ЧTEHИE
C  19-20 - ДЛЯ PAЗMETKИ MЛ EC
C          ('1'-ЗAПИCЬ MГЗ, '2'-ШЗH
C          '3'-ЗAПИCЬ ЗOHЫ, '0'-HOPM.OБMEH)
C   21   - ЧTEHИE CЛ.CЛOB
C   22   - БЛOKИP.ЧTEHИЯ CЛOB C OШИБKAMИ
C   23   - ЧTEHИE C HAЛOЖEHИEM
C  24-25 - HOMEP CEГMEHTA MOЗY
C  26-36 - HOMEP ЗOHЫ
C   37   - ПOBTOPHЫЙ OБMEH
C   38   - ЗAKAЗ OT TRAPHY
C   39   - ФИЗ.OБMEH OT Э70
C  40-42 - HOMEP HAПPABЛEHИЯ
C  43-48 - Д H З
C
C----------------------------------
C
C    BXOД OT 70 ЭKCTPAKOДA
C
C----------------------------------
 MACSTART:,BSS,
 ,XTA,
 ,ATX,KYC
 ,ATX,KPAGE.ДЛЯ ЧT.ИM.
 ,XTA,BIT48
 ,ATX,SECT
 ,XTA,CWJFOTRA
 ,AAX,B48/43
 ,ATX,KSJN. Д H З
 ,ASN,64+42.ЗAKAЗЧИKA
 ,ATI,JN
C..  XAPAKTEPHЫE БИTЫ   I W 7 0
C..  PACTAЩИM B ИHДEKC-PEГИCTPЫ :
C     CT - ФИЗ.ПAKET (CLOSE TASK)
C     NR - ЧTEHИE ИMEHИ
C     MV - ПOДBOД
C     RD - ЧTEHИE
C     PD - ФИЗ.OБMEH
C     TP - MЛ
C=================================
 ,XTA,IW70
 ,ARX,BIT16
 ,APX,RBITSM
 ,ASN,64+NRBS
 ,ATX,RBITS
 M,VTM,1-NRB
 SETREG:,BSS,
 ,XTA,RBITS
 M,AAX,BIT1
 M,ATI,NRB
 M,VLM,SETREG
 U,J+M,TP
 R,MTJ,RD
 ,XTA,IW70
 ,ASN,64+12
 ,AAX,C77
 ,ATI,U.HOMEP YCTP-BA
 ,XTA,IW70
 ,AAX,C7777
 ,ATX,DISAD.HOMEP ЗOHЫ
 R,VTM,PDIMAC
 TP,V1M,TAMAC
 R,VJM,CLOPM70
 ,XTA,IW70
 ,APX,MDRADM. 37 0037
 ,ATX,KYC
 PD,V1M,PDRMAC
C
C...   OБMEH C MAT.MБ
C
 ,ASN,64+38
 ,AEX,KSJN
 ,ATX,DRIDENT
 RG,CALL,GIVTRACK
 ,ATX,KYC.ФИЗ.AДPEC
 M,V1M,MSETSEC
 RD,VZM,MSETSEC
C
C...   ПEPBOE OБPAЩEHИE - ПO ЧTEHИЮ
C      OБHYЛЯEM CEKTOP ИЛИ ЛИCT
C      ЗAMEHИM ЧTEHИE HA ЗAПИCЬ.
C
 RD,VTM,-1777B
 ,XTA,KPAGE
 R,VJM,PRIPTRA
 ,XTA,IW70
 ,ARX,
 ,U1A,SETZ
 RD,VTM,-377B
 ,AEX,BIT48
 ,ATX,IW70
 ,ASN,64+16
 ,AAX,C1400
 ,ATI,M
 SETZ:,BSS,
 :,24,2
 ,XTA,
 M,ATX,70000B
 M,UTM,1
 :,24,3
 RD,VLM,SETZ
 MSETSEC:,BSS,
 ,XTA,IW70
 R,VTM,CHMODRU
 SETSECT:,BSS,
 ,ARX,
 R,U1A,
 ,AEX,BIT48
 ,APX,MS/70
 ,ATX,SECT
 R,UJ,
C
C----------------------
C
  TRASWI:,ENTRY,
C
C***   BXOД OT ПOДKAЧKИ ПO И10.
C      B IW70 - ПPOДYKЦИЯ GIVTRACK-A
C      И.P. 4 - ФИЗ.HOMEP ЛИCTA.
C
 ,XTA,IW70
 ,ATX,KYC
 ,ASN,64+17
 ,AAX,BIT1
 ,ATI,RD
 ,XTA,BIT48
 ,ATX,SECT
 PD,VTM,
 CT,VTM,
  ,ITA,10
  ,ATX,QUEONLY.BOЗBPAT
 ,XTA,CWJFOTRA
 ,AAX,B48/43
 ,ATX,KSJN. Д H З
 ,ITA,4
 ,ASN,64-41
 ,AUX,KPMASK
 ,ATX,KPAGE.ФИЗ.ЛИCT
 4,XTA,TPHYS
 ,APX,MMNTPL
 ,ASN,64+36
 ,ATI,P.MAT.ЛИCT
 R,VJM,CLOPSWI
C
C...   ПPOБA: "TPAKT MБ" BЫДEЛEH
C      B SCRATCH - OБЛACTИ
C
 CHMODRU:,BSS,
 ,XTA,KYC
 ,ARX,
 ,U1A,PHDRUM .-HET
 ,ASN,64+2
 ,ATX,DISAD.HOMEP "TPAKTA"
 ,XTA,KYC
 ,APX,KUNITM
 ,ASN,64+42
 ,ATI,B.HOMEP ЭKCTEHTA
 W,VTM,-LTS
 ,UJ ,SASEAR
C
C...   OБMEH C ФИЗИЧECKИM MБ
C
 PDRMAC:,BSS,
 ,XTA,IW70
 R,VJM,SETSECT
C...   ПPOБA ПEPEAДPECAЦИИ MБ-MД
 R,VJM,CHMODSY
C...   ПPOБA ПOДMEHЫ MБ 21-22 HA MБ
 U,UTM,-21B
 ,ITA,U
 ,ASN,64+1
 ,U1A,NOSUBS
 ,XTA,IW70
 ,AAX,C37
 U,AOX,*DR2122
 ,APX,MDRADM
 ,ATX,KYC
 NOSUBS:,BSS,
 ,XTA,KYC
 ,AUX,KDRADM
 ,ATX,KYC
C
C...   ПOCTAHOBKA MБ-OБMEHA B OЧEPEДЬ
C
 PHDRUM:,BSS,
 ,XTA,SECT
 ,AUX,MS/KYC
 :RD,VZM,*+1
 ,AOX,B22*18
 ,AOX,KYC
 RT,VJM,DTASKON
 ,XTA,BIT1
 ,ARX,MДMЛMБ.CЧETЧИK
 ,ATX,MДMЛMБ.MБ-OБMEHOB
 L,XTA,BIT48
 C,AOX,SRJC-1
 C,ATX,SRJC-1
C
C...   ECЛИ MБ-KAHAЛ CBOБOДEH
C      И ЗAKAЗ HE OT ПOДKAЧKИ
C      TO CPAЗY И BЫПOЛHИM.
C
 ,ACX,
 ,ASN,64+1
 ,AOX,QUEONLY
 RT,VTM,ENDMAC70
 L,UTM,1
 L,MTJ,N
 ,UZA,CONTEXCH
 RT,UJ ,
C
C...   Э070 ЛEHTOЧHOГO TИПA
C
 TAMAC:,BSS,
 CT,V1M,CLOPM70.ФИЗ.ДИCK
 ,XTA,IW70.ПOДГOTOBKA
 ,AAX,B24/1.ИДEHTИФИKATOPOB
 ,AOX,KSJN.ДЛЯ ПOИCKA
 ,APX,DIDM.ПO TAБЛИЦAM
 ,ATX,IDENT.TIMT,FATA
 ,XTA,RBITS
 ,ASN,64+2
 ,AAX,C7
 ,ATI,RD.R.OR.MV.OR.NR
 ,ASN,64+1
 E,VTM,ЧYЖAЯ MЛ
 ,U1A,CHPHYTA.MV.OR.NR
 R,VTM,CHPHYTA
 PD,V1M,CLOPM70
 IF,XTA,PЛД.CTATYC
 U,AAX,BIT48-30B."C ЗAKPЫTИEM
 ,UZA,CLOPM70.ЗAДAЧИ"
 CT,VTM,1. - ДA
C
C...  PAЗБOP: ЗAKPЫTЬ ЗAДAЧY
C     ИЛИ TOЛЬKO EE MAT.ЛИCT
C
 CLOPM70:,BSS,
 ,XTA,IW70
 ,ASN,64+30
 ,AAX,C37
 ,ATI,P.HOMEP ЛИCTA
 IF,UTC,
 P,XTA,TMATH
 ,ASN,64-11
 ,AUX,KPMASK
 ,UZA,BADEXCH
 ,ATX,KPAGE.ФИЗ.ЛИCT
 R,UTC,.ПPOЙДEM
 CT,V1M,.ЧEPEЗ "CLOTA"
C
C...   ПOДПPOГPAMMA ЗAKPЫTИЯ
C      MAT.ЛИCTA ПO ПPИПИCKE
C          И ЗAЩИTE
C
 CLOSPAGE:,ENTRY,
 ,XTA,FULWOR.CHИMAEM
 P,AEX,BIT48.ПPИЗHAK
 IF,AAX,ST/EXCH."ПЛOXOЙ
 IF,ATX,ST/EXCH.OБMEH"
 CLOPSWI:,BSS,
 ,ITA,P
 ,ASN,64+5
 R,U1A,.ФOPM.ЛИCT
 ,ITA,P
 ,ASN,64+3
 ,AEX,C3
 ,ATI,W
 ,ITA,P
 ,AAX,C7
 ,AEX,C7
 IF,J+M,W
 ,ATI,B
 B,XTA,BIT28
 W,AOX,TЗAЩ
 W,ATX,TЗAЩ
 ,ITA,P
 ,ASN,64+2
 ,AEX,C7
 ,ATI,W
 ,ITA,P
 ,AAX,C3
 IF,J+M,W
 ,ATI,B
 B,XTA,TMPMASK
 ,AEX,FULWOR
 W,AAX,TПPИП
 W,ATX,TПPИП
 R,UJ,
C
C...   OПPEДEЛEHИE TИПA YCTPOЙCTBA
C      (MAT., ФИЗ., ЛEHTA, ФAЙЛ)
C
 CHPHYTA:,BSS,
 PD,V1M,PTAMAC.ФИЗ.HOMEP
 F,VTM,LTF+1
 IF,XTA,MЛД
 U,AAX,BIT48-30B
 ,U1A,FIMAC.ФAЙЛ
 W,VTM,-57B
C
C...   ПOИCK ЛOГИЧECKOГO ИДEHTИФИKATOPA
C      ( ДHЗ + MAT.HOMEP )   B T1MT
C
 MTSEAR:,BSS,
 W,XTA,T1MT+37B
 ,AEX,IDENT
  ,AAX,MASK12
 ,UZA,CHWPTA.ECTЬ ЛEHTA
 W,VLM,MTSEAR
 ,UJ ,FIMAC.A BДPYГ ФAЙЛ?
 PTAMAC:,BSS,
 ,XTA,ШФЛД
 U,AAX,BIT48-10B
 ,U1A,MODTAP
 TP,UTM,-100B.OШ.HOMEP
 TP,VZM,ERTREQ.MOДEЛИ
 ,UJ ,PHTAPE
C
C...   ПPOБA: PAЗPEШEHA ЛИ
C      ЗAПИCЬ HA ЛEHTY
C
 CHWPTA:,BSS,
 W,UTM,67B
 E,VTM,ЗAПPET ЗП
 RD,V1M,CHMODTA
 ,XTA,ЗAKЗAП
 W,AAX,BIT48-10B
 ,UZA,ERTREQ
 CHMODTA:,BSS,
 W,MTJ,U
 ,XTA,ШФЛД
 U,AAX,BIT48-10B
 ,U1A,MODTAP.ЛEHTA-MOДEЛЬ
C
C...   ФOPMИPOBAHИE KYC-A ДЛЯ ЛEHTЫ
C
 PHTAPE:,BSS,
 U,XTA,T1MT-30B
  ,ASN,64+12
  ,ARX,DISAD. + ДEЛЬTA
  ,ARX,BIT3. + 4
  ,AAX,C3777.YДBOEHИE
  ,ASN,64-25.HOMEPA ЗOHЫ
 ,ATX,KYC
 ,ITA,U
 ,ASN,64-42
 ,AUX,KUNITM
 ,AOX,KSJN
 ,AOX,KYC
 R,VTM,
 NR,J+M,CT
 :MV,VZM,*+1
 R,VTM,BITMOV
 :NR,VZM,*+1
 R,VTM,BITNAM
 R,AOX,
 R,V1M,TCLOTA
 ,ATX,KYC
 ,XTA,IW70.БИTЫ 44-43
 ,ASN,64-4. - PAЗMETKA
  ,AUX,B19*12.MЛ EC
 :,UZA,*+1
 ,AOX,BIT11
 ,AOX,KPAGE
 ,AOX,KYC
C
C...   ПPOБA: HE ЗAKPЫTЬ ЛИ ЗAДAЧY
C
 TCLOTA:,BSS,
 :CT,VZM,*+1
 CT,VJM,CLOTA.ЗAKPЫTЬ
 :PD,VZM,*+1
 ,AOX,BIT39
 CHRDTA:,BSS,
 :RD,VZM,*+1
 ,AOX,B22*18
 ,ATX,KYC
 ,XTA,
 RD,V1M,TTASKON
C
C...   ПOЛYЧEHИE KOHTPOЛЬHOЙ CYMMЫ
C      ПPИ ЗAПИCИ ЗOHЫ HA ЛEHTY
C
 J,VTM,-1777B
 ,XTA,KYC
 R,VJM,PRIPTRA
 :,24,2
 ,XTA,
 LSTAPE:,BSS,
 J,ARX,71777B
 J,VLM,LSTAPE
 TTASKON:,BSS,
 :,24,3
 RT,VJM,TASKON.B OЧEPEДЬ
 EPHTAP:,BSS,
 ,XTA,BIT25
 ,ARX,MДMЛMБ.CЧETЧИK
 ,ATX,MДMЛMБ.MЛ-OБMEHOB
 L,XTA,BIT48
 U,AOX,SJMT-30B
 U,ATX,SJMT-30B
C
C...  ПOCTAHOBKA PAБOTЫ ПO ЛEHTE
C     B ШKAЛY PAБOT ЗAДAЧИ OБMEHA
C
 :,24,2003B
 ,XTA,TRABUS
 C,AAX,BIT44-3
 ,U1A,ENDMAC70.YЖE ECTЬ
 ,XTA,BIT40
 C,AOX,BIT44-3
 ,AEX,TRABUS
 ,ATX,TRABUS
 C,XTA,BIT36-3
 ,CALL,CLOMIR
 :,24,3
 ,UJ ,ПPOДPAБ
C
C...   OБMEH C ФИЗИЧECKИM ДИCKOM
C
 PDIMAC:,BSS,
 ,XTA,IW70
 ,ASN,64-30
 ,AUX,KUNITM
 ,ATX,DID
 ,XTA,IW70
 ,ASN,64+24.K O П
 ,AOX,BIT5
 ,AAX,C77
 PHDISC:,BSS,
 ,AOX,DID
 ,ATX,KYC
 ,XTA,DISAD
 ,ARX,BIT4
 ,AAX,C7777
 ,ASN,64-25."ЗOHA"
 ,AOX,KYC
 ,AOX,BITPAG
 :CT,VZM,*+1
 CT,VJM,CLOTA
 :RD,VZM,*+1
 ,AOX,BITRD
 ,ATX,KYC
 ,XTA,SECT
 ,AEX,BIT48
 ,UZA,DPAGEX
 ,AEX,BIT48
 ,ASN,64+24
 ,AOX,BDSEC
 DPAGEX:,BSS,
 ,AOX,KYC
 RT,VTM,DAFSET
 DTASKON:,BSS,
 ,AOX,KSJN
 ,AOX,KPAGE
 :PD,VZM,*+1
 ,AOX,BIT39
  :,ATX,KYC
  ,APX,KPMASK
  ,ASN,64+41.БИT 6 B TPHYS -
  ,ATI,L. - ПPИЗHAK, ЧTO
  ,XTA,BIT6.ЛИCT ЗAHЯT B OБMEHE
  L,AOX,TPHYS.C MБ ИЛИ MД.
  L,ATX,TPHYS
 ,XTA,
 RD,V1M,TASKON
C
C...   KOHTPOЛЬHOE CYMMИPOBAHИE ДЛЯ MД/MБ
C
 L,VTM,1-4
 ,XTA,KYC
 R,VJM,PRIPTRA
  M,VTM,
 ,XTA,SECT
 ,ARX,
 ,UZA,LSPAG
 ,ASN,64+37
 ,AAX,C1400
   ,ATI,M
 L,VTM,
 ,XTA,
 LSPAG:,BSS,
 ,ASN,64-12
 ,ATX,WLSUM
 ,XTA,
 :,24,2
 J,VTM,-377B
 LSSEC:,BSS,
  M,ARX,70000B
 M,UTM,1
 J,VLM,LSSEC
 :,24,3
 J,VJM,S48TO12
 ,AOX,WLSUM
 L,VLM,LSPAG
 TASKON:,ENTRY,
 TASKON:,BSS,
 ,WTC,TRACNT
 L,VTM,
 L,ATX,WL2
 ,ITA,L
 ,ARX,C1
 ,ATX,TRACNT
 ,XTA,KYC
 L,ATX,WL1
 ,ASN,64+39
 ,AAX,C7
 ,ATI,C
 ,XTA,
 ,ATX,KYC
  ,XTA,QUEONLY
  RT,UZA,
  L,XTA,BIT48
  ,AOX,SCSWING
  ,ATX,SCSWING
  ,XTA,
  RT,UJ,
C
C...   ПOИCK ЛOГИЧECKOГO ИДEHTИФИKATOPA
C          B TAБЛИЦE ФAЙЛOB
C
 FIMAC:,BSS,
 F,UTM,-1
 F,VZM,ERTREQ.HE HAШЛИ
 F,XTA,FATA-1
 ,AEX,IDENT
  ,AAX,MASK12
 ,U1A,FIMAC
C
C...         ФAЙЛ HAЙДEH
C      ЧTEHИE ИMEHИ - BЫДAДИM 0
C      ПOДBOД - ПPOИГHOPИPYEM
C
 NR,V1M,SETACC
 MV,V1M,ENDMAC70
 U,XTA,BIT48-30B.ЧTOБЫ HE
 IF,AOX,MЛД.ИCKATЬ B T1MT
 IF,ATX,MЛД.B ДPYГOЙ PAЗ
 ,XTA,DISTIME
 IF,A+X,BISTIME
 IF,ATX,BISTIME
 F,XTA,FATA-1
 ,AAX,B36/31
 E,VTM,ЗAKPAБMД
 ,UZA,TDISEAR.SCRATCH-ФAЙЛ
C
C...   ИЗBЛEЧEHИE HOMEPA MOДYЛЯ
C      ИЗ TAБЛИЦЫ ФAЙЛOB
C
 CHESCR:,BSS,
 F,XTA,FATA-1
 ,ASN,64-12
 ,AUX,KUNITM
 ,UZA,BADEXCH
 ,ATX,DID
 RD,V1M,CHDISAD
 E,VTM,ЗAПPET ЗП
 F,XTA,FATA-1
 ,AAX,BITWP
 ,UZA,ERTREQ
C
C...   KOHTPOЛЬ BЫXOДA ЗA ГPAHИЦY ФAЙЛA
C
 CHDISAD:,BSS,
 F,XTA,FATA-1
 ,ASN,64+12
 ,AAX,C7777
 ,ARX,DISAD
 ,ATX,DISAD
 F,XTA,FATA-1
 ,AAX,C7777
 ,NTR,3
 ,A-X,DISAD
 E,VTM,ЧYЖ TPEK
 ,U1A,ERTREQ
 F,XTA,FATA-1
 ,ASN,64-20
 ,AUX,C50
 ,UJ ,PHDISC
C
C...   OБMEH CO SCRATCH-ФAЙЛOM
C
C      ПOИCK B TAБЛИЦE ЭKCTEHTOB
C
 TDISEAR:,BSS,
  ,XTA,IDENT.YЖИMAHИE
  ,ARX,SCR*D.ДHЗ И MN
  ,APX,SCR*P.ДO 5 БИT
  ,ATX,IDENT
  ,ASN,64+16
  ,AOX,IDENT
  ,ASN,64+16.YTPOEHИE
  ,AOX,IDENT.ИДEHTИФИKATOPA
  TDS1:,BSS,
 W,VTM,-LTD
  ,ATX,DID
 SEW:,BSS,.ПO CЛOBAM
 W,AEX,TDIS+LTD
 B,VTM,1-3
 SEB:,BSS,.ПO ЭKCTEHTAM
 ,ASN,64-16
 ,ATX,WS
 ,YTA,
 ,UZA,GLOBYTE.HAШЛИ
 ,XTA,WS
 B,VLM,SEB
 ,XTA,DID
 W,VLM,SEW
C...   ECЛИ HET CBOБOДHOГO MECTA
C      B TAБЛ.ЭKCTEHTOB - БYДEM
C      BЫШИБATЬ ЮЗEPA !!!
 F,XTA,FATA-1.SCRATCH
 ,AAX,C3777.ЮЗEPA
 ,UZA,ERTREQ.ИCЧEPПAH
 F,XTA,FATA-1
 ,ARX,C3777.YMEHЬШAEM
 ,AEX,BIT12.ЛИMИT
 F,ATX,FATA-1
 ,XTA,.И BOЗЬMEM
  ,UJ,TDS1.CBOБ.ЭKCTEHT
C
C...   ЗAXBAT ЭЛEMEHTA
C     B TAБЛИЦE ЭKCTEHTOB
C
 GLOBYTE:,BSS,
 ,ITA,B
 ,ASN,64-4
 ,ATI,R
 ,XTA,IDENT
 R,ASN,64+32
 W,AOX,TDIS+LTD
 W,ATX,TDIS+LTD
 B,UTM,LTD*3+2
 W,J+M,B
 W,J+M,B
 W,J+M,B
 W,VTM,-LTS
C
C...   ПOИCK ЗAXBAЧEHHOГO ЭKCTEHTA
C      B TAБЛИЦE SCRATCH-OБЛACTEЙ
C
 SASEAR:,BSS,
 B,MTJ,R
 W,WTC,TSF+LTS
 B,UTM,1
 ,ITA,B
 ,AAX,BIT15
 ,U1A,SETFAD
 W,VLM,SASEAR
 ,UJ ,BADEXCH
 SETFAD:,BSS,
 ,ITA,R
 ,ASN,64-5
 ,ATX,DID
 W,XTA,TSF+LTS
 ,ASN,64+15
 ,UJ ,MODSYS
C
C...   ПEPEAДPECAЦИЯ ФИЗ.БAPAБAHOB
C      16,17,21,22  HA ДИCK
C
 CHMODSY:,BSS,
  U,XTA,BIT48-10B
  ,APX,DISPMD
  ,UZA,CHMODSY1
  ,AAX,BIT48
  :,U1A,*+1.1-Й MБ (16)
  ,XTA,DSHIFT.2-Й MБ (17)
 :,AAX,C7777
  ,ARX,DISPMD
  E,VTM,DISPMD - SYSMD
  ,UJ,MAKDID
  CHMODSY1:,XTA,Ш/ДИCП
 JN,AAX,BITS
  R,U1A,
 U,XTA,BIT48-10B
 IF,APX,SYSMD
 R,UZA,
 ,ANX,B24/1
 ,ASN,64-5
 IF,ARX,SYSMD
  IF,MTJ,E
  MAKDID:,BSS,
 ,AAX,C7777
 ,ATX,DID
  E,XTA,SYSMD
 ,ASN,64+12
 MODSYS:,BSS,
 ,ATI,F
 ,XTA,DISAD
 ,AAX,C37
 ,ARX,DID
 ,ATX,DISAD
 ,UJ ,CHESCR
C
C...   ЗAKAЗ AДPECOBAH K MOДEЛИ MЛ
C
 MODTAP:,BSS,
 NR,V1M,MODNAM
 MV,V1M,ENDMAC70
 MODTEX:,BSS,
 U,XTA,T1MT-30B
 ,ASN,64+12. + ДEЛЬTA
 ,ARX,DISAD
 ,AAX,C7777
 ,ATX,DISAD
 U,XTA,T2MT-30B.ИHДEKC ПO
 ,ATI,F.TAБЛИЦE ФAЙЛOB
 ,UJ ,CHESCR
 MODNAM:,BSS,
 U,XTA,TANAMEMT-30B
 SETACC:,BSS,
 IF,ATX,
 ,UJ ,ENDMAC70
C
C
C...   ДИCПETЧEPCKИЙ BXOД
C      B ЗAДAЧY OБMEHA
C       И14 - BOЗBPAT
C       И13 - AДPEC KYC-A
C      PEЗYЛЬTAT - B SAVAC
C
 TRAPHY:,ENTRY,
 TRAPHY:,BSS,
 :,24,2003B
 ,RTE,177B
 ,ATX,SAVSR
 ,ITA,S
 S,VTM,SAVI15
 ,ITS,M
 ,ITS,J
 ,XTS,DISREG
 ,ATI,27B
 ,XTA,TRACNT
 ,ARX,MTRACNT
 ,UZA,FULQUE
 ,XTA,ГYC
 ,AAX,B48/43
 J,AOX,
 ,ATX,KYC39.ДOБABKA ДHЗ
 ,XTA,TRABUS
 ,UZA,FRETRA
 ,AOX,BIT39
 ,UJ ,SETBUS
 FRETRA:,BSS,
 ,XTA,KYC39
 ,ATX,KYC40
 ,XTA,BITTRA
 ,AOX,RDSCAL
 ,ATX,RDSCAL
 ,XTA,DISREG
 ,ATX,IFTRA+S27
 ,XTA,ADREQ40
 ,ATX,IFTRA+S33."ФИЗOБM1"
 ,XTA,BIT40
 SETBUS:,BSS,
 ,ATX,TRABUS
 ,UJ ,ETRAPH
C
C...ЗAKPЫTИE KЛИEHTA, ECЛИ OЧEPEДЬ ПOЛHA
C
 FULQUE:,BSS,
 M,VTM,TRAPHY
 ,XTA,BITJ
 ,AOX,FULQUESC
 ,ATX,FULQUESC
 ETRAPH:,BSS,
 M,MTJ,33B
 ,UJ ,CLANEWTA
C
C...   PAБOTA TRAM70 OT 39 P. TRABUS
C      (ИЛИ OT 40, ECЛИ БЫЛA CBOБOДHA)
C
 ФИЗOБM1:,BSS,. OT 39
 ,XTA,KYC39
 ,ATX,KYC40
 ,XTA,TRABUS
C   ПPИ ДOБABЛEHИИ HOBЫX PAБOT
C   CMEHИTЬ AДPEC B CЛEД.KOMAHДE!
 ,AAX,BIT38.!!!
 ,AOX,BIT40.ДЛЯ ПPABИЛЬHOЙ
 ,ATX,TRABUS.PAБOTЫ ENDMACRO
C
 PHYREQ40:,BSS,. OT 40
 ,XTA,KYC40
 ,AAX,KPMASK
 ,ATX,KPAGE
 ,XTA,KYC40
 ,AAX,B48/43
 ,ATX,KSJN
 ,ASN,64+42
 ,ATI,JN
 JN,WTC,*YCЫ
 IF,VTM,
 PD,VTM,
 ,XTA,BIT48
 ,ATX,SECT
 ,XTA,KYC40
  CT,VJM,CLOTA. + BIT38 = KYC
 ,ASN,64+17
 ,AAX,BIT1
 ,ATI,RD
 ,XTA,KYC
 ,APX,KUNITM
 ,ASN,64+42
 ,ATI,U
 ,ASN,64+3
 ,ATI,C
 ,XTA,KYC
 C,VZM,DIPREQ
 C,UTM,-7
 C,VZM,NSPREQ
 ,AAX,BIT39
 ,U1A,MODPREQ
 ,ITA,C
 ,AAX,BIT3
 ,U1A,TAPREQ
 R,VTM,PHDRUM
 ,XTA,KYC
 ,ASN,64+2
 ,ATX,DISAD
 ,AAX,BITPAG+2
 ,U1A,CHMODSY
 ,XTA,KYC
 ,APX,MS/KYC
 ,ATX,SECT
 ,UJ ,CHMODSY
 TAPREQ:,BSS,
 ,XTA,KYC
 ,UJ ,CHRDTA
C
C...  "7" B 40-42 P.  KYC-A  TRAPHY
C   - ЗAKAЗ OT HECTAHДAPTHOГO ДPAЙBEPA.
C   ПPИ ЭTOM B 1-15 P. - AДPEC MACCИBA
C   ИЗ 2 CЛOB :
C  1 : HACTOЯЩИЙ KYC (ПPИ OБMEHE ЛИCT HE 0)
C  2 : AДPECA ПPOЦEДYP ПYCKA И KOHЦA
C   AДPECA ПOMEЩAЮTCЯ B WL2.
C   KYC CHAБЖAETCЯ ДHЗ B 48:43 PAЗP.,
C   "1" 38 P. И ПOMEЩAETCЯ B  WL1.
C
 NSPREQ:,BSS,
 ,ATI,M
 M,XTA,
  ,AOX,KSJN
  ,AOX,BIT38
 ,ATX,KYC
 ,APX,KUNITM
 ,ASN,64+42
 ,ATI,U
 M,XTA,1
  ,AOX,KSJN
 RT,VJM,TASKON
 L,XTA,BIT48
 ,AOX,БYPЖYЙ
 ,ATX,БYPЖYЙ
 L,XTA,WL1
 ,AAX,KPMASK
 ,UZA,EPHTAP
 L,XTA,BIT48
 C,AOX,SRJC-1
 C,ATX,SRJC-1
 ,UJ ,EPHTAP
 DIPREQ:,BSS,
 ,ASN,64+11
 ,AAX,BIT1
 ,ARX,C3
 ,ASN,64-39
 ,UJ ,DPAGEX
 MODPREQ:,BSS,
 ,XTA,KYC
 ,ASN,64+25
 ,ARX,C77774
 ,ATX,DISAD
 ,UJ ,MODTEX
C
C...  ЗAKPЫTИE ЗAДAЧИ, ПOДABШEЙ OБMEH.
C
 CLOTA:,BSS,
 ,AOX,BIT38
 ,ATX,KYC
 :,24,2003B
 JN,XTA,BITS
 ,AEX,FULWOR
 ,AAX,RDSCAL
 ,ATX,RDSCAL
 ,XTA,KYC
 :,24,3
 CT,UJ ,
C
C...  ПPИПИCKA ЛИCTA ИЗ KYC-A OБMEHA
C     K 34 MAT.ЛИCTY ЗAДAЧИ OБMEHA
C
 PRIPTRA:,ENTRY,
 PRIPTRA:,BSS,
 ,APX,KPMASK
 ,ATX,RRR
 ,UZA,BADEXCH
 ,AUX,TMPMASK
 ,ATX,IFTRA+TПPИП
 ,ATX,SWINGIF+TПPИП
 ,ITA,R
 R,VTM,-8
 :,ATX,1
 R,VLM,*
 ,ATI,R
 ,XTA,RRR
 ,AUX,PПMASK
 ,MOD,27B
 ,ATI,
 R,UJ,
C
C...  CBEPTKA 48-PAЗPЯДHOЙ KOHTPOЛЬHOЙ
C     CYMMЫ B 12-PAЗPЯДHYЮ
C
 S48TO12:,ENTRY,
 S48TO12:,BSS,
  ,ATX,FSUM
  ,XTA,
  ,ATX,RSUM
  ,XTA,FSUM
  COMPR:,ASN,64+12
  ,ATX,FSUM
  ,YTA,
  ,ARX,RSUM
  ,ATX,RSUM
  ,XTA,FSUM
  ,U1A,COMPR
  ,XTA,RSUM
  ,ASN,64+36
  ,ARX,RSUM
  ,AAX,C7777
 J,UJ ,
C
C...   БЫЛИ OШИБKИ B ЗAKAЗE HA OБMEH
C
 ERTREQ:,BSS,
 ,ITA,E
 IF,ATX,I14
 IF,XTA,S33
 IF,ATX,COPYS33
 M,VTM,ERRPRI
 ,ITA,M
 IF,ATX,S33
 ,XTA,DISREG
 IF,ATX,S27
 MV,J+M,NR
  N,VTM,.БEЗ TASKOFF
 ,XTA,KYC
 ,AOX,KPAGE
  IF,ATX,.CYMM.=KYC
  ,ATX,OFFKYC
 ,AAX,BIT38
 ,U1A,OPEN
  ,XTA,IW70.CYMM.=IW70
  IF,ATX,
 NR,V1M,ENDMAC70
  ,UJ ,OPEN
C
C
 MSKADRMD:,EQU,KDRADM
 CON14:,EQU,KUNITM
 CBEPKCYM:,EQU,S48TO12
 ПPИПOБME:,EQU,PRIPTRA
 CONTEXCH: ,ENTRY,
C
C*********          BXOД: BЫПOЛHEHИE ЗAKAЗA, YЖE ПOCTABЛEHHOГO B OЧEPEДЬ
C
  CONTEXCH:,24,2003B
  12,XTA,WL2-1
  14,ATX,CSUM-1
  12,XTA,WL1-1
  14,ATX,TBUC-1
  14,ATX,TBUT-1
  9,UZA,
 13,VJM,CHPOP.P-OПEPAЦИЯ
 9,UTC,
 15,V1M,.CEMAФOP ЗAKPЫT
  12,XTA,BITS
  14,ATX,SBUC-1
 EXCHMБ:,BSS,
 14,XTA,TBUC-1
 ,AAX,ЧTEHИE
  ,UIA,BЫT
C.........ФOPMИPOBAHИE CЛYЖEБHЫX CЛOB ДЛЯ ЗAПИCИ HA MБ (TPAKTA)
C         (ЧETHЫE CЛYЖ. CЛOBA - AДPECA CEKTOPOB, HEЧETHЫE - KC)
 14,XTA,TBUC-1
 ,AAX,C3
 ,ASN,64-1
 14,ARX,BC1700
  ,ATI,13
 14,XTA,TBUC-1
  13,ATX,
 ,AAX,ЛИCTOБM.
 ,UZA,MБЗAП1C.
 14,XTA,TBUC-1
 ,ARX,BIT1
  13,ATX,2
 ,ARX,BIT1
  13,ATX,4
 ,ARX,BIT1
  13,ATX,6
 14,XTA,C SUM-1
 WR KS:,ASN,64-12.
 ,ATX,WS
 ,YTA,
   13,ATX,1
  13,UTM,2
  ,ITA,13
 ,AAX,C7
 ,UZA,BЫT
 ,XTA,WS
 ,UJ,WR KS
  BЫT:13,VTM,-7
 :,ATX,1.
  13,VLM,*
C.........BЫДAЧA KOMAHДЫ OБMEHA C MБ
 ,XTA,BIT3.
 14,ATX,TMБ-1.
 14,XTA,TBUC-1
          14,  EXT  ,               .
            ,  ATI  ,               .
  14,XTA,BIT47
  9,MTJ,13
  ,UJ,PERMIR
 MБЗAП1C:,BSS,.
C.........ФOPMИPOBAHИE CЛYЖEБHЫX CЛOB ДЛЯ ЗAПИCИ HA MБ (CEKTOPA)
 14,XTA,C SUM-1.
 ,AAX,C7777.
  13,ATX,1
 ,UJ,BЫT.
C
C
C...   KOHEЦ PAБOTЫ ЗAДAЧИ OБMEHA
C      ПO ПPИEMY ЗAKAЗA OT  Э070
C
 ENDMAC70:,ENTRY,.
 ENDMAC70:,BSS,
  :,24,3
  ,XTA,QUEONLY
  ,UZA,NOSWING
  ,ATI,10
  ,XTA,
  ,ATX,QUEONLY
  10,UJ,.BOЗBPAT B ПOДKAЧKY
 NOSWING:,BSS,
C         KOHEЦ PAБOT ПO ЭK70 ИЛИ KAH.OПOЗ.
 ,XTA,FWCR.БЫЛ OП.KAH.:
 ,U1A,TOTRAINT.ДA
  ,XTA,BIT40
  ,UJ,TO TRAINT
C
C...   KOHEЦ PAБOTЫ ПO ЛEHTOЧHOMY KAHAЛY
C
  ALLEOM:14,XTA,CSUMSTAT-1
  ,AAX,C377
  ,UZA,PUTMASK
  14,XTA,TYPEMT-3
  ,AAX,BIT35.B ДYБHE
  ,ASN,64+2."MФ 40" = CDC608
  ,U1A,PUTMASK.( B 6 HAПP-И )
  14,XTA,BIT39
  PUTMASK:,AOX,TRAMASK
  ,CALL,PERMIR
  14,XTA,BIT47
  TO TRAINT:,24,2003B
  ,AEX,FULWOR
  ,AAX,TRABUS
  ,ATX,TRABUS
 ,XTA,.ЧИCTKA ПPИЗHAKA
 ,ATX,FWCR.KAHAЛA OПOЗH.
C
C
C...   OCHOBHOЙ BXOД B ЗAДAЧY OБMEHA:
C      BЫБOP OЧEPEДHOЙ PAБOTЫ
C
 TRAINT:,ENTRY,.
 TRAINT:,BSS,.
   :   ,24,2003B
 ,XTA,TRABUS
 ,UZA,ENDTRA. HA BЫXOД ИЗ ЗAДAЧИ OБMEHA
  ,AAX,PRTMTCH
  ,U1A,PRT/IS
  ,XTA,TRABUS
  PRT/IS:,BSS,
  ,ANX,
  ,ATI,14
  14,UJ,SW/TRA -1
C
C********
  SW/TRA:,BSS,
  :,UJ,END DRUM
  :,UJ,END DRUM
  :,UJ,DISC
  :,UJ,DISC
  :14,UTM,-2
  ,UJ,END TAPE
  :14,UTM,-2
  ,UJ,END TAPE
  :14,UTM,-2
  ,UJ,END TAPE
  :14,UTM,-2
  ,UJ,END TAPE
  :,UJ,ФИЗOБM1.ПO P.40
 :,UJ,ФИЗOБM1.ПO P.39
 :,XTA,BIT38.KAHAЛ
 ,ATX,FWCR.OПOЗHABAHИЯ
   :   ,24,3
 ,UJ,RECMDT
C
C...   CAMOЗAKPЫTИE TRAM70
C      ПPИ OTCYTCTBИИ PAБOT
C
 ENDTRA  :  ,  BSS  ,               .
            ,  XTA  , RDSCAL        .
 ,AOX,OFFESC.
 ,AAX,ZBITJ
            ,  ATX  , RDSCAL        .
            ,  XTA  ,               .
 ,ATX,OFFESC.
            ,  XTA  , C7            .
   ,ATX,IFTRA+S27
            ,   UJ  , SELECT        .
C
C*****   OБPAБOTKA KOHЦA OБMEHA C MБ
C
  END DRUM:,24,3
C         HE ПPOПYCK ЛИ ЭTO B KPИT.ИHT.
 14,XTA,SBUC-1
 ,UZA,NEWDRTA.ДA
C.....    ЭTO OKOHЧAHИE OБMEHA
C.....   БЫЛИ ЛИ AППAPATHЫE OШИБKИ
 14,XTA,TBUC-1
 ,ASN,64+11. O Ш M  ИЛИ
 ,AAX,B25*16.KOH.OБM.
 ,U1A,OШM/KO. - ДA
 14,XTA,TBUC-1
            ,  AAX  , ЧTEHИE        .
 ,UZA,XOБMБ.
 14,XTA,TBUC-1
 5,VJM,ПPИПOБME.
C.........YCTAHOBKA PEAKЦИИ HA ПPEPЫBAHИE: KOHTPOЛЬ ЧИCЛA
 10,VTM,ПOBTOБM1.C YЧETOM OШM
 ,ITA,10.
            ,  ATX  ,W04265         .
C.........
          14,  XTA  , T B U T -1    .
            ,  AAX  , ЛИCTOБM       .
 ,U1A,БЫЛИЧT1.
          14,  XTA  , T B U T -1    .
            ,  AAX  , B12/11        .
            ,  ASN  ,64+2           .
 ,ATI,11.OTH.AДREC HAЧAЛA CEKTOPA
          14,  XTA  , T B U T -1    .
            ,  AAX  , C3            .
            ,  ASN  ,64-1           .
 14,ARX,BC1700.
            ,  ATI  , 10            .
C---------          KOHTPOЛЬ - 'TO ЛИ MЫ ПPOЧИTAЛИ'
 12,VTM,0
 14,XTA,CSUM-1
 ,ASN,64-36
 ,ATX,R1
 ,UJ,L04361
 ПPOBAДP:,BSS,.
C......   KOHTPOЛЬ ПYTAHИЦЫ CEKTOPOB
 ,ATX,ЖEAД.
 ,XTA,C2003.БЛOKИP.
 ,ATI,21B.OCTAH. ПO KЧ
 ,XTA,ЖEAД.
 10,AEX,.
 ,AAX,MSKADRMD.
 5,UZA,.AДPECA-O'KEY
 ,XTA,CHEKMDFL.AДPEC.KOHTPOЛЬ
 5,UIA,.- БЛOKИPOBAH
 ,XTA,BIT40.
 14,ARX,CTMБ-1.ПYTAHИЦY CEKT.
 14,ATX,CTMБ-1.B CTATИCTИKY
 ,XTA,C13.
 ,ATI,21B.OCTAH. ПO KЧ
 14,XTA,TBUT-1.
 ,AAX,BIT27. OШ.MБ.
 ,UIA,KPAHTЫ. - ПOBTOPHO
 ,XTA,ЖEAД.
C.........ПOДГOTOBKA TT-TEKCTA И BЫДAЧA HA TT 'OШ. MБ...'
 11,VJM ,KOД MБ.
 ,ISO,6HOШ.MБ:
 ,XTA,ШИФP.
 ,ATX,ПOAД+1.
 10,XTA,
 11,VJM,AДP MБ.
 ,ATX,ПOAД
 14,MTJ,11
  :,24,2003B
 14,VTM,TOШMБ
 ,ITA,14.
 14,VJM,TTPRINT.
 11,MTJ,14.
 ,UJ,ПOБ2.
 KPAHTЫ:,BSS,.
C
C.....   ДBYKPATHAЯ ПYTAHИЦA CEKTOPOB
C        (BЫБPOC ЗAДAЧИ)
C
 11,VJM,ДAЙДHЗ
 ,ATI,11.
 11,V1M,HEПOДK1.
C.........COBCEM ПЛOXAЯ CИTYAЦИЯ C MБ
   :   ,24,2003B.
 14,VTM,TMБCДOX.
 ,ITA,14.
 14,VJM,TTPRINT.TT-ПEЧATЬ- 'MБ CДOX'
   :   ,24,3.
 ,UJ,*. ЦИKЛ C OTKP. ПPEP.
C
C.....   ПOДПPOГPAMMЫ ПOДГOTOBKИ COOБЩEHИЙ
C
 KOД MБ:,BSS,
 ,ATX,ЖEAД
 11,XTA,
 ,ATX,TOШMБ
 ,ITA,11
 ,ATX,SAVI11
 11,VJM,ДAЙ ДHЗ.
 ,UZA,ПOДKAЧ
  13,XTA,*YCЫ
  ,ASN,64-27
 ,AUX,BY4/5
 ПOДKAЧ:,AOX,KШИ
 ,ATX,ШИФP
 ,XTA,ЖEAД
 11,VJM,AДP MБ
 ,ATX,ЖEAД
 14,XTA,TBUT-1
 ,AOX,BIT27
 14,ATX,TBUT-1
 ,WTC,SAVI11
 ,UJ,1
 AДP MБ:,BSS,
 ,APX,MSKADRMD
 ,AUX,UPM1
 ,AOX,K AДMБ.
 11,UJ,
 ДAЙДHЗ:,BSS,.
 14,XTA,TBUT-1.
 ,AAX,BIT38.
 ,UZA,NONPHY.
 14,XTA,TBUT-1.
 ,ASN,64+42.
 ,ATI,13.
 ,AEX,ДHЗПOДK.
 11,UZA,.BOЗBPAT(ЗAKAЗ OT ПOДKAЧKИ)
 BДД:,ITA,13.
  11,U1A,
  ,XTA,EXCHJOB+1
  ,ATI,13
 11,UJ,.
 NONPHY:14,XTA,TBUT-1.
 ,APX,KPMASK.
 ,ASN,64+41.
 ,ATI,13.
 13,XTA,TPHYS.
 NPH1:,ASN,64+27.
 ,AAX,C77.
 ,ATI,13.
 ,AEX,ДHЗПOДK.
 ,U1A,BДД.
 ,XTA,EXCHJOB.
 ,UJ,NPH1.
C.....
 OБPKOШMA:,BSS,.
C---------         БЛOK, PAБOTAЮЩИЙ OT ИMEHИ BЫБPACЫBAEMOЙ ЗAДAЧИ
  14,VTM,AOШM
 ,UJ,OШMAШИHЫ
C---------
 L04357:,XTA,DISREG
            ,  ATI  , 21B           .
          14,  XTA  , T B U C -1    .
            ,  AAX  ,BIT39          .
 ,AAX,CHEKMDFL.
            ,  U1A  , XOЧTMБ1       .
 ,XTA,BIT1.
 14,ARX,CTMБ-1. KC MБ
 14,ATX,CTMБ-1.B CTATИCTИKY
 14,XTA,TBUT-1
 ,AAX,BIT27.
 ,U1A,*8ПOBT.
 14,XTA,TBUT-1
 11,VJM,KOД MБ
 ,ISO,6HKC MБ:
 14,MTJ,11
 14,XTA,TBUT-1
 ,AAX,ЧTEHИE
 ,U1A,HE ЗПMБ
 ,XTA,T ЗПMБ.
 ,ATX,TOШMБ
 HE ЗПMБ:,XTA,A*TOШMБ
 14,VJM,TTPRINT.
 11,MTJ,14.
 ,XTA,CHEKMDFL+1
 ,UZA,ПOBTOБMБ
 ,UJ,XOЧTMБ1.
 БЫЛИЧT1:,BSS,.
C---------         ПPOBEPKA ПPOЧTEHHOГO TPAKTA
 12,VTM,-3.
 14,WTC,BC1700.
 10,VTM,.
 11,VTM,0.C HUЛEBOГO CEKTOPA
 14,XTA,CSUM-1
 ,ATX,R1
 L04361:,BSS,.
C.........KOHTPOЛЬHOИ CYMMИPOBAHИE  OДHOГO CEKTOPA
 10,MTJ,9.
 4,VTM,-7.
 5,VJM,CYMMИP.
 9,MTJ,10.
 13,VJM,CBEPKCYM.
 ,ATX,RRR.
C......   HE БЫЛO ЛИ ПYTAHИЦЫ CEKTOPOB
 ,ITA,10.
 ,ASN,64+1.
 ,AAX,C3.
 14,AOX,TBUT-1.
 5,VJM,ПPOBAДP.ЗA KOHTPOЛEM
C......   COBПAЛA ЛИ KOHTP. CYMMA
 ,XTA,RRR.
 10,AEX,1.
 ,UIA,L04357. - HET
 14,XTA,TBUT-1
 ,AAX,ЧTEHИE
 ,U1A,L04362
 ,XTA,R1
 ,ASN,64-12
 ,ATX,R1
 ,YTA,
 ,AEX,RRR
 ,UZA,L04362
 14,XTA,TBUT-1
 ,APX,MSKADRMD
 ,AUX,CON1
 ,ASN,64+3
 ,AEX,TPAKTCT.TPAKT CTATИCTИKИ MOЖET БЫTЬ ЗAKPЫT
 ,U1A,L04357
 12,V1M,L04362
 14,MTJ,11
 14,VTM,YЧET
 ,ITA,14
 14,VJM,TTPRINT
 11,MTJ,14
 L04362:,BSS,
 ,XTA,C13.
 ,ATI,21B.OCTAH. ПO KЧ
 10,UTM,2.
 12,VLM,L04361.
 ,UJ,XOБMБ.
 ПOBTOБM1:,XTA,B25*16
 ,ATX,WS
 ,XTA,BIT25
 ,UJ,OШM1
 OШM/KO:,ATX,WS.
 OШM1:,BSS,
 14,ARX,CTMБ-1.
 14,ATX,CTMБ-1
 14,XTA,TBUT-1
 ,AAX,BIT27
 ,U1A,*8ПOBT
 ,XTA,WS
 ,APX,B25*16
 ,ASN,64-2
 ,YTA,
  ,ATI,11
 11,UTM,-1
 11,V1M,ЦK MБ
 14,XTA,TBUC-1
 ,AAX,ЧTEHИE
 ,U1A,ЦK MБ
 11,VTM,3
 ЦK MБ:11,XTA,TEXER0
 ,UTC,TEX/ER
 ,ATX,
 14,XTA,TBUT-1
 11,VJM,KOД MБ
 TEX/ER:,BSS,1
   :   ,24,2003B
  14,MTJ,11
 ,XTA,A*TOШMБ
 14,VJM,TTPRINT
  :,24,3
 11,MTJ,14
 ПOBTOБMБ:,BSS,.
 *8ПOBT:,BSS,.
C.........ЗAKAЗ ПOBTOPEH 8 PAЗ
 14,XTA,CTMБ-1.
 ,AAX,MOШMБ.HAБPAЛИ OШИБOK
 ,UZA,*8П. - HET
 5,VTM,*8П.
  TO CTMБ:,BSS,
 14,XTA,CTMБ-1.
 ,ATX,БYCT+1.
 ,XTA,.
 14,ATX,CTMБ-1.
 14,XTA,TBUT-1.
  ,AOX,BIT27.ДЛЯ
  ,AEX,BIT27.ПOBTOPA
  14,ATX,TBUT-1.ДИAГHOCTИKИ
 ,ATX,БYCT.
 ,ITA,5.
 ,AOX,STD.
 ,UJ,STATIST.
 *8П:,24,2003B
 11,VJM,ДAЙДHЗ.
 ,ATI,11.
C.........ПPOBEPKA: ЗAKAЗ OT ПOДKAЧKИ
 ,UZA,ПOБ2.ДA- ПOBT. OБMEHA
 ,XTA,OUTJOBSC.
 11,AAX,BITS.
C.........ПPOBEPKA: ЗAДAЧA BЫБPACЫBAETCЯ
 ,UZA,ПOБ2.HET- ПOBT. OБMEHA
C.........BЫБPOC ЗAДAЧИ, POCПИCЬ ПИCTA И TPAKTA)ПO BOЗMOЖHOЖHOCTИ
C.........HE ЧTEHИE ЛИ ДИCПETЧEPA
 14,XTA,TBUC-1.
 ,AEX,DISPBASE.
 ,AAX,CON14.
 ,UZA,ПOБ2.ЧTEHИE ДИCП.MБ
 14,XTA,TBUC-1
 ,APX,MSKADRMD
 ,AUX,CON1
 ,ASN,64+3
 ,AEX,TPAKTCT
 ,UZA,ПOБ2
 14,XTA,TBUC-1.
 ,APX,CON14.
 ,ASN,64+42.
 ,ATI,12.
C.........ПOДГOTOBKA K POCПИCИ И POCПИCЬ ЛИCTA OБMEHA
  10,VTM,-1023
 14,XTA,TBUT-1
 5,VJM,ПPИПOБME
   :   ,24,2.
  ,XTA,
 ПOБ1:10,ATX,71777B.
 10,VLM,ПOБ1.
   :   ,24,2003B
C.........ЗAMEHA ЗAKAЗA(ЧTEHИE) - ЗAKAЗOM(ЗAПИCЬ)
C         ДЛЯ POCПИCИ (ПЛOXOГO) TPAKTA
 14,XTA,TBUT-1
 ,AAX,ЧTEHИE.
 ,UZA,XOБMБ.
C.........ПPOBEPKA: MБ ЗAKPЫT HA 1 TP
 ,XTA,1.
 12,AAX,BIT24.
 ,U1A,XOЧTMБ2.ДA- CЧИTAEM: OБMEH ПPOШEЛ
 14,XTA,TBUT-1
 ,AAX,CON7.
 ,AOX,ЛИCTOБM.
 14,ATX,TBUT-1
 14,ATX,TBUC-1
C.........HA ЧИCTKY TPAKTA
 ,XTA,
 14,ATX,CSUM-1
 ПOБ2:,BSS,.
 14,XTA,TBUC-1
 ,AAX,ЧTEHИE
 ,U1A,ПOБ3
 ,XTA,KOHTPCTP
 ,U1A,KOHTPЗAП
 ПOБ3:,BSS,.
 14,XTA,TBUC-1
 14,AEX,TBUT-1
 ,AAX,ЧTEHИE
 ,UZA,ПOБ4
 14,XTA,TBUT-1
 14,ATX,TBUC-1
 ,XTA,TRABUS
 ,AOX,ЖДYKOHTP
 ,ATX,TRABUS
 ,XTA,
 ,ATX,ЗAHKOHTP
 ,ATX,ЖДYKOHTP
 ПOБ4:,BSS,
  9,VTM,L01642
 ,UJ,EXCHMБ
 ЖДEM:14,XTA,BITS
 ,ATX,ЖДYKOHTP
 L01642:,BSS,
  14,XTA,BITS
  ,UJ,TOTRAINT
 HEПOДK1:,24,2003B.
 11,XTA,*YCЫ.
 ,ATX,OUTCW.
 13,VTM,OБPKOШMA.
 ,ITA,13.
 ,CALL,OUTJOB.
C!!!   OUTJOB HE BCEГДA BOЗBPAЩAET
C      B I15 AДPEC ИПЗ
 ,XTA,OUTCW+1
 15,ATX,COPYS33.
 XOЧTMБ1:,BSS,
 XOЧTMБ2:,BSS,
 XOБMБ:,BSS,
   :   ,24,2003B
 ,XTA,KOHTPCTP
 ,UZA,XOБMБ3
 14,XTA,TBUT-1
 ,AAX,ЧTEHИE
 ,U1A,XOБMБ3
 14,XTA,TBUC-1
 ,AAX,ЧTEHИE
 ,U1A,XOБMБ2
 KOHTPЗAП:,BSS,
 ,XTA,ЗAHKOHTP
 ,U1A,ЖДEM
 14,XTA,TBUC-1
 ,AAX,CON6
 ,AOX,KOHTPCTP
 14,ATX,TBUC-1
 ,ATX,ЗAHKOHTP
 ,UJ,ПOБ4
 XOБMБ2:,BSS,
 ,XTA,TRABUS
 ,AOX,ЖДYKOHTP
 ,ATX,TRABUS
 ,XTA,
 ,ATX,ЗAHKOHTP
 ,ATX,ЖДYKOHTP
 XOБMБ3:,BSS,
 14,XTA,SBUC-1
 ,ANX,
 ,ATI,12
 14,XTA,CTMБ-1
  :,UZA,*+1
 5,VJM,TOCTMБ
   :   ,24,3.HA KOHEЦ
 9,VJM,KOHOБMБ.ЗAKAЗA
 ,XTA,
 14,ATX,TBUC-1
 14,ATX,TBUT-1
 13,VJM,CHVOP.V-OПEPAЦИЯ
 NEWDRTA:,BSS,
C   ...   BЫБOPKA HOBOЙ PAБOTЫ HA БAPAБAHE
  14,XTA,SRJC-1
  ,UZA,L01642
  ,ANX,
  ,ATI,12
 9,VTM,L01642
 ,UJ,CONT EXCH.
C
C
C...   PAБOTA OT 44-41 P.  TRABUS :
C      ПPOXOД ЗOHЫ MЛ
C
 END TAPE:,BSS,
 :,24 ,2003B
  ,MOD,237B. Г P П
  ,AOX,FINZON.+ ПPOГP.ДYБЛЬ
 C,AAX,BIT36-3
  ,UZA,HET ПPEP
  ,AEX,FULWOR
  ,AAX,FINZON.ГACИM
  ,ATX,FINZON.B ДYБЛE
 C,XTA,CSUMSTAT-1
  ,AAX,C377.KTO ДBИГAЛCЯ
  ,UZA,LB3.TOГO И
  ,ATX,ПPOBMФ.OПPOCИM
 C,AAX,ДBECA-3
  ,UZA,CLCПP
  ,ANX,C7
  ,AAX,C7
  ,ATI,I
  ,AEX,C7
 C,ARX,BC1700
  ,ATI,U
C
C...   ECЛИ ДBИЖETCЯ MЛ EC
C      OПPOCИM EE БEЗ BЫБOPKИ
C
  ,XTA,B39/25
 R,CALL,LSOSTEC1
 C,XTA,NEW-3
 I,AAX,BIT8
  ,U1A,HET ПPEP
 C,ATX,ДBECA-3
  U,XTA,SJMT-30B
  ,AAX,БYPЖYЙ
  ,UZA,LB3
  ,XTA,SOST.ДЛЯ EC*608
  ,ASN,64+24.ЗДECЬ
  C,ATX,TBUT-1. Ц K C
C
C...   ECЛИ BCE CTOЯT ИЛИ KOHЧИЛ
C      ДBИЖEHИE MФ EC - OПPOCИM BCEX
C
 LB3:,BSS,
  ,XTA,C377
  ,ATX,ПPOBMФ
C
C...   HAЧAЛO ЦИKЛA OПPOCA COCTOЯHИЯ MФ
C
 CLCПP:,BSS,
 :,24 ,3
  ,XTA,ПPOBMФ
  ,ANX,C7
  ,AAX,C7
  ,ATI,I
  ,AEX,C7
 C,ARX,BC1700
  ,ATI,U
 C,XTA,ШKEC-3
  ,AAX,B39/25
  ,AOX,BIT24
 R,CALL,LSOSTEC
 I,XTA,BIT8
  ,AEX,ПPOBMФ
  ,ATX,ПPOBMФ
  ,U1A,CLCПP
  ,MOD,237B
 C,AAX,BIT36-3
  ,UZA,HET ПPEP
C
C...   BCEX OПPOCИЛИ, A ПPEPЫBAHИE
C      HE CБPOCИЛOCЬ !
C
  ,XTA,BIT11
 :,24 ,3
  ,ARX,CЧETП
  ,AAX,B24/1
  ,ATX,CЧETП
  ,U1A,END TAPE
  ,ITA,C
 C,MTJ,I
 C,VTM,HEYГACИM
  ,ASN,64-16
  ,AOX,TБЯKA
 C,ATX,2
  ,ITA,C
 :,24 ,2003B
 C,CALL,TTPRINT
 I,MTJ,C
  ,UJ ,END TAPE
C
C...   ПPEPЫBAHИE "ПPOXOД ЗOHЫ" ПOГAШEHO.
C      HAЧИHAEM PAБOTY HA KAHAЛE.
C
 HET ПPEP:,BSS,
  C,XTA,TYPEMT-3.B ДYБHE
  ,AAX,BIT35. MФ 40 = CDC608
  ,UZA,KOHЦЫ
  ,EXT,4101B.CДB
  ,ASN,64+6.MФ
  ,AAX,BIT1.CDC608
  KOHЦЫ:,24,3
 C,AOX,NEW-3
 C,AOX,ДBECA-3
  ,AAX,C377
  ,AEX,C377
 C,AAX,CSUMSTAT-1
 C,AEX,CSUMSTAT-1
 C,ATX,CSUMSTAT-1
  ,YTA,
 C,AOX,W05741-3.MФ, ГДE
 C,ATX,W05741-3.KOHEЦ ДBИЖEHИЯ
  ,XTA,
  ,ATX,CЧETП
  ,ATX,ПPOBMФ
C
C...   HACTPOЙKA ПAPAMETPOB KBY-KAHAЛA
C
 ПPOДPAБ:,BSS,
 ,XTA,
 ,ATX,CH
 R,VTM,*7
 ,UJ,PUT16
C
C...   ШAГ CKAHEPA ПO KBY-KAHAЛY
C
 *1:,BSS,
 ,XTA,BIT1
 C,ARX,COUNT-3
 ,AAX,C7
 C,ATX,COUNT-3
 ,XTA,BIT1
 ,ARX,CH
 ,ATX,CH
 ,AEX,KPYГ
 ,UZA,ALLEOM.ПPOШЛИ KPYГ
C
C...   BЫБPAЛИ OЧEPEДHOЙ MФ
C
 *7:,BSS,
 C,XTA,ДBECA-3
 ,U1A,ALLEOM.A KAHAЛ ЗAHЯT
 C,XTA,COUNT-3
 ,ATI,I
 ,AEX,C7
 C,ARX,BC1700
 ,ATI,U
 U,XTA,SJMT-30B
 ,UZA,*1.HET PAБOTЫ
 ,ANX,
 ,ATI,N.ИHДEKC ПO WL
 C,XTA,ШHГ-3
 ,ASN,64+8.ЖДEM
 C,AOX,ШHГ-3.ГT/ЗП
 C,AOX,CSUMSTAT-1.ИЛИ YЖE
 I,AAX,BIT8.ДBИЖEMCЯ
 ,U1A,*1
 ,ATX,IDMT
 U,XTA,T2MT-30B
 ,AAX,C/ПEP.ИДET
 ,AEX,C/ПEP.ПEPEMOTKA
 ,UZA,*1
  ,NTR,3
 C,XTA,CHBOSS-3.HA KAHAЛE
 ,UZA,HET MOH.MOHOПOЛИЯ
 ,ASN,64-18. - ПPOПYCKAEM
 N,AEX,WL1-1.TOЛЬKO
 ,AAX,B48/43.EE
 ,UZA,HET MOH.BЛAДEЛЬЦA
 C,XTA,TBUC-1
 ,UZA,*1
 C,XTA,SBUC-1
 N,AEX,BITS
 ,U1A,*1
 HETMOH:,BSS,
 RT,VTM,*1
 N,XTA,WL1-1
 ,ASN,64+42
 ,ATI,JN. Д H З
 JN,MTJ,RF
 RF,UTM,-JNREC.ПP-K OПOЗH.
 N,XTA,WL1-1
 ,AAX,BITПOДB
 ,ATI,MF
 N,XTA,WL1-1
 ,AAX,BITИMЯ
 ,ATI,NF
 N,XTA,WL1-1
 ,AAX,KPMASK
 :,UZA,*+1
 R,VJM,PRIPTRA
 ,XTA,NH
 ,ATI,DC.YBY-KAHAЛ
 DC,XTA,BC1700.AДPECA
 ,ATI,CW.CЛYЖ.CЛOB
 C,XTA,W05741-3
 I,AAX,BIT8
  C,AEX,W05741-3
  C,ATX,W05741-3
   ,YTA,.БЫЛ ПPOXOД
 ER,VTM,24.ЗOHЫ
  R,VTM,KO БЭCM. - ДA
   ,U1A,SWI
 C,XTA,SRJC-1.ECTЬ ГOTOBЫЙ
 N,AAX,BITS.K OБMEHY ЗAKAЗ
 ER,VTM,
 R,VTM,ПOДЗAKMЛ. - HET
  ,UZA,SWI
 R,VTM,BЫПOЛH
 C,XTA,TBUC-1.YBY-KAHAЛ
 ,WTC,N8.CBOБOДEH
 ,AOX,TBUC-1
 ,U1A,*1. - ЗAHЯT
C
C...   YXOД, ECЛИ HAДO, HA ПPOЦEДYPY
C      OTPAБOTKИ ПO HECTAHД. ЗAKAЗY
C      BOЗBPAT OTTYДA - ПO  RT.
C
 SWI:,BSS,
  ,XTA,
 ,ATX,CH
 ,XTA,БYPЖYЙ
 N,AAX,BITS
 R,UZA,
  N,XTA,WL2-1
 ER,ASN,64
   ,ATI,R
 :ER,V1M,*+1
 RO,VJM,PRT UP
  N,XTA,WL1-1
  R,UJ ,
C
C...   OPГAHИЗAЦИЯ ПPEДBAPИTEЛЬHЫX
C              ПOДBOДOB
C
 ПOДЗAKMЛ:,BSS,
 R,VTM,C17
 NF,V1M,KORRTAB
 U,XTA,T1MT-30B
 ,AAX,BIT1
 ,UZA,HE ИMЯ
 N,XTA,WL1-1
 ,AAX,BIT38
 R,VTM,C7
 ,U1A,KORRTAB
 R,VTM,C1
 KORRTAB:,BSS,
 R,XTA,
 N,AOX,WL1-1
 N,ATX,WL1-1
  ,XTA,ZBIT1
 U,AAX,T1MT-30B
 U,ATX,T1MT-30B
 U,XTA,T2MT-30B
  ,AAX,B48/13
  ,AOX,C1
 U,ATX,T2MT-30B
 HEИMЯ:,BSS,
 N,XTA,WL1-1
  ,ASN,64+12
 U,AEX,T2MT-30B
  ,AAX,B24/14
 U,AEX,T2MT-30B
 U,ATX,T2MT-30B
  ,ARX,C7777
  ,AAX,C7777
 ,ATX,NZON.CZ-1
  ,AEX,C7777
 R,VTM,ПOДЗAK1
  ,UZA,ПPЯДBИЖ1
 ПPOBPEB:,BSS,
 U,XTA,T2MT-30B
  ,ASN,64+12
 ,AAX,C7777.DZ
 ,X-A,NZON.CZ-DZ-1
 ,U1A,ПPЯДBИЖ1.CZ.LE.DZ
 ,ATX,WS
 I,XTA,PEBEPC
 C,AOX,CSUMSTAT-1
 C,ATX,CSUMSTAT-1
  ,XTA,C2
  ,ARX,NZON
  ,ATX,NZON.CZ+1
  ,A-X,WS.DZ+2
  ,A-X,WS.2*DZ+3-CZ
  ,UZA,SET2MT.CZ.LE.2*DZ+3
 C,XTA,ШKEC-3
  ,UZA,SET2MT
   ,ARX,
 :,U1A,*+1
   ,UTC,BIT9-BIT7
  ,XTA,BIT7
 U,EXT,100B-30B
  ,XTA,C/ПEP
 U,AOX,T2MT-30B
 U,ATX,T2MT-30B
  ,XTA,
  ,ATX,NZON
 ПPЯДBИЖ1:,BSS,
  ,XTA,FULWOR
 I,AEX,PEBEPC
 C,AAX,CSUMSTAT-1
 C,ATX,CSUMSTAT-1
 SET2MT:,BSS,
 U,XTA,T2MT-30B
  ,AAX,B41/1
  ,AAX,B48/13
  ,AOX,NZON
  SETT2:,BSS,
 U,ATX,T2MT-30B
 R,UJ,
  KOPPHЗOH:,XTA,C1
   U,ARX,T2MT-30B
  ,AAX,ZBIT13
  ,UJ,SETT2
 ПOДЗAK1:,BSS,
 NF,V1M,HA BЫП
  ,XTA,REWINDSC
 U,APX,BIT48-30B
  ,ASN,64+44
 N,AOX,WL1-1
 N,ATX,WL1-1
  ,AAX,BIT4
 R,VTM,HA BЫП
  ,U1A,NO REW
  ,XTA,B39/25
 U,AAX,T2MT-30B
 U,ATX,T2MT-30B
  ,XTA,C7
 N,AOX,WL1-1
 N,ATX,WL1-1
  ,UJ,HA BЫП
 NO REW:,BSS,
 N,XTA,WL1-1
  ,AAX,BIT19
  ,U1A,KOPPHЗOH
 N,XTA,WL1-1
  ,AAX,C1
  ,U1A,HA BЫП
 HA ПOДB:,BSS,
 I,XTA,ПOДBOД
 C,AOX,CSUMSTAT-1
 C,ATX,CSUMSTAT-1
 U,XTA,T2MT-30B
  ,AAX,C/ПEP
  ,AEX,C/ПEP
  ,UZA,*7
 C,XTA,ШKEC-3
 I,AAX,BIT24
  ,U1A,OБM5017
 C,XTA,CSUMSTAT-1
 I,AAX,PEBEPC
 :,UZA,*+1
  ,UTC,C7777-C1
 R,VTM,
 R,XTA,C1
 U,ARX,T2MT-30B
  ,AAX,ZBIT13
 U,ATX,T2MT-30B
  ,AAX,C7777
  ,ATX,NZON
 U,XTA,T2MT-30B
  ,ASN,64+12
  ,AAX,C7777
  ,A-X,NZON
  ,ATI,L.DZ-CZ
 L,V1M,NEQNZ
 N,XTA,WL1-1.DZ=CZ
  ,AAX,B21/19
  ,UZA,HA BЫП
 R,V1M,HA ДBИЖ
 MF,V1M,XOPOБM1
 HA ДBИЖ:,BSS,
 MV,VTM,1
 R,VZM,EXCHДBИЖ
 ШЗH:,BSS,
 MV,VTM,3
 ,UJ,EXCHДBИЖ
 NEQNZ:,BSS,
 N,XTA,WL1-1
  ,APX,B21*18
  ,ASN,64+46
  ,ATI,F
 F,V1M,CHEFLA
  ,XTA,B21*18
 N,AOX,WL1-1
 N,ATX,WL1-1
 CHEFLA:,BSS,
 L,UTM,-1
 L,V1M,HA ДBИЖ
 F,UTM,-1.DZ=CZ+1
 F,VZM,HA ДBИЖ.READ
C
C...   ЗAKAЗ ГOTOB K OБMEHY
C
 HA BЫП:,BSS,
  ,XTA,FULWOR
 I,AEX,ПOДBOД
 C,AAX,CSUMSTAT-1
 C,ATX,CSUMSTAT-1
 N,XTA,BITS
 C,AOX,SRJC-1
 C,ATX,SRJC-1
  ,UJ,*7
C
C...   YBY-KAHAЛ ЗAKAЗY BЫДEЛEH
C
 BЫПOЛH:,BSS,
  ,XTA,REWINDSC
 U,AAX,BIT48-30B
  ,U1A,HE CMOTKA
 C,XTA,ШKEC-3
 I,AAX,BIT8
  ,UZA,ЧT CC
 MЛHAHAЧ:,BSS,
 U,XTA,BIT48-30B
  ,AOX,REWINDSC
  ,ATX,REWINDSC
  ,XTA,ZC7
 N,AAX,WL1-1
 N,ATX,WL1-1
  ,AAX,BIT19
  ,UZA,EXCHMЛ1
  ,UJ,ЗAПMГЗ
 HECMOTKA:,BSS,
 N,XTA,WL1-1
  ,AAX,C3
  ,U1A,BЫПOЛH/
 N,XTA,WL1-1
  ,AAX,B20/19
  ,UZA,BЫПOЛH/
  ,AEX,B20/19
  ,UZA,EXCHMЛ1
  ,AEX,BIT19
  ,UZA,ШЗH
 ЗAПMГЗ:,BSS,
 MV,VTM,20B
 ,UJ,EXCHДBИЖ
C
C...   ЗAXBAT YBY-KAHAЛA
C
 BЫПOЛH/:,BSS,
  U,XTA,T1MT-30B.KOHTPOЛЬ
  ,AAX,CREADIS.ЗAПИCИ BKЛЮЧEH
  ,UZA,BЫПOЛH//.-HET
  R,VJM,POP.ЗAXBAT ЛИCTA
  P,VIM,ALLEOM.HE ДAЛИ
  C,WTC,TYPEMT-3
  CW,VTM,.BOCCTAHOBЛEHИE
  N,XTA,WL1-1
  ,AEX,SCDICH
  ,AAX,KPMASK.P-OПEPAЦИЯ
  ,UZA,BЫПOЛH//.YЖE БЫЛA
C...  ЗAMEHA B KYC-E HOMEPA ЛИCTA
  N,XTA,WL1-1
  ,AEX,ЧTEHИE
  C,ATX,TBUT-1.CTAPЫЙ KYC
  ,AEX,SCDICH.HOBЫЙ ЛИCT
  ,AAX,KPMASK
  N,AEX,WL1-1
  N,ATX,WL1-1
  BЫПOЛH//:,BSS,
 N,XTA,BITS
 C,ATX,SBUC-1
 N,XTA,WL1-1
 C,ATX,TBUC-1
 R,VJM,ДYБ
 U,XTA,T2MT-30B
  ,AAX,C7777
  ,U1A,EXCHMЛ1
 N,XTA,WL1-1
  ,AAX,B20/19
  ,UZA,ЧTCC
 N,AEX,WL1-1
  ,AOX,BIT19
  ,AAX,ZBIT12
 N,ATX,WL1-1
 ЧTCC:,BSS,
  ,XTA,BIT3
 N,AEX,WL1-1
  ,AOX,BIT2
 N,ATX,WL1-1
 EXCHCЛCЛ:,BSS,
 N,XTA,WL1-1
  ,AAX,B21*18
  ,U1A,EXC2
  ,XTA,REWINDSC
 U,AAX,BIT48-30B
  ,UZA,EXC2
  ,XTA,B21*18
 N,AOX,WL1-1
 N,ATX,WL1-1
 EXC2:,BSS,
 N,XTA,BITS
 C,ATX,SBUC-1
 N,XTA,WL1-1
 C,ATX,TBUC-1
  ,XTA,
 CW,ATX,3
 CW,ATX,6
  R,VJM,ДYБ
  R,VTM,B21*18
  N,XTA,WL1-1
   ,AAX,BIT4
   ,ASN,64+2
   ,AEX,C3
   ,ATI,MV
 ЗAПYCKMЛ:,BSS,
 RT,VTM,*7
 RO,VTM,-8
  :,ATX,1
 RO,VLM,*
  C,XTA,TBUC-1
  R,AOX,
   ,AAX,KYCMAC
   ,AOX,BN8
 DC,EXT,
   ,XTA,FULWOR
  :,24 ,2003B
 DC,AEX,BIT29-3
   ,ATI,
   ,MOD,37B
   ,ATI,
 RO,VJM,PRT UP
  :,24 ,3
   ,UTC,BIT14
 EXCHДBИЖ:,BSS,
   ,XTA,
   ,ATX,EXCHFL
  ,XTA,FIX DENS.BЫCOK.ПЛOTH.
  ,U1A,SET DENS.ФИKCИPOBAHA
 U,XTA,T1MT-30B
  ,AAX,B11/10
  SET DENS:,ATX,DENS
 RF,V1M,NO DENS
 NF,VZM,NO DENS
  U,XTA,T1MT-30B.BAPЬИPYEM
   ,AAX,BIT26.ПЛOTHOCTЬ
   ,ASN,64+15.ПPИ
   ,AOX,DENS.OПOЗHABAHИИ
   ,ATX,DENS
 NO DENS:,BSS,
 C,ARX,ШKEC-3
  ,U1A,MTEC5012
 ,XTA,DENS
 ,ASN,64-1
 ,ATX,DENS
  ,ITA,MV
  ,ATX,WS
  ,AAX,BIT2
  ,ASN,64+1
  ,AEX,B16/1
  ,AAX,WS
  ,ASN,64-2
  ,ATI,MV
  ,APX,MASK5017
  ,ANX,
  ,ASN,64-3
  ,ATI,R
  ,XTA,EXCHFL
  ,U1A,MTEC5012
 C,XTA,ШKEC-3
  ,AAX,BIT47
  ,UZA,MTEC5012
  ,XTA,TKYC17
 R,ASN,64-8
  ,AAX,C377
  ,ATI,MV
 MTEC5012:,BSS,
  ,ITA,MV
  ,AOX,DENS
 :,24,2003B
   ,AOX,EXCHFL
 U,EXT,100B-30B
   ,ASN,
   ,ASN,
  R,CALL,LSOSTEC1
  C,XTA,NEW-3
  I,AAX,BIT16
   ,U1A,NO READY
  I,XTA,BIT8
  C,AOX,CSUMSTAT-1
  C,ATX,CSUMSTAT-1
  I,XTA,BIT8
  C,AAX,ШKEC-3
  C,ATX,ДBECA-3
  ,XTA,MAXTIMET.YCT.TAЙMEPA
 C,ATX,MTTIMCNT-3
 C,XTA,BIT36-3
  ,AOX,TRAMASK
  ,ATX,TRAMASK
 :,24,3
 RT,UJ,
C
C...   ЗAXBAT YBY-KAHAЛA
C
 EXCHMЛ1:,BSS,
 N,XTA,BITS
 C,ATX,SBUC-1
 N,XTA,WL1-1
 C,ATX,TBUC-1
  ,AAX,ЧTEHИE
  ,U1A,EXЧTMЛ1
C
C...   ФOPMИPOBAHИE CЛYЖEБHЫX CЛOB
C      ЗAПИCЫBAEMOЙ ЗOHЫ
C
 :,24 ,2
  ,XTA,71777B
 :,24 ,3
 CW,ATX,5.ПOCЛ.CЛOBO
   ,XTA,CW0C
 CW,ATX,.CПEЦKOД
  U,XTA,TANAMEMT-30B
 CW,ATX,1.ИMЯ
 CW,ATX,2.ЛEHTЫ
   ,XTA,LWC
  :,24 ,2
   ,ATX,71777B
  :,24 ,3
  U,XTA,T2MT-30B
   ,AAX,B24/1
   ,AOX,CW36C
 CW,ATX,3.HOMEP
 CW,ATX,6.ЗOHЫ
  N,XTA,WL2-1
 CW,ATX,7.KOHTP.CYMMA
  N,XTA,WL1-1
  R,CALL,AUTOR
  CW,ATX,4.KTO ПИCAЛ
 EXЧTMЛ1:,BSS,
 C,XTA,TBUC-1
  ,AAX,BIT37
 :,UZA,*+1
  ,UTC,MAST2MT-B41/1
  ,XTA,B41/1
 U,AAX,T2MT-30B
 U,ATX,T2MT-30B
 EXCHДYБ:,BSS,
  R,VTM,
 MV,VTM,1
 DC,XTA,BIT5-3
   ,EXT,31B
  C,XTA,TBUC-1
   ,AAX,ЧTEHИE
   ,UZA,ЗAПYCKMЛ
  C,XTA,TBUC-1
   ,AAX,CЛYЖCЛOB
   ,U1A,HE ЧT ЗOH
  :,24 ,2
   ,ATX,71777B
 HEЧTЗOH:,BSS,
 :,24 ,3
  ,XTA,
 CW,ATX,3
 CW,ATX,6
 C,XTA,TBUC-1
  ,AAX,HAЛOЖ
  ,UZA,ЗAПYCKMЛ
 U,XTA,T2MT-30B
  ,AAX,BIT43
  ,UZA,ПPOДHAЛ
 U,AEX,T2MT-30B
 U,ATX,T2MT-30B
  ,XTA,HAЛOЖ
 C,AEX,TBUC-1
 C,ATX,TBUC-1
  ,UJ ,ЗAПYCKMЛ
 ПPOДHAЛ:,BSS,
  ,XTA,BIT42
 U,ARX,T2MT-30B
 U,ATX,T2MT-30B
  ,UJ ,ЗAПYCKMЛ
C
C...   OTPAБOTKA KOHЦA ДBИЖEHИЯ
C      MЛ B PEЖИME БЭCM-6
C
 KO БЭCM:,BSS,
   ,XTA,OUTJOBSC
 JN,AAX,BITS.HA CHЯTИE
   ,U1A,*OUTJOB.ЗAДAЧИ
  C,XTA,NEW-3
  I,AAX,BIT16
   ,U1A,NO READY.ПPOПAЛA ГT
  N,XTA,WL1-1
   ,AAX,BIT18
   ,U1A,MФ ГOT
   ,XTA,CONTRWR
  U,APX,BIT48-10B
  I,ASN,64+24
  C,AAX,NEW-3
   ,UZA,MФ ГOT
   ,UTC,3.ПPOПAЛA ЗП
C
C...   OTPAБOTKA HEГOTOBHOCTИ MФ
C
 NO READY:,BSS,
  R,VTM,3
  C,MTJ,RO
   ,ASN,64+8
   ,ATX,KYCДB
 MF,V1M,XOPOБM1.HA ПOДBOДE
 :,24,3
 RF,V1M,HE OПO
 NF,V1M,CT1
  I,XTA,BIT48
   ,AOX,KYCДB
   ,ATX,KYCДB
 HE OПO:,BSS,
 N,XTA,WL1-1
  ,AAX,BIT7. TT-ПEЧATЬ
  ,U1A,HE ГOTOB.YЖE БЫЛA
  ,XTA,BIT7
 N,AOX,WL1-1
 N,ATX,WL1-1
  ,ITA,U.COOБЩEHИE
 M,VJM,MФ ПЛOX.OПEPATOPY
 RO,MTJ,C
  I,XTA,BIT8
  C,AOX,W05741-3
  C,ATX,W05741-3
  C,XTA,TBUC-1
   ,UZA,*7
  C,XTA,SBUC-1
  N,AEX,BITS
   ,U1A,*7
C
C...   OCBOБOЖДEHИE YBY-KAHAЛA
C
  C,ATX,TBUC-1
  C,ATX,SBUC-1
 DC,XTA,BIT5-3
   ,EXT,31B
  N,XTA,BITS
  C,AEX,SRJC-1
  C,ATX,SRJC-1
  CW,MTJ,MF.AДP.CЛ.CЛ.
  R,VJM,VOP.ПOPTИT CW
  R,VTM,8
  ,CALL,YCTBEPCB.OT ГPEXA
  C,WTC,TYPEMT-3
  CW,VTM,
  N,XTA,WL1-1
   ,AAX,B21/18
 RO,VTM,*7
   ,UZA,RES LW
   ,AEX,B20/19
 RO,U1A,
C
C...   BOCCTAHOBЛEHИE ПOCЛEДHEГO
C      CЛOBA ЛИCTA OБMEHA
C
  RES LW:,BSS,
  CW,XTA,5
  :,24,2
  ,ATX,71777B
  :,24,3
  RO,UJ,
C
 HE ГOTOB:,BSS,
  ,XTA,KYCДB
 C,AOX,ШHГ-3
 C,ATX,ШHГ-3
  ,UJ,*1
C
C
 MФ ГOT:,BSS,
 ER,VTM,KO/ЗП
  N,XTA,WL1-1
   ,AAX,BIT7
   ,UZA,MФ TIM
  N,XTA,WL1-1.ECЛИ БЫЛA
   ,AAX,MASWL.ПPOПAЖA
   ,AEX,C13.ГT/ЗП,
  N,ATX,WL1-1.ПPOBEДEM
   ,XTA,B48/13.ЗAHOBO
  U,AAX,T2MT-30B.ИHИЦИAЛИЗAЦИЮ
  U,ATX,T2MT-30B
   ,UJ,HA BЫП
 MФ TIM:,BSS,
 U,XTA,T1MT-30B
  ,AAX,BIT12
 :,UZA,*+1
 R,VJM,TAPERR.БЫЛO "KДB"
  :MF,MTJ,ER.ПPЯЧEM
 MF,V1M,MФ ГOT1
 JN,XTA,*YCЫ
   ,ATI,R
   ,AAX,B30/16
   ,UZA,MФ ГOT1
  U,XTA,T1MT-30B
   ,AAX,B29/26
   ,U1A,MФ ГOT1
   ,XTA,TAPTIME
  R,A+X,BISTIME
  R,ATX,BISTIME
 MФ ГOT1:,BSS,
 C,XTA,CSUMSTAT-1
 I,AAX,ПOДBOД
  ,U1A,HA ПOДB
C
C...   ПPABKA "KЧ" B CЛYЖ.CЛOBAX OБMEHA
C
  CW,MTJ,MF
  R,VTM,8
  I,MTJ,RO
  :,24,2003B
  ,CALL,YCTBEPCB
  :,24,3
  RO,MTJ,I
  ER,MTJ,MF
  ER,VTM,KO/ЗП
C
 N,XTA,WL1-1
  ,AAX,C3
  ,AEX,C3
  ,U1A,БЫOБMЛ1
 N,XTA,WL1-1
  ,AAX,BIT3
  ,U1A,ЧTCC
  ,MOD,237B
 DC,AAX,BIT29-3
  R,VTM,HAЧ OБM MЛ
   ,UZA,TAPERR
  N,XTA,WL1-1
   ,AAX,BIT4
   ,UZA,EXCH CЛ CЛ
  N,AEX,WL1-1
  N,ATX,WL1-1
 NF,V1M,KOHTPИM
   ,XTA,REWINDSC
  U,AAX,BIT48-30B
   ,UZA,MЛ HA HAЧ
C
C...   KOHTPOЛЬ CЛYЖ.CЛOB ПPИ
C      ИHИЦИAЛИЗAЦИИ ЗAKAЗA
C
 KOHTPИM:,BSS,
   ,EXT,4035B
 DC,AAX,BIT12-3.OШBY
   ,U1A,OШMBY
 RO,VJM,ПPOB CЛ CЛ
   ,XTA,NZON
   ,AAX,ZBIT1
   ,ASN,64-12
   ,AEX,NZON
   ,AAX,B24/13
   ,U1A,TAPERR
 CW,XTA,1
   ,ATX,IDMT
 CW,AEX,2
   ,UZA,COBП ИД MЛ
 CW,XTA,4
   ,AEX,CW4C
 :NF,V1M,*+1
  R,VTM,COBП ИД MЛ
  :,UZA,TAPERR
   ,XTA,TP DI
   ,AAX,BIT DI
   ,UZA,TAPERR
 CW,XTA,2
  R,CALL,MTDISPAC
   ,ATX,IDMT
 RT,VTM,*1
 COBПИДMЛ:,BSS,
 NF,V1M,CT1
   ,XTA,IDMT
  U,AEX,TANAMEMT-30B
   ,UZA,COBП TN
  U,XTA,TANAMEMT-30B
   ,AEX,NO CHECK
   ,AAX,B48/13
   ,UZA,MT NO CH
   ,XTA,ШЧYMЛ
  U,AAX,BIT48-30B
   ,U1A,HAЧ OБM MЛ
  U,XTA,BIT48-30B
   ,AOX,ШЧYMЛ
   ,ATX,ШЧYMЛ
   ,ITA,U
  R,VTM,-3
  C,MTJ,RO
  M,VJM,MФ ПЛOX
 RO,MTJ,C
 HAЧOБMMЛ:,BSS,
  ,XTA,BIT4
 N,AOX,WL1-1
 N,ATX,WL1-1
  ,UJ,EXCH CЛ CЛ
 MT NO CH:,BSS,
  ,XTA,IDMT
 U,ATX,TANAMEMT-30B
 COBП TN:,BSS,
 N,XTA,WL1-1
  ,AEX,C3
 C,ATX,TBUC-1
 TO ПPOPE:,BSS,
  ,XTA,C7777
  ,AAX,NZON
  ,ATX,NZON
 R,VJM,ПPOB PEB
 N,XTA,BITS
 C,AEX,SRJC-1
 C,ATX,SRJC-1
 C,XTA,TBUC-1
  ,AAX,ZB37*23
 N,ATX,WL1-1
  ,XTA,
 C,ATX,TBUC-1
  ,UJ,HA ПOДB
C
C...   OKOHЧEH OБMEH C ЗOHOЙ
C      ИЛИ ЧTEHИE CЛYЖ.CЛOB ПPEДЗOHЫ
C
 БЫOБMЛ1:,BSS,
 R,VTM,OK OБ
 N,XTA,WL1-1
  ,AAX,B21*18
  ,U1A,KOPPHЗOH
 N,XTA,WL1-1
  ,AAX,B20/19
  ,UZA,KOPPHЗOH
  ,AEX,B20/19
 RO,VTM,*3 ЭT PAЗM
 R,VTM,RES LW
  ,UZA,KOPPHЗOH
 C,XTA,ШKEC-3
  ,AAX,BIT47
  ,ASN,64+28
  ,ARX,BIT19
 N,ARX,WL1-1
 ЗK*:,BSS,
 N,ATX,WL1-1
  ,UJ,HA BЫП
 *3ЭTPAЗM:,BSS,
 N,XTA,WL1-1
  ,AAX,BIT12
  ,U1A,XOPOБM1
 C,ATX,TBUC-1
  ,XTA,BIT20
  ,AEX,BIT12
 N,AEX,WL1-1
  ,UJ,ЗK*
C
C...   OKOHЧEH OБMEH C ЗOHOЙ
C
 OK OБ:,BSS,
 C,XTA,TBUC-1
  ,AAX,ЧTEHИE
 R,VTM,OШOБMЛ1
 RO,VTM,БЫЛOЧTMЛ
  ,U1A,ПPOB CЛ CЛ
  ,MOD,237B.KOHEЦ
 DC,AAX,BIT29-3.OБMEHA
   ,U1A,CH OШM ЗП. - БЫЛ
 RO,VTM,TAPERR. - HET
 R,VJM,RES LW
  ,XTA,B21*18.ПOBTOPИM
 C,AOX,TBUC-1.ЗAПИCЬ
 C,ATX,TBUC-1.OБEИX ЗOH
  ,UJ,ПOBOБMЛ3
 CH OШMЗП:,BSS,
   ,EXT,4035B
 DC,AAX,BIT5-3.OШM
 ER,VTM,
   ,UZA,CH ДYБ ЗП. - HET
 ER,VTM,OШ/ЗП
  R,VJM,TAPERR
 CH ДYБЗП:,BSS,
 U,XTA,T2MT-30B.OCHOBHAЯ
  ,ARX,BIT48.ЗOHA
  ,UZA,ЗAДYБЗOH. - ДA
 RO,VJM,RES LW
 ER,VZM,XOPЗAП1.ДYБЛЬ O'KEY
  U,XTA,T2MT-30B
   ,AAX,BIT44
   ,UZA,XOPЗAП1.OCH. O'KEY
 ПOBOБMЛ3:,BSS,
 C,XTA,TBUC-1
  ,AOX,BIT37
 N,ATX,WL1-1
C
C...   PEBEPC MЛ ДЛЯ ПOBTOPEHИЯ OБMEHA
C
  ПOBOБMЛ1:5,VJM,VOP
  BЗAД:,BSS,
  ,XTA,
 C,ATX,SBUC-1
 C,ATX,TBUC-1
 I,XTA,ПOДBOД
 I,AEX,PEBEPC
 C,AOX,CSUMSTAT-1
 C,ATX,CSUMSTAT-1
 N,XTA,BITS
 C,AEX,SRJC-1
 C,ATX,SRJC-1
 RT,VTM,TRAINT
  RO,VTM,ШЗH
 PRT DOWN:,BSS,
  ,XTA,PRTMTCH
 C,AEX,BIT44-3
  ,WTC,N8
  ,AOX,BIT44-3
 PRT SET:,BSS,
   ,ATX,PRTMTCH
 RO,UJ,
 PRT UP:,BSS,
  ,XTA,FULWOR
  ,WTC,N8
  ,AEX,BIT44-3
  ,AAX,PRTMTCH
 C,AOX,BIT44-3
  ,UJ,PRT SET
C
C...   ЗAПИCЬ ДYБЛЯ ЗOHЫ
C
 ЗAДYБЗOH:,BSS,
 :ER,VZM,*+1
   ,AOX,BIT44.OШИБKA B
  U,ATX,T2MT-30B.OCH.ЗOHE
  R,VJM,ДYБ
   ,XTA,C1
 CW,ARX,6.+1 K HOMEPY
 CW,ATX,6.ЗOHЫ
 CW,ATX,3
   ,UJ,EXCH ДYБ
C
C...   ЗAПИCAЛИ OБE ЗOHЫ
C
  XOPЗAП1:,BSS,
  U,XTA,T1MT-30B
  ,AAX,CONTROL
  ,UZA,XOPOБM1
C
C...   OPГAHИЗAЦИЯ KOHTPOЛЬHOГO ЧTEHИЯ
C
  ,XTA,CREADIS
  U,AOX,T1MT-30B
  U,ATX,T1MT-30B
  ,XTA,ЧTEHИE
  N,AOX,WL1-1
  N,ATX,WL1-1
  ,UJ,BЗAД
C
C...   P-  И  V-OПEPAЦИИ ДOCTYПA
C      K ЛИCTY CEKTOPHOГO OБMEHA
C      OБPAЩEHИE ПO  R.
C
  POP:C,XTA,BIT47
  ,UJ,PS*POP.ПOPTИT CW
C
  VOP:,BSS,.ПOPTИT CW,DC
  U,XTA,T1MT-30B
 ,AAX,CREADIS
  ,UZA,VOP1
  U,AEX,T1MT-30B
  U,ATX,T1MT-30B
  ,XTA,ЧTEHИE
  VOP1:N,AEX,WL1-1
  N,ATX,WL1-1
  ,AEX,SCDICH
  ,AAX,KPMASK
  R,U1A,.HE БЫЛO P
  C,XTA,TBUT-1.BOCCTAHOBЛEHИE
  N,ATX,WL1-1.ЗAKAЗA
  C,XTA,BIT47.HA ЗAПИCЬ
  ,UJ,PS*VOP
C
C...   OKOHЧИЛOCЬ ЧTEHИE ЗOHЫ
C
 БЫЛOЧTMЛ:,BSS,
  ,XTA,NZON
  ,AAX,ZBIT1
  ,ASN,64-12
  ,AEX,NZON
  ,AAX,B24/13.HOMEP ЗOHЫ
  ,U1A,TAPERR.БPEДOB
  ,XTA,NZON
  ,ARX,C1
 U,AEX,T2MT-30B
  ,AAX,C7777
 R,VTM,TO ПPOPE
  ,U1A,TAPERR.HE TA ЗOHA
 N,XTA,WL1-1
  ,AAX,CЛYЖCЛOB
  ,U1A,БЫ ЧT CC2
 RO,VTM,KЧ ЧT
  ,ITA,RO
  ,ATX,RETTRA
  ,XTA,C2003
  ,ATI,21B
 :,24 ,2002B
  ,XTA,71777B
 :,24 ,2003B
  ,AEX,LWC
 ER,VTM,CБ/CИ
  R,VTM,ГAШ HAЛ
   ,U1A,TAPERR
   ,XTA,C13
   ,ATI,21B
   ,EXT,4035B
   ,ATX,PE FL
   ,ASN,64+7
   ,AOX,PE FL
 DC,AAX,BIT5-3
   ,ATX,PE FL
   ,UZA,MAKE KS
  R,VJM,OШMBY
  C,XTA,TBUC-1
   ,AAX,HAЛOЖ
   ,UZA,SET HAЛ
 MAKE KS:,BSS,
 CW,XTA,7
   ,ATX,CHSUMR
 CW,XTA,5
  :,24 ,2002B
   ,ATX,71777B
  :,24 ,2003B
 ER,VTM,-37B
 CW,VTM,
  R,VJM,SUMMING
   ,AEX,CHSUMR
   ,U1A,OШ KC
  U,XTA,T1MT-30B
  ,AAX,CREADIS
  ,UZA,XOPOБM1
C
C...  ПPИ KOHTPOЛЬHOM ЧTEHИИ
C     CPABHИM CЧИTAHHYЮ KC C
C     CYMMOЙ ЛИCTA, ЧTOБЫ ЗACEЧЬ
C     CИTYAЦИЮ, KOГДA ЗAПИCЬ
C     HE ПPOИЗOШЛA BOOБЩE.
C
  ,XTA,CHSUMR
  N,AEX,WL2-1
  ,UZA,XOPOБM1
C
C...     CYMMЫ HE COBПAЛИ
C
  OШ KC:,BSS,
 ER,VTM,KC/MЛ
  R,VJM,TAPERR
   ,XTA,PE FL
   ,U1A,SET HAЛ
 ГAШ HAЛ:,BSS,
  C,XTA,TBUC-1
  ,AOX,HAЛOЖ
  ,AEX,HAЛOЖ
  ,UJ,WR TBUC
 KЧ ЧT:,BSS,
 R,VJM,OШMBY
 SET HAЛ:,BSS,
  ,XTA,HAЛOЖ
 C,AOX,TBUC-1
 WR TBUC:,BSS,
 C,ATX,TBUC-1
  ,XTA,NH
  ,ATI,DC
 DC,XTA,BC1700
  ,ATI,CW
  ,XTA,BIT48
 U,ARX,T2MT-30B
  ,U1A,ПOBOБMЛ3
 U,ATX,T2MT-30B
 R,VTM,EXCH ДYБ
  ,UJ,ДYБ
 OШOБMЛ1:,BSS,
 N,XTA,WL1-1
  ,AAX,CЛYЖCЛOB
  ,UZA,ГAШ HAЛ
 MF,V1M,ГAШ HAЛ
  ,UJ,ПOBOБMЛ1
 БЫЧTCC2:,BSS,
 MF,V1M,XOPOБM1
  ,XTA,Z21*18*1
 N,AAX,WL1-1
 N,ATX,WL1-1
  ,UJ,BЫПOЛH
 OШMBY:,BSS,
 ER,VTM,OШ/ЧT
   ,UJ,TAPERR
C
C...  ИЗBЛEЧEHИE HOMEPA ЗOHЫ
C     ИЗ CЛYЖEБHЫX CЛOB
C
 ПPOBCЛCЛ:,BSS,
 CW,XTA,3.KOПИЯ 1
 CW,AOX,6.KOПИЯ 2
   ,ATX,NZON
   ,AAX,B39/25
   ,AEX,CW36C.CПEЦKOД
 ER,VTM,OШ/CC
 RO,UZA,
C
C...   OБPAБOTKA OШИБOK OБMEHA C MЛ
C
 TAPERR:,BSS,
   ,XTA,C13
   ,ATI,21B
   ,XTA,BIT26
  U,ARX,T1MT-30B.CЧ-K ПO
  ER,AOX,BIT36-1
  ,ATX,WS
  U,ATX,T1MT-30B.TEK.OБMEHY
   ,APX,B30*12*5.HE ЛOПHYЛ
  R,UZA,
  ,ASN,64-1
  ,UZA,TERR1.ER ЗAДAHO
  ER,VTM,OШ/ЗП
   ,AAX,BIT48
  :,UZA,TERR1
 ER,VTM,KДB/MФ
  TERR1:,BSS,
  C,MTJ,RO
 RF,VZM,ERREC
  M,VTM,TЦKMФ
 ER,XTA,MTDIAGN-1
  M,ATX,
   ,ITA,U
   ,ASN,64-42
   ,AUX,BY1/2
   ,AOX,Z12Ш
  M,ATX,1
 JN,XTA,*YCЫ
   ,ASN,64-27
   ,AUX,BY5/6
  M,AOX,1
  M,ATX,1
  U,XTA,T2MT-30B
   ,ASN,64+13
   ,ARX,C77774
   ,AAX,C3777
   ,ATX,TИПOШ
   ,ASN,64-33
   ,AUX,BY1/5
   ,AOX,TNZON
  M,ATX,2
   ,ITA,M
  M,VJM,TTPRINT+1
  :,24 ,3
 RO,MTJ,C
  ,XTA,B36/31.ГACИM "KO","KДB"
  ,AOX,MAST1MT.И CЧ-K OШИБOK
  U,AAX,T1MT-30B
  U,ATX,T1MT-30B.ГOTOBИM CYMMATOP
  ,ASN,64+6.ДЛЯ "ПЛOXAЯ MЛ":
  ,ASN,64-12.MATHOM, БИTЫ OШИБOK,
  ,AOX,TИПOШ.HOMEP ЗOHЫ
  ,ATX,TИПOШ
   ,XTA,WS
   ,ASN,64+6
   ,AOX,WS
   ,AAX,C6
  R,UZA,
   ,AEX,BIT2
  :,U1A,*+1
   ,UTC,MЛKAЮK
 RF,VTM,.7
 RT,VTM,TИПOШ.9
  L,VTM,.11
  N,XTA,WL1-1
 RO,VTM,XOPOБM1
   ,UJ,ABCHT
 CT1:,BSS,
  ,XTA,NZON
  ,ARX,C1
  ,AAX,C7777
  ,ATX,NZON
 R,VJM,SET2MT
  ,XTA,IDMT
  ,UJ,CT2
 ERREC:,BSS,
   ,ITA,U
  R,VTM,
 :NF,VZM,*+1
  M,VJM,MФ ПЛOX
 RO,MTJ,C
   ,XTA,
 CT2:,BSS,
 JN,WTC,*YCЫ
   ,ATX,
   ,ITA,U
 JN,WTC,*YCЫ
   ,ATX,I14
   ,UJ,XOPOБM1
 *OUTJOB:,BSS,
 NF,V1M,XOPOБM1
 MF,V1M,XOPOБM1
   ,XTA,
  :,24 ,2
  R,VTM,-1777B
 :R,ATX,71777B
  R,VLM,*
  XOPOБM1:,24,3
  U,XTA,T1MT-30B
  ,ASN,64-12.OШИБKИ
  ,AUX,C200*.TEK.OБMEHA
  ,UZA,AFT ST. - HE БЫЛO
  ,ASN,64+7
  ,ATX,БYCT+1.CБPOC OШИБOK
  N,XTA,WL1-1.B ИHЖ.
   ,ATX,БYCT.CTATИCTИKY
  ,XTA,ST/T
  ,UJ,STATIST
  AFT ST:,BSS,
   ,XTA,FULWOR
  I,AEX,ПOДBOД
  I,AEX,PEBEPC
  C,AAX,CSUMSTAT-1
  C,ATX,CSUMSTAT-1
  C,XTA,SBUC-1
  N,AEX,BITS
   ,U1A,CLEAR
  C,ATX,TBUC-1
 RO,VJM,PRT DOWN
 CLEAR:,BSS,
   ,XTA,MAST1MT
  U,AAX,T1MT-30B
  U,ATX,T1MT-30B
   ,XTA,FULWOR
  U,AEX,BIT48-30B
   ,AAX,ШЧYMЛ
   ,ATX,ШЧYMЛ
  R,VJM,VOP
  RT,VTM,TRAINT.BOЗBPAT
  MF,VIM,TASKOFF.ПOДBOД
  C,WTC,TYPEMT-3
  CW,VTM,.AДPEC CЛ.CЛOB
C
C...   БЛOK OKOHЧAHИЯ ЗAKAЗA HA OБMEH
C      RT - BOЗBPAT
C      N - ИHДEKC ПO WL1 ИЛИ 0
C      CW - AДPEC CЛYЖ.CЛOB (ДЛЯ MЛ И MД)
C
C      ПPOИЗBOДИTCЯ, ECЛИ HAДO, ПEPEПИCЬ
C      CЛYЖ.CЛOB B ИПЗ ЮЗEPA.
C      OTKPЫBAETCЯ ЗAДAЧA ИЛИ EE MAT.ЛИCT
C      ПPИ HEHYЛEBOM  N ДEЛAETCЯ BЫЧИCTKA
C      ЗAKAЗA ИЗ OЧEPEДИ OБMEHOB.
C
C      ПOPTЯTCЯ: MV,R,TP,F,P,S
C
C
 KOHOБMEH:,ENTRY,.MЛ/MД
  :,24,3
  N,XTA,WL1-1
  ,ASN,64+42
  ,ATI,F. Д H З
  ,XTA,SCSWING
  N,AAX,BITS
  ,U1A,KOHOБMБ.ПOДKAЧKA
  ,XTA,ИПЗ/ECTЬ
  F,AAX,BITS
  ,UZA,KOHOБMБ.HET ИПЗ
  ,AAX,Ш/ДИCП
  ,U1A,KOHOБMБ.ДИCП.ЗAKAЗ
C
C...   ПPOБA: ECTЬ ЛИ ЗAKAЗ HA
C      ПEPECЫЛKY CЛYЖ.CЛOB ЮЗEPY
C
  F,WTC,*YCЫ
  S,VTM,CWPLAC
  S,XTA,REGSCAL-CWPLAC
  ,AAX,BIT39
  ,UZA,KOHOБMБ
  R,VTM,-7
C
C...   ПEPEПИCЬ B ИПЗ ЮЗEPA
C!!!   ECЛИ ЗAДAЧA BЫДAЛA ЗAKAЗ
C      HA ЧT.ИMEHИ, A EE ИПЗ YKAЧAЛИ
C???   BЫЙДET HEПPИЯTHOCTЬ
C
  :CW,UTC,
  R,XTA,7
  S,ATX,
  R,VLM,*-1
C
  KOHOБMБ:N,XTA,WL1-1.BXOД ДЛЯ MБ
  ,ATX,OFFKYC
C
  OPEN:,BSS,.BXOД ДЛЯ ERTREQ
   ,XTA,OFFKYC
   ,APX,KPMASK
   ,ASN,64+11
   ,ATX,MPAGE
  ,ASN,64+30
   ,ATI,P.ФИЗ.ЛИCT
C   ЧT.ИMEHИ MЛ - P=0
C   RECMDT      - P=77
   ,XTA,OFFKYC
   ,AAX,BIT38
   ,ASN,64+23
  ,ATI,TP.TИП ЗAKAЗA
 P,XTA,TPHYS
  ,ASN,64-15
  :TP,VZM,*+1.ИЗ TPHYS
  ,XTA,OFFKYC.ИЗ KYC-A
  :,ASN,64+42
  ,ATI,F.ДHЗ
C   F=0 : TP>0  RECMDT
C         TP=0  SWING, ECЛИ ЗAKPЫTA ПO RP,
C               A ЗAKOHЧEH OБMEH C NEWRP
 F,XTA,*YCЫ
  ,ATI,S.ИПЗ
 TP,V1M,OPETA
  ,AAX,B37/31
  ,AEX,MPAGE
  ,U1A,CLEXB
C...   ЗAДAЧA ЗAKPЫTA "ПO ЛИCTY"
  ,XTA,MPAGE
 F,AEX,*YCЫ
 OPETA:,BSS,
 F,ATX,*YCЫ
  ,XTA,EXCHSC.BЫЧИCTKA ИЗ
  F,AOX,BITS.ШKAЛЫ ЗAДAЧ,
  F,AEX,BITS.ЗAKPЫTЫX ПO
  ,ATX,EXCHSC.OБMEHY
 F,XTA,BITS
  ,AOX,OFFESC
  ,ATX,OFFESC
  CLEXB:,XTA,ZB7*6
  P,AAX,TPHYS
  P,ATX,TPHYS
  F,VZM,ENDOP.RECMDT,SWING
  ,APX,MMNTPL
  ,ASN,64+36
  ,ATI,MV.MAT.ЛИCT
  ,ASN,64+5
  ,U1A,ENDOP
  ,XTA,Ш/ДИCП
  F,AAX,BITS
  ,U1A,ENDOP
C   ДЛЯ ЗAKAЗA OT ЭKCTPAKOДA
C   C ЗAKPЫTИEM ЗAДAЧИ (ФИЗ.ПAKET,
C   TECT.CTATYC) HYЖHO ЧИCTИTЬ TMATH
  TP,VIM,NOP
  N,VZM,HE COБ
C
C...   ГEHEPAЦИЯ ДЛЯ ЮЗEPA
C      COБЫTИЯ: "KOHEЦ OБMEHA"
C
  S,XTA,SCEX
   ,UZA,HE COБ
 MV,AOX,BIT48
  S,ATX,SCEX
  F,XTA,*YCЫ
   ,AEX,
   ,XTA,HA COБ
   ,UJ,COБЫTИE
C
C...   OTKPЫTИE ЛИCTA ЮЗEPA
C      ПO ПPИПИCKE И ЗAЩИTE
C
 HE COБ:,BSS,
 :,24 ,3
  ,ITA,MV
 ,ASN,64+3
 ,AEX,C3
 ,ATI,R
  ,ITA,MV
 ,AAX,C7
 ,AEX,C7
  S,J+M,R
 ,ATI,TP
 TP,XTA,BIT28
 ,AEX,FULWOR
 R,AAX,TЗAЩ
 R,ATX,TЗAЩ
 ,ITA,MV
 ,ASN,64+2
 ,AEX,C7
 ,ATI,R
 ,ITA,MV
 ,AAX,C3
  S,J+M,R
 ,ATI,TP
 ,ITA,P
 ,ASN,64-41
 TP,AUX,TMPMASK
 R,AOX,TПPИП
 R,ATX,TПPИП
 NOP:,BSS,
  P,VZM,ENDOP.ЧT.ИMEHИ
  S,J+M,MV
  ,XTA,FULWOR
  ,AEX,BIT46
  MV,AAX,TMATH
  MV,ATX,TMATH
 ENDOP:,BSS,
  N,VZM,ENDMAC70
C
C...   ПOДПPOГPAMMA ИCKЛЮЧEHИЯ ЗAKAЗA
C      ИЗ OЧEPEДИ ЗAKAЗOB HA OБMEH
C
 TASKOFF:,ENTRY,
 TASKOFF:,BSS,
 LL1:,EQU,LQTRA-1
 LL2:,EQU,LQTRA-2
  ESJMT:,EQU,SCSWING
 :,24 ,2003B
  ,XTA,FULQUESC
  ,AOX,OFFESC
  ,ATX,OFFESC
  ,XTA,
  ,ATX,FULQUESC
 :,24 ,3
  ,XTA,TRACNT
  ,UZA,BADEXCH
  ,ARX,C377
  ,AAX,C377
  ,ATX,TRACNT
  ,XTA,FULWOR
 N,AEX,BITS
  ,ATX,OFFKYC
 F,VTM,SBUC-ESJMT
 OFFSC:,BSS,
 F,XTA,ESJMT
  ,APX,OFFKYC
 F,ATX,ESJMT
 F,VLM,OFFSC
 N,XTA,WL1-1
  ,ATX,OFFKYC
 N,MTJ,F
 F,UTM,-LL1
 OFFWL:,BSS,
 F,XTA,WL1+LL1
 F,ATX,WL1+LL2
 F,XTA,WL2+LL1
 F,ATX,WL2+LL2
 F,VLM,OFFWL
 RT,UJ,
C
C-----
C
C......OPГAHИЗAЦИЯ ПEPEXOДOB ЧEPEЗ MГЗ
C      ДЛЯ ЗABOДCKOГO KOHTPOЛЛEPA .....
 ДYБ:,BSS,
 14,XTA,ШKEC-3
 ,AAX,BIT47
 5,UZA,
  ,ITA,5
  14,ATX,SCMT57-3
  14,XTA,ШKEC  -3
 13,AAX,BIT24
 ,UZA,SKIPMARK
 OБM5017:,BSS,
 14,AEX,ШKEC-3
 14,ATX,ШKEC-3
  U,XTA,T2MT-30B
 ,AAX,C/MГЗ
  ,U1A,MAPKEP
 SKIPMARK:,BSS,
 14,XTA,CSUMSTAT-1
 13,AOX,ПOДBOД
 14,ATX,CSUMSTAT-1
  13,XTA,BIT24
 14,AOX,ШKEC-3
 14,ATX,ШKEC-3
 MV,VTM,1
  ,UJ,EXCHДBИЖ
  MAPKEP:,XTA,FULWOR
  13,AEX,PEBEPC
  13,AEX,ПOДBOД
  14,AAX,CSUMSTAT-1
  14,ATX,CSUMSTAT-1
C
C...   ,XTA,.CБPOC
C      ,EXT,32B.БAYCA
C
  14,XTA,SCMT57-3
  ,ATI,5
  5,UJ,
C
C---
C
 MФ ПЛOX:,ENTRY,
 MФ ПЛOX:,BSS,
C...OTPAБOTKA HEГOTOBHOCTИ MФ
C...HA CYMMATOPE N. MФ
C...И.P.5 - TИП CИTYAЦИИ
C   0 - HE OПOЗHAHA MЛ.
C   3 - HE ГOTOB MФ.
C   6 - HET ЗAПИCИ HA MФ.
 ,ATX,HOMФ
 5,VIM,MФП.HEГOTOB.
 ,XTA,MISTERX.
 ,WTC,HOMФ.
 ,AOX,BIT48-30B.
 ,ATX,MISTERX.
 MФП:,XTA,HOMФ.
 ,ASN,64-42.
 ,AUX,BY1/2.
 ,AOX,Z12.
 5,UTM,THEOПO.
 5,ATX,2.
 ,ITA,5.
 14,MTJ,5.
   :   ,24,2003B.
 14,VJM,TTPRINT.
   :   ,24,3.
 5,UJ,.
 SUMMING:,ENTRY,.
 SUMMING:,BSS,
C
C    I4 И I11 ЗAДAЮTCЯ ПPИ OБPAЩEHИИ
C
 CYMMИP:,XTA,.
 ,ATX,RRR.
 L01551:10,VTM,-31.
 ,XTA,C2003.
 ,ATI,21B.HET OCT. ПO KЧ
 ,XTA,RRR.
   :   ,24,2002B.
 11,ARX,70000B.
 11,UTM,1.
 10,VLM,*-1.
   :   ,24,2003B.
 ,ATX,RRR.
 ,XTA,C13.
 ,ATI,21B.OCTAHOB ПO KЧ
 ,ATI,.
 ,ATI,.
 4,VLM,L01551.
 ,XTA,RRR.K.CYMMA
 5,UJ,.
C
C-----  OCTAHOB ПO BCЯKИM БЯKAM
C
  BAD EXCH:,BSS,
  ,33,70707B
  ,UJ,*
C-----
 ,END,
 ПPOBMЛ:,NAME,
C
C*****   ИЮЛЬ 1983.  H.C.ГOHЧAPOB
C
C      ПPOBEPKA COCTOЯHИЯ MAГHИTOФOHOB
C
 *YCЫ    :LC,BLOCK,ГYC,RDSCAL
 *ПДИCK  :,LC,1
 TABLEMT :LC,BLOCK,(16),T1MT(32),(16),T2MT(32)
 REWINDSC:LC,BLOCK,(3),CHBOSS(4)
 STATUSMT:LC,BLOCK,MBUS,OLD(4),NEW(4)
            ,CONT,(4),SOST(2)
            ,CONT ,ДBECA(4),SCANER
            ,CONT ,ШKEC(4),(4),ШHГ(4)
 *STATBUF:LC,BLOCK,(1),MOUT
 CMDREC  :LC,BLOCK,(2),NMOD,(1),CEKMET
            ,CONT ,RCHBUSY
 IFREC   :,EQU,632B
 *0      :,BLOCK,(3),S33(2),S27
 GIVTRA  :,SUBP,
 TRATAPE :,SUBP,
 INITTAPE:,SUBP,
 MDRECOGN:,SUBP,
 ПPOBГOMЛ:,EQU,MDRECOGN
 *677    :B,BLOCK,BITS(11),BIT38(2),BIT36(10)
           ,CONT ,BIT26(10),BIT16(3),BIT13
           ,CONT ,BIT12(7),BIT5(4),BIT1
 B41/25  :,EQU,=:0037 7777
 B24/10  :,EQU,=7777 7000
 B24/9   :,EQU,=7777 7400
 B24/1   :,LC,1
 C1      :,EQU,BIT1
 C3      :,LC,1
 C7      :,LC,1
 C13     :,LC,1
 *CB     :LC,BLOCK,(1),C17
 C37     :,LC,1
 C77     :,LC,1
 C777    :,LC,1
 DISREG  :,LC,1
 BC1700  :,SUBP,
 BITJREC :,EQU,BIT26
 C/MГЗ   :,EQU,BIT36
 C/ПEP   :,EQU,=:0000 06
 TPИБИT  :,EQU,=4010 0200
 *1      :,BLOCK,U(4),R(8),I,C,S
  ,XTA,C1
  ,ARX,SCANER
  ,AAX,C37
  ,ATX,SCANER
  ,AAX,C3
  ,ATI,C
 C,UTM,3
  ,XTA,*ПДИCK
 C,AAX,BITS
  ,UZA,TIMD
 S,VTM,SREG
  ,ITA,U
  ,ASN,64-15
  ,ITS,R
 S,AEX,
  ,ASN,64-15
  ,ITS,I
 S,AEX,
 R,VTM,
 C,XTS,ШKEC-3
  ,UZA,LSOSTEC1
 C,XTA,CHBOSS-3
  ,U1A,CHECK
 C,XTA,ДBECA-3
  ,ATI,S
  ,ANX,C17
  ,AEX,C77
 S,V1M,TO/LS
  ,XTA,SCANER
  ,ASN,64+2
 TO/LS:,BSS,
 C,ARX,BC1700
  ,ATI,U
  ,AAX,C7
  ,AEX,C7
  ,ATI,I
 C,XTA,ШKEC-3
  ,AAX,B41/25
 S,V1M,LSOSTEC1
 LSOSTEC:,ENTRY,
 :,24 ,2003B
 U,EXT,100B-30B
 LSOSTEC1:,ENTRY,
 LSOSTEC1:,BSS,
  ,AAX,B41/25
  ,ATX,RS*MASK
 C,XTA,ШKEC-3
  ,U1A,MTEC
 C,EXT,4100B
 C,AEX,NEW-3
  ,AAX,B24/1
 C,AEX,NEW-3
 C,ATX,NEW-3
  ,UJ ,LS/RET
 MTEC:,BSS,
 S,VTM,LS/RET
  ,XTA,TPИБИT
 I,ASN,64
  ,ATX,MASK
 C,ARX,ШKEC-3
  ,U1A,K5H
  ,EXT,4117B
  ,ASN,64-1
  ,AAX,B24/10
  ,ATX,SOST
  ,EXT,4115B
  ,AAX,C777
  ,AOX,SOST
  ,ATX,SOST
  ,ASN,64-24
  ,AOX,SOST
  ,ATX,SOST
  ,AEX,BIT1
  ,APX,=20 0000 0201
  ,AUX,MASK
  ,AEX,MASK
  ,UJ ,SETNEW
 K5H:,BSS,
 C,EXT,4110B
  ,ASN,64-24
  ,ATX,SOST
 C,EXT,4100B
 SETNEW:,BSS,
 C,AEX,NEW-3
  ,AAX,MASK
 C,AEX,NEW-3
 C,ATX,NEW-3
 R,V1M,SET2
  ,XTA,ГYC
  ,AEX,ГYC+26B
  ,UZA,LS/RET
 SET2:,BSS,
  ,XTA,SOST
 U,AEX,T2MT-30B
  ,AAX,RS*MASK
 U,AEX,T2MT-30B
 U,ATX,T2MT-30B
  ,YTA,
  ,AAX,C/ПEP
  ,UZA,LS/RET
 U,AEX,T2MT-30B
  ,AAX,C/ПEP
  ,AEX,C/ПEP
  ,UZA,TRA TAPE
 LS/RET:,BSS,
 R,UTC,
 R,V1M,
 CHECK:,BSS,
  ,XTA,SREG
  ,ATI,I
  ,ASN,64+15
  ,ATI,R
  ,ASN,64+15
  ,ATI,U
 C,XTA,NEW-3
 C,AEX,OLD-3
  ,AAX,B24/9
  ,AOX,MOUT
  ,AOX,NMOD
  ,AOX,CEKMET
  ,UZA,CH/ST
  ,XTA,MBUS
  ,U1A,CH/ST
  ,XTA,BITJREC
  ,AOX,RDSCAL
  ,ATX,RDSCAL
 S,VTM,ПPOBГOMЛ
  ,ITA,S
  ,ATX,IFREC+S33
  ,XTA,C13
  ,ATX,IFREC+S27
  ,ATX,MBUS
 CH/ST:,BSS,
 C,XTA,ШHГ-3
  ,ASN,64+32
  ,UZA,TIMD
 C,AAX,NEW-3
 S,VTM,
  ,UZA,INITTAPE
 TIMD:,BSS,
  ,XTA,SCANER
  ,U1A,NOTSEC
  ,XTA,C1
  ,ATX,CEKMET
 NOTSEC:,BSS,
  ,AEX,BIT5
  ,AOX,RCHBUSY
 I,U1A,
 I,MTJ,S
  ,XTA,BIT38
  ,UJ ,GIVTRA
 SREG   :,BSS,1
 RS*MASK:,BSS,1
 MASK   :,BSS,1
  ,END,
  EC*608:,NAME,
C*
C*****  A.Г.ЩИPEHKO, A.П.CAПOЖHИKOB,
C*            H.C.ГOHЧAPOB
C*
C*     YПPABЛEHИE  MAГHИTOФOHAMИ
C*     EC5012,  EC5017,  CDC608
C*
C*******************************************
C*
C*             ,XTA,INF
C*             ,*57,7777B
C*
C*      INF - ИHФOPMAЦИOHHOE CЛOBO :
C*      ----------------------------
C*  1. ДЛЯ BCEX TИПOB MAГHИTOФOHOB :
C*     13-18 PAЗP. - MAT.HOMEP MФ.
C*           ( ДЛЯ CDC608 - 0 ).
C*     31-35  - MAT.HOMEP ЛИCTA.
C*     37-40  - KOД OПEPAЦИИ.
C*     48-47  - ПЛOTHOCTЬ ЗAПИCИ
C*           (ECЛИ HE YKAЗAHA ПPИ ЗAKAЗE MЛ)
C*     24     - TECTOBЫЙ PEЖИM
C*           (БEЗ ПOBTOPEHИЯ OБMEHA).
C*  2. ДЛЯ MФ CDC608 :
C*     46     - TИП ЧETHOCTИ (BCD ИЛИ BIN)
C*     45     - OБMEH 256 CЛOB, ИHAЧE 512.
C*  3. ДЛЯ MAГHИTOФOHOB EC :
C*     23     - ЧTEHИE C HAЛOЖEHИEM
C*     22     - БЛOK.ЗAПИCИ B MOЗY OШ.БAЙTA
C*     21     - OБMEH ПO 80 (160) БAЙTOB.
C*     19     - OБMEH ЛИCTOM, ИHAЧE CEKTOPOM
C*           (ПPИ "0" B 1-12 PAЗPЯДAX).
C*     1-12   - OБMEH ЗAДAHHЫM ЧИCЛOM CЛOB.
C*     27-25  - KOЛ. HEИCП. БAЙTOB
C*           ( ECЛИ HE "0" B 12-1 P. )
C*
C*******************************************
C*
C*       K O Д Ы   O П E P A Ц И Й :
C*       ---------------------------
C*       0 - OПPOC COCTOЯHИЯ
C*       1 - ЧTEHИE
C*       2 - ЗAПИCЬ
C*       3 - ШAГ HA ЗOHY BПEPEД (ШЗB)
C*       4 - ШAГ HA ЗOHY HAЗAД  (ШЗH)
C*       5 - ПOИCK MAPKEPA BПEPEД (ШГЗB)
C*       6 - ПOИCK MAPKEPA HAЗAД  (ШГЗH)
C*       7 - ПEPEMOTKA
C*       8 - ПEPEMOTKA C PAЗГPYЗKOЙ
C*       9 - ЗAПИCЬ MAPKEPA (ЗПMГЗ)
C*      10 - CTИPAHИE
C*
C*******************************************
C*
C*   HA BЫXOДE ИЗ ЭKCTPAKOДA И.P.14
C*   COДEPЖИT KOЛИЧECTBO ПPOЧИTAHHЫX
C*   (ЗAПИCAHHЫX) CЛOB.
C*   CYMMATOP HA BЫXOДE :
C*   48-25  - CЧETЧИK PEKOPДOB
C*   16-1   - PEГИCTP COCTOЯHИЯ MФ.
C*
C*******************************************
C*   БИTЫ PEГИCTPA COCTOЯHИЯ :
C*   16 - БЫЛ ПPOГPAMMHЫЙ CБPOC ("KДB")
C*   15 - БЫЛA OШИБKA ПPИ ЧTEHИИ ЦKC
C*   14 - ПPИЗHAK ПEPEПOЛHEHИЯ БAYC-A
C*   13 - БЫЛ CБOЙ ПPИ ЗAПИCИ
C*   12-11  - PEЗEPB
C*   10 - COCTOЯHИE ДBИЖEHИЯ HAЗAД
C*    9 - COCTOЯHИE ПEPEMOTKИ
C*    8 - ГOTOBHOCTЬ (ГT)
C*    7 - COCTOЯHИE ДBИЖEHИЯ (CДB)
C*    6 - HET ЗAЩИTЫ ЗAПИCИ (HЗЗ)
C*    5 - KOHEЦ ЛEHTЫ (KЛ)
C*    4 - CЧИTAH (ЗAПИCAH) MAPKEP (MГЗ)
C*    3 - HAЧAЛO ЛEHTЫ (HЛ)
C*    2 - OШИБKA ПPOДOЛЬHOГO KOHTPOЛЯ (OПK)
C*    1 - OШИБKA ЧETHOCTИ БAЙTA (OЧБ)
C*
C******************************************
C*
 '''''''''''''''''''''''''''''''''''''''''
 '                                       '
 '      ПPOГPAMMA OБCЛYЖИBAET TAKЖE      '
 '      ЭKCTPAKOДЫ ,*57,1 И ,*57,4       '
 '      C ИHTEPФEЙCOM  A.B.ГYCEBA.       '
 '                                       '
 '''''''''''''''''''''''''''''''''''''''''
 '
 '
 *677:B,BLOCK,BITS,B48,B47,B46,B45
       ,CONT,B44,B43,B42,B41,B40,B39
       ,CONT,B38,B37,B36,B35,B34,B33
       ,CONT,B32,B31,B30,B29,B28,B27
       ,CONT,B26,B25,B24,B23,B22,B21
       ,CONT,B20,B19,B18,B17,B16,B15
       ,CONT,B14,B13,B12,B11,B10,B9
       ,CONT,B8,B7,B6,B5,B4,B3,B2,B1
 *YCЫ:LC,BLOCK,ГYC,RDSCAL
 *ИПД:LC,BLOCK,(42),OUTJOBSC
 SMASAV:LC,BLOCK,SAVAC,SAVSR,SAVAR
          ,CONT,SAVI15,SAVI14,SAVI13
  Д H З :LC,BLOCK,(1),BITJ,ZBITJ
 TPHYS:,LC,1
 STATUSMT:LC,BLOCK,(5),NEW(4),(6)
 ,CONT,ДBECA(4),(1)
        ,CONT,ШKEC(4)
    ,CONT,(4),ШHГ(4)
 ,CONT,MISTERX
 SCALSTRA:LC,BLOCK,SBUC(6),SRJC(6)
 TASKSTRA:LC,BLOCK,TBUC(6),TBUT(6),CSUMSTAT
  DISREG:,LC,1
  ABTIME:,LC,1
  READSCAL:,LC,1
  LISTSTRA:LC,BLOCK,WL1
 COMPUT16:LC,BLOCK,CANYBY,(1),B11OFKUS
 REWINDSC:LC,BLOCK,(1),ЗAKЗAП
  TIMERS:LC,BLOCK,(4),MTTIMCNT(4)
  CONTIME:LC,BLOCK,(6),TCDC608
  YCTP:LC,BLOCK,(6),YCDC608
 TABLEMT:LC,BLOCK,(16),T1MT(32),(16)
     ,CONT,T2MT(32),(16),TANAM
 BASEEC:,LC,1
 C76000:,LC,1
 C77777:,LC,1
 C3777:,LC,1
 C1777:,LC,1
  C777:,LC,1
 C377:,LC,1
 C177:,LC,1
 C77:,LC,1
 BC1700:,SUBP,
 C30:,EQU,BC1700+3
  C60:,EQU,BC1700+6
 B24/1:,LC,1
  *CB:,LC,
  B48/13:,EQU,*CB+4
  *TLCONST:,LC,
  B48/47:,EQU,*TLCONST+2
  MASK12:,LC,
  C7:,LC,1
 *C:LC,BLOCK,C6
 C3:,LC,1
  ERRMACRO:,SUBP,
 MФ ПЛOX:,SUBP,
 TRAPHY:,SUBP,
 TASKOFF:,SUBP,
 NRESOFF:,SUBP,
 NRECMT:,SUBP,
  TYPEMT:,SUBP,
  TRAINT:,SUBP,
  RETURN  :,SUBP,
  NEWTA   :,SUBP,
  TIME10  :,SUBP,
  PERMIR:,SUBP,
  TRA TAPE:,SUBP,
  A БAYC:,EQU,TYPEMT+6
  MAXJOB:,EQU,=12
  *TAKCA:LC,BLOCK,(8),COEF
C******************************************
C       ПOЛE  B  ИПЗ  ЗAДAЧИ
 *102:B,BLOCK,RLEN
 *135:B,BLOCK,S14(0),S15,БAYC,S13
    ,CONT,PAY(0),S32,SI(4),S(2)
  ,CONT,STAT,AC
    ,CONT,MT,B/UN,TYP,ПOB,JOB
    ,CONT,PAGE,W/EXCH,RETURN/,TIME
C******************************************
C
C       БИTЫ  PEГИCTPA  COCTOЯHИЯ :
  K Д B  :,EQU,B16
  Ц K C  :,EQU,B15
  ПБAYC  :,EQU,B14
  CБЗAП  :,EQU,B13
  ПЛOT   :,EQU,B12
  CДBH   :,EQU,B10
  ПPM    :,EQU,B9
  Г T    :,EQU,B8
  C Д B  :,EQU,B7
  H З З  :,EQU,B6
  K Л    :,EQU,B5
  M Г З  :,EQU,B4
  H Л    :,EQU,B3
  OПK    :,EQU,B2
  OЧБ    :,EQU,B1
C
 SC/BCC:,EQU,=:3744
 SC/BWM:,EQU,=:0260
C
C*****   PAЗHЫE БYДИЛЬHИKИ :
  BIG  TIM :,EQU,=240.ПPOXOД ЗOHЫ
  CLE  TIM :,EQU,B5.CTИPAHИE
  SMALLTIM :,EQU,B4.PEBEPC
C
C*****   ДИAГHOCTИKA :
C
  ЧYЖ MЛ :,EQU,18
  ЗAПPЗП :,EQU,21
  ПЛOXЭK :,EQU,14
C
C*****   PEГИCTPЫ :
C
 1:EQ,BLOCK,U,TY,BA,RR,R
     ,CONT,F,L,JN,RT,DC(0),IF
     ,CONT,A,N,I(0),J,C(0),M,ST
C
C
 B:,WTC,ГYC.ПPЯЧEM
 ST,VTM,S15.BCE
 ,XTA,SAVI15.ЧTO
 ,XTS,.MOЖEM
 ,XTS,SAVI13.B ИПЗ
 ,ITS,32B
 ,AOX,SAVSR
   ,ITS,RR
   ,ITS,BA
   ,ITS,TY
   ,ITS,U
  M,MTJ,BA
 BA,REL,B.*******
   ,ITS,BA
 ,ATX,BASEEC
  ,XTA,DISREG
  ,ATI,27B
   ,NTR,3
   ,XTA,
 ST,ATX,W/EXCH-S
 ST,ATX,PAGE-S
 ST,ATX,RLEN-S
  ,XTA,SAVI14
  ,U1A,OT ГYCEBA
 ,XTA,SAVAC.ПOCMOTPИM
  :,24,3.ЧTO ПOДAЛИ
 ST,ATX,AC-S
C+++ FOR HAPA +++
   ,AAX,C3777.ДЛИHA
   ,UZA,HAЧAЛO.HE ЗAДAHA
   ,X-A,B11.ДOП. KOД
   ,U1A,ERR4.>2000B
   ,ASN,64-12
   ,AEX,B12
 ST,ATX,RLEN-S.KYC ГOTOB
 ST,XTA,AC-S
   ,ASN,64+24
   ,AAX,C7.UBC
   ,UZA,HAЧAЛO.ПOЛHOE CЛOBO
   ,ATI,M
   ,A-X,C6
   ,UZA,ERR4.MHOГOBATO
  M,XTA,B6
   ,AEX,B11
 ST,AEX,RLEN-S
 ST,ATX,RLEN-S
C+++ END HAPA +++
 HAЧAЛO:ST,XTA,AC-S
 TY,VTM,.CDC
  J,VTM,
 ,ASN,64+12
 ,AAX,C77.HOM. MЛ
 ,UZA,TCDC.CDC-608
 ,XTS,ДHЗ.ИЩEM
 ,ASN,64-6.ЛEHTY
 ST,AOX,
   ,ASN,64-36
  U,VTM,-31
 ST,ATX,1.B БPЗ
 STAP:U,XTA,T1MT+31
 ST,AEX,1
  ,AAX,MASK12
  ,UZA,ISTAP.HAШЛИ
  U,VLM,STAP
 ERR1:,BSS,
 ERR2:M,VTM,ЧYЖ MЛ
   ,UJ,ERROR
 ERR3:M,VTM,ЗAПPЗП
   ,UJ,ERROR
 ERR4:M,VTM,ПЛOXЭK
  ,UJ,ERROR
C
C---
C
C...   KYC OБMEHA CDC608 :
  E CDC:,BSS,
  ,AAX,B48/13
 ST,ATX,AC-S.ПPOЧИCTKA
  ,ASN,64+40
  ,AAX,C60.BCD+LEN
  ,AOX,B7.CDC
  ,UJ,FORMWEX
C...   COCTOЯHИE CDC608 :
  S CDC:,BSS,
  ,EXT,4101B
  ,UJ,S1
C...   KBY-ПAPAMETPЫ CDC608 :
  T CDC:,BSS,
  ,XTA,TYPEMT+4.MЫ HE B
  ,UZA,ERR4.ДYБHE
  U,VTM,40B
  ,XTA,YCDC608
  ,U1A,T1CDC
  U,ATX,T2MT-30B
  ,XTA,ГYC
  ,ATX,YCDC608
  T1CDC:,AEX,ГYC
  ,ASN,64+42
  ,U1A,ERR2
  ,XTA,B12.BЫБOPKA
  ,EXT,130B.MФ CDC
  C,VTM,4.
  U,XTA,B48-10B
  ,AOX,ЗAKЗAП
  ,ATX,ЗAKЗAП
  ,UJ,DENSITY
C------------
 ISTAP:U,UTM,30B+31.ФИЗ.HOMEP
   ,ITA,U. MФ
   ,ASN,64+3
   ,ATI,C.HAПPABЛEHИE
  C,XTA,ШKEC-3
   ,UZA,ERR2.HE EC
 TY,VTM,15.EC5012
  ,ARX,
  ,U1A,DENSITY
 TY,VTM,30.EC5017
  J,VTM,-1
C---
C      ECЛИ ПЛOTHOCTЬ HE OБЬЯBЛEHA
C      ПPИ ЗAKAЗE MЛ EC, ИCПOЛЬЗYEM
C      48-47 PAЗPЯДЫ ИHФ.CЛOBA :
C---
  DENSITY:,BSS,
  ,XTA,B11
  ,AOX,B10
  U,AAX,T1MT-30B
  J,ASN,64
   ,U1A,TYPMT
 ST,XTA,AC-S
   ,AAX,B48/47
  J,ASN,64+37
C
C---
C
  TYPMT:,BSS,
 ST,ATX,ПOB-S.CЧ-K + ПЛOTH.
   ,ITA,TY
 ST,ATX,TYP-S.XPAHИM TИП
 SOS:RR,VJM,SOST
 ST,XTA,AC-S
 ,ASN,64+36.TAЩИM
 ,AAX,C377.JOB
 ST,ATX,JOB-S
 ,UZA,EXIT.' OПPOC '
  ,X-A,MAXJOB
  ,U1A,ERR4
C
C...   ПPOБA: HE ИДET ЛИ ПEPEMOTKA
C
  U,XTA,T2MT-30B
  ,AAX,C/ПEP
  ,AEX,C/ПEP
  ,U1A,HEПEP
 TY,VIM,HEПEP.ПPOПYCTИM EC
 ST,XTA,STAT-S
  ,AEX,C Д B.  NOT
  ,AAX,C Д B. "BUSY"
  ,ASN,64-26
  U,AEX,T2MT-30B
  U,ATX,T2MT-30B
   ,*57,3.ЖДEM KOHЦA
  C,VTM,4.ПEPEMOTKИ
  ,UJ,SOS
  HEПEP:,BSS,
 RR,VTM,FIN-B
 ST,XTA,JOB-S
 ,X-A,B2.' OБMEH '
 ,U1A,MOVONLY
   ,ASN,64-17
   ,U1A,READ
 ,XTA,ЗAKЗAП.MOЖHO
  U,AAX,B48-10B.ЛИ ПИCATЬ
 ,UZA,ERR3
   ,XTA,
 READ:ST,ATX,W/EXCH-S
 ,ASN,64-1.18 B 19
 ST,AOX,W/EXCH-S
 ST,ATX,W/EXCH-S
 ST,XTA,AC-S
 TY,VZM,ECDC
C+++ FOR HAPA +++
  ,AAX,=2500 0000.B HAPE
 ST,ATX,.ПPИЗHAK
  ,AAX,B19.OБMEHA ЛИCTOM
  ,ASN,64+16. - БИT 3
 ST,AOX,
  ,AOX,B4.ПPИЗH."EC"
C+++ END HAPA +++
  FORMWEX:,BSS,
 ST,AOX,W/EXCH-S
  ,XTS,ГYC
  ,AAX,=:77
 ST,AOX,
 ST,ATX,W/EXCH-S.B KYC
  ,ASN,64-30.18 B 48
  :,24,2003B
  ,AUX,BITJ
  ,AOX,READSCAL
  ,ATX,READSCAL
 ST,XTA,AC-S
 ,ASN,64+20
 ,AAX,C76000
   ,ATI,M
   ,ASN,64+10
   ,ATI,J.MAT.ЛИCT
  :,24 ,2000B
  M,XTA,
 :,24,2003B
  ,XTA,ZBITJ
  ,AAX,READSCAL
  ,ATX,READSCAL
  ,WTC,ГYC
  J,XTA,30B.TMATH
 ,ASN,64+30
 ,AAX,C177
   ,ATI,M.HOM. ФИЗ.
 ST,ATX,PAGE-S.ЛИCTA
   ,XTS,B1.ФИKCИPYEM
  M,AOX,TPHYS
  M,STX,TPHYS
 :,24,3
 ,ASN,64-41.B KYC
 ,AUX,MSKP.OБMEHA
 ST,AOX,W/EXCH-S
 ST,ATX,W/EXCH-S
 RR,VTM,ANAL-B
 MOVONLY:,BSS,
 ST,WTC,JOB-S
 ,XTA,TABMOV-1
 ,AAX,B46.ПOИCK MГЗ
 ,UZA,NOEOF
 ST,XTA,JOB-S.ЗAMEHA
 ,A-X,B2.ШГЗB,ШГЗH
 ST,ATX,JOB-S.HA ШЗB,ШЗH
 RR,VTM,CHMF-B
 NOEOF:BA,J+M,RR
   ,ITA,RR
 ST,ATX,RETURN/-S
 ST,MTJ,J
   ,ITA,U.ФOPMИPOBKA
  ,ASN,64-42.KYC-A
  ,AUX,=0070 0000 0000 1600
 ST,AOX,W/EXCH-S.(БEЗ ДHЗ)
 ,XTS,BASEEC
 ,ASN,64-24.AДPECA
 ,AOX,BASEEC.ПPOЦEДYP
 TY,VIM,NO DEL
  ,ARX,B2.CM.ЗAПYCK
  NO DEL:,BSS,
 ,ARX,ADR2
   ,ITS,J
   ,AOX,=:007
 ST,MTJ,J
 ST,ATX,.B "STAT" !
  M,VJM,TRAPHY
 ST,UTM,-3
 ST,XTA,JOB-S
 EXIT:,24,3
  ,AEX,B4. П И P
  ,AAX,C77777
  ,U1A,ИCXOД1
C...   ПOCЛE PAЗГPYЗKИ MФ - OTKAЗ
C      И ГAШEHИE ИMEHИ MЛ B TAБЛИЦE
  U,ATX,TANAM-30B
  ,XTA,DISREG
  ,ATI,27B
  U,XTA,T1MT-30B
  ,ASN,64+36
  ,AAX,C77
  ,UZA,ИCXOД1
   ,ATI,M
  M,XTA,B48-30B
   ,*57,4000B
  U,XTA,B48-30B
   ,AAX,TYPEMT+7.NORECTP
   ,AOX,MISTERX
   ,ATX,MISTERX
 ИCXOД1:,BSS,
  :,24 ,3
 ST,XTA,S14-S
   ,ASN,64+15
   ,U1A,K ГYCEBY
 ИCXOД:,24,2003B
  U,XTA,T2MT-30B
   ,ASN,64-24
 ST,AOX,STAT-S
   ,ATX,SAVAC
 ST,WTC,PAGE-S
  M,VTM,
  M,XTA,TPHYS
 ,AOX,B1.PACФИKCИPYEM
 ,AEX,B1.ЛИCT
  M,STX,TPHYS
   ,STI,U.BOCCTAHOBЛEHИE
   ,STI,TY.ИЗ ИПЗ
   ,STI,BA.BCEГO
   ,STI,RR
 ,STI,33B
 ,MTJ,27B
   ,STI,J
   ,STI,M
 ST,XTR,PAY- S15
   ,ATI,ST
  ,WTC,BASEEC
  ,XTA,RETNR-B
  ,UJ,NRESOFF
  RETNR:,Z00,NRECMT
  ,Z00,RETURN
C=======================
  ADR2:,BSS,
  ,Z00,KOHЗOH - B
  ,Z00,ЗAПYCK - B
C=======================
C=======================
C   PAБOTA OT TRAM70
C   ЗAДAHЫ:
C    U  - HOMEP YCTPOЙCTBA
C    C  - KBY-HAПPABЛEHИE
C    DC - YBY-HAПPABЛEHИE
C    I  - OБP.KOД HOMEPA B HAПP.
C    N  - ИHДEKC ЗAKAЗA B WL
C    JN - ДHЗ ЗAKAЗЧИKA
C    RT - BOЗBPAT
C-----------------------------
  ЗAПYCK:,24,2003B
  ,WTC,BASEEC
   ,XTA,B48/25 - B
  R,CALL,LSOSTEC
C><><><><><> ЗДECЬ BXOД ДЛЯ CDC608 <><><><>
  :,24,3
  ,UTC,ЗAПYCK1-KOHЗOH1
 KOHЗOH:RR,VTM,
   ,WTC,BASEEC
 BA,VTM,
 JN,WTC,*YCЫ
 ST,VTM,S.ИПЗ
 ST,WTC,JOB-S
  F,VTM,
 ST,XTA,TYP-S
   ,ATI,TY.TИП MФ
  ,U1A,CHNRD
  ,ATX,TCDC608
  CHNRD:,BSS,
  N,XTA,WL1-1.MOЖET
   ,AAX,B7.БЫЛA
   ,UZA,SOST/.ПOTEPЯ
  N,AEX,WL1-1.ГOTOBHOCTИ
  N,ATX,WL1-1
 RR,VTM,ЗAПYCK1-KOHЗOH1
 SOST/:,BSS,
 RR,UTM,KOHЗOH1
C----OПPOC COCTOЯHИЯ MФ--
 SOST:,BSS,
   ,ITA,J
 ST,ATX,STAT-S
 TY,VZM,SCDC.CDC-608
  U,XTA,T2MT-30B
 ,ASN,64+24
 S1:,AAX,C77777
  J,VTM,-14
  U,XTS,T1MT-30B
 ,AAX,B12.' KДB '
 ,UZA,S2
  U,AEX,T1MT-30B.ГACИM
  U,ATX,T1MT-30B
   ,XTA,B16
 S2:J,XTS,TABN+14
 TY,ASN,64
 ST,AAX,-2
   ,UZA,S3
  J,XTA,B1
 S3:ST,AOX,
  J,VLM,S2
 ST,UTM,-1
 ST,WTC,STAT-S
  J,VTM,
 ST,ATX,STAT-S.B ИПЗ
 RR,UJ,
 ЗAПYCK1:,XTA,OUTJOBSC
 JN,AAX,BITS.MOЖET YЖE
   ,U1A,KPAHTЫ.BЫKИHYЛИ
 ST,XTA,STAT-S
 ,AAX,ГT
 ,UZA,NOREADY
  F,XTA,TABMOV-1
 ,AAX,B47.ПPИЗHAK
   ,ASN,64+44.ШЗH,ШГЗH,ПPM
 ST,AAX,STAT-S
   ,U1A,BEGTAP
  F,ARX,TABMOV-1.48-ПPИЗHAK
   ,U1A,CHDIR.ПPM,ПИP
 TY,ASN,64
   ,AAX,C77777
 TY,V1M,REWIND
   ,UTC,20B.CDC
 REWIND:U,EXT,50B.ПEPEMOTKA
   ,ATI,
   ,XTA,C/ПEP
 BEGTAP:U,AOX,T2MT-30B
   ,AAX,B48/25.ЧИCTИM
  U,ATX,T2MT-30B
   ,UJ,KPAHTЫ
 CHDIR:,BSS,
  F,XTA,B48
   ,AAX,SC/BCC
   ,UZA,EXCH
   ,AAX,SC/BWM
  :,UZA,*+1
   ,UTC,B24/1-B1
   ,XTA,B1
  U,ARX,T2MT-30B
  U,AEX,T2MT-30B
   ,AAX,B24/1
  U,AEX,T2MT-30B
  U,ATX,T2MT-30B
 EXCH:ST,XTA,W/EXCH-S
   ,UZA,ПYCKEC
C+++ FOR HAPA +++
 ST,XTA,RLEN-S
 ,UZA,EXCH1
 ,EXT,16B.(16B)
C+++ END HAPA +++
 EXCH1:ST,XTA,W/EXCH-S
   ,AOX,B11OFKUS
  C,ATX,TBUC-1
 DC,EXT,.OБMEH
   ,ATI,
  N,XTA,BITS
  C,ATX,SBUC-1
   ,UJ ,ПYCKEC
 MOVE1:,BSS,
   ,ITA,RR
 ST,ATX,RETURN/-S
 ПYCKEC:,BSS,
  F,XTA,TABMOV-1
 TY,ASN,64
 ,AAX,C77777
 ,AOX,B24.CБPOC ГPП
 ST,AOX,ПOB-S. + DENS
  :,24 ,2003B
 TY,VIM,MOVE2
  R,VTM,TIMER608
   ,ITS,R
   ,STX,TCDC608
  ,UTC,20B
  MOVE2:,BSS,
  U,EXT,50B.ПOEXAЛИ
   ,XTA,ABTIME
 ST,ATX,TIME-S
   ,ASN,
 TY,VZM,MOVE3
   ,ASN,
  R,CALL,LSOSTEC1
  C,XTA,NEW-3
  I,AAX,B16
   ,U1A,NOREADY
  I,XTA,B8
 MOVE3:,BSS,
  C,ATX,ДBECA-3
  I,XTA,B8
  C,AOX,CSUMSTAT-1
  C,ATX,CSUMSTAT-1
   ,XTA,BIG TIM
  C,ATX,MTTIMCNT-3
  :,24 ,3
 RT,UJ,.B TRAM70
 NOREADY:,24 ,2003B
  M,MTJ,RR
   ,XTA,B7
  N,AOX,WL1-1.ПP-K
  N,ATX,WL1-1.HEГOTOB
   ,ITA,U.HOM. MФ
  R,VTM,3
  M,VTM,AFTPR
 TY,VIM,MФ ПЛOX
  M,VTM,WAITRD
   ,ITA,M
   ,ATX,TCDC608
  M,VTM,TNR608
   ,ITA,M
  M,CALL,TTPRINT1
 AFTPR:,BSS,
 RR,MTJ,M
  I,XTA,B8
  C,AOX,ШHГ-3
  C,ATX,ШHГ-3
  :,24,3
  C,XTA,TBUC-1
 RT,UZA,
  C,XTA,SBUC-1
  N,AEX,BITS
 RT,U1A,
  C,ATX,TBUC-1
  C,ATX,SBUC-1
 DC,XTA,B5-3
   ,EXT,31B
 RT,UJ,
C==========================
C  ПO KOHЦY ДBИЖEHИЯ
C--------------------------
  KOHЗOH1:,BSS,
  ,NTR,2
  ,XTA,ABTIME.ДOБABKA K
 ST,A-X,TIME-S.KOMMEP.
   ,A*X,COEF
 ST,A+X,110B-S.BPEMEHИ
 ST,ATX,110B-S.ЗAДAЧИ.
  R,VTM,2
  C,XTA,SBUC-1
  N,AEX,BITS
   ,U1A,NO RBUS
  C,XTA,TBUC-1
  ,UZA,NO RBUS
  ,AAX,B18
  ,ASN,64+17
   ,ATI,R.0,1
   ,XTA,
  C,ATX,TBUC-1
  C,ATX,SBUC-1
   ,EXT,4036B
 TY,VZM,LEN.CDC
   ,WTC,AБAYC
   ,EXT,
   ,ASX,AБAYC
 LEN:,BSS,
   ,AAX,C1777.ПPИ БAYC=0
   ,U1A,B БAYC. ДEЛAEM
   ,XTA,B11.БAYC=1024
 B БAYC:ST,ATX,БAYC-S
   ,ATI,L
 DC,XTA,B8.YCTAHOBKA
  ,EXT,31B.PБYC-5(6)
  NO RBUS:,BSS,
  ,NTR,3
 ST,XTA,STAT-S
   ,AAX,=17 6777.ДOЛOЙ CДBH
  R,AAX,CLERS.ПPOЧИCTKA
 ST,ATX,STAT-S.ПAPAЗИTOB
  ,AAX,ГT
  ,UZA,FIN
 ST,WTC,RETURN/-S.ПEPEKЛЮЧATEЛЬ
 ,UJ,
C.....KOHEЦ   OБMEHA.....
 ANAL:,BSS,
  A,VTM,70000B-1
 ST,XTA,W/EXCH-S
  ,AAX,B23
  ,UZA,NO LOOK
  N,XTA,WL1-1
  F,CALL,ABCHT
 NO LOOK:,24,3
 ST,XTA,STAT-S
  ,AAX,=100030.KДB+EOF+EOT
  ,U1A,FIN
 ST,XTA,W/EXCH-S
  ,AAX,B18
  ,UZA,WW
  U,XTA,T1MT-30B
C...   ПPOБA: DENS=H .AND. STATUS=7
  ,AAX,=2016
  ,AEX,=2016.CЧИTATЬ ЦKC
  ,U1A,OKKS.-HET
  ,ATX,KSUM
  F,VTM,.ДЛЯ ИHBEPCИЙ
C
C....  ПOДCЧET 6 BAPИAHTOB ЦKC, T.K.
C      MЫ HE ЗHAEM ЧИCЛO БAЙTOB B
C      ПOCЛEДHEM CЧИTAHHOM CЛOBE.
C
 L WORD:A,UTM,1
  R,VTM,-5
  :,24,2
  A,XTA,
  :,24,3
  ,ATX,WORD
  L SYM:,XTA,WORD
  ,ASN,64-8
  ,ATX,WORD
  ,YTA,
  ,ATX,SYMB
  ,ACX,B1
  ,ASN,64-8
  ,AOX,SYMB
  ,AAX,C777
  ,AEX,KSUM
  F,AEX,.ИHBEPCИЯ 3-6 P.
  ,ASN,64+1
  ,ATX,KSUM
  ,YTA,
  ,AEX,
  ,UZA,NO INV
  ,XTA,B9
  ,UTC,=74
 NO INV:F,VTM,
  ,AOX,KSUM
  ,ATX,KSUM
  R,ATX,KSUM6+5
  R,VLM,L SYM
  L,UTM,-1
  L,VIM,L WORD
C
C.....  CPABHEHИE C ЦKC, ПOЛYЧEHHOЙ
C       ИЗ KMЛ  ( 16-24 P. TBUT )
C
  :,XTA,SYMB.ECЛИ
  ,U1A,*+1.ПOCЛEДHИЙ
  ,UTC,-5.БAЙT ПYCT
 :A,VTM,.ЦKC=KSUM6(6)
  C,XTA,TBUT-1
  ,ASN,64+15
  ,AEX,=727
  ,ATX,KSUM
  ,AAX,B9
  ,UZA,CHKS
  ,XTA,=74
  ,AEX,KSUM
  ,ATX,KSUM
  CHKS:,XTA,KSUM
  A,AEX,KSUM6+5
  ,AAX,C777
  ,UZA,OKKS
  A,VLM,CHKS
C...   ПPИ HECOBПAДEHИИ BЫДAEM "OЧБ"
  ,XTA,OЧБ
 ST,AOX,STAT-S
 ST,ATX,STAT-S
  OKKS:,UTC,ЦKC-CБЗAП.ПPИ ЧTEHИИ
  WW:,XTA,CБЗAП.ПPИ ЗAПИCИ
  ,AOX,C3.OПK+OЧБ
 ST,AAX,STAT-S
   ,UZA,FIN.O'KEY
 ST,XTA,AC-S
  ,AAX,B24.TECTOBЫЙ
  ,U1A,FIN.PEЖИM
 ST,XTA,ПOB-S.CЧETЧИK
   ,ARX,B45.ПOBTOPHЫX
 ST,ATX,ПOB-S.OБMEHOB
   ,UZA,FIN. - ЛOПHYЛ
  N,XTA,BITS
  C,AEX,SRJC-1
  C,ATX,SRJC-1
  F,VTM,4.ШЗH
 RR,VJM,MOVE1
C...BEPHYЛИCЬ HA ЗOHY..
C...TEПEPЬ OПЯTЬ OБMEH.
C...ПPИ ЧTEHИИ ИЛИ.....
C..CTИPAHИE ПPИ ЗAПИCИ
 ST,XTA,W/EXCH-S
 ,AAX,B18
 ,U1A,REP/EXCH
  F,VTM,10.CLEAR
 RR,VJM,MOVE1
 REP/EXCH:,BSS,
 RR,VTM,ANAL
   ,ITA,RR
 ST,ATX,RETURN/-S
  N,XTA,BITS
  C,AOX,SRJC-1
  C,ATX,SRJC-1
 RT,UJ,.B TRAM70
C....ПOИCK MГЗ......
 CHMF:,BSS,.JOB=3,4
  F,UTM,-3.0,1
  F,J+M,F.0,2
   ,XTA,=100010
  F,AOX,KЛ."KЛ" ИЛИ "HЛ"
 ST,AAX,STAT-S
 RT,UZA,.HET MF
C---------
 KPAHTЫ:,BSS,
 FIN:,24,2003B
 JN,XTA,BITS
 ,AOX,RDSCAL
 ,ATX,RDSCAL
 RT,VTM,TRAINT
  ,UJ,TASKOFF
C
C...  PAБOTA OT ПPEPЫBAHИЙ ПO 10 P. ГPП :
C       1.  OЖИДAHИE ГOTOBHOCTИ CDC608
  WAIT RD:,EXT,4101B
  ,AAX,B8
  ,UZA,TIME10
  ,XTA,ШHГ+1
  ,AOX,B1
  ,AEX,B1
  ,ATX,ШHГ+1
  ,XTA,
  ,ATX,TCDC608
  C,VTM,4
 ST,CALL,TRA TAPE
  ,U1A,RETURN
  ,UJ,NEWTA
C...    2.  БYДИЛЬHИK HA ПPOXOД ЗOHЫ
  TIMER608:,XTA,MTTIMCNT+1
  ,ARX,C377
  ,AAX,C377
  ,ATX,MTTIMCNT+1
  ,U1A,TIME10
  ,ATX,TCDC608
  ,XTA,B12."KДB"
  ,AOX,T1MT+10B
  ,ATX,T1MT+10B
  ,XTA,B13
  ,EXT,130B
  J,VTM,RETURN
  ,XTA,B33.HA BCЯKИЙ
  ,UJ,PERMIR.CЛYЧAЙ
C
C
  ERROR:,24,2003B
  ,WTC, Г Y C
 ST,VTM,
 ST,XTA,AC
   ,ATX,SAVAC
 ST,XTA,S13
   ,ATX,SAVI13
 ST,XTA,S15
  ,ATX,SAVI15
  ,ASN,64+15
  ,ATX,SAVI14
  ,UJ,ERRMACRO
 '
 '
 '''''''''''''''''''''''''''''''''''''''''
 '   BCTABKA ПO ПOBOДY OБCЛYЖИBAHИЯ
 '   ЭKCTPAKOДA  57 C AДPECAMИ  1 И 4.
 '   BOЗHИKЛA OT HEЖEЛAHИЯ ПEPEПИCЫBATЬ
 '   ПPOГPAMMЫ A.B.ГYCEBA  B LIBRARY 2.
 '''''''''''''''''''''''''''''''''''''''''
 '
  *0:,BLOCK,Г/MAT,Г/ФИЗ,Г/ЛИCT
  ,CONT,Г/ПЛOT,Г/PAЗM,Г/BCD
  ,CONT,Г/COC, Г/БAYC(2),Г/ЗOH(2)
  ,CONT,Г/MOV, Г/EX(2)
  ,CONT,Г/BPEM,Г/OШ
 '
  OT ГYCEBA:,24,3
  ,ASN,64-15
 ST,AOX,S14-S
 ST,ATX,S14-S
  ,AAX,B16
  ,U1A,Г/CDC
  :,24,
 TY,VTM,15.EC5012
 IF,UTM,2.ПPИBEДEHИE
 IF,XTA,Г/MAT
   ,ATI,U
 IF,XTA,Г/EX
  :,24,3
C+++ FOR HAPA +++
C--- ПEPBOЗДAHHЫE ПPOГPAMMЫ A.B.ГYCEBA
C--- HE YMEЛИ ПИCATЬ ЛИCTOM, A 19-Й
C--- PAЗPЯД KYCA BЫCTABЛЯЛИ BCEГДA.
C--- ПOЭTOMY B HAPE CTAЛИ ИCПOЛЬЗOBATЬ
C--- CBOБOДHЫЙ 3-Й PAЗPЯД, KAK ПPИЗHAK
C--- ЗAПИCИ ЛИCTOM.
 ST,ATX,
   ,AAX,B18.ЧИTAEM
   ,ASN,64+15.BCEГДA
 ST,AOX,.ЛИCT
   ,APX,=2400 0004
   ,AUX,=2500 0000
C+++ END HAPA +++
 ST,ATX,JOB-S
  U,VIM,Г/XOP
C.....  ЗAKAЗ - HA ФИЗ.OБMEH
  :,24,
 IF,XTA,Г/ФИЗ
  :,24 ,3
 ST,ATX,AC-S.ЛEГKO ЗAБЫTЬ
   ,ATI,U .ПOДATЬ HOMEP MЛ
   ,A-X,C30
   ,ASN,64+5
   ,U1A,ERR4
  U,XTA,T1MT-30B
  ,ASN,64+36
  ,AAX,C77
   ,ATI,U
 Г/XOP:,BSS,
   ,ITA,U
   ,ASN,64-12
 ST,AOX,JOB-S
  ,UJ,Г
  Г/CDC:,24,
 IF,XTA,Г/EX
  :,24,3
  ,AAX,C60
  ,ASN,64-40
 TY,VTM,.CDC608
 Г:,AOX,B24."TECT"
 ST,ATX,AC-S
  :,24 ,
 IF,XTA,Г/MOV
  :,24 ,3
 ST,ATX,JOB-S
C+++ FOR HAPA +++
  ,ASN,64+24.KYC ЗAKAЗA
 ST,ATX,RLEN-S.ДЛИHЫ
C+++ END HAPA +++
  :,24,
 IF,XTA,Г/EX
  :,24 ,3
  M,VTM,10
  ,UZA,Г1
  ,AAX,B18
  :,U1A,*+1
  ,UTC,1.ЗAПИCЬ
 :M,VTM,1.ЧTEHИE
  ,UJ,Г3
  Г1:,BSS,
 Г2:M,XTA,TABMOV-1
 TY,ASN,64
 ST,AEX,JOB-S
   ,AAX,C777
 TY,VZM,Г/C1.CDC
  ,AAX,C377.M.Б. БEЗ "PREC"
  Г/C1:,BSS,
  ,UZA,Г3
  M,UTM,-1
  M,VIM,Г2
 Г3:,ITA,M
   ,ASN,64-36
 ST,AOX,AC-S
 ST,ATX,AC-S
   ,XTA,ABTIME
 ST,ATX,TIME-S
  :,24 ,
 IF,XTA,Г/ЛИCT
   ,ATI,M
 IF,XTA,Г/MOV
  :,24,3
  ,ASN,64+9.DENS. B 1-2
  ,ASN,64-26
   ,ITS,M
 ST,AOX,
   ,ASN,64-20
 ST,AOX,AC-S
 ST,ATX,AC-S
  ,UJ,HAЧAЛO
 '
  K ГYCEBY:,NTR,2
  U,XTA,T2MT-30B
   ,ATI,M.ЗOHA
 ST,XTA,STAT-S
   ,AAX,C377
   ,ATI,J.COCT
 RR,VTM,
  ,AAX,KЛ
  :,UZA,*+1
 RR,VTM,5
 :ST,XTA,STAT-S
  ,AAX,KДB
  :,UZA,*+1
 RR,VTM,3
  :,XTA,ABTIME
 ST,A-X,TIME-S
   ,AEX,.B PMP
 ST,XTA,БAYC-S
  :,24 ,
 IF,ATX,Г/БAYC
   ,YTA,
 IF,ATX,Г/BPEM
   ,ITA,RR
 IF,ATX,Г/OШ
   ,ITA,M
 IF,ATX,Г/ЗOH
   ,ITA,J
 IF,ATX,Г/COC
  :,24 ,3
 TY,VZM,ИCXOД
 ST,XTA,STAT-S
   ,ASN,64-32
   ,AUX,=3 37777
   ,ITS,TY
   ,AAX,B5.KBYMЛ
   ,ASN,64-10
 ST,AOX,
  :,24 ,
 IF,ATX,Г/COC
   ,ITA,U
 IF,ATX,Г/ФИЗ
 IF,UTM,-2.BOЗBPAT
  ,UJ,ИCXOД
 '
 '
C**************************
C    TAБЛИЦA KOMAHД (JOB)
C    KYCЫ BCЯKИX ДBИЖEHИЙ
C   1-15.....ДЛЯ CDC-608
C  16-30.....ДЛЯ EC-5012
C  31-45.....ДЛЯ EC-5017
C  48-46 P. - BCПOMOГATEЛЬHЫE БИTЫ:
C   48    -   ABTOHOMHOE ДBИЖEHИE
C   47    -   ДBИЖEHИE HAЗAД
C   46    -   ПOИCK MAPKEPA
C**************************
 TABMOV:,BSS,
  1,LOG,0 20005 20401 00010.ЧTEH.
  2,LOG,0 20005 20401 00010.ЗAП.
 3,LOG,0 00005 00401 00010.ШЗB
 4,LOG,2 00011 00403 00014.ШЗH
 5,LOG,1 00021 00404 00050.ШГЗB
 6,LOG,3 00041 00410 00054.ШГЗH
 7,LOG,6 00401 00500 00200.ПPM
 8,LOG,4 01001 00600 00400.ПИP
 9,LOG,0 00101 00420 00030.ЗПMГЗ
 10,LOG,0 00201 00440 00100.CTИP
C****************************
C****************************
C...TAБЛИЦA БИTOB COCTOЯHИЯ..
C   ПO BEPTИKAЛИ - AHAЛOГИЧHO
 TABN:,BSS,
  1,LOG, 20000 00000 00000.OШKC
  2,LOG, 10000 00000 00000.ПБAYC
  3,LOG, 02000 00000 00000.CБЗAП
  4,LOG, 00010 00000 01000.CHП,HDENS
  5,LOG, 00004 00000 00400.CB,LDENS
  6,LOG, 00002 00000 00000.CДBH
  7,LOG, 00400 00400 00100.ПPM
  8,LOG, 00200 00200 00200.ГT
  9,LOG, 00001 00100 00100.CДB
 10,LOG, 00020 00040 00040.HЗЗ
 11,LOG, 00100 00020 00020.KЛ
 12,LOG, 04000 00010 00010.MГЗ
 13,LOG, 00040 00004 00004.HЛ
 14,LOG, 01000 00002 00002.OШПK
 15,LOG, 40000 00001 00001.OШЧБ
C-------------------------------
 B48/25:,OCT,77777777
  C/ПEP:,OCT,0000 0600
 MSKP:,LOG,1 4037 0000
  TNR608:,ISO,18HNO READY CDC608''''''
C
C.....  MACKИ ПPOЧИCTKИ P.C. :
C
  CLERS:,BSS,
  ,LOG,1 37777.ЗAП:ЦKC
  ,LOG,1 67777.ЧT:CБЗAП
  ,LOG,1 07777.ДB:TE ЖE + ПБAYC
C
C...   HEPAЗДYБЛИPOBAHHЫE ЯЧEЙKИ
C   (ИCПOЛЬЗYЮTCЯ ПPИ PAБOTE OT TRAM70)
C
  KSUM :,BSS,1
  SYMB:,BSS,1
  WORD :,BSS,1
  KSUM6:,BSS,6
  ,BSS,
 ,END,
 MACRO66 :,NAME,
 SMASAV  :LC,BLOCK,SAVAC
 NRESCALL:,SUBP,
 NRES66  :,SUBP,
 *11:,BLOCK,L,R,J,M,S
  ,ATX,SAVAC
  ,UTC,CONST
  ,XTA,
  ,UJ ,NRESCALL
  CONST:,Z00,NRES66
        ,Z00,J
 MOVE66:,ENTRY,
 'ПEPECЫЛKA ИЗ M B S MACCИBA
 'ДЛИHЫ 4*(-L)+(3-R)+1
 M,XTA,
 R,UTM,-3
 L,VZM,SHRT
 L,UTM,1
 LOOP:,BSS,
 M,XTS,1
 M,XTS,2
 M,UTM,4
 M,XTS,-1
 REST:,BSS,
 M,XTS,
 L,VLM,LOOP
 SHRT:,BSS,
 M,UTM,1
 R,VLM,REST
 S,ATX,
 J,UJ ,
  ,END,
 NRES066:,NAME,. ДYБHA - 81
 ',,.+ HAPA - 84
C*************************************
C*HEPEЗИДEHT ЭKCTPAKOДA OБPAЩEHИЯ K CП
C*
C*    BЫЗOB CП ПPOИЗBOДИTCЯ TAK
C* 13, VTM ,PAБ ПOЛE    .CЮДA CЯДET CП
C*:  , LIB ,INF         .ИHФOPM.ДЛЯ CП
C* PI,     ,HOMEP CП    .
C* B PI ECTЬ B24 -  CП БYДET BЫЗЫBATCЯ
C*                ПOBTOPHO (COXPAHИTЬ)
C*                B ЭTOM CЛYЧAE   ИP13
C*                YBEЛИЧИTCЯ HA  ДЛИHY
C*                CП   ПOCЛE  BOЗBPATA
C* B PI ECTЬ B23 -  ЭTO COБCTBEHHAЯ CП
C*                MATEMATИKA.  OCTATOK
C*               ПPABOЙ KOMAHДЫ - MAT.
C*               AДPEC CП B  ФOPMATE :
C* <HAПP><MБ><TPAKT><CEKT><AДP B CEKT>
C* B PI ECTЬ B22 -  CПEЦИAЛЬHЫЙ PE-
C*                ЖИM ДЛЯ ЧAЙKOBCKOГO.
C*     INF - ДЛИHA MACCИBA ПOДЛEЖAЩEГO
C*   OБMEHY, A ИP13 KYДA EГO ЧИTATЬ-6,
C*   ПPABAЯ KOMAHДA - ФИЗ AДPEC ПO  MБ
C*   B AHAЛOГИЧHOM ФOPMATE.
C*
C*
C*B HAЧAЛE CП COДEPЖИTCЯ 2 CПEЦ CЛOBA:
C*     30 - 16P            15- 1P
C*<ДЛИHA CП B ПAM.ЗAДAЧИ><ДЛИHA HA MБ>
C*    2-E: CПEЦ  KOД B KOДE GOST
C*          CП*066
C*  ИЛИ ДYБЛЬ AДPECA CП HA MБ B CЛYЧAE
C*  CП MATEMATИKA
C*B CЛYЧAE CИCTEMHOЙ CП B ПEPBOM CЛOBE
C*   B 48-37 PP  ДYБЛЬ HOMEPA CП.
C*BXOД B CП ПO AДPECY <ИP13>+10B
C*B CЛOBE<ИP13>+1 AДPEC BOЗBPATA ИЗ CП
C*             +5 ИP13
C*             +2 PEЖИM AY
C*ИP14 PABEH ИCП.AДPECY ЭKCTPAKOДA *66
C*************************************
 *ИПД:LC,BLOCK,(53),PARTISAN
 TPHYS:,LC,
 ШЛAГБAYM:LC,BLOCK,(8),CLT
 *YCЫ:LC,BLOCK,MCW
 SMASAV:LC,BLOCK,SAVAC,SAVSR,SAVAR
          , CONT,SAVI15,SAVI14,SAVI13
 ДHЗ:LC,BLOCK,JN,BITJ
  DISPBASE:LC,BLOCK,(4),DISPFIL,DISPNAM
 *360    : B,BLOCK,ИПЗЖДY(1),(31),CP
 ЧИCЛOCП:,LC,1
 SEMDOWNS:,SUBP,
 SEMUP  :,SUBP,
 OUTMACRO:,SUBP,
 MACRO64 :,SUBP,.
 MACRO70S:,SUBP,.
 ERRPRI  :,SUBP,
 *0:,BLOCK,(24),TMP(8),TDEF
    , CONT,NPPW(31),REGSCAL(4)
    , CONT,BUFS27(15),SCEM
 *700:B,BLOCK,B48(8)
       ,CONT,B40,B39(12)
       ,CONT,B27(6)
       ,CONT,B21(17)
       ,CONT,B4(3)
       ,CONT,B1
 C3:,LC,1
 C13:,LC,1
 C1777:,LC,1
 C3777:,LC,1
 C76000:,LC,1
 C77777:,LC,1
 LTBCP:,EQU,440B
 LT1  :,EQU,LTBCP-1
 ENDTBCP:,EQU,TBCP+LT1
 ENDTBCP:I,BLOCK,(1),SREGSC,IW5
          , CONT,EBCП(0),NCП
          , CONT,IW70,IW72
          , CONT,SAC,SSR(2),SM,SJ
          , CONT,CЛ/ЭK(8),SB
          , CONT,SP/REM,SP/OWN,SP/22
 *5:,BLOCK,B,P,E(0),O,C(0),A,I,LP,L,R,J,M,S
 BEG:,BSS,.БAЗA
 J,REL,*
  :,ARX,*+2
  ,WTC,SAVI13
  J,VTM,
  ,UJ,SEMDOWNS
 :,Z00,CLT
  ,Z00,*+1-BEG
 :,XTA,SAVAR
  ,ATI,J.BOCCT.БAЗЫ
 S,VTM,SAC.MAГAЗИH
  ,XTA,SAVAC
  ,XTS,SAVSR
  ,XTS,SAVI15
  ,XTS,SAVI14
  ,ATX,A/ЭK
  ,XTS,SAVI13
  ,ATX,IW5
  ,ITS,32B
  ,ATX,R/ЭK
  ,ATI,M
  ,WTC,MCW
  ,XTA,REGSCAL
  ,ATX,SREGSC
  ,AOX,B48*4
  ,WTC,MCW
  ,ATX,REGSCAL
  ,MOD,101B
  ,NTR,3
 :,24 ,
 M,XTA,-1
 :,24 ,3
 S,ATX,
  ,ATX,DAD
  ,ASN,64+24
  ,STX,I/ЭK
 M,VTM,J-B
 :M,UTM,-1
 M,ITS,B
 M,V1M,*-1
 J,MTJ,B
 B,REL,BEG.HOBAЙ БAЗOBЫЙ PEГИCTP
 L,VTM,-3
 :,XTS,CЛ/ЭK
 L,APX,B21
 L,VLM,*-1
  ,U1A,MLF
  ,XTA,C13
  ,ATI,27B
  ,XTA,ЧИCЛOCП
  ,U1A,TBCП/TYT
 L,VTM,-LT1
 :L,UTC,LT1
  ,ATX,TBCP.ЧИCTKA TBCП
 L,VLM,*-1
 TBCП/TYT:,BSS,
 ,WTC,MCW.
 ,XTA,SCEM.
 ,AAX,BЭ66/77.
 ,AEX,BЭ66/77.
 M,VTM,RET64
 M,MTJ,32B.
 M,VTM,INF*64.
 ,UZA,MACRO64+2.ПEЧATЬ
 RET64:,BSS,.
 J,VTM,.
 E,VTM,HETYTИ-BEG.
  ,XTA,SP/REM.
 ,UZA,ЗA CП.ПOKA HE БЫЛO
  ,XTA,JN
  ,ASN,64-42
  ,XTS,CЛ/ЭK
  ,ASN,64-26
  ,ASN,64+6
 S,AEX,
  ,ATX,EBCП
 M,VTM,-LT1.L=0
 SE TB:,BSS,
 M,XTA,ENDTBCP
 :,U1A,*+1
 M,MTJ,L
  ,AEX,EBCП
  ,ASN,64+15
  ,UZA,BCП.CП YЖE ECTЬ
 M,VLM,SE TB
  ,XTA,B1
 :,24 ,2003B
  ,ARX,ЧИCЛOCП
  ,ATX,ЧИCЛOCП
  ,XTA,SJ
 :,24 ,3
  ,AEX,EBCП
 L,ATX,ENDTBCP
 ЗA CП:,BSS,
C*** ПOЛYЧEHИE MATЛИCTA ДЛЯ ПEPEKAЧKИ
 L,VTM,1-31.
 P,VTM,
 LMEM:,WTC,MCW.
 L,XTA,TMP+31.
 ,ASN,64+30.
 P,UTM,2000B.
 ,UZA,FREEPA.
 L,VLM,LMEM.
C*** BCE ЛИCTЫ ЗAHЯTЫ, ПOЭTOMY BOЗЬMEM
C    HA OДИH ЛИCT PAHЬШE ЧEM TOT, KYДA
C    CП БYДET ПИCATЬCЯ.HO OH HE ДOЛЖEH
C    БЫTЬ 0-Й! BMECTO 0-ГO BOЗЬMEM 37Й
 ,XTA,SJ.
 ,AAX,C76000.
 ,ATI,P.
 P,UTM,-2000B
 P,V1M,FREEPA
 P,UTM,-2000B
 FREEPA:,BSS,
  ,ITA,P
  ,ASN,64-20
  ,ATX,IW70
 ,AOX,CIW72.
 ,ATX,IW72.
 ,*72,IW72.
 ,XTA,SP/22.
 ,UIA,ЧAЙKOBCK.
 ,XTA,SP/OWN.
 ,UIA,CBOЯ CП.
C***ЗAKAЗ HA "HOPMAЛЬHYЮ" CИCTEMHYЮ CП
C   CИCTEMHЫE CП  ДOЛЖHЫ HAXOДИTЬCЯ HA
C   ЛEHTE/MOДEЛИ  DISPNAM,  HAЧИHAЯ  C
C   ЗOHЫ  DISPFIL+10B.
C***
 ,XTA,B48/16.
 ,*57,4040B.
 ,AEX,B48/16.
 ,AAX,B48/16.
 ,UZA,ERR CП.
 ,ANX,C27.
 ,ATI,J.
  ,ASN,64-12.
  ,XTS,DISPFIL.
  ,AAX,C3777.
 S,AEX,.
  ,ARX,B4.+10=HAЧ.CП
  ,AEX,B40.
  ,AEX,IW70.
  ,ATX,IW70.
 L/MTW:,BSS,.
 ,XTA,DISPNAM.
 ,*57,2400B.
 ,UZA,ERR CП.
 ,AAX,B47/46.
 ,UZA,HAШA.
 ,*57,3.
 ,UJ,L/MTW.ЖДEM MЛ/MOДEЛЬ
 HAШA:,BSS,.
 M,VJM,EXCH.
  ,XTA,CЛ/ЭK
  ,ASN,64-36
  ,ATX,NCП
 P,MTJ,C
 :,24,
 C,XTA,.
 :,24,3
 ,AEX,KATKOД.
 ,UIA,ERR CП.
 SE1:,24,.
 C,XTA,2.
 :,24,3.
 ,UZA,ERR CП.
 ,AEX,NCП.
 ,ATX,DAD.
 ,ASN,64+36.
 C,UTM,1.
 ,U1A,SE1.
 ,XTA,DAD.
 ,ASN,64+10.
 ,UZA,BMOVE.
 ,ARX,IW70.
 ,ATX,IW70.
 ,UJ ,IREAD.
 ЧAЙKOBCK:,UTC,B39.
 CBOЯ CП :,XTA,.
 ,AEX,B40.
 ,XTS,DAD.
 ,ASN,64-28.
 ,AUX,CMD.
 S,AEX,.
 ,AEX,IW70.
 ,ATX,IW70.
 IREAD:,BSS,
 M,VJM,EXCH
 BMOVE:,BSS,
 R,VTM,HA CП
  ,XTA,SJ
  ,ATI,A
  ,XTA,DAD
  ,AAX,C1777
  ,ATI,I
 P,J+M,I
  ,A-X,C1777
  ,ATI,L
  ,XTA,SM
  ,ATI,LP
  ,XTA,SP/22
  ,U1A,LMOVE
 :,24 ,
 I,XTA,
 :,24 ,3
  ,ATI,LP.ДЛИHA CП
  ,ASN,64+15
  ,ATI,M
  ,ITA,LP
  ,X-A,C74000
 E,VTM,ДЛИHHO-BEG.
  ,UIA,ERR CП.ДЛИHHAЯ CП
 ,XTA,SP/REM.
 ,UZA,ПPOB CП.
 ,ITA,M
 ,ARX,IW5.
 ,ATX,IW5.
 ПPOB CП:,24,.
 I,XTA,1.
 :,24,3.
 ,AEX,KATKOД.
 ,UZA,LMOVE.
 ,AEX,KATKOД.
 ,AEX,CЛ/ЭK.
  ,ASN,64-28.
 E,VTM,OБPAЩ-BEG.
  ,UIA,ERR CП.
 LMOVE:,BSS,
 :,24 ,
 I,XTA,
 I,UTM,1
 A,ATX,6
 :,24 ,3
 A,UTM,1
 LP,UTM,-1
 LP,VZM,RET PT.
 L,VLM,LMOVE
 P,MTJ,I
 L,VTM,-1777B
 ,XTA,B1.
 ,ARX,IW70.
 ,ATX,IW70.
 M,VTM,LMOVE.
 J,V1M,EXCH.
 ,ARX,C7740
 ,AOX,C7740
 ,AEX,C7740
 ,ATX,IW70.
 EXCH:,XTA,C13.
 ,WTC,MCW.
 ,ATX,BUFS27.
 M,MTJ,32B
 ,XTA,IW70.
 ,UJ,MACRO70S.
 BCП:,BSS,
 M,XTA,ENDTBCP
  ,ATX,SJ
 HA CП:,BSS,
 M,VTM,.
  ,WTC,SJ
 A,VTM,-2
  ,XTA,SP/22
  ,U1A,SET32
 A,UTM,2
  ,XTA,R/ЭK.
  ,ATI,32B.
  ,XTR, SSR
  ,XTA,IW5
 :,24 ,
 A,ATX,5
  ,ITA,32B
 A,ATX,1
  ,RTE,77B
 A,ATX,2
 SET32:,BSS,
 :,24 ,3
  ,ITA,A
  ,ATX,SJ
 A,UTM,8
 A,MTJ,32B
 EXIT:,BSS,
 S,VTM,SB
 J,VTM,P-R
 S,XTA,
 :J,STI,R
 J,VLM,*
  ,XTA,SREGSC
 :,24 ,2003B
  ,WTC,MCW
  ,ATX,REGSCAL
  ,AAX,B4
 :,U1A,*+1.
  ,MOD,107B.
 S,XTA,
  ,STX,SAVI13
  ,STX,SAVI14
  ,STX,SAVI15
  ,STX,SAVSR
  ,ATX,SAVAC
  ,MTJ,27B
  ,ITA,B
 :,ARX,*+1
  ,UJ ,SEMUP
 :,Z00,CLT
  ,Z00,*+1-BEG
 :,WTC,SB
 B,VTM,
 M,VZM,OUTMACRO
  ,UJ ,ERRPRI
 ERR CП:,BSS,.
 R,VJM,RET PT.
  ,XTA,CЛ/ЭK.
  ,ATX,SAC.
  ,XTA,R/ЭK.
  ,ATI,32B.
 E,MTJ,M.
  ,UJ ,EXIT.
 RET PT:,BSS,
  ,XTA,C13
  ,ATI,27B
 B,J+M,E.
  ,*72,IW72.OTДAЛИ ЛИCT
 R,UTC,.
 J,VZM,.
 J,XTA,B48-30B.OTKAЗ OT
  ,*57,4000B.ЛEHTЫ C CП
 R,UJ ,.
C======================================
 NLP:,EQU,36B
 ETPH:,EQU,=36 00000 01
 TMPM:,EQU,=4 2007 6000
 NUMP:,EQU,40B
 LLIB:,EQU,=10 0000.NUMP*1024
 NUMS:,EQU,350B
C......................................
 TMLP:,EQU,TMP+NLP
 LIPA:,P*P,(NLP)1024
 CATP:,EQU,LIPA
 CATS:,EQU,CATP+NUMP
 LIBS:,EQU,CATS+NUMS
 NP1 :,EQU,NUMP-1
 LCAT:,EQU,NUMP+NUMS
C--------------------------------------
 SREGSC:I,BLOCK,(1),OWNPARTI,PAR
         , CONT,C0(0),IPF,IPL
 PATAB:,EQU,SB+1
 STACK:,EQU,PATAB+NUMP
 PSHIF:,EQU,PATAB-BEG
C--------------------------------------
 MLF:,BSS,
 C,VTM,CATS-2
  ,XTA,SM
  ,ATI,P
  ,UZA,CLEAR
  ,XTA,CЛ/ЭK
  ,ATI,O
  ,XTA,
  ,ATX,SM
 :,24 ,
 P,XTA,
 :,24 ,3
  ,ATX,PAR
 CLEAR:,BSS,
 :,24 ,2003B
  ,XTA,PARTISAN
  ,AAX,BITJ
  ,AEX,BITJ
  ,ATX,OWNPARTI
  ,AEX,PARTISAN
  ,ATX,PARTISAN
  ,XTA,CP
 :,24 ,3
  ,UZA,NEWLIB
 R,VJM,REQLP
 P,VZM,OVFL
 SEARS:,BSS,
 :,24 ,
 C,XTA,2
 :,24 ,3
 C,UTM,2
  ,UZA,SABS
  ,ATX,C0
  ,AEX,PAR
  ,ASN,64+18
  ,U1A,SEARS
 O,V1M,EMLF
 SETMP:,BSS,
 :,24 ,
 C,XTA,1
 :,24 ,3
  ,ATX,SM
  ,ATI,A
  ,ASN,64+24
  ,XTS,C0
  ,ATX,PAR
  ,AAX,C77777
  ,A-X,B1
 S,ARX,-1
 S,ATX,
  ,AAX,C1777
  ,STX,IPL
  ,ASN,64+10
 S,XTS,-2
  ,ASN,64+10
  ,ATI,I
 S,A-X,
  ,STI,LP
  ,AAX,C1777
  ,ATX,IPF
 LP,MTJ,L
  B,MTJ,M
 MCATP:,BSS,
 :,24 ,
 I,XTA,CATP
 :,24 ,3
 M,ATX,PSHIF
 M,UTM,1
 I,UTM,1
 L,VLM,MCATP
 B,MTJ,I
 I,XTA,PSHIF
  ,AEX,CP
  ,UZA,PAGE0
 PAGEL:,BSS,
 R,VJM,RETLP
 I,XTA,PSHIF
 R,VJM,REQLP
 PAGE0:,BSS,
  ,XTA,C1777
 LP,V1M,NLASP
   ,XTA,IPL
 NLASP:,BSS,
 I,UTM,1
  ,X-A,IPF
  ,ARX,C3
  ,ASN,64+2
  ,ATI,L
  ,YTA,
  ,ASN,64+46
  ,ATI,R
  ,XTA,IPF
  ,ATI,M
 M,UTM,LIPA
 A,MTJ,S
 O,VZM,PAGEM
 M,MTJ,S
 A,MTJ,M
 PAGEM:,BSS,
 :,24 ,
  ,CALL,MOVE66
 S,MTJ,A
 O,VZM,PAGEE
 M,MTJ,A
 PAGEE:,BSS,
 :,24 ,3
  ,XTA,
  ,ATX,IPF
 LP,VLM,PAGEL
 EMLF:,BSS,
 R,VJM,RETLP
  ,XTA,SM
  ,UZA,RETZ
  ,ATI,A
  ,XTA,PAR
 :,24 ,
 P,ATX,
 A,XTA,7
 :,24 ,3
  ,AAX,C77777
  ,XTS,SM
  ,ASN,64-24
 S,AEX,
 :,24 ,
 P,ATX,1
 RETZ:,BSS,
 :,24 ,2003B
  ,XTA,OWNPARTI
  ,AEX,PARTISAN
  ,ATX,PARTISAN
 :,24 ,3
 M,VTM,
  ,UJ ,EXIT
 NEWLIB:,BSS,
 B,MTJ,C
 I,VTM,-NP1
 RESLOOP:,BSS,
 :,24 ,2003B
  ,XTA,B48
  ,WTC,MCW
 S,VTM,
 S,AOX,TMLP
 S,ATX,TMLP
 :,24 ,2000B
  ,XTA,LIPA
 R,VJM,RETLP
  ,ITA,L
 C,ATX,PSHIF
 ,ASN,64+5
 ,UZA,ZP
 C,UTM,1
 I,VLM,RESLOOP
 C,XTA,PSHIF-NUMP
  ,ATX,CP
 I,VTM,-NP1
 R,VJM,REQLP
 SETLOOP:,BSS,
 C,XTA,PSHIF-NUMP
 C,UTM,1
 :,24 ,
 I,ATX,CATP+NP1
 :,24 ,3
 I,VLM,SETLOOP
  ,UJ ,OVFL
 ZP:,BSS,
 L,ATX,TPHYS
 ,UJ,RESLOOP
 SABS:,BSS,
 O,VZM,EMLF
 C,MTJ,M
 M,UTM,2-LIBS
 M,VZM,OVFL
 :,24 ,
 C,ATX,2
 P,XTA,1
  ,ASN,64+24
 C,AEX,1
 C,ATX,1
  ,ASN,64-24
 P,XTA,
 C,ATX,
 :,24 ,3
  ,ATX,C0
  ,YTA,
  ,XTS,C0
  ,AAX,C77777
 S,ARX,
  ,ASN,64-24
 :,24 ,
 C,ATX,3
 :,24 ,3
  ,ASN,64+24
  ,X-A,LLIB
  ,UZA,SETMP
 OVFL:,BSS,
 C,VTM,CATS
  ,XTA,BLIB
 :,24 ,
 C,ATX,1
  ,XTA,
 C,ATX,
 :,24 ,3
 P,VZM,EMLF
  ,UJ ,SABS
 REQLP:,BSS,
 :,24 ,2003B
  ,WTC,MCW
 S,VTM,
  ,ATI,L
  ,ASN,64-30
 S,AOX,TMLP
 S,ATX,TMLP
  ,ASN,64-11
  ,AUX,TMPM
 S,AOX,TMP
 S,ATX,TMP
  ,XTA,B27
 S,AEX,TDEF
 S,ATX,TDEF
  ,XTA,JN
  ,ASN,64-27
  ,AEX,ETPH
 L,ATX,TPHYS
  ,XTA,B1
 S,ARX,NPPW
 TOPUT:,BSS,
 S,ATX,NPPW
 M,CALL,PUTTMP
 :,24 ,3
 S,VTM,STACK
  ,ATI,
 R,UJ ,
 RETLP:,BSS,
 :,24 ,2003B
 M,VTM,-3
 :,ATX,1
 M,VLM,*
  ,WTC,MCW
 S,VTM,
 S,XTA,TMLP
  ,ASN,64+30
  ,ATI,L
  ,YTA,
  ,ASN,64+18
 S,ATX,TMLP
  ,XTA,TMPM
 S,AOX,TMP
  ,AEX,TMPM
 S,ATX,TMP
  ,XTA,B27
 S,AOX,TDEF
 S,ATX,TDEF
  ,XTA,B1
 L,ATX,TPHYS
  ,XTA,CM1
 S,ARX,NPPW
  ,UJ ,TOPUT
 BLIB:,Z00,LCAT
 ,,
 CM1:,OCT,7777 7777 7777 7776
C======================================
 INF*64:,,TEXTRAS.
 ,,R/ЭK.
 :,010,.
 ,,
 :2,016,5
 ,,
 :,024,.
 ,,.
 :2,026,8.
 ,011,1.
 :,050,.
 ,,.
 :2,055,5
 8,,.BCE!
C**************************************
 B48/16:,OCT,7777 7777 7760 0000
 B48*4 :,OCT,4000 0000 0000 0010
 BЭ66/77:,OCT,0000 1001
 C27:,LOG,27
 C7740:,LOG,7740
 TEXTRAS:,GOST,6HЭ66 A'231'
 A/ЭK:,OCT,
 :,GOST,2H/'231'
 I/ЭK:,OCT,
 DAD :,OCT,.
 :,GOST,6HBЭ ='231'
 R/ЭK:,OCT,.
 C74000:,LOG,74000
 B47/46:,OCT,3
 CMD   :,OCT,0000 0000 0037 0037
 KATKOД:,GOST,6HCП*066
 CIW72:,OCT,1041 0077
 HETYTИ:,GOST,12HHET CП'231'
 OБPAЩ :,GOST,12HOБPAЩ. K CП'231'
 ДЛИHHO:,GOST,12HДЛИHHЫЙ CП'231'
 TBCП:,ENTRY,
 TBCP:,BSS,
 ,END,
 AEROPLAN:,NAME,.ББ+AП+AЛ
C******************************************C
C                                          C
C    ПЛAHИPOBAHИE BЫБOPKИ B PEШEHИE ЗAДAЧ  C
C    ИЗ OБЛACTИ BBOДA C YЧETOM BPEMEHИ     C
C    BBOДA И ЗAKAЗAHHOГO B ПACПOPTE        C
C    ЗAДAЧИ BPEMEHИ.  OTPAБOTKA ПPИKAЗOB   C
C    OПEPATOPA:
C        NNФП   ,  NNБП   ,   NNПП  .      C
C               * * * * * *                C
C    OЧEPEДЬ KAЖДOГO ИЗ YKAЗAHHЫX ПPИKA-   C
C    ЗOB PACЧИTAHA HA 8 ЗAДAЧ.             C
C      MHEMOHИKA:
C      ФП - ПPИOPИTET O ЧEPEДИ ФOHOBЫX     C
C    ЗAДAЧ,
C      БП - БEЗYCЛOBHЫЙ ПPИOPИTET ЗAДAЧE,  C
C    T.E. ЗAПYCTИTЬ, KAK TOЛЬKO OCBOБOДИT- C
C   CЯ KAKOЙ-HИБYДЬ ШИФP,
C      ПП - ПPИOPИTET C YЧETOM ПPEДПOЧTИ-  C
C   TEЛЬHOГO BPEMEHИ ДЛЯ ШИФPA.            C
C                                          C
C******************************************C
 STARTJOB:,SUBP,
 NSTAJOB:,LC,1.
 *YCЫ:,LC,1.
 MШ:,LC,1.
 *ИПД: LC,BLOCK,(1)
 ,CONT,XNFM
 ,CONT,(1)
 ,CONT,XNFB
 ,CONT,XNW
 ,CONT,(1),XNCM
 ПAPTИЗAH:,EQU,*ИПД+65B
 БЛOKAЦПY:,EQU,*ИПД+66B
 DISPBASE:LC,BLOCK,(6),NEVM
 *ИПK:LC,BLOCK,INFBA
 *TLBUFI:LC,BLOCK,FCIN
 *TLSIZE:LC,BLOCK,(1),XNPC
 NPC0:,WEQ,XNPC
 NPC:,EQU,NPC0-1
 NPC1:,EQU,NPC-1
 NFB:,WEQ,XNFB
 NFM:,WEQ,XNFM
 NW :,WEQ,XNW
 NCM:,WEQ,XNCM
 INFEX:,EQU,*ИПK+NCM
 NB :,EQU,NW-NFB
 NF:,EQU,NW-NFM
 NF1:,EQU,NF-NB
 BITS:,EQU,677B
 CAT:,EQU,2000B.
 LISTIM:,EQU,4000B
 ЛИПЗ:,EQU,6000B
 NЛИПЗ:,EQU,=:0000 03
 NJOB:,EQU,1772B.
  Ш/CBЯ:,EQU,*ИПД+15B
 BATCH:,EQU,*ИПД+40B
 STOBH:,EQU,*ИПД+41B
 PRIPL/SC:,EQU,*ИПД+47B.ЭKCПPECCЫ
 PRIPLTER:,EQU,*ИПД+43B.ЭKCПP.TEPM.
 *1000:B,BLOCK,TYPETASK,TЖДY,TПACП
 ,CONT,ITПACП,PNEX,POUND
 ,CONT,WIGHT,W0004
 ,CONT,SC*PLAN
 ,CONT,SC/BATCH
 ,CONT,N,ФЛЭKC,N//,N**
 ,CONT,USESCR
 ,CONT,USEMT
 ,CONT,ABSMT
 ,CONT,MTNC
 ,CONT,BITM
 ,CONT,USEMD
 ,CONT,ABSMD
 ,CONT,FILCOUNT
 ,CONT,MONOSC(10)
 ,CONT,BITJ,IFX,WS1
 ,CONT,WS,DATA
 FREXT:,EQU,*ИПД+34B
C*******************************************
 1,BASE,*
 14,VTM,TPL
 ,ITA,14.
 ,FUN,76B.
 12,VTM,-1777B
 ,DRUM,IWTIM
 ,XTA,LISTIM
 ,AEX,=6HTIMES
 ,UZA,БEГИH
 ,XTA,
 L1:12,ATX,LISTIM+1777B
 12,VLM,L1
 БEГИH:,BSS,
C*******************************************
 13,VJM,CB/ШИФP
 ,UZA,CLOSE.HET ШИФPOB
 ,*50,114B
 ,ASN,64-13
 ,ATX,DATA
 :,XTA,RCAT.
 ,*57,7
C*******************************************
C     ПPOBEPKA HAЛИЧИЯ БEЗYCЛOBHOГO
C       Э K C П P E C C A.
C*******************************************
 ,*65,PRIPL/SC+1
 ,AOX,.
 ,UIA,Э*1.ECTЬ
C*******************************************
C     ПPOBEPKA HAЛИЧИЯ ЭKCПPECCHOЙ
C     ЗAЯBKИ ДЛЯ   Ф  O  H  A.
C*******************************************
 9,VTM,NW-1.
 9,*65,BITS.
 ,AAX,SC/FREE.
 ,UZA,Э*1.HET ФOH. ШИФPA
 ,*65,PRIPL/SC
 ,ASN,64-42.
 ,UZA,Э*1.HET ЗAЯBKИ
 ,ATX,N.
 2,VJM,ПYCK.
 9,VIM,ФИHAЛ.ЗAПYCTИЛИ ФOH
 Э*1:,BSS,.ЭKCПPECC HE COCTOЯЛCЯ
C*******************************************
C     ПPOБA БEЗYCЛOBHOГO ЭKCПPECC
C     ЗAПYCKA ЗAДAЧИ HA ЛЮБOЙ ШИФP.
C*******************************************
 5,VTM,1-2
 3,VTM,PRIPL/SC+1
 LБП:3,*65,
 ,ASN,64-42.
 ,UZA,Э*21
 ,ATX,N
 Э*2:,BSS,.ПOИCK CB.ШИФPA
 ,XTA,SC/FREE.
 ,AAX,=34.TOЛЬKO Ш4-Ш2
 ,UZA,Э*21.EГO HET
 ,ANX,.ДHЗ
 ,ATI,9.
 2,VJM,ПYCK.
 9,VZM,Э*2.HE COCTOЯЛOCЬ.
 ,UJ,ФИHAЛ.BЫШЛO
 Э*21:3,VTM,PRIPLTER
 5,VLM,LБП
 Э*3:,BSS,.ДAЛЬШE
C*******************************************
C     BOT TYT-TO И HAЧИHAETCЯ HEKAЯ
C        П Л A H И P O B K A .
C*******************************************
 ПЛAHИP:,BSS,.
C***
C     ПOДБOP MИHИMAЛЬHOГO CBOБOДHOГO
C              ШИФPA.
C***
 ,XTA,SC/FREE.
 ,AAX,=20 0200
 ,ATX,W0001.
 ,AEX,SC/FREE.
 ,ARX,=17 7540
 ,AOX,W0001.
 ,APX,=20 0236
 ,AUX,=176
 ,ATX,SC*PLAN.
 ПЛAH:,BSS,.
 3,VTM,-60.
C*******************************************
C     BЫЯCHEHИE KATEГOPИИ ДAHHOГO
C         Ш  И  Ф  P  A  .
C*******************************************
 ,XTA,SC*PLAN
 ,UZA,CLOSE
 ,ANX,
 ,ATI,9
 ,XTA,=:4
 9,ASN,64-1
 ,ATX,TYPETASK
 ,AEX,SC*PLAN
 ,ATX,SC*PLAN
C
C   ЗHAЧEHИЯ PAЗPЯДOB TYPETASK:
C
C     B7 - ДИAЛOГOBЫЙ ШИФP
C     B6 - MT-ПAKET + CBЯЗЬ
C     B5 - Ш4
C     B4 - Ш3
C     B3 - Ш2
C     B2 - Ш1
C
 4,VTM,CAT.
 ,XTA,.
 ,ATX,N//.
 ,ATX,POUND
C*******************************************
C     YCTAHOBKA ФЛAГA OБPAБOTKИ ЗAДAЧИ
C     ИЗ ЭKCПPECC-ПOTOKA.
C*******************************************
 L*3:,BSS,.
 3,MTJ,14.
 14,UTM,61.
 ,ITA,14.
 ,ASN,64-42.
 ,ATX,W0004.
C---  ПPOБA HA HAЛИЧИE ЭKCПPECCA B ПOTOKE
 ,ASN,64+6.
 ,AOX,W0004.
 ,ATX,N**.
 ,ASN,64+12.
 ,AOX,N**.
 ,ATX,N**.
 ,ASN,64+24.
 ,AOX,N**.
 ,ATX,N**.
 ,*65,PRIPL/SC+2
 ,AEX,N**.
 ,ATX,N**.
 ,AAX,K40*.
 ,ATX,W0002.
 ,AEX,N**.
 ,ARX,K37*.
 ,AOX,W0002.
 ,AAX,K40*.
 ,ACX,
 ,ASN,64+3.
 ,ATX,ФЛЭKC.
 ,UZA,ПЭKC.
 4,XTA,.
C   48=1 - ГOTOBA K ПЛAHИPOBKE
C  47=1 - CПЛAHИPOBAHA И ЗAПYЩEHA
C  46=1 - ПACПOPT OCBOБOЖДEH
C       (BEGBATCH,DELJOB,MT)
C   37=1 - ПPOДOЛЖEHИE ПACПOPTA
 ,AEX,=:4.
 ,AAX,=:7001
 ,UIA,KPЭKC.KOKHYTA
 ,*65,6.6-Й TP
 ,AAX,=4000 0000.24-Й PД
 ,UIA,ПЭKC.ПP-KA PEC.БЛOKИPOBAHA
 2,VJM,CHECKRES.
 ,XTA,ABSMT.
 ,AOX,ABSMD.
 ,UIA,KPЭKC.HE BCE PECYPCЫ
C***  YЧET BPEMEHИ BBOДA ******************
 ПЭKC:,BSS,.
 ,XTA,
 ,ATX,TЖДY
 4,XTA,
 ,ASN,64+18
  ,AAX,=4
  ,AEX,=4.ПPИЗHAK
  ,ATI,11."HE П/B"
 4,XTA,1
 11,VZM,T*0
 ,AAX,=1777
 ,ATI,14
 14,XTA,LISTIM
 ,UZA,T*
 ,AEX,DATA
 ,ASN,64+29
 ,U1A,T*
 14,XTA,LISTIM
 ,UJ,T*0
 T*:,BSS,
 ,XTA,=R5.0
 ,ATX,TЖДY
 4,XTA,1
 T*0:,ATX,W0003
 ,AEX,DATA
 ,ASN,64+29.
 ,UZA,T*1.
 ,UTC,1
 T*1:12,VTM,.12ИP = 0
C                           -CYTKИ TEKYЩИE
 ,*65,542B.TEKYЩ. BPEMЯ
 ,ATX,W0002.
 ,XTA,W0003
 ,ASN,64+12.
 ,AAX,=37 7777.
 ,AOX,I0.
 ,NTR,2.
 ,E+N,64.
 ,ATX,W0001.BPEMЯ BBOДA
 12,VZM,T*3.HET KOPPEKЦИИ
 ,XTA,=R8.64E+4
 ,AMX,W0001.
 ,A+X,W0002.
 ,UJ,T*4.ПOДПPABИЛИ
 T*3:,XTA,W0002.
 ,AMX,W0001.
 T*4:,A*X,TIMWA.
 ,A+X,TЖДY
 ,ATX,TЖДY.B MИHYTAX
C*******************************************
C     YЧET ПACПOPTHOГO BPEMEHИ ЗAДAЧИ.
C*******************************************
 4,XTA,5.
 ,ASN,64+36
  :,U1A,*+1
  ,XTA,=5
  :,AOX,I0
 ,ATX,ITПACП
 ,NTR,2.
 ,E+N,64
 ,ATX,TПACП.B MИHYTAX
C*******************************************
C     ПPOБA PEЖИMA: (CЧET.OR.OTЛAДKA)
C*******************************************
 ,*65,6.
 ,AAX,K40.
 ,UIA,T*5.
 ,UTC,KC-KO
 T*5:12,VTM,KO
 ,XTA,TYPETASK
 ,APX,=176
 ,ANX,
 ,ATI,14
 ,XTA,=:4
 ,WTC,ITПACП
 ,ASN,64-1
 ,U1A,T*6
 ,XTA,=1
 T*6:14,UTC,
 12,AAX,-1
 ,UZA,KPЭKC
C*******************************************
C     HAKИДKA HA HEYДAЧHOE COЧETAHИE
C        CO   C  B  Я  З  Ь  Ю.
C*******************************************
 ,XTA,ФЛЭKC.
 ,UZA,M/2.
 ,XTA,=R1.0
 M/2:,ATX,PNEX.
 ,XTA,TYPETASK.
 ,ASN,64+5
 ,UZA,ПB/ONLY.Ш4-1
 ,ASN,64+1
 ,U1A,DIA/ONLY.Ш20-10
C
C    HA  Ш05-07  ПYCKAEM TOЛЬKO ЗAДAЧИ
C    OT CBЯЗИ И MT-ПAKETA.
C
 4,XTA,
 ,AAX,=77 000 000
 ,AEX,=04 000 000.ПB
 ,UZA,KPЭKC
  ,AAX,=01 000 000.MT
 ,UZA,M/3.CBЯЗЬ
 4,XTA,4
 ,AAX,=3000
 ,U1A,KPЭKC.ДИAЛOГ
 M/3:,BSS,
C*******************************************
C
C     BЫЧИCЛEHИE ФYHKЦИИ BECA ЗAДAЧИ
C
C*******************************************
 ,XTA,TЖДY
 ,A/X,TПACП
 ,ATX,WIGHT
 ,XTA,=R1.0
 ,A-X,PNEX.
 ,A*X,=R12.0
 ,A+X,WIGHT
 ,ATX,WIGHT.
 DCD:,BSS,
C*** KOHEЦ BЫЧИCЛEHИЙ ФYHKЦИИ BECA ЗAДAЧИ *
 ,XTA,WIGHT.
 ,A-X,POUND.
 ,U1A,KPЭKC.ECTЬ ЛYЧШE
 ,XTA,WIGHT.
 ,ATX,POUND.
 ,XTA,W0004.
 ,ATX,N//.
 KPЭKC:4,UTM,16.
 3,VLM,L*3.
 ПYCK*:9,MTJ,6.
 ,XTA,N//.
 ,ATX,N.
 ,UZA,EШ*.
 ,XTA,TYPETASK
 ,ASN,64+5
 ,U1A,ПYCK5/20
 9,MTJ,6.
 2,VJM,ПYCK.
 9,VIM,ФИHAЛ.
 6,MTJ,9
 EШ*:,BSS,.
 ,UJ,ПЛAH
 ПB/ONLY:4,XTA,
 ,AAX,=77 000 000
 ,AEX,=04 000 000
 ,UZA,M/3.ПB
 ,UJ,KPЭKC
 DIA/ONLY:4,XTA,
 ,AAX,=77 000 000
 ,AEX,=01 000 000
 ,U1A,KPЭKC.HE OT MT
 4,XTA,4
 ,AAX,=3000
 ,UZA,KPЭKC.HE ДИAЛOГ
 ,XTA,ФЛЭKC
 ,UZA,M/3.П.ПPИOPИTET
C
C     ЦИKЛ ПOИCKA MT-KAHAЛA XOЗЯИHA ЗAДAЧИ
C
 5,VTM,-NPC1
 ML:5,*65,FCIN+NPC
 ,ATI,13
 13,VZM,EML.ПYCTOЙ
  ,AAX,=:0000 34.TИП T-П/C.
  ,AEX,=:0000 04."MT"
  ,U1A,EML
 13,*65,3
  4,AEX,1
 ,AAX,=1777
 ,UZA,M/3.ECTЬ!
 EML:5,VLM,ML
 ,UJ,KPЭKC.HET COБECEДHИKA
 ПYCK5/20:,BSS,
 ,ASN,64+1
 ,UZA,ПYCK5/7
 LS5/20:,XTA,SC/FREE
 ,AAX,=37 7400
 ,UZA,EШ*
 ,ANX,
 ,ATI,9
 ,XTA,=:4
 9,ASN,64-1
 ,ATX,BITJ
 ,AEX,SC/FREE
 ,ATX,SC/FREE
 2,VJM,ПYCK
  9,VZM,LS5/20.HEYДAЧA
C
C     ЗAПYCK  ДИAЛOГOBOЙ  ЗAДAЧИ
C
C   ИCПOЛЬЗYEM PAЗPЯДЫ 5-ГO CЛOBA ПACПOPTA
C   (ПOCЛEДHEГO CЛOBA NAME ЗAДAЧИ):
C
C   13    - 'TPR'
C   12    - 'TNP' (BMECTE C 13-M PP.)
C   11:10 - TИП ЗAПYCKA ЗAДAЧИ:
C           0 - MT-ПAKET (БEЗ ДИAЛOГA)
C           1 - 'TEP'
C           2 - 'CЛY'
C           3 - 'CЛA'
C   09    - OCTAHOB ПO AДP. 100B
C
 4,XTA,4
 ,ASN,64-2
 ,AAX,=16000.B13/11
 ,ATX,IFX
 ,ITA,9
 ,ASN,64-9
 ,AOX,=50
 ,AOX,=:4
 ,AOX,NЛИПЗ
 ,*50,212B.ДAЙ ИПЗ
 ,ATI,13
 ,XTA,IFX
 ,AAX,=2000
 ,ASN,64-7
 ,ATX,WS
 4,XTA,4
 ,ASN,64+5
 ,AAX,=300
 ,AOX,WS
 ,AOX,=7300 0000 0000 0010
 13,*52,100B.REGSCAL
 ,XTA,WS.OTЛ
 ,UZA,HEOTЛПYC
 ,XTA,=100.OCT.100B
 13,*52,26B
 HEOTЛПYC:,XTA,IFX
 ,AAX,=1 0000
 ,UZA,FLY.HE CЛY
 ,XTA,BITJ
 ,AEX,
 ,*53,ПAPTИЗAH
 ,XTA,IFX
 ,AAX,=4000
 ,UZA,FLY.HE CЛA
 ,XTA,BITJ
 ,AEX,
 ,*53,БЛOKAЦПY
 FLY:,UJ,ФИHAЛ
 ПYCK5/7:,BSS,
 5,VTM,-2.
 KZ**:,BSS,.
 9,*65,BITS.
 ,AAX,SC/FREE.
 ,UZA,F**.
 9,MTJ,6
 2,VJM,ПYCK.
 9,VIM,ФИHAЛ.
 6,MTJ,9.
 F**:,BSS,.
 9,UTM,-1.
 5,VLM,KZ**.
 ,UJ,EШ*
 ФИHAЛ:,BSS,.
C*******************************************
C     ПOДПPГPAMMA ДOOБPAБOTKИ ПACПOPTA
C     ЗAПYЩEHHOЙ ИЗ ПOTOKA ЗAДAЧИ.
C*******************************************
 4,XTA,.
 ,AOX,=:6.ЗAПPOC BEGBATCH
 ,AEX,=:4.И CБPOC ГT.
 4,ATX,
 4,XTA,1
 ,AAX,=1777.NUS
 ,AOX,IFX
 ,AEX,
 ,XTA,=1 7777
 9,*53,INFEX-NFM
 ,XTA,WS1.TOЛKHEM
 ,*50,212B.ЗAДAЧY
 13,VTM,CLOSE.
 ,XTA,WCAT.
 ,*57,7.
 4,XTA,1
 ,AAX,=1777
 ,ATI,11
 ,*65,542B
 ,NTR,7
 ,A+X,I0
 ,NTR,6
 ,AAX,=37 7777
 ,ASN,64-12
 11,ATX,LISTIM
 ,*50,114B
 ,ASN,64+16
 ,ASN,64-29
 11,AOX,LISTIM
 11,ATX,LISTIM
 ,XTA,=6HTIMES
 ,ATX,LISTIM
 ,XTA,IWTIM
 ,AEX,=:001
 ,DRUM,.*****
 ,XTA,N.YБPATЬ ИЗ
 ,UJ,YБEPИ.ПPИOP. OЧEPEДИ
 CLOSE:,BSS,
  ,XTA,=3
 ,FUN,210B.
  ,XTA,=R30.0
  ,*57,13B.ПAYЗA
 ,UJ,БEГИH.
 ПYCK:,BSS,.
C*******************************************
C     ПOДПPOГPAMMA ПOДГOTOBKИ K ЗAПYCKY
C         HA ШИФP C ДHЗ, ЗAДAHHЫM B
C                 ИP9.
C*******************************************
 ,XTA,N.
 ,ASN,64+18
 ,ATX,N1.30-25 NBB
 ,ASN,64+20
 ,ATI,4
 4,UTM,CAT-16
 4,XTA,
 ,AAX,=7700 0000.N ЛAБ
 ,AOX,N1
 ,ATX,N1
  4,XTA,4.NAME(3)
  ,AAX,=77
  ,ASN,64-36.ДOБABИM
  ,AOX,N1.HOMEP
  ,ATX,N1.TEPMИHAЛA
 ,ITA,9
 ,AOX,=7770 0003 7770 0000
 ,*72,3.ЗAПYCK
 ,ATX,WS1.OTBET
 14,MTJ,9
 2,UTC,
 9,VZM,.OTЛYП
 ,XTA,N1
 9,*52,INFBA-NFM
 ,XTA,
 ,ATX,IFX.ЗAГOT. INFEX
 2,UJ,.BOЗBPAT ПO ИP2
C*******************************************
C
C     ПOИCK BCEX CBOБOДHЫX B ДAHHЫЙ MOMEHT
C                 Ш И Ф P O B.
C
C     ПOCЛE OБPAЩEHИЯ HA CYMMATOPE - ШKAЛA
C       CBOБOДHЫX И PAЗPEЩEHHЫX ДЛЯ ПOTOKA
C                 Ш И Ф P O B.
C       CYMMATOP = 0 - CBOБOДHЫX ШИФPOB
C                   H  E  T.
C*******************************************
 CB/ШИФP:,BSS,.
 9,VTM,1-NF.
 ,XTA,.
 ,ATX,SC/FREE
 C*1:,UTC,NF-1
 9,*65,*YCЫ+NFM.
 ,AAX,=77777 00000.
 ,UIA,C*2.ШИФP - ЗAHЯT
 ,UTC,NF-1.
 9,*65,BITS+NFM.
 ,AOX,SC/FREE
 ,ATX,SC/FREE
 C*2:9,VLM,C*1.
 ,*65,6
 ,ASN,64+23
 ,AAX,COGRAN
 ,U1A,*1
 ,XTA,COGRAN
 *1:,BSS,
 ,ATX,SC/BATCH
 ,AAX,SC/FREE
 ,ATX,SC/FREE.
 13,UJ,.ACC = ШKAЛA
C*******************************************
C       YЧET  HAЛИЧИЯ  PECYPCOB
C*******************************************
C      ИCПOЛЬЗYEM ПACПOPTHYЮ ИHФOPMAЦИЮ:
C     USEMT(K-BO MЛ) - 42:45P. 0-ГO CЛOBA
C     USEMD(K-BO MД) - 38:41P. 0-ГO CЛOBA
C     NEXT (K-BO SCR.ЭKCT.) - 1:5P. 5-ГO CЛ.
C     C 7-ГO CЛOBA ПACПOPTA HAЧИHAETCЯ
C      TNLIST - CПИCOK ИMEH MЛ
C      (ИMЯ=77...77  -  MЛ NO CHECK)
C     ЗA TNLIST CЛEДYET  FILCOUNT  - CЛOBO,
C     COДEPЖAЩEE CЧETЧИKИ ФAЙЛOB HA ЗAKAЗAHHЫX
C     ПAKETAX (ДЛЯ OДHOГO ДП - ПOЛE ИЗ 5 БИT)
C     ДAЛEE - CПИCOK ДИCKOB/ФAЙЛOB.
C      ЭЛEMEHT CПИCKA BЫГЛЯДИT TAK:
C     1. ИMЯ ДП
C     2. ИMЯ БOCCA (+B48, ECЛИ MOHOПOЛИЯ)
C     3.CПИCOK ИMEH ФAЙЛOB (+B48,ECЛИ ЗAПИCЬ)
C---------
C     HOMEP ЭЛEMEHTA-ПPOДOЛЖEHИЯ ПACПOPTA
C     ЗAДAETCЯ B 31-36 PAЗP. 0-ГO CЛOBA.
C---------
C
 CHECKRES:,BSS,.
  ,NTR,3
  ,*63,FREXT
  ,ATX,ABSMT
  4,XTA,5
  ,AAX,=37
  ,X-A,ABSMT
  2,U1A,.MAЛO SCRATCH
  ,NTR,2
  ,XTA,
  ,ATX,ABSMT
  ,ATX,MTNC
 12,VTM,-9.
  ZSCR:,ATX,USESCR
  4,XTA,
  ,ASN,64-3
  ,ASN,64-4
  ,ATX,USEMD
  ,YTA,
  ,ATI,10
 ,AOX,I0
  ,E+N,64
  ,ATX,USEMT
  4,MTJ,11
C
C--- BЫБOPKA ИMEH ЗAKAЗAHHЫX MЛ
C    И OПPOC ИX COCTOЯHИЯ.
C
  S/MT:10,VZM,S/MD
  14,VJM,GIVPW
  10,UTM,-1
  ,AEX,=7777 7777 7777 7777
  14,VTM,MTNC
  ,UZA,PLAS1
  ,AEX,=7777 7777 7777 7777
  ,*57,400B
  14,VTM,ABSMT
  ,UZA,PLAS1.MЛ HET
  ,AAX,=:1
  ,UZA,S/MT.CBOБOДHA
  PLAS1:14,XTA,
  ,A+X,=R1.
  14,ATX,
  ,UJ,S/MT
C
C--- ПOДПPOГPAMMA BЫБOPKИ OЧEPEДHOГO
C    CЛOBA ПACПOPTA ЗAДAЧИ.
C
  GIVPW:12,VLM,GP1
  11,XTA,.ПEPEXOД
  ,AAX,=:0000 77.HA
  ,ASN,64+26.ПPOДOЛЖEHИE
  ,ATI,11.ПACПOPTA
  11,UTM,CAT-16
  12,VTM,-14.C 1-ГO CЛOBA
  GP1:11,UTC,
  12,XTA,15
  14,UJ,
C
C--- ИHФ.CЛOBO ДЛЯ ЗAПPOCA ДП.
  INF57:13,Z07,7240B
  8,Z00,MONOSC
C--- ИHФ.CЛOBO ДЛЯ OTKAЗA OT ДП
 OTK:13,Z07,7240B.
 8,Z06,
C
C--- ПPOБA HAЛИЧИЯ ДИCKOBЫX PECYPCOB
C
  S/MD:15,VTM,USEMD
  ,XTA,USEMD
  ,ASN,64-4
  ,YTA,
  ,UZA,REDINF.HE ЗAKAЗ.
  ,ATI,10
 ,AOX,I0
  ,E+N,64
  10,UTM,-1
  ,ITS,10
  ,ASN,64-24
  ,AOX,INF57
  ,XTS,=:4.БИT
  ,ATX,BITM.ЗAKAЗA
  14,VJM,GIVPW.FILCOUNT
  ,XTS,.MONOSC
  ,XTS,
  DNAM:14,VJM,GIVPW
  15,ATX,
  14,VJM,GIVPW
  ,AUX,BITM
  ,AOX,MONOSC
  ,ATX,MONOSC
  10,VZM,TO57
  ,XTA,BITM.ПEPEXOД
  ,ASN,64+1.K CЛEД.
  ,ATX,BITM.ПAKETY
  ,XTA,FILCOUNT
  ,ASN,64-1
  ,ASN,64-4
  ,ATX,FILCOUNT
  ,YTA,
  ,ATI,13
C--- ГЛOTAEM ИMEHA ФAЙЛOB
  FILES:13,VZM,NEWDI
  14,VJM,GIVPW
  13,UTM,-1
  ,UJ,FILES
  NEWDI:10,UTM,-1
  ,UJ,DNAM
C
C--- ЗAПPOC ПAKETOB
C
  TO57:,XTA,ABSMD
  ,*57,-1
 ,ACX,I0
  ,E+N,64
 REDINF:,BSS,.
 ,ATX,ABSMD.
 ,XTA,OTK.
 ,*57,-1
 2,UJ,.
C******************************************
C
C       ПOДПPOГPAMMA YБИPAHИЯ ЗAДAЧИ
C       ИЗ ПPИOPИTETHЫX   OЧEPEДEЙ .
C
C*******************************************
 YБEPИ:,BSS,.
 11,VTM,6
 14,VTM,-2
 Y*1:,ATX,N1.
 11,ASN,64.
 ,AOX,N1.
 11,J+M,11.
 14,VLM,Y*1.
 11,VTM,-2
 Y*2:,*76,OUT/SC.
 11,VLM,Y*2.
 11,VTM,PRIPLTER-PRIPL/SC
 11,UTM,-2
 ,*76,OUT/SC
 13,UJ,.
 OUT/SC:14,REL,*
 ,ATX,N1.
 11,AEX,PRIPL/SC+2.
 ,ATX,W0001.
 ,AAX,K40*.
 ,ATX,W0002.
 ,AEX,W0001.
 ,ARX,K37*.
 ,AAX,K40*.
 ,AOX,W0002.
 ,ATX,W0002.
 ,AEX,K40*.
 ,ATX,W0003.
 ,UZA,Y*3.
 ,ACX,.
 ,NTR,3
 ,A*X,I6.
 ,YTA,64.
 ,ATX,N2.
 ,XTA,W0003.
 ,ASN,64+5.
 ,ARX,K37*.
 ,AAX,K37*.
 ,AOX,W0002.
 ,ATX,W0002.
 11,XTA,PRIPL/SC+2.
 ,APX,W0002.
 ,WTC,N2.
 ,ASN,64.
 11,ATX,PRIPL/SC+2.
 Y*3:,XTA,N1.
 ,MTJ,23.
 2,IJ,.
C*******************************************
 RCAT:9,,
 ,,CAT.
 WCAT:10,,.
 ,,CAT.
 IWTIM:,LOG,0014 0200 0016 0030
C*******************************************
C       KOHCTAHTЫ И PAБOЧИE ЯЧEЙKИ
C*******************************************
 COGRAN:,LOG,37 7776.Ш20-01
C***   ПPEДПOЧTИTEЛЬHЫE BPEMEHA CЧETA ПO ШИФPAM
 KC:,BSS,.
 ,OCT,7777 7777 7777 7777
 ,OCT,7777 7777 7777 7777
 ,OCT,7774.1-10
 ,OCT,7777 7777 7777 7777.Ш3
 ,OCT,7777 7777 7777 7777.Ш2
 ,OCT,0000 0017 7777 7777.21-...
C***   ПPEДПOЧTИTEЛЬHЫE BPEMEHA OTЛAДKИ ПO ШИФPAM
 KO:,BSS,.
 ,OCT,7777 7777 7777 7777.ДИAЛOГ
 ,OCT,7774.1-10
 ,OCT,76.1-5
 ,OCT,7774.1-10
 ,OCT,7777 7777 77.1-30
 ,OCT,0000 0777 7777 7777.16-...
C***  PAБOЧИE KOHCTAHTЫ ПЛAHИPOBKИ (MOЖHO YTOЧHЯTЬ)
 TIMWA:,REAL,0.0166666667
C*******************************************
C     A ЭTO HE MEHЯTЬ HИ B KOEM CЛYЧAE:
C*******************************************
 K40:,LOG,40
 K40*:,OCT,4040 4040 4040 4040
 K37*:,OCT,3737 3737 3737 3737
 I0:,INT,0
 I6:,INT,6
 TPL:,ISO,12HПЛAHИPOBЩИK''
C*******************************************
 SC/FREE:,BSS,1
 N1:,BSS,1
 N2:,BSS,1
 W0001:,BSS,1
 W0002:,BSS,1
 W0003:,BSS,1
C
C===============================
C
 ,END,AEROPLAN.
 DISPINIT:,NAME,.
C
C=====  ПAPAMETPЫ ЧИCЛA ЗAДAЧ =====
C
 *ИПД:LC,BLOCK,(4),XNЖДY(5),Ш/FULL
 ,CONT,(20),LAЦПY(2)
 NЖДY:,WEQ,XNЖДY
 *YCЫ:,LC,
 TYPSEM:,EQU,*ИПД+7
 BATCH:,EQU,*ИПД+40B
 PRIPL/SC:,EQU,*ИПД+47B.ЭKCПPECCЫ
C
C---------------------------------
C     OБOЗHAЧEHИЯ MOДИФИKATOPOB.
 *1:,BLOCK,B,T,PR,L0,L,R,TW,TR,LL
 SB:,EQU,12.БЛOKИPOBKA ПOИCKA
C
 NSEC:,EQU,20
C     OБOЗHAЧEHИЯ ИCTИHHЫX AДPECOB.
 IINF:,EQU,506B.KAT.BBOДA MБ
 DTEL:,EQU,101B.
 BIT13:,EQU,743B
 BIT1:,EQU,757B.
C
  STATUSMT:LC,BLOCK,(32),MISTERX
 TABLEMT:LC,BLOCK,(64),T2MT
 FATA:,LC,1
  TABDRU:,LC,16
  NFRITR:,LC,1
  DISPBASE:LC,BLOCK,(6),NUMEVM
C
 NSTAJOB:,LC,1.
 MSTART:,LC,1.
 LCAT:,EQU,2000B.
 LZON:,EQU,4000B.
 OUTSTAT:,EQU,0.KAK B TAPEPRNT
 NZON1:,EQU,1677B
 *1700:B,BLOCK,NZON,INF,SCFR(32),KOTP1
 ,CONT,MASK,WBUF(3),KOTP2,WDRUMR
 ASIGN:,EQU,1777B.AДP.ПPИЗ.CПEЦ.
  COMX:,EQU,1776B
 *230:B,BLOCK,(1),(32),ADS(2)
 ДOПYПP:LC,BLOCK,(10),NTRFRO
 CAT:,EQU,LCAT
 SCFRC:,EQU,CAT+1720B.
 SCFRZ:,EQU,CAT+1740B.
  WSC1:,EQU,CAT+1760B
 LASTW:,EQU,CAT+1775B.
 MARK:,EQU,CAT+1777B
 LLC:,P/P,(LCAT)16
 LLZ:,P/P,(LZON)16
  FLMAMБ:,LC,1
  CLMTSP:,SUBP,
 ,CALL,PRINTDR.
 BAS:B,BASE,*.
 ,*65,1.
 ,ATX,KOTP1.KOПИЯ TP1
 ,*65,NTRFRO.KYC ROLL TPAKYA
 ,ATX,WBUF+2
 ,*65,2
 ,ATX,KOTP2.KOПИЯ TP2
 ,AAX,BCNDR
 ,ATX,WBUF.ПPИЗH.HE HAДO
 ,ATX,WBUF+1.PAЗMEЩ.KAT
 TR,VTM,1.KOЛИЧ.CПEЦ.TP
 L,VTM,1-16
 ,U1A,CHROLL.TOЛЬKO ROLL
 ,*65,ADS.KYC KAT.BBOДA
 ,AAX,PTRAPHY
 ,ATX,WBUF
 ,UZA,*1
 ,ARX,C4
 *1:,ATX,WBUF+1
 TR,VTM,3
 CATDI:,BSS,
C         ЦИKЛ ПOИCKA MECTA HA MБ
 TW,VTM,3
C         HE ЗAKPЫT ЛИ MБ HA ПYЛЬTE
 L,*65,BIT1
  ,AAX,=177637.БEЗ 21,22
  ,UZA,NEXTD
 ,AAX,KOTP1
 ,U1A,NEXTD.ЗAKPЫT
 L1DR:,BSS,
C         ЦИKЛ ИЗYЧEHИЯ MБ
 TW,UTM,-1
 TW,XTA,WBUF
 ,U1A,EL1DR.TAKOЙ ECTЬ
 R,VJM,ADAM
C         ECTЬ ЛИ MECTO ДЛЯ TAKOГO TИПA
 ,AAX,MASK
 ,U1A,NEXTD.HET,HA CЛEД.MБ
 EL1DR:TW,V1M,L1DR
C         БAPAБAH OПPEДEЛEH, ЗAXBAT TPAKTOB
 TW,VTM,3
 LTAKE:,BSS,
 TW,UTM,-1
 TW,XTA,WBUF
 ,U1A,ETAKE
 R,VJM,ADAM
 ,AOX,MASK
 ,*76,WRWT.ФИЗ.ЗAПИCЬ
C         ПOЛYЧEHИE KYCA TPAKTA
 L,MTJ,R
 R,UTM,15+10B.AДP. MБ
 ,ITA,R
 ,ASN,64-5
 ,ATI,R
 TW,J+M,R.AДPEC TPAKTA
 ,ITA,R
 ,ASN,64-37
 ,AUX,PTRAPHY
 TW,ATX,WBUF
 ETAKE:TW,V1M,LTAKE
 ,UJ,GO ON.BCE ЗAXBATИЛИ
 NEXTD:L,VLM,CATDI.HA CЛEД. MБ
C         MECTA ПOД CПEЦ. TPAKTЫ HET,
C         ПEЧATЬ HA TT И HOBAЯ ПOПЫTKA.
 L,VTM,TEKNDR.
 PR,VTM,1-NSEC.ДЛИHA ЗAДEPЖKИ
  13,VJM,TTPR
 R,VTM,BAS.HA HOBYЮ ПOПЫTKY
 ,UJ,ПOДOЖДEM
 ADAM:,BSS,
C         ПOДПPOГPAMMA ПOЛYЧEHИЯ AДPECA И
C         MACKИ ЭЛEMEHTA TABDRU ПOE
C         L=NKAHAЛA * 8 + N MБ -8 -15
C         TW=N TPAKTA.        PEЗYЛЬTAT:
C         T=AДPEC, MASK=MACKA.
C         CYMMATOP=CЛOBO ИЗ TABDRU
  L,MTJ,T
  TW,*65,700B
  T,UTM,TABDRU+15
 ,ATX,MASK
 T,*65,.CЛOBO TABDRU
 R,UJ,
  WARXT:T,ARX,
 WRWT:,BSS,
C         ФИЗ. ЗAПИCЬ ПO AДPECY T
 T,ATX,
 ,MTJ,27B
 2,IJ,
  CHROLL:,XTA,WBUF+2
  ,UZA,CATDI
 GO ON:,BSS,
C         ЧИCTKA ROLL TPAKTA, ECЛИ HAДO
 ,XTA,KOTP2
 ,AAX,BCLROLL
 ,UZA,CATWORK.HE HAДO
 ,XTA,WBUF+2
 ,APX,PTRAPHY
 ,AUX,CUN70.ПOЛYЧEHИE
 ,ARX,CCINP.ИHФ.CЛOBA
 ,ATX,WDRUMR.ДЛЯ 70-ГO ЭKCTP.
 ,XTA,
 ,ATX,LCAT+1.ЧИCЛO CБPOШ.
 ,ATX,LCAT+2.ШK. BOCCT.
  ,XTA,MISTERX
  ,ATX,LCAT+COMX.MISTERX
 ,XTA,ROLLSIGN
 ,ATX,LCAT+ASIGN
 ,DRUM,WDRUMR
 CATWORK:,BSS,
C         YCTAHOBKA INF CЛOBA ROLL TPAKTA
 ,XTA,WBUF+2
 ,AOX,C6000
 T,VTM,NTRFRO
 ,*76,WRWT
C         PAЗMEЩEHИE B ЯЧEЙKAX CCЫЛOK
C         HA KATAЛOГИ ИX AДPECOB.
 TR,UTM,-1
 TR,VZM,ECATWORK.KAT. HA ДИCKE
C         PAБOTA C KATAЛOГOM BBOДA
 ,XTA,WBUF
 ,APX,PTRAPHY
 ,AUX,CUN70
 ,AOX,B39
 T,VTM,IINF
 ,*76,WRWT
 ECATWORK:,BSS,
  SB,VTM,7
C
C.. ПOДCЧET K-BA CBOБ. TPAKTOB
C
  ,XTA,
  ,ATX,CNT
  L,VTM,-15
C
C  B CTPOKИ TABDRU, COOTB.PAЗPEШEHHЫM
C  HA ПYЛЬTE MБ, BПИCЫBAEM ФИЗ.HOMEPA
C  ЭTИX MБ  / ДЛЯ GIVTRACK /
C
  */*:SB,UTM,1
  L,*65,BIT1
  ,AAX,KOTP1
  ,U1A,*//*.MБ ЗAKPЫT
  L,MTJ,T
  T,UTM,TABDRU+15
  ,ITA,SB
  ,*76,WARXT
  ,AEX,FULW
  ,ASN,64+16
  ,ACX,CNT
  ,ATX,CNT
  *//*:L,VLM,*/*
  T,VTM,NFRITR
  ,XTA,CNT.ЗAПИCЬ
  ,*76,WARXT.B CЧETЧИK
 *2:,XTA,KOTP2
 ,AAX,BBLS.ПPOБA БЛ.ПOИCKA
 ,U1A,BCE MЛ.ECTЬ БЛOKИPOBKA
 SB,VTM,.CБPOC ФЛAГA БЛ.
C     ПOИCK OБЛACTEЙ BBOДA/BЫBOДA.
  Ц1:PR,VTM,1-NSEC
  L0,VTM,-1
  Ц2:L0,XTA,NAM+1
  ,*57,540B
  L0,ATI,TR
  ,UZA,HET MЛ
  R,VTM,Ц1
  ,ASN,64+6
 ,U1A,HET MЛ
  TW,VZM,HET MЛ
 L0,VLM,Ц2
  ,UJ,BCEMЛ
  ПOДOЖДEM:,*57,3
  PR,VLM,ПOДOЖДEM
  R,UJ,
  HET MЛ:L0,VLM,Ц2
  L,VTM,DIAGN-2
  TW,V1M,TT1
  L,UTM,2
  TT1:TR,V1M,TT2
  L,UTM,4
  TT2:R,VTM,*2
  13,VJM,TTPR
  SB,VTM,1
  ,UJ,ПOДOЖДEM
  TTPR:,ITA,L
  ,FUN,76B
  13,UJ,
C---
 BCE MЛ:,BSS,
 ,XTA,KOTP2
 ,AAX,BCNDR
 LL,VTM,-1.KATAЛOГИ
 ,UZA,TCLO.- HA MБ
C.....   KATAЛOГИ ПPИ OБЛACTЯX
 SB,V1M,END TAP.И БЛOK.ПOИCKA
C
C=====   OБPAБOTKA KATAЛOГOB BBOДA/BЫBOДA
C
 TCLO:,XTA,KOTP2
 LL,AAX,BCATO.HYЛИTЬ KATAЛ.
 ,UZA,NOTZERO
C.....   OБHYЛEHИE OЧEPEДHOГO KATAЛOГA
 ,*65,TYPSEM.CEMAФOP
 LL,ASN,64-2.ДOCTYПA K ФAЙЛY
 ,ARX, .ДBYXMAШИHHЫЙ
 ,U1A,CLEAR.- HET
 ,XTA,=12 000 000
 ,FUN,16.ЖИBA ЛИ
 ,AEX, .COCEДHЯЯ MAШИHA
 ,U1A,NOTZERO.- ДA
 CLEAR:,BSS,
 L,VTM,1-1024
 13,VTM,WRO 0
 LZCO:,XTA,
 L,ATX,LCAT+1023
 L,VLM,LZCO
 L,VTM,1-16
 LZCO 1:L,UTC,15
 14,VTM,
 ,ITA,14
 ,ASN,64-2
 ,AOX,B48/45
 L,ATX,SCFRC+15
 ,ITA,14
 ,ASN,64-5
 ,AOX,B48/17
 L,ATX,SCFRZ+15
 L,VLM,LZCO 1
 14,VTM,1720B
 ,ITA,14
 ,ATX,LASTW
 14,VTM,1740B
 ,ITA,14
 ,ATX,LASTW+1
 SB,V1M,WRO 0.= HET OБЛACTEЙ
 BLOCKEND:,BSS,
C         П/П OПPEДEЛEHИЯ ЧИCЛA ЭKCTEHTOB
C         B OБЛACTИ. HA BXOДE TR =
C         HOMEPY MOДEЛИ(ФИЗ.ЛEHTЫ).
C         БЛOKИPYЮTCЯ OTCYTCTBYЮЩИE ЗOHЫ
 ,NTR,7
 14,VTM,
 ,ITA,TR.COOTB.OБЛACTЬ
 ,A-X,C30.- MOДEЛЬ
 ,UZA,WRO 0. = HET
 TR,*63,T2MT-30B
 ,ATI,14
 14,*63,FATA-1.CCЫЛKA ФAЙЛA
 ,ATX,MASK
 ,ASN,64+12
 ,AAX,C7777
 ,ATX,INF.AДPEC HAЧAЛA
 ,*65,757B.BIT1
 ,ARX,MASK
 ,AAX,C7777.AДPEC KOHЦA +1
 ,A-X,INF.ДЛИHA
 ,ASN,64+5.ДЛИHA/32
 ,ATI,14
 14,UTM,-16
C.....   БЛOKИPOBKA XBOCTA OБЛACTИ
 CEC:14,VZM,WRO 0
 ,XTA,Z48/17
 14,AAX,SCFRZ+16
 14,ATX,SCFRZ+16
 14,VLM,CEC
 13,UJ,
 NOTZERO:,BSS,
C     HAДO ЛИ ЧИCTИTЬ OБЛACTЬ BЫBOДA
 SB,VIM,TZSI
 LL,VZM,CLEOUT
 ,*65,6
 ,AAX,BIT13
 ,UZA,TZSI
C   ПOГИБШИM ЗAДAЧAM - ПOTOKOBЫЙ ПPИOPИTET
 ,XTA,IRCATI
 ,*57,7
 L0,VTM,-60
 T,VTM,LCAT
 KI1:T,XTA,
 ,ARX,
 ,UZA,KI2
 ,AAX,=:3
 ,UZA,KI2
 ,XTA,B48
 T,AOX,
 T,ATX,
 L0,MTJ,14
 14,UTM,61
 ,XTA,SC/ПП
 ,ASN,64-6
 ,ATX,SC/ПП
 ,ITA,14
 ,AOX,SC/ПП
 ,ATX,SC/ПП
 KI2:T,UTM,16
 L0,VLM,KI1
 ,XTA,SC/ПП
 ,*52,PRIPL/SC+2
 ,XTA,IWCAT
 ,*57,7
 ,UJ,TZSI
 CLEOUT:,BSS,
 ,XTA,IRCAT
 ,*57,7
 L0,VTM,1-63
 ,*65,NUMEVM
 ,ASN,64+46
 ,ASN,64-1
 ,ATI,10.2*NUMEVM
 ,ASN,64-35
 ,ATX,CNT.NEVM /38-37/
 10,UTM,WSC1.AДP ПOCЛ.ЭKCTEH
 T,VTM,LCAT
 ,XTA,MARK
 LL,AEX,CODES+1
 PR,VTM,-10
 L,VTM,BADCAT
 ,U1A,TT2
C   HAЧHEM ПPOCMOTP ЭЛEMEHTOB KATAЛOГA
 KA2:T,UTM,5
 T,XTA,OUTSTAT
 ,AEX,CNT.ЗAДAЧA
 ,AAX,=:0003.C HAШEЙ MAШИHЫ
 ,U1A,EKA2.- HET
 T,XTA,OUTSTAT
 ,ARX,
 ,UZA,KA30.ЗAKOHЧEHA
 ,AAX,B47
 ,UZA,EKA2.
 KA30:T,XTA,OUTSTAT
 ,AAX,C777
 ,ATX,NZON1
 ,U1A,L/ZON
C......OCBOБOЖДEHИE ЭЛEMEHTA KATAЛOГA
 FR/EL:L0,UTC,63
 14,VTM,
 ,ITA,14
 ,ASN,64-42
 ,AUX,=1700003
 ,ATI,13
 ,ASN,64+15
 ,ATI,14
 ,XTA,B48
 13,ASN,64
 14,AOX,SCFRC
 14,ATX,SCFRC
 ,UJ,EKA2
 L/ZON:,ATX,NZON
 ,AOX,IRZON
 ,*57,7
 ,XTA,NZON
 ,ASN,64+5
 ,ATI,14.ADR SC
 ,ASN,64-5
 ,AEX,NZON
 ,ATI,13.PLACE IN SC
 T,XTA,OUTSTAT
 ,AEX,LZON
 ,AAX,M CHZ
 ,UZA,N/ZON.CBOЯ ЗOHA
C  OCBOБOДИM ЗOHY
 ,XTA,B48
 13,ASN,64
 14,AOX,SCFRZ
 14,ATX,SCFRZ
C   ECЛИ ЗOHA ПEPBAЯ ЖE,TO
C   OCBOБOДИM И ЭЛEMEHT KATAЛOГA
 ,XTA,NZON
 ,AEX,NZON1
 ,UZA,FR/EL
 ,UJ,EKA2
 N/ZON:,ITA,14
 ,ASN,64-5
 10,AEX,1
 ,AAX,C777.LAST EXTENT
 ,U1A,NEXT.- HET
 ,XTA,B48
 13,ASN,64
 10,AAX,1.ИCKЛЮЧИM
 10,AEX,1.ЗOHY ИЗ
 10,ATX,1.LAST EXTENT
 NEXT:,XTA,LZON
 ,AAX,C777
 ,U1A,L/ZON
 EKA2:L0,VLM,KA2
C  BEPHEM OCTATKИ ПOCЛ. ЭKCTEHTA
 10,XTA,1
 ,AAX,C777
 ,ASN,64+5
 ,ATI,14
 10,XTA,1
 14,AOX,SCFRZ
 14,ATX,SCFRZ
 ,XTA,
 10,ATX, .ЭЛ.KAT
 10,ATX,1.ЭKCTEHT
 13,VJM,BLOCKEND
C.....   БЛOKИPOBKИ CЛYЖEБHOГO
 WRO 0:LL,VZM,WRO 1.= BЫBOД
 ,XTA,Z47/45
 ,AAX,SCFRC+15
 ,ATX,SCFRC+15
 WRO 1:LL,V1M,WRO 2.= BBOД
 ,XTA,Z48
 ,AAX,SCFRC
 ,ATX,SCFRC
 WRO 2:,XTA,Z48
 ,AAX,SCFRZ
 ,ATX,SCFRZ
C.....
 LL,XTA,CODES+1
 ,ATX,MARK
C.....
 LL,XTA,IWCAT+1.= ЗAПИCЬ
 ,*57,7.  KATAЛOГA
 LL,VZM,TZSI.BЫBOД
C.....
 TZSI:TW,MTJ,TR
 LL,VLM,TCLO
C     HAДO ЗAПYCKATЬ PACПEЧATKY.
 SB,V1M,ENDTAP.HE HAДO
C     ЗAПYCK ЗAДAЧИ PACПEЧATKИ.
 ,*76,STARTPRT.
  L,VTM,TSTART
  13,VJM,TTPR
 ,*57,3
C
C   ЗAПYCK ПЛAHИPOBЩИKA
C
 ,XTA,=10.ПЛAHИPOBЩИK
 ,ATX,NJPT
 ,XTA,=36
 ,*52,BATCH
 ,*76,STARTPRT
C...
 ENDTAP:,BSS,.
  ,XTA,
 ,*76,ДYБHA
 STARTPRT:14,REL,*.
 ,XTA,
   ,ATX,FL MAMБ
 ,ATX,DTEL.
 ,XTA,NJPT.
 ,ATX,NSTAJOB.
 ,ATX,MSTART.
 ,XTA,Ш/FULL
 ,ATX,LAЦПY+1
  ,MTJ,27B
  2,IJ,
C---------
 ДYБHA:14,REL,*
  ,ATX,FLMAMБ.PAЗPEШ.ПYCK
 13,VTM,1-12
 ДYБ:13,XTA,ПPИП+11
 ,WTC,*YCЫ+NЖДY
 13,ATX,43B
 13,VLM,ДYБ
  14,VTM,574B
  ,UJ,CLMTSP
 ПPИП:,BSS,
C.....   ПPИПИCKA ЖДYЩEГO TECTA
 ,LOG,113 6447
 ,LOG,112 0451
 ,LOG,112 0451
 ,LOG,113 4451
 ,LOG,072 2757
 ,LOG,012 2451
 ,LOG,763 4451
 ,LOG,
C.....   ЗAЩИTA ЖДYЩEГO TECTA
 ,LOG,0744 000000
 ,LOG,1144 000000
 ,LOG,1174 000000
 ,LOG,1100 000000
 MCHZ:,LOG,77 77 77 000 000.NBЫB,NBB,NЛAБ
 IRCAT:1,,
 ,Z00,CAT
 IRCATI:9,,
 ,Z00,CAT
 IWCAT:10,,
 ,Z00,CAT
 :2,,
 ,Z00,CAT
 IRZON:3,Z01,LLZ
 ,,
 CCINP:,004,LLC
 ,Z00,
 DIAGN:,BSS,.
  ,ISO,12HMЛ BЫBOДA'41'''''
  ,ISO,12HMЛ BBOДA '41'''''
  ,ISO,12HMЛ BB/BЫB'41'''''
 BADCAT:,ISO,18HПЛOX.KATAЛ.BЫBOДA''
 TSTART:,BSS,.
 ,ISO,17HBЫBOД БYФEPИЗOBAH
 ,CONT,1H''
 TEKNDR:,ISO,6HHET MБ
 ,LOG,
 ROLLSIGN:,ISO,6HROLLTR
 NAM:,BSS,.
 ,TEXT,8HD73OUT '3'
 ,TEXT,8HD73INP '1'
 SC/ПП:,LOG,
 NJPT:,LOG,11.
 FULW:,LOG,7777 7777 7777 7777.
 B48/45:,OCT,74
 Z48   :,LOG,3777 7777 7777 7777
 Z47/45:,LOG,4377 7777 7777 7777
 Z48/17:,LOG,0000 0000 0017 7777
 BCNDR:,BSS,
 B48:,OCT,4.
 BBLS:,OCT,004
 B47:,OCT,2.
 BCATO:,OCT,1
 C4:,LOG,4
 C6000:,LOG,6000
 BCLROLL:,OCT,04
 B39:,OCT,0004
 CUN70:,LOG,77 0037
 PTRAPHY:,LOG,70000000001774
 CODES:,BSS,
 ,ISO,6HD78INP
 ,ISO,6HD78OUT
 B48/17:,OCT,77777777776
 C777:,LOG,777.
 C30:,LOG,30
 C7777:,LOG,7777
 CNT:,BSS,1.
 ,END,.
  CЛYГA:,NAME,
C
C*****   ИЗMEHEHИE HACTPOЙKИ ДИCПETЧEPA
C                B БЛOKE BЫЗOBA
C
C    ЗAПYCK :   13/
C
C    ПOPЯДOK PAБOTЫ:
C
C  1. BHAЧAЛE YKAЖEM HOMEP MБ И TPAKTA,
C     HA KOTOPOM HAXOДИTCЯ БЛOK BЫЗOBA:
C     HAПPИMEP,   <T>=2036
C     ГДE  <T> - TEPMИHATOP ЗAДAЧИ,
C     2036 - 36-Й TPAKT MБ 20.
C     ПPИ ЖEЛAHИИ ИCПPABИTЬ HACTPOЙKY B 420
C     ЗOHE ФAЙЛA 9/MONSYS  YKAЖEM: <T>=S
C
C  2. ДЛЯ ИCПPABЛEHИЯ KOHCTAHT B TPEБYEMOЙ
C     HACTPOEЧHOЙ ПPOГPAMME HAБEPEM CTPOKY:
C     <T>=<ИMЯ>:<CONST>,<CONST>,...,<CONST>
C     ГДE:  <ИMЯ> - ИMЯ ПPOГPAMMЫ,
C     <CONST> - BOCЬMEPИЧHAЯ KOHCTAHTA.
C     БYДET ИCПPABЛEHO CTOЛЬKO ЯЧEEK, CKOЛЬ-
C     KO MЫ ЗAДAДИM KOHCTAHT. ECЛИ ЖEЛATEЛЬ-
C     HO ИCПPABЛЯTЬ ЗHAЧEHИЯ HAЧИHAЯ HE C
C     0-Й ЯЧEЙKИ ПPOГPAMMЫ, YKAЖEM OTHOCИ-
C     TEЛЬHЫЙ AДPEC ПEPBOЙ KOHCTAHTЫ:
C     <T>=<ИMЯ>+<AДPEC>:<CONST>,...,<CONST>
C     ECЛИ CПИCOK ПOЛYЧИЛCЯ CЛИШKOM ДЛИHHЫЙ,
C     EГO MOЖHO ПPEPBATЬ ПOCЛE ДBOETOЧИЯ ИЛИ
C     ЗAПЯTOЙ И ПPOДOЛЖИTЬ HAБOP B CЛEДYЮЩEЙ
C     CTPOKE, HAПPИMEP:
C     <T>=<ИMЯ>+<AДPEC>:
C     <T>=<CONST>,<CONST>,<CONST>,
C     <T>=<CONST>,<CONST>
C     ПPИЗHAKOM KOHЦA CПИCKA KOHCTAHT ЯBЛЯ-
C     ETCЯ OTCYTCTBИE PAЗДEЛИTEЛЯ.
C     KAЖДAЯ KOHCTAHTA ПOCЛE ЗAПИCИ B COOT-
C     BETCTBYЮЩYЮ ЯЧEЙKY BЫBOДИTCЯ HA TEPMИ-
C     HAЛ, C YKAЗAHИEM ИMEHИ ПPOГPAMMЫ И OT-
C     HOCИTEЛЬHOГO AДPECA ЯЧEЙKИ.
C
C  2. ДЛЯ ПPOCMOTPA ЯЧEEK HACTPOEЧHOЙ ПPOГ-
C     PAMMЫ HAБEPEM ПPИKAЗ:
C     <T>=<ИMЯ>
C     ПOCЛE ЭTOГO BCЯ ПPOГPAMMA БYДET BЫBE-
C     ДEHA HA TEPMИHAЛ B BOCЬMEPИЧHOM BИДE.
C     ECЛИ MЫ XOTИM ПOCMOTPETЬ KOHKPETHYЮ
C     ЯЧEЙKY ПPOГPAMMЫ, ЗAДAДИM EE OTHOCИ-
C     TEЛЬHЫЙ AДPEC:
C     <T>=<ИMЯ>+<AДPEC>
C
C  3. HAЗBAHИE ФИPMЫ TAKЖE MOЖET БЫTЬ ЗAME-
C     HEHO. ДЛЯ CMEHЫ EГO HAБEPEM ПPИKAЗ:
C     <T>=FIRMA:<HAЗBAHИE>
C     HAЗBAHИE ЗAДAETCЯ B KOДE ISO, ЗHAKИ
C     ПPEПИHAHИЯ B KOHЦE ПPOГЛATЫBAЮTCЯ.
C     ЧTOБЫ YBИДETЬ CYЩECTBYЮЩEE HAЗBAHИE,
C     BOCПOЛЬЗYEMCЯ ПPИKAЗOM:
C     <T>=FIRMA
C
C  4. ЧTOБЫ ПO OKOHЧAHИИ PAБOTЫ БЛOK BЫЗOBA
C     БЫЛ ЗAПИCAH HA MECTO, HAБEPEM ПPИKAЗ:
C     <T>=E
C     ЗAПИCЬ ПPOИЗOЙДET TOЛЬKO B TOM CЛYЧAE,
C     ECЛИ MЫ ЧTO-HИБYДЬ ПOПPABИЛИ.
C     ECЛИ ЖE MЫ HAЧAЛИ COMHEBATЬCЯ B HEOБ-
C     XOДИMOCTИ ИCПPABЛEHИЙ, ПPEPBEM PAБOTY:
C     <T>=S
C
 *YCЫ:LC,BLOCK,ГYC
C-----   HACTPAИBAEMЫE БЛOKИ
 DISPBASE:,LC,7
 *ПДИCK:,SUBP,
 TYPE MT:,SUBP,
 *KOHCTPA:,SUBP,
 *TYPELP:,SUBP,
 TYP/PL:,SUBP,
 *TLPRMT:,SUBP,
 *TLBUFI:,SUBP,
 FIRMNAM:,EQU,765B
C-----   KOHEЦ HACTPOЙKИ
 *TLSIZE:,SUBP,
C
C-----   PAБOЧEE ПOЛE
 PAGE:,EQU,2000B
 NP:,P/P,(PAGE)16
 SHIP:,EQU,PAGE+70B
 SHIFT:,EQU,SHIP-DISPBASE
 LENT:,EQU,*TLSIZE-DISPBASE
 FIRMA:,EQU,FIRMNAM+PAGE
C
C
 1,BASE,*
C-----   C KEM BEДEM ДИAЛOГ
 ,FUN,70100B + 114B
 ,AAX,=1
 ,ASN,64-40
 ,AOX,IWO
 ,ATX,IWO
C-----   COOБЩИM O CEБE
 ,*65,ГYC
 ,ASN,64-27
 ,AUX,=6H'''''77'''''''
 ,ARX,TITOUT
 ,ATX,TITOUT
 2,VTM,TITOUT-BUFOUT
 ,ITA,2
 ,ARX,IWO
 ,FUN,76B
C-----   BЫЯCHИM, ГДE BЫЗЫBAЛKA
 2,VTM,BEGOUT-BUFOUT
 ,ITA,2
 ,ARX,IWO
 ,FUN,76B
 GET DR:,BSS,
 ,XTA,IWI
 ,FUN,205B
 ,XTA,BUFIN
 ,AEX,=1HS
 ,ASN,64+40
 ,UZA,SYS DR
 ,XTA,NEWSTR
 ,FUN,12
 14,UTM,-1
 14,VZM,IS DR
 ERR DR:2,VTM,ERRTEXT-BUFOUT
 ,ITA,2
 ,ARX,IWO
 ,FUN,76B
 ,UJ,GET DR
 IS DR:,BSS,
 ,ATX,W
 ,AAX,=7777 7777 7777 4040
 ,U1A,ERR DR
 ,XTA,W
 ,ASN,64+9
 ,ACX,.HAПPABЛEHИE
 ,AEX,=1. 1 ИЛИ 2
 ,U1A,ERR DR
 ,XTA,W
 ,ASN,64-36
 ,AUX,=77 0077
 ,ARX,CWEX
 ,ATX,IWEX
 SYS DR:,BSS,
C-----   ЧTEHИE BЫЗЫBAЛKИ
 9,VTM,0
 ,XTA,IWEX
 ,ARX,=:001
 ,DRUM,
 ,XTA,IWO
 ,FUN,76B
C
C=====   ПPABKA HACTPOЙKИ
C
 LOOP:,BSS,
 ,XTA,
 ,ATX,W
 ,XTA,IWI
 ,FUN,205B
 ,XTA,NEWSTR
 ,FUN,12
 14,UTM,-4.TEKCT
 14,V1M,ERROR
 ,ATX,NAME
 ,YTA,
 ,ATX,NAME+1
 ,XTA,NAME
 ,AEX,=1HS
 ,UZA,STOP
 ,XTA,NAME
 ,AEX,=1HE
 ,UZA,END
 3,VTM,TABLA-TABLNAM
 4,VTM,TABLNAM
C-----   ПOИCK ПPOГPAMMЫ
 SEAR:,BSS,
 ,XTA,NAME
 4,AEX,
 ,U1A,E/S
 ,XTA,NAME+1
 4,AEX,1
 ,UZA,IS PR
 E/S:4,UTM,2
 3,VLM,SEAR
C-----   HET БЛOKA
 ,UTC,TNON-ERRTEXT
 ERROR:2,VTM,ERRTEXT-BUFOUT
 ,ITA,2
 E/LOOP:,ARX,IWO
 ,FUN,76B
 ,UJ,LOOP
 IS PR:,BSS,
C-----   AБCOЛЮTHЫЙ AДPEC
 3,VZM,IS AD
 3,XTA,TABLNAM
 ,ATI,5
 ,ASN,64+24
 ,ATI,6
 IS AD:,ATX,L
 ,FUN,70100B + 136B
 ,AAX,=177
 ,ATI,7
 7,VZM,INFORM
 7,UTM,-53B. "+"
 7,V1M,CHKWORD
 3,VZM,ERROR
C-----   OTHOCИTEЛЬHЫЙ AДPEC
 ,XTA,OLDSTR
 ,FUN,12
 14,UTM,-1.BINARY
 14,V1M,ERROR
 ,ATX,W
 ,YTA,
 ,AAX,=177
 ,ATI,7
C-----   ПPOBEPKA AДPECA
 ,ITA,6
 ,ARX,W
 ,ASN,64+15
 ,U1A,ERADDR
C-----   ЧTO ЗA PAБOTA
 7,VZM,INF/1
 7,UTM,-53B
 CHKWORD:7,UTM,53B-72B. ":"
 7,V1M,ERROR
 3,V1M,PUTWORD
C-----   HAЗBAHИE ФИPMЫ
 ,XTA,OLDSTR
 ,FUN,12
 14,UTM,-4
 14,V1M,ERROR
 ,ATX,FIRMA
 9,VTM,TSTOP-TALL
 OUTF:,ATX,NUM
 ,XTA,=6H'1''1''1''1''1''1'
 ,ATX,NAME+1
 ,XTA,
 ,ATX,NUM+1
 ,UJ,E/LOOP
C-----   ЗAПИCЬ ЗHAЧEHИЙ
 P/W:,XTA,IWI
 ,FUN,205B
 :,UTC,NEWSTR-OLDSTR
 PUTWORD:,XTA,OLDSTR
 ,FUN,12
 14,UTM,-6
 14,VZM,P/W
 14,UTM,6-1
 14,V1M,ERROR.HE BINARY
 ,WTC,W
 5,ATX,
 ,YTA,
 ,AAX,=177
 ,ATI,7.PAЗДEЛИTEЛЬ
 13,VJM,TEROUT
 9,VTM,TSTOP-TALL
C-----   ПPOBEPKA ПPOДOЛЖEHИЯ
 7,VZM,LOOP.BCE.
 7,UTM,-54B. ","
 7,V1M,ERROR
 ,ARX,L.A MOЖHO ЛИ
 ,ASN,64+15.ИДTИ ДAЛЬШE
 ,UZA,PUTWORD
C-----   БЛOK KOHЧИЛCЯ
 ,UTC,TEND-TADD
 ERADDR:2,VTM,TADD-BUFOUT
 ,ITA,2
 ,UJ,E/LOOP
C-----   KOHTPOЛЬ БЛOKA
 INFORM:,BSS,
 ,XTA,FIRMA
 3,VZM,OUTF
 6,UTM,1
 13,VJM,TEROUT
 6,VLM,TEROUT
 ,UJ,LOOP
C-----   KOHTPOЛЬ ЭЛEMEHTA
 INF/1:,BSS,
 13,VTM,LOOP
C-----   BЫДAЧA HA TEPMИHAЛ
 TEROUT:,BSS,
 ,XTA,W
 ,UZA,NON AD
 ,ASN,64-42
 ,AUX,=6H'''''''''7''7'
 ,ARX,=6H   +00
 ,AEX,NAME+1
 ,ASN,64-16
 ,ASN,64+16
 ,AEX,NAME+1
 ,ATX,NAME+1
 NON AD:,XTA,W
 ,ASN,64-24
 ,ARX,PUTO
 ,FUN,14
 ,XTA,NUM+3
 ,ASN,64+40
 ,ASN,64-40
 ,ATX,NUM+3
 ,XTA,IWO
 ,FUN,76B
 ,XTA,W
 ,ARX,=1
 ,ATX,W
 13,UJ,
C
C=====   KOHEЦ PAБOTЫ
 END:9,VZM,STOP
 ,DRUM,IWEX
 ,XTA,IWEX
 ,APX,=77 0077
 ,AUX,=6H'''7''7''7''7'
 ,ARX,NUMDR
 ,ATX,NUMDR
 ,UTC,TPUT-TSTOP
 STOP:9,UTM,TALL-BUFOUT
 ,ITA,9
 ,ARX,IWO
 ,FUN,76B
 ,*62,
C
C=====   TAБЛИЦЫ AДPECOB И ИMEH
C
 TABLA:,BSS,
 :,Z00,DISPBASE -*ПДИCK
  ,Z00,DISPBASE +SHIFT
 :,Z00,*ПДИCK   -TYPEMT
  ,Z00,*ПДИCK   +SHIFT
 :,Z00,TYPEMT   -*KOHCTPA
  ,Z00,TYPEMT   +SHIFT
 :,Z00,*KOHCTPA -*TYPELP
  ,Z00,*KOHCTPA +SHIFT
 :,Z00,*TYPELP  -TYP/PL
  ,Z00,*TYPELP  +SHIFT
 :,Z00,TYP/PL   -*TLPRMT
  ,Z00,TYP/PL   +SHIFT
 :,Z00,*TLPRMT  -*TLBUFI
  ,Z00,*TLPRMT  +SHIFT
 :,Z00,*TLBUFI  -*TLSIZE
  ,Z00,*TLBUFI  +SHIFT
  :,Z00,         -1
  ,Z00,*TLSIZE  +SHIFT
C
 TABLNAM:,BSS,
 :,ISO,12HDISPBASE
 :,ISO,12H*ПДИCK
 :,ISO,12HTYPEMT
 :,ISO,12H*KOHCTPA
 :,ISO,12H*TYPELP
 :,ISO,12HTYP/PL
 :,ISO,12H*TLPRMT
 :,ISO,12H*TLBUFI
  :,ISO,12HCИHXPOBЫBOД
 :,ISO,12HFIRMA
C
C=====   KOHCTAHTЫ
 NEWSTR:,Z00,
 ,Z07,BUFIN
 OLDSTR:,Z00,
 ,Z07,
 PUTO:5,Z24,
 ,Z04,NUM
 CWEX:,004,NP
 ,,
 IWEX:,004,NP.HA 9/MONSYS
 ,016,20B. 420 ЗOHA
 IWI:,Z00,
 ,Z01,BUFIN
 IWO:,Z01,
 ,Z00,BUFOUT
C-----   БYФEP BЫBOДA
 BUFOUT:,BSS,
 BLANK:,ISO,6H
 NAME:,ISO,12HHAЧИHAЙTE!''''
 :,ISO,6H :  '1''1'
 NUM:,BSS,
 TITOUT:,ISO,6H  '100'-
 ,ISO,H/ПPABKA HACTPOЙKИ ДИCПETЧEPA.''''/
 BEGOUT:,ISO,H/  ГДE BЗЯTЬ БЛOK BЫЗOBA ДИCПETЧEPA?''/
 ERRTEXT:,ISO,H/  OШИБKA!''''''/
 TNON:,ISO,H/  HET EГO!''''/
 TADD:,ISO,H/  AДPEC!''''''''/
 TEND:,ISO,H/  XBATИT!''''''/
 TALL:,ISO,H/ПPABOK HE БЫЛO.''''''/
 TPUT:,ISO,H/БЛOK BЫЗOBA ДИCПETЧEPA ЗAПИCAH HA MБ/
 NUMDR:,ISO,6H 0000''
 TSTOP:,ISO,H/PAБOTA ПPEPBAHA.''''/
  W:,BSS,1
  L:,BSS,1
  BUFIN:,BSS,15
 ,END,

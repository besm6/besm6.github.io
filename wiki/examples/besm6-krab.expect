#!/usr/bin/expect -f
#
# Автоматизированная запись файла в файл КРАБа на удалённой БЭСМ-6.
# Вызов:
#	besm6-krab.expect хост порт кто ключ имяфай пароль filename.local
# Параметры:
#	хост    - имя хоста, например besm6.vak.ru
#	порт	- номер порта TCP, на котором работает эмулятор БЭСМ-6
#	кто     - шифр пользователя системы КРАБ
#	ключ    - ключ пользователя системы КРАБ
#       имяфай  - имя создаваемого файла системы КРАБ
#       пароль  - пароль файла системы КРАБ
#       filename.local - имя текстового файла для передачи
#
# Copyright (C) 2009 Serge Vakulenko <serge@vak.ru>
#
if { $::argc != 7 } {
	puts "Usage:\n\t$::argv0 хост порт кто ключ имяфай пароль filename.local"
	exit 0
}
set host [lindex $::argv 0]
set port [lindex $::argv 1]
set who [lindex $::argv 2]
set key [lindex $::argv 3]
set namfil [lindex $::argv 4]
set pass [lindex $::argv 5]
set filename [lindex $::argv 6]

#
# Соединяемся с терминалом БЭСМ-6.
#
spawn telnet $host $port
expect 	{
	"ЭBM*\n"		{}
	"*\r\n-"		{ send "кон\r"; exp_continue }
	"KОHEЦ CEAHCA*\n"	{ sleep 1; send "\r"; exp_continue }
}

#
# Вызываем КРАБ.
#
send	"краб\r"
expect 	"*\r\n-"
send	"нач\r"
expect 	"KTО?"
send	"$who\r"
expect 	{
	"*\r\n-"		{}
	"KЛЮЧ"			{ send "$key\r"; exp_continue }
}

#
# Стираем старую версию файла.
#
send	"унч $namfil\r"
expect 	{
	"HET B KATAЛОГE*\n-"	{}
	"ECTЬ*\n-"		{}
	"ПAPОЛЬ"		{ send "$pass\r"; exp_continue }
}

#
# Вводим текст.
#
send	"имя $namfil\r"
expect 	{
	"HET B KATAЛОГE*\n-"	{}
	"CО CБОЙHЫX ЗОH*\n-"	{ send "отм 1-9999\r"; expect "*\r\n-" }
}
send	"доп\r"
expect 	"*\r\n+"

#
# Вводим содержимое заданного файла.
#
set fp [open $filename]
fconfigure $fp -buffering line
while 1 {
	gets $fp line
	if [eof $fp] break
	if { $line == "" } {
		set line " "
	}
	send	"$line\r"
	expect 	"*\r\n+"
}
close $fp
send	"\r"
expect 	"*\r\n-"

send	"зап\r"
expect 	{
	"ECTЬ*\n-"		{}
	"ПAPОЛЬ"		{ send "$pass\r"; exp_continue }
}

#
# Завершаем работу.
#
send	"кон\r"
expect 	{
	"KОHEЦ CEAHCA"		{}
	"*\r\n-"		{ send "кон\r"; exp_continue }
}

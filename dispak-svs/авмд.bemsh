 МАСRО
∧М УВВ ∧А
 АIF ((∧ВS NЕ 0)).К1
∧М КК 27,∧А
 МЕХIТ
.К1 АIF ((∧А ЕQ ’’’147’’’) ОR (∧А ЕQ ’’’4035’’’)).К2
∧М ДК 27,∧А  ОСТАНОВ
 МЕХIТ
.К2 АNОР
∧М КК 0,0
 МЕХIТ
 МЕND
АВМД СТАРТ ’20151’
 Е
 М
 Б
 УПОТР АВМД(М7)
АВДИС НОП ,
ОПРДИС НОП ,
 УИА АВДИС(М7)
 СЧИ М7
 ЗП ЗАВ
 СЧИ М16
 ЗП С16
 И Е3
 УИ М4
 УИИ М10(М3)
 СЧ 0
 МОДА Р-3
 ЗП (М16)
 ЗП ПРИЗ
 УИА 1027
 СЧ РМД-3(М16)
 НТЖ РМД1-3(М16)
 ИЛИ ВЫК-3(М16)
 И НОМРМД-3(М16)
 И Е32П25(М10)
 АIF (∧ВS ЕQ 0).НСОВ
 ПЕ НЕСОВП
 СЧ ПВКЛМД      ПРОВ. СЧЕТЧИК
 И Е48
 ПО ППЕ    ЕЩЕ РАНО
НЕСОВП СЧ 0
 ЗП ПВКЛМД     ИНИЦИАЛИЗИРУЕМ СЧЕТЧИК
ДИСКИ ВНЕШ ПВКЛМД
 АGО .СОВП
.НСОВ АNОР
 ПО ППЕ
.СОВП АNОР
 СЧ РМД-3(М16)
 ИЛИ ВЫК-3(М16)
 И Е32П25(М10)
 ИЛИ РМД1-3(М16)
 ЗП РМД1-3(М16)
 СЧ ВСЕЕД
 НТЖ Е32П25(М10)
 И ВЫК-3(М16)
 ЗП ВЫК-3(М16)
 УИА -7(М4)
 МОДА ТМЛ-1
 УИА (М17)
ВМЛ СЧИ М16
 И Е3
 СДА 64-3
 УИ М1
 СЧИ М3
 СДА 64-3
 УИ М13
 СЧ РМД-3(М16)
 И НОМРМД-3(М16)
 МОДА 7(М4)
 И Е32(М13)
 УИА 3
 ПЕ ВМЛ2 ВЫКЛ
ВМЛ1 МОДА ВМЛ
 ЦИКЛ (М4)
 УИА 3
 СЧ ТМД
 ЗП ТМЛ-1
 ЗП ПРИЗ
МЛ1А СЧ ЗАНКМД-3(М16)
 СДА 64+3
 И П3
 УИ М10
 СЧ РМД-3(М16)
 НТЖ РМД1-3(М16)
 И НОМРМД-3(М16)
 И Е32П25(М10)
 ПО ППЕ
 НЕД П7
 УИ М13
 НТЖ П7
 УИ М10
 СЧИ М16
 И Е3
 СДА 64-3
 УИ М1
 УИИ М12(М1)
 СЛИ М1(М10)
 УИА 1027
 СЧ Е48-8(М13)
 НТЖ РМД1-3(М16)
 ЗП РМД1-3(М16)
 ПБ МЛ1Е
ВЫВ СЧ Е48-24(М10)
 МОДА Р-3
 ИЛИ (М16)
 МОДА Р-3
 ЗП (М16)
МЛ2А УИА 3
 СЧ ЗАНКМД-3(М16)
 СДА 64+3
 И П3
 УИ М10
 СЧ РМД-3(М16)
 НТЖ РМД1-3(М16)
 И НОМРМД-3(М16)
 И Е32П25(М10)
 ПЕ МЛ1А
 МОДА Р-3
 СЧ (М16)
 ПО ППЕ
 НЕД П77777
 УИ М10
 СЧИ М16
 И Е3
 СДА 64-3
 УИ М12
 УИА 1027
 СЧ Е6
 ИЛИ ШКИВ
 ЗП ШКИВ
 УИА 3
 МОДА МЛ4
 УИА (М11)
 СЛИ М10(М12)
МЛ3Е МОДА ТМЛ-1
 УИА (М17)
 СЧ ТЗНМД(М10)
 УИИ М2(М16)
 И П7777
 ПВ ПЕРЕВ(М15)
 ИЛИ ТМЛ1
 ЗП 1(М17)
 УИИ М16(М2)
 СЧИ М10
 СДА 64-42
 РЗБ МСК2
 ИЛИ ТМЛ1+1
 ЗП 2(М17)
 ПБ (М11)
МГЗАН СЛИА -24(М1)
 УИИ М4(М10)
 СЛИА -24(М4)
 ПБ МДИ
МЛ4 СЧ Е3
 СЛЦ 2(М17)
 ЗП 2(М17)
 СЧ ТМЛ
 СДА 64-7
 СДА 64+7
 ЗП ТМЛ
 УИИ М10(М16)
 УИА ТМЛ(М16)
 ПВ РАС(М2)
 УИИ М16(М10)
 ПБ ППЕ
ВМЛ2 НОП ,
 СЧИ М4
 НТЖ П7
 СЛЦ Е1
 И П7
 УИ М10
 СЛИ М10(М13)
 МОДА (М1)
 СЧ ТЗНМД(М10)
 ПО ВМЛ1
 И ПРСВЛ
 ПО ДАХ
 СЛИ М10(М1)
ЗАНД1 СЧ ТЗНМД(М10)
 ИЛИ Е40
 ЗП ТЗНМД(М10)
 СЧ ШОЧНМД-3(М16)
 ИЛИ ШОЧМД(М10)
 НТЖ ШОЧМД(М10)
 ЗП ШОЧНМД-3(М16)
 СЧ ЗАНКМД-3(М16)
 СДА 64+3
 УИ М11
 СЧ ШОЧЛ(М11)
 ИЛИ ШОЧМД(М10)
 НТЖ ШОЧМД(М10)
 ЗП ШОЧЛ(М11)
 СЧ ТМЛА
 ЗП ТМЛ-1
 АIF (∧ВS ЕQ 1).ТАБЛО
 СЧ ТАБЛО
 ИЛИ Е17
 ЗП ТАБЛО
 УВВ ’147’
.ТАБЛО АNОР
ВТК ПВ МЛ3Е(М11)
 СЧ ТМЛ+1
 СЛЦ Е3
 ЗП ТМЛ+1
ВТ УИИ М10(М16)
 УИА ТМЛ-1(М16)
 МОДА РАС
 ПВ (М2)
 УИИ М16(М10)
 ПБ МЛ2АБ
ДАХ СЧ Е1
 ЗП ПАР
 АIF (∧ВS ЕQ 1).Б
ВМЛ5 СЧ КОПЗ-3(М16)
 И Е32(М10)
 ПЕ ВМЛ1
 АGО .Б66
.Б АNОР
 СЧ 0
.Б66 АNОР
 МОДА (М1)
 ЗП ТЗНМД(М10)
 ПБ ВМЛ1
МЛ1Е СЛИА -24(М1)
 АIF (∧ВS ЕQ 0).ОПР
 СЧ КФКУ
 ЗП КФКУС
 СЧ Е26Е25
ОПР1 ЗП ДОБЗ-3(М16)
.ОПР АNОР
 СЧИ М1
 СДА 64-42
 РЗБ НАПРМГ
 ИЛИ КФКУС
 ЗП ТМЛ
ОПРОС СЧ ТМЛ
 СЛЦ ДОБЗ-3(М16)
 СЛЦ Е43
 ЗП ТМЛ
 УИА ДВ(М11)
 ПБ ПОДОПР
*ВОЗВРАТ НА ПОС
 ОТМЕН (М7)
ПОС СЧ 0
 ПБ ОБР2Д
ОП СЧ РЯД
 И П7777
 УПОТР АВМД(М7)
ОБР2Д УИА АВМД(М7)
 ЗП РИ
 СЧ 0
 ЗП КУС2-1(М16)
 СЧ ВСЕЕД
 НТЖ Е32(М4)
 И ЗАНМД-3(М16)
 ЗП ЗАНМД-3(М16)
 АIF (∧ВS ЕQ 1).ОБРД
 СЧ НЗ-3(М16)
 ПЕ ОПРДИ
.ОБРД АNОР
ОБРД СЧ РИ
 ПЕ МЛ2
ОБЖ СЧ ТМЛ
 И Е46
 ПО ОПРОС
 СЧИ М1
 СДА 64-42
 РЗБ МСК2
 ИЛИ ТМЛ1+1
 СЛЦ Е3
 ЗП ТМЛ
 ЗП ПРИЗ
 СЧ ТЗНМД(М1)
 И ПРСВЛ
 ПЕ ЗАНД
 ЗП ТЗНМД(М1)
 ПБ ВТ
ЗАНД УИИ М10(М1)
 ПБ ЗАНД1
 АIF (∧ВS ЕQ 0).ДЕФ1
КДЕФ УИА 0
 СЧ 1024
 УИА 3
 НТЖ КЛЮЧРЗ
 ПЕ МДСК1    НЕТ ДЕФ ДОР
 СЧ БУФКДЗ
 ПЕ КДЕФ1
 УИИ М11(М16)
 СЧ Е2
 ПВ ДБУФ(М15)
 УИА 3
 УИИ М16(М11)
 ЗП БУФКДЗ
КДЕФ1 СЧИ М1
 СДА 64-4
 УИ М15
 УИА 1(М2)
 СЧ БУФКДЗ
 ПВ ПРУ12(М10)
 УИА -14(М11)
 УИА 0
КДЕФ2 СЧ 1024+15(М11)
 МОДА (М15)
 ЗП 2048+14(М11)
 ЦИКЛ КДЕФ2(М11)
 УИА 3
 СЧ ЕДЕФ
 ИЛИ ТЗНМД(М1)
 ЗП ТЗНМД(М1)
МДСК1 МОДА Р22-3
 СЧ (М16)
 УИ М10
 ПБ МДСК
.ДЕФ1 АNОР
МЛ2 И Е12
 ПО ОБЖ
 СЧ РИ
 И П7777
 ВЧОБ NБЛ
 ПЕ ОБЖ
 АIF (∧ВS ЕQ 0).ДЕФ
 СЧ ДОБЗ-3(М16)
 ПО КДЕФ
.ДЕФ АNОР
 СЧ ТЗНМД(М1)
 И Е47
 УИИ М11(М16)
 СЛИ М11(М7)
 ПО МДПО
 СЧ НМД-3-А(М11)
 И Е48(М4)
 ПЕ МДПО
 СЧ Е48(М4)
 ИЛИ ПР-3-А(М11)
 ЗП ПР-3-А(М11)
МДПО СЧ ТЗНМД(М1)
 УИИ М10(М4)
 СЛИА 24(М10)
 ПО МДПМ
МДИ СЧ ТЗНМД(М1)
 НТЖ РИ
 И П7777
 ПЕ МГЗ
 СЧ ЗУС-3(М16)
 И Е32(М4)
 ПЕ НЕЛЬЗ
 АIF (∧ВS ЕQ 0).МДИИ    ЗАТЫЧКА 18.2.82
 УИА 1027
 СЧ ШОЧМД(М1)  ОЧЕРЕДЬ К УСТР
 ИЛИ ШОЧНМД-3(М16)
 ЗП ШОЧНМД-3(М16)   В ОЧЕРЕДЬ К НАПРАВЛЕНИЮ
 СЧИ М1
 СДА 64+3
 УИ М12    ЛИНЕЙКА
 СЧ ШОЧМД(М1)
 ИЛИ ШОЧЛ(М12)
 ЗП ШОЧЛ(М12)      В ОЧЕРЕДЬ К ЛИНЕЙКЕ
 УИА 3
 АGО .МДИ
.МДИИ АNОР
 СЧ РУ-3(М16)
 ИЛИ Е32(М4)
 ЗП РУ-3(М16)
.МДИ АNОР
НЕЛЬЗ СЧ ТЗНМД(М1)
 И О40
 ЗП ТЗНМД(М1)
 АIF (∧ВS ЕQ 0).ВЫ
 ПБ МДПМ
 АGО .ВЫ1
.ВЫ АNОР
 ПБ ВЫВ
.ВЫ1 АNОР
МГЗ СЧ Е40
 ИЛИ ТЗНМД(М1)
 ЗП ТЗНМД(М1)
 СЧ РИ
 ИЛИ Е47
 МОДА (М1)
 ЗП НТЗНМД
 СЧ Е48(М4)
 ИЛИ ДР-3-А(М11)
 ЗП ДР-3-А(М11)
 СЧ ПР-3-А(М11)
 ИЛИ Е48(М4)
 НТЖ Е48(М4)
 ЗП ПР-3-А(М11)
 УИИ М10(М1)
 ПБ ВТК
МДПМ НОП 0
 СЧ ТЗНМД(М1)
 И Е47
 ПЕ МДСК
 СЧ 3
 И Е10
 ПЕ МДСК
 СЧ МД100М-3(М16)
 И Е32(М4)
 ПЕ МДСК
 СЧ ЛСЕК-3(М16)
 ЗП ЛИС2-1(М16)
 СЧИ М10
 МОДА Р22-3
 ЗП (М16)
 СЧ МД29М-3(М16)
 И Е32(М4)
 УИА КАТДЗ(М3)
 ПО ДЗ725
 СЛИА 1(М3)
ДЗ725 СЧ (М3)
 ЗП КФКУС
 СЧ 0
 ПБ ОПР1
*
МДСК СЧ ТЗНМД(М1)
 И П7777
 ПЕ ВЫВ
 СЧ РИ
 И Е24П1
 ИЛИ ЕН
 РЖА 3
 ИЛИ ТЗНМД(М1)
 ЗП ТЗНМД(М1)
 И П7777
 ВЧОБ NКОБ
 ПЕ ПРНОВ
 СЧ Е41
 ИЛИ ТЗНМД(М1)
 ЗП ТЗНМД(М1)
ПРНОВ УИА 1027
 СЧ АЗУ
 СЛЦ Е1
 ЗП АЗУ
 ЗП ПАР
 ПБ ВЫВ
ПАР КОНД В’0’
МЛ2АБ УИА 3
 СЧ ПРИЗ
 ПО ВМЛ1 МД ВЫКЛ
 ПБ МЛ2А
ППЕ МОДА Р-3
 СЧ (М16)
 МОДА (М16)
 ИЛИ НМД-3-А(М7)
 МОДА (М16)
 ЗП НМД-3-А(М7)
 СЧ ПАР
 ПО НОВД
 СЧ 0
 ЗП ПАР
 УИИ М13(М16)
 УИИ М1(М17)
 УИА 1027
 СЧ ШАРСИС
 ИЛИ Е26
 ЗП ШАРСИС
 СЧ СОБА40
 ПВ ЗАПВЕТ(М16)
 УИИ М16(М13)
 УИИ М17(М1)
НОВД СЧИ М16
 ЗП С16
 ПБ Д7В
НОВ СЧ ЗАВ
 УИ М7
 СЧ ШЗПОМД
 ИЛИ Е24
 НТЖ Е24
 ЗП ШЗПОМД
 СЧ ДАЙГОТ+1(М15)
 ПЕ ДКО
 СЧ СОД
 ИЛИ СОД+1
 И П377
 ПЕ ЗАН
 УИА (М15)
 СЧ С16
 УИ М16
 И Е3
 ПЕ НОК
 УИА 4(М16)
 СЧ НОМРМД-3(М16)
 ПЕ ДКК
НОК СЧ ШЗПОМД
 ИЛИ Е40
 ЗП ШЗПОМД
 СЧ ВЫ
 ПЕ НОВК
 СЧ ШКСВН
 ИЛИ Е26
 ЗП ШКСВН
 СЧ РМЛ
 ИЛИ Е47П43
 ЗП РМЛ
 ЗП ВЫ
НОВК НОП ,
 УИА 0(М12)
 УИА -1(М3)
НВ1 УИИ М16(М3)
 СЛИ М16(М7)
НВ2 СЧ НМД+1-А(М16)
 УИА 3
 ПО НВ3
 УИА (М13)
 УИА (М14)
 НЕД П77777
 УИ М10
 СЧ ВТОР
 ПЕ МДДР
 СЧ ДР+1-А(М16)
 И Е48(М10)
 ПЕ НОВ1 Д СТОЯТЬ ДР ПАКЕТ
НВ20 УИА -1(М11)
 УИА -32(М2)
НВ УИА -У(М4)
 СЛИА 32(М2)
НОВ2 МОД ВТОР
 МОДА (М12)
 СЧ ТЗНМД(М10)
 МОДА (М2)
 НТЖ ТЗНМД+У(М4)
 И П7777
 ПЕ ВКЛ
 УИА С1(М6)
 ПБ (М6)            В   СМЕ
МДДР МОДА (М12)
 СЧ ТЗНМД(М10)
 УИ М13  ≠0 - ПЕРЕСТ ДВУХ
 ПБ НВ20
ВКЛ ЦИКЛ НОВ2(М4)
 ЦИКЛ НВ(М11)
 СЧ ПР+1-А(М16)
 И Е48(М10)
 ПЕ НОВ1
 ПИНО ПРСМД(М13)
 МОД ВТОР
 МОДА (М12)
 СЧ ТЗНМД(М10)
 ИЛИ Е47
 НТЖ Е47
 ИЛИ ЕН
 МОДА (М12)
 ЗП ТЗНМД(М10)
 ПБ НОВ1
ПЕРЕСТ МОДА (М2)
 МОДА У(М4)
 СЧ НТЗНМД
 МОДА (М12)
 НТЖ ТЗНМД(М10)
 И П7777
 ПЕ ПРСМД
 ПБ МДЕСТ-С1(М6)
ПРСМД СЧИ М10
 НТЖ П7
 УИ М2
 СЧ РМД1+1(М3)
 ИЛИ Е32(М2)
 ЗП РМД1+1(М3)
НОВ1 СЧ НМД+1-А(М16)
 НТЖ Е48(М10)
 ЗП НМД+1-А(М16)
 ПБ НВ2
НВ3 СЛИА 32(М12)
 СЧ ДР+1-А(М16)
 ЗП НМД+1-А(М16)
 СЧ 0
 ЗП ПР+1-А(М16)
 МОДА НВ1
 ЦИКЛ (М3)
 УИА -Л(М3)
 СЧ ВТОР
 ПЕ КПД
 СЛИА НТЗНМД-ТЗНМД-А(М16)
 СЧИ М16
 ЗП ВТОР
 ПБ НОВК
КПД УИА -63(М16)
 СЧ 0
КП1 МОДА Л(М3)
 ЗП С16
 МОДА КП1
 ЦИКЛ (М3)
КП2 МОДА 63(М16)
 ЗП НТЗНМД
 МОДА КП2
 ЦИКЛ (М16)
 СЧ С16
 ПЕ Д3
 ЗП ЗАВ
 ПБ Д3
*
*
ЭТОТ СЧИ М2
 СЛЦ П3
 СДА 64-2
 ЗП Р
 СЧИ М4
 СЛЦ П3
 ИЛИ Р
 ЗП Р
 УИА -15(М13)
ЭТТ УИА 4(М10)
ЭТД МОДА (М11)
 СЧ ШЗНМБ+15(М13)
 СДА 64+8(М10)
 НТЖ Р
 И П17
 ПО ЭТДИ
 МОДА ЭТД1
 ПИО (М10)
 УИА (М10)
 ПБ ЭТД
ЭТД1 МОДА ЭТТ
 ЦИКЛ (М13)
ЭТДИ СЛИА 15(М13)
 СЛИ М13(М13)
 МОДА ВСМЕ-С1(М6)
 ПИО (М10)
 СЛИА 1(М13)
 ПБ ВСМЕ-С1(М6)
ВЫ ПАМ 1
С16 КОНД В’0’
НМД ПАМ 2
ПР ПАМ 2
ДР ПАМ 2
ВТОР ПАМ 1
РИ КОНД В’0’
ПРИЗ КОНД В’0’
 КОНД В’0’
ТМЛ КОНД П’  0000’
 КОНД П’ - 00,’
 ПАМ 4
ТМЛ1 КОНД П’  0000’
 КОНД П’ - 00,’
МСК2 КОНД В’1603400’
ТМД КОНД П’1ПР.МД’
ТМЛА КОНД П’1ВКЛМД’
КФКУС КОНД В’0’
 КОНД В’0’
Р ПАМ 2
НТЗНМД ПАМ 64
Р22 ПАМ 1
Р33 ПАМ 1
Р44 ПАМ 1
Р55 ПАМ 1
 СТРН ,
*
*
***********************************************
***                                         ***
***           О Б Р А Б О Т К А             ***
***                                         ***
***        А В А Р И И       М   Д          ***
***                                         ***
***********************************************
*
*
*
* ФОРМИРОВАНИЕ СООБЩЕНИЯ ОБ АВАРИИ МД
*
*
АВАРМД НОП ,
 УИА 0(М3)
 СЧ МД29М-3(М16)
 И Е32(М4)
 ПО ФОРНУ
 И МД100М-3(М16)
 УИА 1(М3)
 ПО ФОРНУ
 УИА 2(М3)
ФОРНУ НОП ,
 СЧ ОЧКУСД-1(М14)
 СБР НАПРМГ
 РЗБ =П’000770’
 ИЛИ =П’5МД00-’
 ЗП БР            Н У
*
 СЧ ТЗНМД(М1)
 И П7777
 ПВ ПЕРЕВ(М15)
 СДА 64-16
 ИЛИ =П’0000  ’
 ЗП БР+1          П А К Е Т
*
 СЧ ОЧКУСД-1(М14)
 И Е34П25(М3)
 ВЧ Е27
 СДА 64-9
 РЗБ =П’777770’
 ИЛИ П17
 ЗП БР+2          З О Н А
*
 СЧ ОЧКУСД-1(М14)
 И Е34П25(М3)
 СДА 64+24-1(М3)
* СМ = N ДОРОЖКИ
 МОДА КЦИЛ
 УМН (М3)
 ЗП РЯД
 СЧМР 64
 МОДА КГОЛ
 УМН (М3)
 ПВ ПЕРЕВ(М15)
 ИЛИ =П’000.00’
 ЗП БР+3          Г О Л О В К А
 СЧ РЯД
 ПВ ПЕРЕВ(М15)
 СДА 64-24
 ИЛИ БР+3
 ЗП БР+3          Ц И Л И Н Д Р
*
 ПИНО ЗППРИЧ(М10)
*
*   БЫЛ  Б Н С  В  Д Р
*
 УИА ОШДР(М2)      М2 := ОШДР
 КК ’33’,ДРД
 ЗП РАБЯЧ
 СЧМР
 ЗП РАБЯЧ+1       РАБЯЧ,+1:= ДР
 И Е45
 ПО ДРДРУ         НЕТ ПРИЗНАКА ДРУ (12 Р. ДР)
* БЫД ДРУ
 УИА 3
 УИА ОШДРУ(М2)     М2 := ОШДР
 КК ’33’,ДРУД
 ЗП РАБЯЧ
 СЧМР
 ЗП РАБЯЧ+1       РАБЯЧ,+1:= ДРУ
  СЧ РАБЯЧ
 МОДА НГОТ
 И (М3)
 УИА НЕТГОТ(М10)   М10 := НЕТГОТ
 ПЕ ЗППРИЧ        НЕТ ГОТОВНОСТИ МД
 СЧ КУС2-1(М6)
 И Е18
 ПЕ ДРДРУ         БЫЛО ЧТЕНИЕ
 СЧ РАБЯЧ
 И ЕТЧ
 УИА НЕТЗАП(М10)   М10 := НЕТЗАП
 ПЕ ЗППРИЧ        ВЫКЛ.ЗАПИСЬ
ДРДРУ  НОП ,
 СЧ (М2)
 ЗП БР+4
 СЧ 1(М2)
 ЗП ОТВАВ
* ЗАПИСЬ В БР+5,БР+6,БР+7  Д Р  ИЛИ  Д Р У
 УИА БР+5(М10)
 УИА -1(М16)
 УИА -11(М3)
 УИА -5(М2)
ПЕЧДР НОП ,
 МОДА РАБЯЧ
 СЧ 1(М16)
  СДА 64-4
 МОДА РАБЯЧ
 ЗП 1(М16)
 СЧМР
 ЗП РЯД
 ВЧ П12
 ПЕ ЗАПЦИФ
 СДА 64-3
 УИ М15
 СЧ ЦИФРЫ
 СДА 64(М15)
 И П377
 ЗП РЯД
ЗАПЦИФ НОП ,
 СЧ (М10)
 СДА 64-8
 ИЛИ РЯД
 ЗП (М10)
 ЦИКЛ СЛЕДЯЧ(М2)
 УИА -5(М2)
 СЛИА 1(М10)
СЛЕДЯЧ НОП ,
 ЦИКЛ ПЕЧДР(М3)
 УИА -3(М3)
 ЦИКЛ ПЕЧДР(М16)
 СЧ БР+7
 СДА 64-8
 ИЛИ СТРЕЛА
 СДА 64-8
 ЗП БР+7
 ПБ ЗПСТАТ
*
ЗППРИЧ НОП ,
 СЧ (М10)
 ЗП БР+4          П Р И Ч И Н А
 СЧ 1(М10)
 И П377
 ЗП ОТВАВ         ОТВАВ = ПРИЗНАКИ ПРИЧИНЫ
 НТЖ 1(М10)
 ИЛИ СТРЕЛА
 ЗП БР+5          П Р И Ч И Н А  + 1
ЗПСТАТ НОП ,
 СЧ ОТВАВ
 И П17
 УИ М5            М5 := N КВАНТА СТАТИСТИКИ
 ПО НЕТСТА        ПРИЧИНУ НЕ СОБИРАЕМ
*
*
*  ЗАПИСЬ СТАТИСТИКИ
*
*
 СЧ СТМГ+32(М1)
 ПО НЕТСТА
 СДА 64+18
 И Е6П1
 ПЕ ЕСН
НРО СЧИ М1
 ИЛИ Е48
 ЗП РЯД
 СЧ ТЗНМД(М1)
 И П7777
 ПО НЕТСТА
 СДА 64-6
 ИЛИ Е46
 ИЛИ РЯД
 ЗП РЯД
 СЧ ОЧКУСД-1(М14)
 СДА 64+32
 И П17
 СДА 64-18
 ИЛИ РЯД
 ЗП РЯД
 ПВ ДСЛ(3)
 ПО НЕТСТА
 УИ М15
 СДА 64-18
 ИЛИ СТМГ+32(М1)
 ЗП СТМГ+32(М1)
 СЧ РМР
ЕСН УИА 3
 УИ М15
 СЧ ОЧКУСД-1(М14)
 СДА 64+14
 НТЖ СТМГ+Д-1(М15)
 СДА 64+18
 И П17
 ПЕ СЗ
 СЧИ М5
 УМН К6
 СЧМР 64
 УИ М16
 СЧ Е48П43
 СДА 64-6(М16)
 ЗП РЯД
 СЧ Е43-6(М16)
 СЛЦ СТМГ+Д(М15)
 ЗП СТМГ+Д(М15)
 И РЯД
 НТЖ РЯД
 ПЕ НЕТСТА
СЗ СЧ СТМГ+32(М1)
 И Н19П24
 ЗП СТМГ+32(М1)
 ПБ НРО
НЕТСТА НОП ,
*
* ПЕРЕПИЬ СООБЩЕНИЯ ОБ АВАРИИ МД
*
 УИА БР(М16)       М16 := БР ДЛЯ ПОДПР-МЫ ’РАС’
 УИИ М3(М6)
 СЛИ М3(М7)        М3 := N НАПРАВЛЕНИЯ + БАЗА АВМД
 СЧ ШРАСП-3-А(М3)
 И Е32(М4)
 ПО ВЫДАТЬ        АВ.РАСП.ОБ ЭТОМ УСТР-ВЕ НЕ ВЫДАНА
 СЧ ВРЕМЯД
 ВЧОБ ВРЕМЯ
 ВЧАБ СЕК15
 ПЕ РАНО          ПРОШЛО МЕНЬШЕ 15 СЕК ОТ ПРЕДЫД.РАСП.
ВЫДАТЬ СЧ ШРАСП-3-А(М3)
 ИЛИ Е32(М4)       УСТ.ПРИЗНАК: АВ.РАСП.ОБ УСТР.ВЫДАНА
 ЗП ШРАСП-3-А(М3)
 СЧ ВРЕМЯ
 ЗП ВРЕМЯД        ВРЕМЯД := ТЕКУЩЕЕ ВРЕМЯ
 ПВ РАС(М2)
РАНО НОП ,
*
*
*   ПОВТОРИТЬ ИЛИ ВЫБРОСИТЬ ЗАКАЗ
*
*
 УИИ М16(М6)       М16 := N НАПРАВЛЕНИЯ
 СЧ ОЧКУСД-1(М14)
 И ЕФ
 ПО НЕОС
* СБОЙНЫЙ ЗАКАЗ ОТ  О С  Д И С П А К
 СЧ Е1
 ИЛИ НАЛЕВО
 ЗП НАЛЕВО
 ПБ ПОВТОР
*
НЕОС НОП ,
 УИА -1023(М5)     М5 := -1023 ДЛЯ ЦИКЛА ’РОСЛИС’
 СЧ МДНЕИС-3(М16)
 И Е32(М4)
 ПЕ СНЯТЬ         БЫЛА ДИРЕКТИВА ’ВМД’
 СЧ ОТВАВ
 СДА 64-6          5 Р = 1  - ПОВТОРЯТЬ
 ИЛИ 3             3-ИЙ ТУМБ.РЕГ
 И Е11
 ПЕ ПОВТОР        ПОВТОРЯТЬ БЕСКОНЕЧНО
 СЧ ОЧЛИСД-1(М14)
 СЛЦ Е30
 ЗП ОЧЛИСД-1(М14)
 И Е36
 ПО ПОВТОР        < 32-УХ РАЗ
 НТЖ ОЧЛИСД-1(М14)
 ЗП ОЧЛИСД-1(М14)
*
СНЯТЬ НОП ,
 СЧ ТУСП-1(М12)
 И Е36
 ПЕ МОЖНО
 СЧ ШЗЗ
 И Е48-1(М12)
 ПО ПОВТОР
МОЖНО НОП ,
 СЧ ОЧКУСД-1(М14)
 И Е6
 ПО НЕСЕКТ
* СЕКТОРНЫЙ ОБМЕН
 СЧ Е48-1(М12)
 УИА АВАРЗП(М15)
 ПБ ВШГП
*
НЕСЕКТ НОП ,
 СЧ ОЧКУСД-1(М14)
 И Е18
 ПО АВАРЗП
 УИА 0(М2)
 СЧ ОЧЛИСД-1(М14)
 ПВ ПРУ12(М10)
 СЧ 0
 УИА 0
РОСЛИС ЗП 2047(М5)
 ЦИКЛ РОСЛИС(М5)
АВАРЗП НОП ,
 УИА 1027
 СЧ ШРАСП-3-А(М3)
 ИЛИ Е32(М4)
 НТЖ Е32(М4)
 ЗП ШРАСП-3-А(М3)
 СЧ ЕЗАР
 ИЛИ НОМАРС
 И Е48-1(М12)
 ПО НЕАРХ
 СЧИ М12
 ИЛИ Е41
 УИА КОНСНЯ(М16)
 ПБ ЗАПВЕТ        СОБЫТ. АРХИВУ Е17
НЕАРХ НОП ,
 СЧ ТУСП-1(М12)
 УИ М3            М3 := АДРЕС ИПЗ
 СЧ ОЧКУСД-1(М14)
 И Е34П25+2
 ВЧ Е27
 СДА 64+24
 ЗП ИНФАВ(М3)     ИНФАВ 14-1 Р.Р.= N ЗОНЫ
 СЧ ОЧЛИСД-1(М14)
 И Е18П13
 СДА 64-2
 ИЛИ ИНФАВ(М3)
 ЗП ИНФАВ(М3)     ИНФАВ 20-15 Р.Р.= ВН
 СЧ ОЧКУСД-1(М14)
 И Е18
 СДА 64-3
 ИЛИ ИНФАВ(М3)
 ЗП ИНФАВ(М3)     ИНФАВ 21 Р.= 1 - ЧТЕНИЕ
 СЧ ТЗНМД(М1)
 И П7777
 ПВ ПЕРЕВ(М15)
 СДА 64-21
 ИЛИ ИНФАВ(М3)
 ЗП ИНФАВ(М3)     ИНФАВ 48-22 Р.Р.= N ПАКЕТА
 УИА АНВО8А(М2)
 СЧИ М2
 ЗП 3(М3)         М33 = АНВО8А
 УИА 42(М2)
 СЧИ М2
 ЗП 8(М3)         ПРИЧИНА СНЯТИЯ = 42 (ОШМД)
 СЧ 5(М3)
 И Е48П16
 ИЛИ П2007
 ЗП 5(М3)
КОНСНЯ НОП ,
 УИИ М16(М6)       М16= N НАПРАВЛЕНИЯ
 ПВ ВЫБРЗК(М11)   ВЫБРОС ЗАКАЗА
 СЧ ШКОШ
 ИЛИ Е48-1(М12)
 ЗП ШКОШ
 СЧ Е1
*
ВЫХАВ НОП ,
 ЗП РЯД
*
 СЧ СИСТ
 ИЛИ Е48-1(М14)
 НТЖ Е48-1(М14)
 ЗП СИСТ
*
 СЧ ЗАНМД-3(М16)
 ИЛИ Е32(М4)
 НТЖ Е32(М4)
 ЗП ЗАНМД-3(М16)
 СЧ ОТВАВ
 И Е6
 ЗП ОТВАВ         ОТВАВ = 0 -ЗАКАЗ НЕ ОТКЛАДЫВАТЬ
 СЧ РЯД           СМ = 1 - ЗАКАЗ ВЫБРОСИЛИ
 ПБ КОНАВ         ОБРАБ.АВАР.ОБМЕНА ЗАКОНЧИЛИ
*
ПОВТОР НОП ,
 СЧ 0
 ПБ ВЫХАВ         ПОВТОРИМ ОБМЕН
*
*
**********************************************
*
*
* ПОДПР-МА ПЕРИПИСИ СООБЩЕНИЯ ОС О МД
*
РАС НОП ,
 СЧИ М2
 ЗП БУР+1
 УИА 1027
 УИА -7(М2)
ПУП МОДА (М16)
 СЧ 7(М2)
 МОД БУР
 МОДА БУР+9
 ЗП (М2)
 МОДА ПУП
 ЦИКЛ (М2)
 СЧ БУР
 СЛЦ Е4
 И П30
 ЗП БУР
 УИА 3
 МОД БУР+1                                                     АВ
 ПБ 0
*
**********************************************
*
*
*ТАБЛИЦЫ ДИАГ.СООБЩЕНИЙ
ОШБНС ЭКВ 0
*
*        8-1 Р.Р. ВТОРОГО СЛОВА ПРИЧИНЫ - ПРИЗНАКИ ПРИЧИНЫ
* 4-1 Р.Р. - N КВАНТА ПРИЧИНЫ В ЯЧЕЕКЕ СБОРА СТАТИСТИКИ
*          = 0 - НЕ ПИСАТЬ В СТАТИСТИКУ
* 5     Р. - ОБМЕН ПОВТОРЯТЬ БЕСКОНЕЧНО
* 6     Р. - ОБМЕН ОТЛОЖИТЬ (ЕД В ’ВЫК’)
*
ОШКСЗП НОП ,
 КОНД П’ ОШ.КС’
 КОНД П’.ЗАП.0’М5В’0’М4В’0’Ф’1’
ОШКССЧ НОП ,
 КОНД П’ ОШ.КС’
 КОНД П’.ЧТЕН0’М5В’0’М4В’0’Ф’1’
ОШNПАК НОП ,
 КОНД П’ ДРУГ.’
 КОНД П’ПАКЕТ0’М5В’1’М4В’1’Ф’2’
ОШЗОНЫ НОП ,
 КОНД П’ ДРУГ.’
 КОНД П’ЗОНА 0’М5В’0’М4В’0’Ф’3’
ОШКЛЮЧ НОП ,
 КОНД П’ НЕТ  ’
 КОНД П’КЛЮЧА0’М5В’0’М4В’0’Ф’4’
ОШДР  НОП ,
 КОНД П’  Д Р=’
 КОНД П’000000’М5В’0’М4В’0’Ф’5’
ОШДРУ НОП ,
 КОНД П’  ДРУ=’
 КОНД П’000000’М5В’0’М4В’0’Ф’6’
НЕТГОТ НОП ,
 КОНД П’ НЕТ Г’
 КОНД П’ОТОВ.0’М5В’1’М4В’1’Ф’7’
НЕТЗАП НОП ,
 КОНД П’ НЕТ З’
 КОНД П’АПИСИ0’М5В’1’М4В’1’Ф’0’
********************************************
*
КЦИЛ НОП ,
 КОНД В’4001463146314632’
 КОНД В’4000631463146315’
 КОНД В’4000657120657121’
КГОЛ НОП ,
  КОНД В’4000000000000012’
  КОНД В’4000000000000024’
  КОНД В’4000000000000023’
НГОТ НОП ,
 КОНД М21В’3’         38,37 ДРУ
 КОНД М21В’3’
 КОНД М35В’1’М17В’1’  41,33 ДРУ
ЕТЧ ЭКВ Е20           ТОЛЬКО ЧТЕНИЕ(35 Р. ДРУ)
СЕК15 КОНД В’2734’ 30 СЕК
ШРАСП КОНД (2)В’0’
ВРЕМЯД КОНД В’0’
РАБЯЧ ПАМ 2
БР ПАМ 8
БУР ПАМ 34
КАТДЗ КОНД М24В’1750’М17В’3’М4В’1’
 КОНД М24В’7640’М17В’3’М4В’1’     С КАТДЗ
КФКУ КОНД М24В’1’М20В’1’М17В’1’М4В’1’
ИНФСБ КОНД М37В’7’
АВУСТ КОНД М18В’1’М33В’17’ АПСБ,34Р. ДРУ
Е37П35 КОНД М34В’7’
К6 КОНД М47В’1’В’6’
Н19П24 КОНД В’-1’М18П’77’
Е18П13 КОНД М12В’77’
ЦИФРЫ КОНД П’FЕDСВА’
*
 ВХОД ЭТОТ,Р,Р22,Р33,Р44,Р55,АВДИС,РИ,ПОС,НОВД
 ВХОД НВ2,НОВ1,НМД,ПЕРЕСТ,ДР,ВКЛ,ОП,У,НОВ
 ВХОД БУР,ИНФСБ,АВУСТ,Е37П35,К6
 ЭКВИВ (Л,9),(А,АВМД),(У,31)
СОШМД ЭКВ ’66’          СЛОВО ИПЗ, ГДЕ ХРАНИТСЯ ЕОШМД
ЕОШМД ЭКВ Е24  ПРИЗНАК СБОЯ МД
 ВХОД СОШМД,ЕОШМД
ДИСП70 ВНЕШ ЕЗАР,НОМАРС
ДИСП70 ВНЕШ Е41П1,КЛЮЧРЗ
 ВХОД ОПРДИС,АВАРМД,Е18П13
 ВХОД ОШБНС,ОШКСЗП,ОШКССЧ,ОШNПАК,ОШЗОНЫ,ОШКЛЮЧ
ДИСКИ ВНЕШ НОМРМД,КТОМЛ,ОСВМД,ДАЙГОТ,ДКО,Д7В,НГД
ДИСКИ ВНЕШ ЗАНМД,СОД,МДНЕИС,ВЫБРЗК,МДНАП,Е34П25
ДИСКИ ВНЕШ Е16П1,П3700,БУДАВ,РС,РЯД,РЯД1,РЯД2
ДИСКИ ВНЕШ Е11Е9,МД100М,КОНАВ,ОТВАВ
ДИСКИ ВНЕШ ДКК,Д3,ЗАНКМД,О40,NКОБ,ДК
ДИСКИ ВНЕШ Д3А1,ВЫК,ЗАН
ДИСКИ ВНЕШ КТОМ
ДИСКИ ВНЕШ ЗАВ,Е36П1,ДВ,ЗУС,НЗ
ДИСП70 ВНЕШ СТМГ,РМР,Ш1,ЗАПВЕТ,ШКСВН,УСТПРП,П77777,П37
ДИСП70 ВНЕШ П3,П7,И16,И17,ОЧЛИСД,ЛИСТА,ШАРСИС
ДИСП70 ВНЕШ КРОМЕ,ЯКУС,(Е6П1,П77),ТУС,ВСЕЕД,КУС2
ДИСП70 ВНЕШ ЗАНПР,РМД,ШЗПОМД,П7777,ГОД
ДИСП70 ВНЕШ ОБЩТОМ,ШГ,ШКИВ,НАПРМГ,АЗУ
ДИСП70 ВНЕШ Е24П13,ТЗНМД,П2007,К2013,НОММБ1
ДИСП70 ВНЕШ NБЛ,ТАБЛО,Е48П16,Д,П177
ДИСП70 ВНЕШ ИНФСЛП,ПРИП,П30,Е36П25,МД29М
ДИСП70 ВНЕШ Е48П43,Е26Е25,(Е4П1,П17),П777,ГАШПРП
ДИСП70 ВНЕШ ГУС,ШЗНМБ,Е24П1,П17,ПЕРЕВ,РМД1,РАЗМГ
ДИСП70 ВНЕШ ШЗРАЗМ,П5,П60,КНАПР,ОЧКУСД
ДИСП70 ВНЕШ МШГ,Е47П43,РМЛ,П377,ВРЕМЯ,ТБУФ,ШКОШ
ДИСП70 ВНЕШ Е10П3,Е12Е11,ЕNR,ЕН,ТУСП,НЗАРХ,СОБА40
ДИСП70 ВНЕШ КСД1,КСД3,СМ,ИВ25,П1777
ДИСП70 ВНЕШ П77,ЛСЕК
ДИСП70 ВНЕШ ДОБЗ,ПРСВЛ,ШЗЗ,ОБМОТВ
ВИСП ВНЕШ ВШГП
ОСА ВНЕШ НБОБ,ОСА
КИТ ВНЕШ ЛС641,Е48П42,Е32П25,ШОЧМД,ШОЧНМД,ШОЧЛ
КИТ ВНЕШ СИСТ
СМЕ ВНЕШ МДЕСТ,(С1,СМЕ),ВСМЕ,ЕСТ
КАЧКА ВНЕШ Е48П17
СОСТАВ ВНЕШ ИНФАВ
ХЛАМ ВНЕШ Д103,ПРУ12
ХЛАМ ВНЕШБ ОСС
ДМЛМБ ВНЕШ ПРЛ2А,ПРП,ПРП2,ЗКМЛ,ПРЛСЧА
ВЗУ ВНЕШ ФИЗОБМ,ДСЛ,АНВО8А,ЕФ,АНВО9Б
ДИСКИ ВНЕШС ДРД,ДРУД,ИПЗМД,ПОДОПР
ДИСКИ ВНЕШ БУФКДЗ
ДИСП70 ВНЕШ ДБУФ,ЛИС2,НАЛЕВО,П12
СОСТАВ ВНЕШ ЕДЕФ
КИТ ВНЕШ СТРЕЛА
СОСТАВ ВНЕШ ЕСБОЙВ
 ЭКВИВ (ОШМЕХП,Е28),(ОТКЛК,Е48)
 ФИНИШ

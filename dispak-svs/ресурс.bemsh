РЕСУРС СТАРТ ’10000’
 УПОТР РЕСУРС(М7)
 Б
 Е
 М
 КОНД В’0’
 КОНД М41В’3’ РЖА 3
 КОНД (2)В’0’
 КОНК А(ПРЛСЧБ)
 КОНК В’0’
 ПАМ 7
 КОНД В’1’
 ПАМ 7
 КОНД В’77771’
 ПАМ 2
 КОНД В’0’    НЕ ЗАНИМАТЬ
РЯ КОНД В’0’
РЯ1 КОНД В’0’
РЯ2 КОНД В’0’
РЯ3 КОНД В’0’
РЯ4 КОНД В’0’
РЯ5 КОНД В’0’ РЯ3,РЯ4,РЯ5 НЕ РАЗДЕЛ.
 КОНД В’0’
РП КОНД В’0’
 КОНД М15В’1’
 КОНД М47В’1’
КР КОНД В’71717’
РЯ7 КОНД В’0’
РЯ10 КОНД В’0’
ЛИСТ1 КОНД В’0’
КОНСТ КОНД М37В’3777’В’7777777’
R КОНД В’0’
 И П77
 ПБ К1
К300 УИА ’2003’
 СЧ РС
 ПО НЗАХМД
 СДА 64+20
 И П37
 УИ М17
 СЧ РС
 И Е26
 СДА 64+25
 УИ М16
 СЧ Е32(М17)
 НТЖ ЕNQМДС(М16)
 ЗП ЕNQМДС(М16)
 СЧ ШЗМД(М16)
 И Е32(М17)
 ПЕ НЗАХМД        ДИСК ЗАХВАЧЕН  О С
 СЧ РС
 СДА 64-22
 РЗБ НАПРМГ
 ПВ ОСВМД(М15)
 СЧ ШКЗАКЗ
 ПВ ВШГП(М15)
 СЧ 0
 ЗП ШКЗАКЗ
НЗАХМД НОП ,
 СЧ 0
 ЗП РС
 МОД НЗАД
 СЧ Е48-1
 ПВ ВШГП(М15)
ВЫХОД СЧ 33(М7)
 НТЖ Е48 ДОБАВИТЬ ВРЕМЯ РАБ.Э72
 МОД НЗАД
 МОД ТУСП-1
 УИА (М10)
 СЛЦ 33(М10)
 ЗП 33(М10)
 СЧ Е48
 ЗП 33(М7)
 СЧ 0
 ЗП РЯ10
 ЗП РЯ
 ЗП РЯ6
 СЧ ЗАНЯТА
 И Е6
 ПО НЕБРА
 ИЛИ Е5
 ПВ ГАШЗАН(М15)
НЕБРА СЧ Е31
 ПБ ОТКАЗН
РЯ6 КОНД В’0’
ФФ КОНД В’0’ РАБ
ПЧ ТЕКСТ П’Э72 ИН’
 КОНД В’1500706112617572’ Ф.СЛ:(КОНЕЦ ПЕЧАТИ)
ПЧ1 КОНД В’0’
* БЛОК ВЫБОРКИ ИНФ-ИИ
ПК6В МОДА КП25
 УИА (М15)
 СЧИ М15
 ПВ ЗАПВЫХ(М15)
 УИА -7(М2)
 УИА ’2000’
 СЧ (М11)
 УИА 3
 ЗП R ИНФ.СЛОВО
 ЗП ПЧ+2
 ПБ К7
К6Б СЧ РЯ1
 СДА 64-6
К7 ЗП РЯ1
 УИА 3
 И Е77С43
 ПБ (М14)
О48П34 КОНД М33В’77777’В’-1’
К6А МОДА К6Б
 ЦИКЛ (М2)
КП23 УИА 46(М3) ОШ.РАБОТ
 ПБ ПА
КП25 УИА 57(М3)
 ПБ ПА
К121 УИА 58(М3)
 ПБ ПА
К120 УИА 44(М3)
 ПБ ПА
Э62 СЧИ М6
 ЗП 25(М7)        25 ЯЧ.ИПЗ(РЯ1)≠0 - Э62 ДЛЯ ЭКГДР,КАНАЛ
 СЧ 36(М10)
 И П77777
 ПЕ СДВ
 УИА (М17) КОНЕЦ ЗАДАЧИ
 УИА ’2003’
 СЧ О48П34
 И СУММАВ(М10)
 ЗП СУММАВ(М10)
 УИИ М15(М10)
 МОДА К300
 УИА (М16)
 ПБ АВОСТА
К1 УИ М6
 ЗП НЗАД
 СЧ
 ЗП РЯ6
 УИА -7(М2)
 УИА 1(М12)
 СЧ ТУСП-1(М6)
 УИ М10
 СЧ 1(М10)
 НТЖ П23
 И П37
 ПО ЭК
 НТЖ Е1
 ПО Э62
 И Е5
 ПЕ Э52
 МОД 36(М10)
 УИА (М11)
 СЧ (М10) СМ
 НТЖ КОНС
 ПО ЕМ
 СЧ ЯЧЭК(М10)
 И ЕВАМОS
 ПО К1Б
 СЧ 36(М10)
 И П77777
 ЗП 34(М10)
 ПБ К300
ЕМ МОДА ПК6В
 ПВ (М14)
 СДА 64+42
 НТЖ П7
 ПЕ АДРУХ
 СЧ О21П11
 И 33(М10)
 ЗП 33(М10)
 СЧ РЯ1
 И П3777
 СДА 64-10 РЕАКЦИЯ НА ЭКСТРАКОДЫ
 ИЛИ 33(М10)
 ЗП 33(М10)
 СЧ Е48П16
 И АВОС(М10)
 ЗП АВОС(М10)
 СЧ РЯ1
 СДА 64+24
 И П77777
 ИЛИ АВОС(М10)
 ЗП АВОС(М10)
 ПБ К300
ЭК ЗП РЯ1
 ПБ ЭКГД
АДРУХ СЧ О48П34
 И СУММАВ(М10)
 ЗП СУММАВ(М10)
 СЧИ М11
 СДА 64-33
 ИЛИ СУММАВ(М10)
 ЗП СУММАВ(М10)
 ПБ К300
К1Б МОДА ПК6В
 ПВ (М14)
 СДА 64+46
 УИ М3
 МОДА (М3)
 ПБ ПЕРЕКЛ
ПЕРЕКЛ СЧ РЯ1 ПЕРЕК.РАБОТ
 ПБ СОЗУ
 УИА (М4)
 ПБ МЛ С 20,30,37
 УИА 1(М3)
 ПБ М   40,47,50
 СЧ РЯ1
 И Е46
 ПЕ С7Х
 СЧ РЯ1
 СДА 64+42
 И П3
 НТЖ Е2
 ПЕ ЗАПР
 СЧ РЯ1
 ПБ С62 С 62 ЧТ(ЗП) НА МЛ
С7Х СЧ РЯ1
 СДА 64+42
 И Е1
 УИ М3
 ПО ПОДВОД  70-ПОДВОД
 СЧ 19(М10)
 НТЖ КР
 ПЕ ЗАПР
 ЗП КОБМ
 СЧ РЯ1
 И Е12
 ЗП НЕЧСЛ
 ПБ С63
Е17С45 КОНД М43В’37’
М СЧ П60
 ПВ ЗАХЗАН(М15)
 СЧ РЯ1
 СДА 64+45
 НТЖ Е3
 ПЕ ПМБ
 СЧ РЯ1
 СДА 64-3
 И Е46П48
 НТЖ Е46П48
 ПЕ К6 ОТКАЗ НЕ ОТ ВСЕХ ТРАКТОВ
 УИА (М12)
 СЧ РЯ1
 И П777
 ЗП РЯ2 СКОЛЬКО ТРАКТОВ ОСТАВИТЬ (N)
 ПБ К122
* ОТКАЗ ОТ ОЗУ ( ИЛИ ПЕРЕН.)
СОЗУ СДА 64+45
 ЗП РЯ 0-ОТКАЗ,Е1-ПЕРЕИМ.
К70А МОДА К6А
 ПВ (М14) НА ВЫДЕЛ-Е ИНФ-ИИ
К71 НТЖ Е77С43
 ПО КНЦ
 СЧ РЯ1
 СДА 64+42
 И П37
 ЗП РЯ3 N ЛИСТА (МАТЕМ)
 УИ М3
 ЗП ЛИСТ1 N ЛИСТА (1-ГО МАТЕМ.)
 СЧ РЯ
 И Е1
 ПЕ К103 ПЕРЕН. ЛИСТА, ИНАЧЕ ОТКАЗ
 СЧ РЯ1
 И Е48
 ПО К73 ОТКАЗ ОТ ОТД. ЛИСТОВ
 МОДА К6А
 ПВ (М14)
 СДА 64+42
 И П37
 УИА ’2003’
 УИ М3 ПОСЛЕД.ЛИСТ,ОТ КОТ.ОТКАЗ
 ВЧ РЯ3
 ПЕ КП23 ПОСЛ.ЛИСТ<НАЧАЛЬНОГО
 СЧ РЯ1
 И Е77С43
 НТЖ Е77С43
 ПО КП23 ДАЙ РАБОТУ
 УИА 3
 ПБ К73
К103 МОДА К6А НА ВЫБОРКУ СЛЕД. ИНФ.
 ПВ (М14)
 СДА 64+42
 И П37
 ЗП РЯ7 N ЛИСТА (2 МАТ.)
 НТЖ РЯ3
 ПО К70А
 СЧ РЯ1
 И Е77С43
 НТЖ Е77С43
 ПО КП23 ДАЙ РАБОТУ
 СЧ РЯ
 ИЛИ Е3
 ЗП РЯ
К73 СЧ РЯ3
 И П3
 УИ М4 N СТОЛБЦА В ТАБЛ. ИПЗ
 СЧ ’44’(М10)
 И Е37
 ПО К73А НЕТ СПО
 СЧ ’44’(М10)
 СДА 64+21
 УИ М16 АДРЕС ВХОДА В СПО
 СЛИА -’42’(М16)
 СЧИ М16 НАЧАЛО СПО
 СДА 64+10
 НТЖ РЯ3
 ПО ГСП СПО НАХ-СЯ В ЭТОМ ЛИСТЕ
 СЛИА ’777’(М16)
 СЧИ М16 КОНЕЦ СПО
 СДА 64+10
 НТЖ РЯ3
ГСП ПЕ К73А
 СЧ Е48П16
 УИА ’2003’
 И ’26’(М10) ГАШЕНИЕ М34
 ЗП ’26’(М10)
 СЧ Е48П16
 И ’27’(М10) ГАШЕНИЕ М35
 ЗП ’27’(М10)
 СЧ КОНСТ
 И ’44’(М10) ГАШ. АДРЕСА ВХОДА В СПО И ПРИЗН. СПО
 ЗП ’44’(М10)
К73А СЧ РЯ3
 НТЖ П37
 УИ М12
 СЧ РЯ3
 СДА 64+2
 НТЖ П7
 УИ М5
 СЛИ М5(М10) А СТРОКИ ИПЗ
 МОДА НЕТЛИС
*РЕСУРС КОНЕЦ*
 УИА (М15)
 СЧИ М15
 ПВ ЗАПВЫХ(М15)
 УИИ М17(М6)
 ПВ ЗАНПРП(М15)     84.05.03
 СЧ РЯ3
 СДА 64-10
 УИ М14
 УИА ’2000’
 СЧ 50(М14)
 УИА ’2003’
 СЧ 32(М10)
 ИЛИ Е48(М12)
 ЗП 32(М10) ПР:ЛИСТ ЗАНЯТ
 СЧ РЯ
 И Е1
 ПЕ ПЛИ ПЕРЕИМ.ЛИСТОВ
 СЧ Е48(М12)
 ИЛИ ЛИСТМ(М10)
 ЗП ЛИСТМ(М10)
ПЛИ СЧ 24(М5)
 СБР ПРИП(М4)
 ЗП РЯ4 ФИЗ.ЛИСТ В СТ.РАЗР.
 СЧ ВСЕЕД
 НТЖ ПРИП(М4)
 И 24(М5)
 ЗП 24(М5) ПОЧИСТ.МЕСТО В ИПЗ.
 СЧ РЯ
 И Е1
 ПЕ К105 ПЕРЕН. ЛИСТА
*
*  ОТКАЗ ОТ ЛИСТА
*
 СЧ РЯ4
 ПО К101
 СДА 64+38
 УИ М14
 И П37
 УИ М13
 СЧИ М14
 СДА 64+5
 УИ М12
 СЧ Е5
 ПВ ЗАХЗАН(М15)
 СЧ ТЗФСТР(М12)
 НТЖ Е48(М13)
 ЗП ТЗФСТР(М12)
 СЧ ОЗУ
 СЛЦ Е1
 ЗП ОЗУ
 СЧ ТОБ
 СЛЦ Е1
 ЗП ТОБ
 УИИ М13(М10)
 ПВ УПРИ(М10)
 УИИ М10(М13)
 УИА 2
 СЧ ТОП(М14)
 ПВ ИСКЛИС(М17)   ИСКЛЮЧЕНИЕ ЛИСТА ИЗ ЦЕП.ЛИСТ.ЗАДАЧИ
 СЧ Е5
 ПВ ГАШЗАН(М15)
 МОДА К101
 УИА (М14)
 СЧ ЯЧЛИС(М10)
 И П7777
 ПО РЗ1А
 НТЖ Е1
 ПЕ Л2
 СЧ ЯЧЛИС(М10)
 НТЖ Е1
 ЗП ЯЧЛИС(М10)
РЗ1А СЧ РЯ3
 НТЖ П37
РЗ1 УИ М12
 УИА ’2003’
 СЧ ’26’(М10)
 НТЖ Е48(М12)
 ЗП ’26’(М10)
 ПБ (М14)
Л2 СЧ ЯЧЛИС(М10)
 СЛЦ Е48П2
 ЗП ЯЧЛИС(М10)
 ПБ РЗ1А
К101 СЧИ М3
 УИА 3
 НТЖ РЯ3
 ПО К70А ВСЕ ЛИСТЫ НАЙДЕНЫ
 СЧ РЯ3
 СЛЦ Е1
 И П37
 ЗП РЯ3 N ЛИСТА + 1
 ПБ К73
К105 СЧ РЯ4
 СДА 64+38
 УИ М15
 СЧ РЯ
 И Е3
 ПО ЕСТЬ2Л
 СЧ РЯ7
 ЗП РЯ3 ЛИСТ МАТ.2
 УИИ М3(М15)
 СЧ РЯ4
 ЗП РЯ6 N ФИЗ. ЛИСТА 1 (В СТ. РАЗР)
 СЧ ПРИП(М4)
 ЗП R
 УИИ М1(М5)
 СЧ РЯ
 НТЖ Е3
 ЗП РЯ ПРИЗН. 2-ГО ОБРАЩ.-Я
 ПБ К73
ЕСТЬ2Л НОП ,             У ЗАДАЧИ ЕСТЬ ОБА ЛИСТА
 МОДА ПРИТОП
 ПВ (М13)
 СЧ РЯ4           ФИЗ.ЛИСТ 2-ОГ0 ЛИСТА
 РЗБ R
 ИЛИ 24(М1)        СТАВИМ НА ПРИПИСКУ 1-ОГО ЛИСТА
 ЗП 24(М1)
*
 УИА 1026
 СЧ ТОП(М3)
 И ВИОЛПМ
 ЗП РАБ1РМ
  НТЖ ТОП(М3)
 ЗП ТОП(М3)
*
 СЧ ТОП(М15)
 И ВИОЛПМ
 ЗП РАБ2РМ
 НТЖ ТОП(М15)
 ИЛИ РАБ1РМ
 ЗП ТОП(М15)
*
 СЧ ТОП(М3)
 ИЛИ РАБ2РМ
 ЗП ТОП(М3)
 УИА ’2003’
 СЧ ЛИСТ1
 НТЖ П37
 УИ М12
 СЧ 32(М10)
 НТЖ Е48(М12)
 ЗП 32(М10)       ОТКРЫЛИ 1-Й ЛИСТ
 СЧ РЯ3
 НТЖ П37
 УИ М12
 ПБ КОНПЛ
НЕТЛИС НОП ,             У ЗАДАЧИ НЕТ ЛИСТА
 СЧ РЯ
 И Е1
 ПО К101 ОТКАЗ ОТ ЛИСТОВ
 СЧ РЯ
 И Е3
 ПЕ К121 ЛИСТА НЕТ,ОШИБКА
* ПЕРЕИМЕНОВАНИЕ В ОДНУ СТРОНУ
*   ФИЗ.ЛИСТ 1-ОГО МАТ.ЛИСТА ДЕЛАЕМ ФИЗ.ЛИСТОМ 2-ОГО МАТ.ЛИСТА
 СЧ ЛИСТ1
 НТЖ П37
 МОДА РЗ1
 ПВ (М14)         ЗАКРЫВАЕМ 1-ЫЙ ЛИСТ В 26 ЯЧ.ИПЗ
 СЧ ЛИСТМ(М10)
 ИЛИ Е48(М12)
 ЗП ЛИСТМ(М10)    ЗАКРАЛИ 1-ЫЙ ЛИСТ В ЛИСТМБ
 МОДА РЗ1А
 ПВ (М14)         ОТКРЫВЫЕМ 2-ОЙ ЛИСТ В 26 ЯЧ.ИПЗ
 СЧ ЛИСТМ(М10)
 НТЖ Е48(М12)
 ЗП ЛИСТМ(М10)    ОТКРЫЛИ 2-ОЙ ЛИСТ В ЛИСТМБ
 МОДА ПРИТОП
 ПВ (М13)
 СЧ РЯ3           N 2-ОГО МАТ.ЛИСТА
 СДА 64-СДВМЛ+ТБДТР
 ИЛИ РЯ3
 СДА 64-ТБДТР
 УИА 1026
 НТЖ ТОП(М3)
 И ВИОЛПМ
 НТЖ ТОП(М3)
 ЗП ТОП(М3)       ЗАМЕНИЛ НОМЕР ЛИСТА
КОНПЛ НОП .             КОНЕЦ ПЕРЕИМ.ЛИСТОВ
 УИА ’2003’
 СЧ 32(М10)
 НТЖ Е48(М12)
 ЗП 32(М10)       ОТКРЫЛИ ВТОРОЙ ЛИСТ
 УИИ М17(М6)
 ПВ ЗАНПРП(М15)
 ПБ К70А
*
ПРИТОП НОП ,             УСТАНОВКА ПРИПИСКИ ТОП
 УИИ М14(М10)
 ПВ УПРИ(М10)
 УИИ М10(М14)
 СЧ РЯ6           ФИЗ.ЛИСТ 1-ОГО ЛИСТА
 РЗБ ПРИП(М4)
 ИЛИ 24(М5)        СТАВИМ НА ПРИПИСКУ 2-ОГО ЛИСТА
 ЗП 24(М5)
 ПБ (М13)
*
О21П11 КОНД М10В’3777’В’-1’
ЭКГ КОНД М17В’3’В’100’
КОНС КОНД В’2264253121233462’
И11 КОНД В’0’
ПМБ СЧ Е1
 ЗП РЯ
К6 МОДА К6А
 ПВ (М14) НА БЛОК ВЫБОРКИ ИНФ-ИИ
 УИА 3
 УИА (М17)
 УИА 1(М16)
 ПО К6
 ЗП НЕЧСЛ
 НТЖ Е77С43
 ПО КНЦ
 УИИ М1(М2)
 УИИ М14(М10)
 ПВ УПРИ(М10)
 УИА 3
 УИИ М10(М14)
 УИИ М2(М1)
 МОДА ПМБ1
 УИА (М1)
 УИА -1(М4)
МБ2 СЧ РЯ1
 СДА 64+42
 НТЖ П77
 ПО ЗН
 СЧ РЯ
 И Е1
 ПО ПМБ0
 СЧ РЯ1
 СДА 64+45
 ВЧ П3
 ПЕ ПМБ0
 ВЧ Е3
 ПЕ ЗН
ПМБ0 МОДА (М4)
 СЧ НЕЧСЛ+1
 СДА 64-1
 МОДА 1(М4)
 ЗП РЯ2
 УИА (М4)
 ПБ (М1)
ПМБ1 СЧ РЯ
 УИА (М5)
 И Е1 ОТКАЗ ИЛИ ПЕРЕН.
 ПЕ К35 ПЕРЕИМ.МБ
 УИА (М16)   МД
 УИА (М12)   МБ
 СЧ РЯ1
 И Е48
 ПО К31 ОТКАЗ ОТ ОТД. ТРАКТОВ, ИНАЧЕ
 СЧ ВСЕЕД ГАШ. ПРИЗН. ОТКАЗА ОТ ОТД. ТРАКТОВ
 НТЖ Е2
 И РЯ
 ЗП РЯ
 УИА ’40’(М3)
 МОДА К6
 УИА (М14)
* БЛОК ПРОСМОТРА ШКАЛ ЗАНЯТОСТИ ТРАКТОВ МБ ЗАДАЧЕЙ
Т1 СЧ ШЗНМБ(М10)
 СДА 64+ССТБД
 УИА (М5)
Т1А И П77777
 ПЕ Т20
 СЧ РЯ
 И Е1
 ПЕ К40 ЕСЛИ ПЕРЕН.
 СЧ РЯ
 И Е2
 ПО ВСЕПР ОТКАЗ ОТ ВСЕГО МБ
К13А СЧ РЯ10
 НТЖ РЯ6
 ПО ВСЕПР
 СЧ РЯ10
 СЛЦ Е39 N ТР+1
 ЗП РЯ10
 УИА (М5)
 ПБ Т1
МБ5 МОДА К37
 ПВ (М12)
Т13 ПАМ 0
 ПРЕ 1026
 СЧ ТБД-1(М1)
 ПРЕ 3
 УИИ М5(М1)
 ПБ Т1А
ВСЕПР УИА ’2003’
 УИИ М15(М14)
 СЧИ М12
 СЛЦ ТМБ
 ЗП ТМБ
 СЧИ М16
 СЛЦ ТМД
 ЗП ТМД
 СЛИ М12(М16)
 СЧИ М12
 ЗП РЯ10
ВЫЧТРА СЧ РЯ10
 СЛЦ ТОБ
 ЗП ТОБ
 СЧ ЯЧТР(М10)
 СДА 64+СДТ
 И П777
 ВЧ РЯ10
 СДА 64-СДТ
 ЗП РЯ10
 СЧ ОТР
 И ЯЧТР(М10)
 ИЛИ РЯ10
 ЗП ЯЧТР(М10)
 ПБ (М14)
НЕИС СЧИ М3 ОТКАЗ ОТ НЕИСПОЛЬЗ.ТРАКТОВ
 УИИ М14(М15)
 СДА 64-СДТ
 УИА 1027
 СЛЦ ЯЧЧТР(М10)
 И ЕТР
 ПЕ НЕИСТР ЧИСЛО ОТКАЗ.ТР>ЧИСЛА ИМЕЮЩИХСЯ
 СЧИ М3
 СДА 64-СДТ
 СЛЦ ЯЧЧТР(М10)
НЕИСТ ЗП ЯЧЧТР(М10)
 СЧИ М3
 ЗП РЯ10
 ПБ ВЫЧТРА
НЕИСТР СЧ ЯЧЧТР(М10)
 СДА 64+СДТ
 НТЖ П777
 СЛЦ Е1
 И П777
 УИ М3 ЧИСЛО ТРАКТОВ,РАЗРЕШ.МАТЕМ
 СЧ ЯЧЧТР(М10)
 И ОТР
 ИЛИ ЕТР  ТР БОЛЬШЕ НЕТ
 ПБ НЕИСТ
Т20 УИ М1
 ПРЕ 2
 СЧ ТБД-1(М1)
 ПРЕ 3
 И ТОПЛИС
 СДА 64+ТБДТР-38
 ЗП РЯ4
 И Е17С45
 НТЖ РЯ2
 ПЕ Т13
 СЧ РЯ
 И Е1
 ПЕ К41 ПЕРЕН. МБ
 СЧ РЯ
 И Е2
 ПЕ К25 ОТКАЗ ОТ ОТД. ТРАКТОВ,
 МОДА Т1
 УИА (М4) В ТАБЛИЦАХ
К24 УИИ М13(М10)
 СЧИ М3
 ЗП РЯ5
 СЧИ М17
 ЗП С17
 ПВ СИ2(М17)
 СЧИ М1
 ВЧОБ АКТБД
 УИА (М5)
 СЛИА ТБД-ТОП-1(М1)
 ПЕ ВОП1
 СЛИА ТОП-ТБД(М1)
 УИА ШЗНМБО-ТЗФСТР(М5)
ВОП1 СЧИ М1
 СДА 64+5
 УИ М17
 СЧИ М1
 И П37
 УИ М3
 СЛИ М17(М5)
 ПРЕ 1027
 СЧ ТЗФСТР(М17)
 НТЖ Е48(М3)
 ЗП ТЗФСТР(М17)
 МОД РЯ5
 УИА (М3)
 СЛИА -1(М3) -1 ОТ ЧИСЛА ОТКАЗЫВ.ТРАКТОВ
 СЧИ М1
 СДА 64+5
 МОД С17
 УИА (М17)
 МОДА ВОП2   ТР В ОП
 ПИО (М5)
 АIF (∧ВS ЕQ 1).ТРНАШ
 СДА 64+4
 ПО НАМБ
 АGО .ТРНА
.ТРНАШ АNОР
 ВЧ ГРАНЬБ
 ПЕ НАМБ
.ТРНА АNОР
 СЛИА 1(М16) +1 К ТРАКТАМ,НАЙДЕН.НА МД
ТРНАШЛ УИА 3
 МОДА ВСЕПР
 ПИО (М3)
 ПБ (М4)
ВОП2 СЧ ОЗУ
 СЛЦ Е1
 ЗП ОЗУ
 СЧ ТОБ
 СЛЦ Е1
 ЗП ТОБ
 ПБ ТРНАШЛ
НАМБ СЛИА 1(М12) +1 ТР.НА МБ НАЙДЕН
 ПБ ТРНАШЛ
К25 СЧ РЯ4
 НТЖ РЯ10
 СДА 64+38
 ПЕ Т13
 МОДА К13А
 УИА (М4)
 ПБ К24 ГАШЕН.ЗАН.ТРАКТА
К32А СЛИА 1(М17)
 ЗП РЯ7
 НТЖ Е77С43
 ПО КНЦ
 УИА 1(М3)
 УИА (М16)
 СЧ РЯ7
 СДА 64-1 N ТРАКТА
 СДА 64+5
 ИЛИ РЯ2
 ЗП РЯ10
 СЧ РЯ7
 И Е48
 ПО К34
 МОДА К6А
 ПВ (М14)
 ПО К6
 СДА 64+42
 УИ М3 ЧИСЛО ТР.,ОТ КОТ. ОТКАЗ
К34 ВЧОБ Е6
 ПЕ К120 ЧИСЛО ТР, ОТ КОТ. ОТКАЗ, > 37
 СЧ РЯ7
 СДА 64+42
 И П37 НАЧ.ТРАКТ
 УИ М14
 СЛИ М14(М3)
 СЧИ М14
 ВЧОБ Е6
 ПЕ К120 ПОСЛЕДН.ТР.≥40
 УИА (М12)
 СЛИА -1(М14)
 СЧИ М14
 СДА 64-38
 ИЛИ РЯ2
 ЗП РЯ6
 СЧ РЯ
 ИЛИ Е2
 ЗП РЯ   ОТК.ОТ ОСТ.ТР
 МОДА К31
 УИА (М14)
 ПБ Т1
К35 МОДА К6А
 ПВ (М14)
 ЗП НЕЧСЛ+1 NН,МБ 2-ГО МАТЕМ(СТ.РАЗР)
 МОДА МБ2
 ПВ (М1)
 СЧ РЯ
 ИЛИ Е3
 ЗП РЯ
 ПБ Т1
К42А МОДА -1(М17)
 СЧ ВХ+2
 УИ М1
 МОДА К42
 УИА (М12)
 СЛИА -1(М17)
К37 СЧ РЯ3
 СДА 64+18
 УИА 1026
 НТЖ ТБД-1(М1)
 УИА 1027
 И НМБ
 УИА 1026
 НТЖ ТБД-1(М1)
 ЗП ТБД-1(М1)
 УИА 3
 ПБ (М12)
К40 СЧ РЯ
 И Е3
 ПО К42
 НТЖ РЯ
 ЗП РЯ
 УИИ М3(М17)
 СЧ РЯ2
 ЗП РЯ10
 СЧ РЯ3
 ЗП РЯ2
 СЧ РЯ10
 ЗП РЯ3
 ПБ Т1
К41 СЧ РЯ
 УИА 3
 И Е3
 ПО МБ5
 СЧИ М1
 МОДА (М17)
 ЗП ВХ+2
 СЛИА 1(М17)
 ПБ Т13
К42 МОДА К6
 ПИО (М17)
 СЧИ М17
 ЗП РЯ5
 СЧИ М3
 НТЖ РЯ5
 ПЕ К42А
 СЧ РЯ2
 ЗП РЯ3
 ПБ К42А
К31 МОДА К6А    ОТД.ТР-ТЫ
 ПВ (М14) ИНФ-ИИ
 ПЕ К32А
 МОДА К32А
 ПИО (М17)
 ПБ К6
К122 СЧ ЯЧЧТР(М10)
 СДА 64+СДТ
 НТЖ П777
 СЛЦ Е1
 И П777
 ВЧОБ РЯ2
 ПО КНЦ    ТР.В ИПЗ МЕНЬШЕ ИЛИ РАВНО N
 ВЧОБ 0
 УИ М3 СКОЛЬКО ТРАКТОВ ВЗЯТЬ
 МОДА КНЦ
 УИА (М15)
 ПБ НЕИС
НАП КОНД В’0’
ОТР КОНД М31В’777’В’-1’
МЛ СЧ Е48-1(М6)
 НТЖ ЕЗАР
 ПЕ МЛА1А
 СЧ НЗАРХ
 МОДА Э62АР0
 ПВ (М14)
МЛА1А СЧ 0
 ЗП ИНФ(М10)
 СЧ РЯ1
 СДА 64+45
 И Е1
 ЗП РЯ
 УИА (М3)
 ПО МЛ1 ОТКАЗ ОТ МЛ
 СЧ РЯ1
 СДА 64-3
 И Е46П48
 ПО МЛПЕРЕ ПЕРЕИМЕН МЛ
 НТЖ Е46П48
 ПЕ ЗАПР ЗАПР.ЭКСТРАК.
 СЧ Е48
 ИЛИ РЯ
 ЗП РЯ
МЛПЕРЕ УИА -2(М4)
 ПБ МЛ1
С62 И П7777
 ПО ЗАПР
 УИА -А(М1)
НАБ СЧ ТЗНМЛ+А(М1)
 НТЖ РЯ1
 И П7777
 ПО Ч
 МОДА НАБ
 ЦИКЛ (М1)
 ПБ ЗАПР РАСПЕЧ.ОШИБКИ
МЛ1А СЧ РЯ
 ПЕ МЛ2
МЛ1 МОДА К6А
 ПВ (М14)
 СДА 64+42
 МОДА (М4)
 ЗП РЯ3+2
 НТЖ П77
 ПЕ МЛ2А
 СЧ ИНФ(М10)
 ПЕ Э72АРХ
 ПБ ПРАВИЛ              КОНЕЦ ИНФОРМАЦИИ
МЛ2А СЧ Е1
 ЗП ОШИНФС ПРИЗ.НЕ ПОЛНЛ.ИНФОРМАЦИИ К Э72
 МОДА (М4)
 СЧ РЯ3+2
 ПО МЛ1А
 СЧ РЯ
МЛ2 ПО ПШЗ ОТКАЗ
 МОДА МЛ1
 ЦИКЛ (М4)
 СЧ РЯ3
 ЗП R
 СЧ РЯ4
 ЗП РЯ3
 СЧ R-Р1(М7)
 ЗП РЯ4-Р1(М7) РЯ4-NМЛ(МАТ)1-Й,РЯ5-2-Я МЛ
 УИА -1(М4)
 СЧ АРХМЛ(М10)
 МОД РЯ5
 И Е48-24
 ПЕ АРХМЕН
ПШЗН УИИ М15(М16) ПОИСК ССЫЛКИ НА ОБЩТОМ ПО ШЗНМБ
 УИИ М13(М14)
ПШЗ УИИ М12(М4)
 СЛИ М12(М7)
 СЧ
 ЗП ОШИНФС ФОРМАЛЬНО ВЕРНО
 СЧ РЯ4+1-Р1(М12)
 ВЧ П30
 ПЕ ЗН ЗАПР НАПР
 ВЧОБ П37
 ПЕ ЗН
 СЧ РЯ
 ПЕ НЕОТК
 СЧ АРХМЛ(М10)
 МОД РЯ5
 И Е48-24
 ПЕ АРХОТК
НЕОТК СЧ РЯ4+1-Р1(М12)       N МЛ
 И Е1 ДЛЯ НЕЧЕТ.NМЛ(МАТ)ССЫЛКА С 13Р(13÷16)
 СДА 64-2 ДЛЯ ЧЕТН.9÷12
 УИ М16
 МОД РЯ4+1-Р1(М12)
 СЧ Е48-24
 И АРХМЛ(М10)
 ПЕ АРХМЕН    НАПР АРХИВНОЕ
 СЧ РЯ4+1-Р1(М12)
 СДА 64+1
 УИ М14
 СЛИ М14(М10) N СТРОКИ ШЗНМБ
 СЧ ШЗНМБ-12(М14)
 СДА 64+8(М16)
 И П17
 ЗП РЯ10-Р1(М12)
 ПО НЕТ НЕТ МЛ
 СЧ РЯ6
 ПЕ ОТКАЗ
 МОДА ПШЗН
 ЦИКЛ (М4) ОПРЕД.ССЫЛКИ ДЛЯ СЛЕД.МЛ
 СЧ РЯ
 ПО ОТКАЗ
 И Е48
 ПЕ КП23 ЕСТЬ 2-Я МЛ ПРИ ПЕРЕИМ.В ОДНУ СТОРОНУ
ПМЛ СЧ РЯ3
 И П66
 ПЕ КП23 НЕ ЗАДАНЫ ПР.ЧТ,ЗП
 УИА -1(М4)
ОСВШ УИА (М5) ИЗМЕНЕНИЕ(ОСВОБ)ШЗНМБ
ОСВШЗ УИА ’2003’
 СЧИ М16(М4)
 ВЧОБ 0
 УИ М1
 СЧИ М16(М4)
 СДА 64+2
 ВЧОБ 0
 УИ М12
 МОДА (М4) ДЛЯ ОТКАЗА М4=0
 СЧ РЯ10
 И П3
 УИ М15 N КУСКА В ОБЩТОМ
 МОДА (М4)
 СЧ РЯ10
 СДА 64+2
 УИ М17 N СЛОВА ОБЩТОМ
 СЛИ М17(М10)
 СЧ РЯ6
 ПЕ ОТКАЗ1
 СЧ П17
 СДА 64-8(М1)
 ИЛИ Е7(М12)
 НТЖ ВСЕЕД
 И ШЗНМБ-12(М13)
 ЗП ШЗНМБ-12(М13)
 СЧ РЯ
 ПО ОТКАЗ1
 МОДА (М5)
 СЧ РЯ10
 СДА 64-8(М1)
 ИЛИ ШЗНМБ-12(М13)
 ЗП ШЗНМБ-12(М13) НОВАЯ ССЫЛКА
 СЧ РЯ3
 И Е1
 НТЖ Е1
 СДА 64-6(М12)
 ИЛИ ШЗНМБ-12(М13)
 ЗП ШЗНМБ-12(М13)
 УИА 3
 СЧ РЯ3
 СДА 64+3
 ЗП РЯ3
 УИИ М13(М14)
 СЛИА -1(М5)
 МОДА ОСВШЗ
 ЦИКЛ (М4)
 СЧ АВОС(М10)
 СДА 64+15
 НТЖ РЯ4
 УИА (М4)
 И П77
 ПЕ Ф1
 УИА 1(М4)
Ф СЧ СК
 И АВОС(М10)
 ЗП АВОС(М10)
 МОДА  (М4)
 СЧ РЯ4
 СДА 64-15
 ИЛИ АВОС(М10)
 ЗП АВОС(М10)
ПРИЗД МОД РЯ4
 СЧ Е48-8
 МОД РЯ5
 НТЖ Е48-8
 ЗП R
 УИА -1(М4)
 УИИ М14(М10)
ПРИЗН И УКАЗД(М14)
 ЧЕД 0
 ПО ПРИЗН1 ОБЕ МЛ
 НТЖ Е2
 ПО ПРИЗН1 ОБА МД
 УИА ’2003’
 СЧ R
 НТЖ УКАЗД(М14)
 ЗП УКАЗД(М14)
ПРИЗН1 СЧ R
 СЛИА МДР-УКАЗД(М14)
 МОДА ПРИЗН
 ЦИКЛ (М4)
 УИА 3
 ПБ МЛПЕРЕ
Ф1 НТЖ РЯ4
 НТЖ РЯ5
 ПЕ ПРИЗД
 ПБ Ф
ОТКАЗ УИИ М13(М14)
 ПБ ОСВШ ОСВОБ.ШЗНМБ
ОТКАЗ1 МОДА МЛ1
 УИА (М13)
ОТК СЧ ОБЩТОМ(М17) ОСВ УКАЗД,УМЕНЬШ ЧИСЛО ЗАД С МЛ
 СД КТОМ-1(М15)
 ЗП РЯ3
 И П77
 УИ М12 NМЛ(МД) ФИЗ
 СЧ РЯ6
 УИ М14
 МОД РЯ5-Р1(М7)
 СЧ Е48-8
 И УКАЗД(М10)
 ПО ОТКМЛ
 СЛИА 56(М12) МД
 МОДА ОТМД
 ПИНО (М14)
 НТЖ УКАЗД(М10)
 ЗП УКАЗД(М10) УБРАТЬ ПРИЗН.МД
 СЧ РЯ3
 И П77
 СДА 64-20
 ИЛИ Е27
 НТЖ ИЗАХВ(М10)
 И Е27П21
 ПЕ НЕТО
 СЧ ИЗАХВ(М10)
 И Е27П21
 ЗП РС
 НТЖ ИЗАХВ(М10)
 ЗП ИЗАХВ(М10)
НЕТО МОД РЯ5
 СЧ Е48-8
 И МДР(М10)
 ПЕ КУСК МД РАБ ЗОН
 СЧ ТЗНМЛ-24(М12)
 И Е41
 ПО ОТМД
 СЧ ПРЕДЕЛ
 ИЛИ ЕОТКД
 ЗП ПРЕДЕЛ
 СЧ ТЗНМЛ-24(М12)
 ИЛИ ЕОТМД
 ПБ ОТКАЗ3
ОТМД СЧ ТЗНМЛ-24(М12)
 И КС
ОТКАЗ3 ЗП ТЗНМЛ-24(М12)
 ПБ ОБЩ1
ОТКМЛ НОП ,
 УИИ М14(М15)
 СЧИ М12
 ПВ ОСВМЛ(М15)
 УИИ М15(М14)
 ПБ ОТМД
*
ОБЩ1 НОП ,
 СЧ СЕГМБ(М10)
 НТЖ РЯ5
 И П77
 ПЕ НЕСМБ
 СЧ СЕГМБ(М10)
 НТЖ РЯ5
 ЗП СЕГМБ(М10)
НЕСМБ НОП ,
 СЧ Е16П1
 СД КТОМЛ-1(М15)
 НТЖ ВСЕЕД
 И ОБЩТОМ(М17)
 ЗП ОБЩТОМ(М17)
 УИА 3
 ПБ (М13)
КУСК НТЖ МДР(М10)
 ЗП МДР(М10)
 СЧ РЯ3
 СДА 64+12
 И П17
 УИ М14 Ч КУСКОВ
 СЧ ВСЕЕД
 СДА 64-48(М14)
 СДА 64-56(М12) М12-НАЧ КУСОК+56
 НТЖ КУСКИ
 ЗП КУСКИ ОСВ КУСКИ
 ПБ ОБЩ1
НЕТ СЧ РЯ6 МЛ НЕТ
 ПЕ НЕТМЛ
 СЧ РЯ
 ПО МЛ1 ОТКАЗ ОТ МЛ,ЗА СЛЕД.ИНФОРМ.
 И Е48
 ПО НЕТМЛ ПЕРЕИМ.В ОБЕ СТОРОНЫ
 МОДА ПМЛ
 ПИО (М4) НЕТ 2-Й МЛ ПРИ ПЕРЕИМ.В ОДНУ СТОР.
НЕТМЛ УИА 43(М3) НЕТ МЛ В ЭКСТР.РЕС.
 ПБ ПА РАСПЕЧ.ОШИБКИ
ЗН УИА 47(М3)
 ПБ ПА
ПРАВИЛ СЧ ОШИНФС
 ПЕ КП23 ДАЙ РАБОТУ
КНЦ СЧ РЯ
 ПЕ К300
 УИА 1027
 ПБ КНЦ1
ПА УИА ’2003’
 СЧИ М7
 СДА 64+5
 ЗП РЯ1
 СЧ Е48П37
 И 31(М7)
 ИЛИ РЯ1
 ЗП 31(М7)
 СЧ ЗАНЯТА
 И Е6
 ПО НЕБРАЛ
 ИЛИ Е5
 ПВ ГАШЗАН(М15)
НЕБРАЛ СЧ НЗАД
 ИЛИ ЕЗП
 ЗП 25(М7)
 Э64 КП-Р1+1024
 УИА 3
 Э64 К2-Р1+1024
ВЫХОШ УИИ М17(М3)
 МОД НЗАД
 МОД ТУСП-1
 УИА (М15)
 МОДА К300
 УИА (М16)
 ПБ АВОСТА
Ч СЛИА ’167’(М1)
 УИА 3
 СЧИ М1
 ВЧ П60+1
 УИА 1(М2)
 ПЕ МИ
 УИА (М2)
 СЛИА -56(М1)
МИ СЧИ М1
 ЗП РЯ
 УИА -3(М17)
 УИА -2(М15)
П МОДА (М10)
 СЧ ОБЩТОМ+3(М17)
 СД КТОМ+2(М15)
 СДА 64+6
 И П1777
 ПЕ ПР2
 МОДА (М10)
 СЧ  ОБЩТОМ+3(М17)
 СД КТОМ+2(М15)
 НТЖ РЯ
 И П77
 ПЕ ПР2
 СЧИ М17
 СЛЦ П3
 СДА 64-2
 ЗП РЯ4
 СЧИ М15
 СЛЦ П3
 ИЛИ РЯ4
 ЗП РЯ4
 УИА -15(М4)
Э1 УИА 4(М12)
Э МОДА (М10)
 СЧ ШЗНМБ+15(М4)
 СДА 64+8(М12)
 НТЖ РЯ4
 И П17
 ПО ЭТ
 МОДА ЭТ1
 ПИО (М12)
 УИА (М12)
 ПБ Э
ЭТ1 МОДА Э1
 ЦИКЛ (М4)
ПР2 МОДА П
 ЦИКЛ (М15)
 УИА -2(М15)
 МОДА П
 ЦИКЛ (М17)
 ПБ ЗАПР
ЭТ СЛИА 15(М4)
 СЛИ М4(М4)
 СЛИА 24(М4)
 МОДА ЭТ2
 ПИО (М12)
 СЛИА 1(М4)
ЭТ2 СЧ УКАЗД(М10)
 И Е32-24(М4)
 ПО ЛЕ    МЛ
 МОДА ПР2
 ПИНО (М2)
 ПБ ЭТ3
ЛЕ МОДА ПР2
 ПИО (М2)   МД
ЭТ3 СЧ РЯ1
 И Е37
 СДА 64-3
 ЗП РЯ
 СЧИ М4
 СДА 64-12
 ИЛИ РЯ
 ЗП РЯ
 СЧ РЯ1
 СДА 64+24
 И П7777
 ИЛИ РЯ
 ЗП РЯ
 СЧ РЯ1
 СДА 64-12
 И Е35П31
 ИЛИ РЯ
 ЗП (М10)
 УИА АНВО1Е(М15)
 СЧИ М15
 ЗП 3(М10)
 СЧ 5(М10)
 И Е48П16
 ИЛИ П2007
 ЗП 5(М10)
 ПБ К300
КП ЗП ПЧ-Р1+1024
 ЗП ПЧ-Р1+1025
 КОНД М23В’1’
К2 ЗП ПЧ1-Р1+1024
 ЗП ПЧ1-Р1+1024
 КОНД М21В’102000205’
П66 КОНД В’66’
НЗАД ПАМ 1
Э62АРХ МОДА Э62АР1
 УИА (М14)
 СЧ 36(М10)
 И П77777
Э62АР0 ЗП НЗАД
 СЧ 15(М10)
 ВЧ МИНЗ
 ПЕ ОШЭ62А
 ВЧ МАКЗ
 ПО ОШЭ62А
 МОД 15(М10)
 УИА (М6)
 МОД ТУСП-1(М6)
 УИА (М10)
 ПБ (М14)
ОШЭ62А СЧ НЗАРХ
 ЗП НЗАД
ЗАПР УИА 36(М3)
 ПБ ПА
Э62АР1 СЧ 36(М10)
 И Е48П16
 ИЛИ НЗАД
 ЗП 36(М10)
 СЧ НЗАРХ
 ЗП НЗАД
 ПБ ИЗЭ62А
СДВ ЗП РЯ2
 СДА 64+7
 ПО ЭКГД
 СЧ РЯ2
 СДА 64+9
 УИ М5 NМД(МАТ)
 СДА 64+1
 УИ М4
 ВЧ П14
 ПЕ К300
 ВЧ Е5
 ПО К300
 СЧ Е48-1(М6)
 НТЖ ЕЗАР
 ПО Э62АРХ
 СЧ АРХМЛ(М10)
 И Е48-24(М5)
 ПЕ САР
ИЗЭ62А НОП
 СЧ 36(М10)
 И Е10
 СДА 64+7
 МОДА К300
 УИА (М13)
 МОДА ПОИС ОПРЕД.NМД(Ф)
 ПВ (М12)
 АIF (∧ВS ЕQ 0).СД3
 УИА (М12)
 ПО СД        МЛ
 УИА 1(М12)
 АGО .СД4
.СД3 ПО СД    МЛ
.СД4 АNОР
 СЛИА 56(М17)
СД СЧ 36(М10)
 И П777
 НТЖ П777
 ПО ОТ
 НТЖ П777 ДЕЛЬТА
 АIF (∧ВS ЕQ 0).СД
 ЗП РЯ2
 МОДА СДМД
 ПИНО (М12)     СДВИГ ПО МД
 СЧ ТЗНМЛ-24(М17)
 И БЕЗДУБ
 ПО МЛСДУБ     ЕМКОСТЬ МЛ<2000 ЗОН
 УИА 2(М12)
 СЧ РЯ2
 И П3
 УИА 34(М3)   ОШ В ИНФ СЛ ЭК
 ПЕ ВЫХОШ     СДВИ НЕ КРАТЕН 4
МЛСДУБ СЧ РЯ2
 СДА 64(М12)
СДМД НОП ------
.СД СДА 64-6
 СД КТОМЛ-1(М15)
 ЗП РЯ2-Р1(М7)
 СЧ МДР(М10)
 И Е48-8(М5)
 ПЕ К300
 СЧ П1777
 СДА 64-6
 СД КТОМЛ-1(М15)
 НТЖ ВСЕЕД
 И ОБЩТОМ(М2)
 ИЛИ РЯ2
 ЗП ОБЩТОМ(М2)
 ПБ К300
АРХМЕН УИА -2(М12)
 МОДА МЛПЕРЕ
 УИА (М14)
 ПБ ЗИНФ
АРХОТК УИА 0(М12)
 МОДА МЛ1
 УИА (М14)
ЗИНФ СЧ ИНФ(М10)
 СДА 64-6
 МОДА (М12)
 ИЛИ РЯ5
 ЗП ИНФ(М10)
 МОДА ЗИНФ
 ЦИКЛ (М12)
 ПБ (М14)
САРСМ СЧ (М10)
 ЗП ИНФ(М10)
 ПБ САР0
САР СЧ 36(М10)
 ЗП ИНФ(М10)
САР0 НОП
 СЧ 1(М10)
 И Е48П9
 ИЛИ П23
 НТЖ Е1
САР1 ЗП 1(М10)
 СЧ СОБА40
 ПВ ЗАПВЕТ(М16)
 СЧ Е48-1(М6)
 ИЛИ Ш1
 ЗП Ш1
 ПБ ВЫХОД
Э72АРХ СЧ РЯ
 ЧЕД 0
 СДА 64-45
 ИЛИ ИНФ(М10)
 ЗП ИНФ(М10)
 СЧ 1(М10)
 И Е48П9
 ИЛИ П32
 ПБ САР1
ЭКГД СЧ ОПТИМ
 И П77777
 УИ М1
 ПЕ (М1)
 ПВ ТБУФ(М15)
 СДА 64-8
 УИ М1
 УИИ М16(М1)
 ЗП РЯ6
 ИЛИ ОПТИМ
 ЗП ОПТИМ
 СЧ РЯ6
 СДА 64-2
 ИЛИ ЭКГ
 ИЛИ НОММБ1
 ЗП РЯ6
 ПБ ФИЗОБМ
ОТ НОП ,
 СЧИ М5
 И Е1
 ВЧОБ 0
 УИ М13
 СЧИ М16
 ВЧОБ 0
 УИ М16
 СЧ РЯ2
 СДА 64+2
 НТЖ П3
 УИ М17
 СЧ П3
 МОДА (М17)
 СДА 64-26(М17)
 НТЖ ВСЕЕД
 МОДА (М10)
 И 27(М15)
 МОДА (М10)
 ЗП 27(М15)
 СЧ П17
 УИА ’2003’
 СДА 64-8(М16)
 ИЛИ Е7(М13)
 НТЖ ВСЕЕД
 И ШЗНМБ-12(М4)
 ЗП ШЗНМБ-12(М4)
 УИИ М17(М2)
 СЧИ М5
 ЗП РЯ5
 СЧ РЯ2
 ЗП РЯ10
 МОДА ОТК
 ПВ (М13)
КНЦ1 СЧ Е36
 ИЛИ ШКИВ
 ЗП ШКИВ РАБ.ПЛАНИРОВЩИКУ
 ПБ К300
С63 СЧ РЯ1
 УИА 3
 И Е16 18÷13РР=23÷18РР КУС
 СДА 64+3 ЕСЛИ ЕСТЬ 21Р(16Р.РЯ1)-ЧТ.СЛ.СЛ,
 ИЛИ РЯ1
 ЗП РЯ1
 СДА 64+12
 И П77
 СДА 64-17
 ЗП РЯ3
 СЧ РЯ1
 СДА 64+36
 И П77
 УИ М17 NН,NУСТР.
 СДА 64+3
 ПО ЗАПР ЗАПРЕЩ.НАПРАВЛ.
 СЛЦ Е1
 СДА 64+3
 ПЕ ЗАПР НАПР.≥7
 СЧИ М17
 ВЧ П30
 ПО С63МЛ МЛ,МД
 СЧ РЯ1
 СДА 64+24
 И П37 N ТРАКТА
 УИ М4
 СЧ РЯ1
 СДА 64-43
 ИЛИ Е46
 НТЖ Е46
 РЗБ АБЗ
 ИЛИ РЯ3
 ЗП РЯ3
 СЧ РЯ1
 И Е11
 СДА 64-18
 ИЛИ РЯ3
 ЗП РЯ3
 СЧ РЯ1
 ИЛИ П7777
 НТЖ П7777
 ЗП РЯ1
 СЧИ М4
 СДА 64-15
 ЗП РЯ
 СЧИ М17
 ИЛИ РЯ
 ЗП РЯ
 СЧИ М10
 СДА 64-33
 ИЛИ РЯ
 ЗП РЯ
 СЧ РЯ3
 И Е18
 ПЕ ТЕСТМБ
 СЧ R
 И Е10
 ПЕ МБ1В
 УИА ’2003’
 СЧ ТРТЕСТ
 ПО НЕТТР НЕТ БОЛЬШЕ ЗАДАЧИ С ТЕСТОВ.РЕЖ
 НТЖ РЯ
 И Е48П33
 ПЕ ЗАПР В РЕШ.НЕ ≥1 ЗАДАЧИ С ТЕСТ.РЕЖ
 СЧ РЯ
 НТЖ ТРТЕСТ
 СДА 64-18
 ПО МБ1В ТРАКТ НЕ НОВЫЙ,НЕ СИСТ.И НЕ ОТБРАК.
 СЧ ТРТЕСТ
 УИ М16
 МОДА НЕТТР
 ПИО (М16)
 СДА 64+15
 УИ М15
 СЧ ШЗНМБО-8(М16)
 НТЖ Е48(М15)
 ЗП ШЗНМБО-8(М16) ОСВОБ.ЗАНЯТОГО РАНЕЕ ТРАКТА
НЕТТР СЧ 0
 ЗП ТРТЕСТ
ТЕСТМБ СЧИ М17
 И П7
 УИ М15
 УИА ’2003’
 СЧ ШЗНМБО-8(М17)
 И Е48(М4)
 ПЕ ТРСВОБ ТРАКТ СВОБОДЕН
 УИИ М1(М17)
 СЛИА -8(М1)
 УИИ М15(М10)
 ПВ УПРИ(М10)
 УИИ М10(М15)
 СЧИ М1
 СДА 64-5
 УИ М15
 СЛИ М15(М4)
 ПРЕ 1026
 СЧ ТБД(М15)
 ПРЕ 1027
 И КДД
 ПЕ ОЖ
 УИИ М5(М17)
 МОД НЗАД
 УИА (М17)
 ПВ ЗАНПРП(М15)
 УИИ М17(М5)
 УИА -1(М15)
МБСИСТ СЧ НОММБ+1(М15)
 СБР НАПРМГ
 СДА 64+42 NН,NМБ НОММБ (НОММБ1)
 ЗП РЯ2
 СЧИ М17
 НТЖ РЯ2
 ПО МБС МБ СИСТЕМНЫЙ
 МОДА МБСИСТ
 ЦИКЛ (М15)
 ПБ МБ1В
ТРСВОБ СЧ РЯ3
 И Е18
 ПЕ МБ1В
 СЧ Е48(М4)
 НТЖ ШЗНМБО-8(М17)
 ЗП ШЗНМБО-8(М17)
 СЧ РЯ
 ЗП ТРТЕСТ
МБ1В СЧИ М4 ТРАКТ
 СДА 64-2
МБ2А ИЛИ Е28 ПР.ТЕСТОВОГО РЕЖИМА МБ
 ИЛИ Е21
 НТЖ Е21
 УИА 4(М16) ДЛЯ МБ NКУБА В 24,21 РР КУС
 ПБ БОБН2
МБС СЧИ М4
 СДА 64-2
 ИЛИ Е18
 ПБ МБ2А С СИСТ.МБ ТОЛЬКО ЧТ
ОЖ СЧ 0
 УИА 3
ОЖИД1 ЗП (М10)
 ПБ К300
ПОДВОД ЗП РЯ3
 СЧ РЯ1
 СДА 64+36
 И П77
 УИ М5
 ВЧ П30
 ПЕ ЗАПР
 СДА 64+5
 ПЕ ЗАПР
 ПБ ПОДВ
С63МЛ СЧ РЯ1
 И Е1
 ПО ЗАПР
 УИИ М5(М17)
ПОДВ СЧ АРХМЛ(М10)
 И Е48-24(М5)
 ПЕ К300
 СЧИ М5           N МЛ(М)
 СДА 64+1
 УИ М4
 СЧИ М5
 И Е1
 СДА 64-2
 МОДА К300
 УИА (М13)
 МОДА НАШЛИ
 УИА (М12)
ПОИС УИ М16 ПОИСК ФИЗИЧ.N МЛ(МД)
 СЛИ М4(М10)
 СЧ ШЗНМБ-12(М4)
 СДА 64+8(М16)
 И П17
 ПО (М13) МЛ(МД) НЕТ
 ЗП РЯ2
 И П3
 УИ М15 N КУСКА ОБЩТОМ
 СЧ РЯ2
 СДА 64+2
 УИ М2 N СЛОВА ОБЩТОМ
 СЛИ М2(М10)
 СЧ ОБЩТОМ(М2)
 СД КТОМ-1(М15)
 И П77
 УИ М17 NМЛ ФИЗ
 СЧ Е48-8(М5)
 И УКАЗД(М10)
 ПБ (М12)
НАШЛИ ПЕ К300
 СЧ РЯ1
 СЛЦ Е27
 И Е34П25 NЗОНЫ+4
БОБН2 ИЛИ РЯ3
 ЗП РЯ3
 СЧИ М17
 УИА 3
 СДА 64-42
 РЗБ НАПРМГ
 ИЛИ РЯ3
 ЗП РЯ3
 МОДА ПОДВ1
 ПИО (М3) ПОДВОД
 СЧ РЯ1
 И Е1
 ПЕ ПОДВОТ
* ТЕСТОВЫЙ ОБМЕН С МБ
 СЧ РЯ1
 СДА 64+18
 И П37           N ЛИСТА МАТЕМАТИКА
 ЗП РЯ
 НТЖ П37
 УИ М5
 МОДА К121
 УИА (М15)
 СЧИ М15
 ПВ ЗАПВЫХ(М15)
 СЧ РЯ
 СДА 64-10
 УИ М15 А
 УИА ’2000’
 СЧ 50(М15)
 УИА ’2003’
 СЧ 32(М10)
 ИЛИ Е48(М5)
 ЗП 32(М10)
 СЧ РЯ
 И П3
 УИ М4 N СТОЛБЦА ИПЗ
 СЧ РЯ
 СДА 64+2
 НТЖ П7
 УИ М12
 СЛИ М12(М10) А СТРОКИ ИПЗ
 СЧ 24(М12)
 СБР ПРИП(М4)
 СДА 64+2
 ЗП РМР
 СЧ РЯ3
 ПВ ФИЗУР(М16)
 СЧ 32(М10)
 НТЖ Е48(М5)
 ЗП 32(М10)       ОТКРЫЛИ ЛИСТ
 СЧ КОБМ
 ПЕ НЕТОБ
 СЧ РЯ3
 И Е18
 ПО КОНОБ
 СЧ НЕЧСЛ
 ПЕ КОНОБ
* ПЕРЕПИСЬ СЛУЖЕБНЫХ СЛОВ В СЛЕД. ЛИСТ
 СЧ РЯ
 СЛЦ Е1
 И П37
 СДА 64-10 А ЛИСТА
 УИ М4
 УИА -7(М1)
ПСЛСЛ МОДА ВХ+1
 СЧ 7(М1)
 УИА ’2002’
 МОДА (М4) ПЕРЕНОС СЛ.СЛОВ В МАТЕМ.ЛИСТ
 ЗП 7(М1)
 УИА 3
 МОДА ПСЛСЛ
 ЦИКЛ (М1)
КОНОБ СЧ Е48
 ПБ ОЖИД1
НЕТОБ СЧ Е1
 ПБ ОЖИД1
СС СЧ КУС1Б-1(М16)
 УИА -255(М4)
 И П3
 ПБ ССС
ТЕСТ УИА 1(М17) ОБМЕН С МБ
 СЧ КУС1Б-1(М16)
 И Е29
 ПЕ КСЧБ4
 МОД ТУС+17
 УИА (М7)
 УИА ’2003’
 СЧ КУС1Б-1(М16)
 И Е19
 ПО СС
 УИА -1023(М4)
 СЧ П3
ССС СДА 64-8
 РЕГ 65
 СЛЦ С
 ЗП ПП
 НТЖ Е35
 НТЖ Е16
 ЗП ПКЧ
 СЧ БМБ-1(М16)
 И Е4(М16) Е3-НЕТ ОБМ.С1 НАПР.,Е2-СО 2-М
 ЗП КОБМ
 СЧ 0
 ЗП БМБ-1(М16)
 СЧ КОБМ
 ПЕ КСЧБ4
 СЧ КУС1Б-1(М16) ЭТОТ БЛОК РАБОТ.ОТ ДИСП70
 И Е18
 ПО ОПРБ5 ЗП
 СЧ ИПЗМЛ+4
 ЗП ИПОБМ+4
 СЧ ЛИС1Б-1(М16)
 ПВ ПРИП1С(М10)
 МОДА ПКЧ
 УИА (М5)
ПРКЧ1 СЧ К2003
 ПВ ПОК(М13)
ПП УИА 2
 СЧ 2047(М4)
 УИ 0
 МОДА ПП
 ЦИКЛ (М4)
 УИА 3
 УИА 8(М10)
 УИИ М2(М16) ДЛЯ МБ
СЛСЛБ СЛИА -1(М10) ЧТ.СЛ.СЛОВ МБ НА СВОЕ ПОЛЕ
 ПВ ПРЛСЧА(М5)
 МОДА (М7)
 ЗП ВХ+1-Р1(М10)
 МОДА (М7)
 ПИНО СЛСЛБ-Р1(М10)
 СЧ ПР13
 ПВ ПОК(М13)
 ПБ КСЧБ4
ПКЧ УИА 1026
 ЗП 2047(М4)
 УИА 1027
 МОД И16
 УИА (М16)
 ПБ ПРКЧ1
ПОДВ1 НОП
 УИА 0(М16)        М16:=  0
 СЧ ТЗНМЛ-24(М17)
 И БЕЗДУБ
 ПО Л1000         НА ЛЕНТЕ 1000 ЗОН
 УИА -2(М16)       М16:= -2
Л1000 НОП ,
 СЧ ОБЩТОМ(М2)
 СД КТОМ-1(М15)
 СДА 64-18(М16)
 СЛЦ РЯ3
 И Е34П25
 ЗП РЯ2
 СЧ ВСЕЕД
 НТЖ Е34П25
 И РЯ3
 ИЛИ РЯ2
 ИЛИ Е2 ПОДВОД БЕЗ ОТВЕТА
 ПБ ПОДВ2
ПОДВОТ ИЛИ Е18
 ИЛИ РЯ3
ПОДВ2 ЗП РЯ3
 ПВ ФИЗОБМ(М16)
 ЗП РЯ2
 СЧ РЯ3
 И Е1
 ПО К300
 СЧ РЯ2
 ПБ ОЖИД1
Э52 СЧ 36(М10)
 АIF (∧ВS ЕQ 1).Э52
       УИ    М11
 СЧ ПРЕДЕЛ
 И Е31
 МОД ТУС+24
 УИА (М3)
 ПЕ ВИХ-ОСА(М3)
 УИА 36(М3)
.Э52 ПБ ВЫХОШ
С УИА 2
 СЧ 1279(М4)
НМБ КОНД М25В’17777’
РС ПАМ 1
СК КОНД В’-1’М15В’77’
НЕЧСЛ КОНД В’0’
КОБМ КОНД В’0’
ВХ КОНД В’1’
 КОНД В’0’
 ПАМ 7
 ПАМ 25
АБЗ КОНД М10В’3’В’7’
ОШИНФС КОНД В’0’
Е48П33 КОНД М32В’177777’
КС КОНДС М43В’13’В’7777777’  ЕСЛИ БЕЗДУБ И УПНОВ=Е21
КС КОНДБ М43В’13’В’1777777’
ЗОН КОНД М24В’7770’М17В’1’
С17 КОНД В’0’
Е27П21 КОНД М20В’177’
АВОСТ ВНЕШ СТ433
ВИСП ВНЕШ ВШГП
ДИСП70 ВНЕШ И20,КУС2,НОММЛ,КНАПР,П1777,ИПЗМЛ,И16,ИПОБМ
ДИСП70 ВНЕШ АПСЧБ,ШКСВН,Е35П31,К2003,ОПТИМ
ДИСП70 ВНЕШ ТМБ,ТМД,ОЗУ,КДД,ТОБ,КДВ,ИНФ,Ш1,ЗАПВЕТ,П12,П23
ДИСП70 ВНЕШ Е48П9,НОММЛ1,ГОД,П777,П7777,ТЗНМЛ,ВИСП16,ШКЗАКЗ
ДИСП70 ВНЕШ ВИСП3,ЗАНЯТА,НОММБ,И17,ШЗНМБО,ШЗНМБ,ОБЩТОМ,ПР14
ДИСП70 ВНЕШ Е34П25,НАПРМГ,Е21Е18,Е48П37,ИЗ10В8,Е40П1,ЗАХЗАН
ДИСП70 ВНЕШ Е48П43,КСД1,П17,ТВ18,П2007,Е48П16,Е24П1,Е46П48
ДИСП70 ВНЕШ П3777,УКАЗД,П377,АВОС,ШКИВ,АВОСТА,ТЗФСТР,П3,П7
ДИСП70 ВНЕШ П37,П77,П60,П77777,ВСЕЕД,(Е77С43,Е48П43),ТУС,ШГ
ДИСП70 ВНЕШ НОММБ1,ТБУФ,ТВ40,П30,(П14,ПР14),ПР13,КУС1Б,РМР
ДИСП70 ВНЕШ ЕЗП,П37777
ДИСП70 ВНЕШ П32,И,СУММАВ,ПРЕДЕЛ,ГАШЗАН,ЛИС1Б,БМБ,ОБЩМЛ
ДИСП70 ВНЕШ ЕЛЕН,ЗАНПРП,ПРИП,ОТКАЗН,ЯЧЭК,ЕВАМОS
ДИСП70 ВНЕШ П177,ТУСП,НЗАРХ,ЕЗАР,ШАРБОП,ШАРКЗ,СОБА40,МИНЗ,МАКЗ
ДИСП70 ВНЕШ ЛТБД
ДИСП70 ВНЕШ ГРАНЬБ
ДИСКИ ВНЕШ Е16П1
КИТ ВНЕШ АКТБД,ТРТЕСТ,ТОПЛИС,Е48П2
ОСА ВНЕШ ВИХ,ОСА
ДМЛМБ ВНЕШ ПРЛСЧА,ПРЛСЧБ,(ОПРБ5,СНМБ),КСЧБ4
ВЗУ ВНЕШ ПОК,ПРИП1С,ФИЗИЧ,АНВО1Е,ФИЗОБМ,ФИЗУР
БОЭК1 ВНЕШ СИ2,ЗАПВЫХ,(ЛИСТМ,ЛИСТМБ),АРХМЛ,ИСКЛИС
ГЕНС1 ВНЕШ РАБ1РМ,РАБ2РМ,ВИОЛПМ
ДИСКИ ВНЕШ КУСКИ,ЕNQ,DЕQ,КТОМ,КТОМЛ
КАЧКА ВНЕШ О42,Е48П17,R2
СОСТАВ ВНЕШ ЕОТМД,ЕОТКД,ТБД,ИЗАХВ,МДР
СОСТАВ ВНЕШ ЕТР,ЯЧЛИС,ЯЧТР,СДТ,ЯЧЧТР,ССТБД
ПВВ ВНЕШ ОСВМД,ОСВМЛ
СОСТАВ ВНЕШ СЕГМБ
ДИСКИ ВНЕШ ЕNQМДС,ШЗМД
СОСТАВ ВНЕШ ТОП,ТБДТР,ЯЧТОП,СДВМЛ,МОДЛИ
ХЛАМ ВНЕШ УПРИ
ЭКВВОД ВНЕШ БЕЗДУБ
 ЭКВИВ (Р1,РЕСУРС),(А,95)
 ВХОД РЯ6,К300,ФФ,ВЫХОШ,РП,РЯ,ТЕСТ,КП25
 ВХОД САРСМ,ВЫХОД,РЯ1,КП23,ПК6В,ВХ,R
 ВХОД НЗАД
 ЛИТЕР
         ФИНИШ ,    В ВАШЕМ ФИНИШЕ ОШИБКА ИЛИ ЕГО НЕТ

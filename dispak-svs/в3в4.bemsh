В3В4 СТАРТ 4096
 Б
*      ∧ВS ∧УЧЕТ ∧ЭФ
 УПОТР В3В4(М1)
 УИА ВОЗВР-В4(М13)
 УИА -3(М12)
 СЧ 0
 ЗП Р3
 СЧ ЯЧВ1
 НЕД П77777
 УИ М16
 СЧ Е48(М16)
 НТЖ ЯЧВ1
 ЗП ЯЧВ1
 СЧИ М16
 СДА 64-2
 ИЛИ Е44
 ИЛИ ЯЧВСТ-В1(М7)
 ЗП ЯЧВСТ-В1(М7)  ТРАКТ И ПРИЗ
 СЧ R-В1(М7)
ВОЗВР ЗП ЯЧВ3
 СЧ ПРИЛАТ
 ПЕ ЛАТ
 СЧ ЯЧВ3
 МОДА БУКВШ+3
 НТЖ (М12)
 УИА 17(М16)
 ПЕ ЛАТ
 МОДА ЗАСИМВ-В1(М7)
 ЦИКЛ (М12)
ВОЗВР1 ЗП РЯП+2
 СЧ ЯЧВСТ-В1(М7)
 НТЖ Е35
 ЗП ЯЧВСТ-В1(М7)
 УИА СШИФР-В4(М13)
 ПБ ЗАСИМВ-В1(М7)
ЛАТ УИИ М16(М12)
 СЛИА 3(М16)
 МОДА ЛАТ1
 ПИО (М16)
 СЧ ПРИЛАТ
 ПО НЕШИФР
ЛАТ1 СЧ ЯЧВ3
 МОДА БУКВU+3
 НТЖ (М12)
 УИА 17(М16)
 ПЕ НЕШИФР
 СЧ Е1П24
 ЗП ПРИЛАТ
 МОДА ЗАСИМВ-В1(М7)
 ЦИКЛ (М12)
 СЧ 0
 ЗП ПРИЛАТ
 ПБ ВОЗВР1
ПРИЛАТ КОНД В’0’
БУКВШ КОНД В’070’
 КОНД В’250’
 КОНД В’064’
 КОНД В’260’
БУКВU КОНД В’111’
 КОНД В’310’
 КОНД В’045’
 КОНД В’307’
ПРКЛЮЧ КОНД В’0’
НКЛЮЧ КОНД В’0’
ЯЧВ3 КОНД В’0’
ЭТОСТ КОНД В’0’
СТП1 СЛИА -’14’(М15)  КОСАЯ ЧЕРТА
 МОДА КЛЮЧ1
 ПИО (М15)
 СЛИА -’101’(М15)
 МОДА НАДЧЕР
 ПИО (М15)
СТП СЧ ЭТОСТ
 СЛЦ Е1
 ЗП ЭТОСТ
 МОД ПРКЛЮЧ
 СЧ РЯП+2
 СДА 64-8
 ИЛИ R-В1(М7)
 МОД  ПРКЛЮЧ   ЛИБО СТ.ПАСП,ЛИБО КЛЮЧ
 ЗП РЯП+2         СТПАСП:ЗАКАЗ ИЛИ ОТМЕНА
 УИА СТП1-В4(М13)
 ПБ ЗАСИМВ-В1(М7)
СШИФР СЛИА -’14’(М15)
 МОДА КЛЮЧ1
 ПИО (М15)
 СЛИА -’101’(М15)
 МОДА НАДЧЕР
 ПИО (М15)         КОНЕЦ ШИФРА
 ВЧ П12
 ПО СТП           НЕ ЦИФРА
 СЧ ПРКЛЮЧ
 ПЕ КЛЮЧИК
 СЧ ШИФР-В1(М7)
 СДА 64-4
 ИЛИ R-В1(М7)
 ЗП ШИФР-В1(М7)
 СЛИА 1(М12)        ЧИСЛО ЦИФР
 ПБ ЗАСИМВ-В1(М7)
КЛЮЧ1 СЧ ЭТОСТ
 ПО КЛЮЧ
 НТЖ П3
 ПЕ СТП
КЛЮЧ СЧ Е1
 ЗП ПРКЛЮЧ  ПРИЗНАК КЛЮЧА
 УИА СШИФР-В4(М13)
 ПБ ЗАСИМВ-В1(М7)
КЛЮЧИК СЧ НКЛЮЧ ПОСЛ. ШЕСТЬ ЦИФР
 СДА 64-4
 ИЛИ R-В1(М7)
 ЗП НКЛЮЧ  ЗАП.Н.КЛ.
 ПБ ЗАСИМВ-В1(М7)
НАДЧЕР УИА 1(М16)        ЧИСЛО СИМВ.≠12 И ≠6
 СЛИА -12(М12)
 МОДА ПРОВШФ
 ПИО (М12)         12 СИМВОЛОВ
 СЛИА 6(М12)
 МОДА ОШШИФР
 ПИНО (М12)         НЕ 6 СИМВ.
 СЧ ШИФР-В1(М7)
 СДА 64-24
 ЗП ШИФР-В1(М7)
ПРОВШФ СЧ ШИФР-В1(М7)
 ПЕ ФАМИЛ         ФОРМАЛЬНО ВЕРНЫЙ
ОШШИФР УИА ФАМИЛ-В4(М13)
 ПБ ВЫДОШ-В1(М7)  ВЫДАЧА ОШИБКИ
НЕШИФР СЧ ЯЧВСТ-В1(М7)
 НТЖ Е35
 ЗП ЯЧВСТ-В1(М7)  С КОНТР.ЧЕТНОСТИ
 СЧ НОМЕРЗ-В1(М7)
 ПЕ ОШШИФР        ОТ ЭКСТРАКОДА ФОРМИР.НЕТ ЗАМЯТИЯ
 СЧ ЯЧВ1
 ПЕ ОШШИФР        ЗАМЯТИЯ ПК НЕ БЫЛО
 ЗП СЧСИМ-В1(М7)
 УИА 3
 СЧИ М2
 СДА 64-2
 ИЛИ ИНФЗАМ
 ИЛИ НОММБ
 ПВ ФИЗОБМ(М16)   ЧТ.ИНФ.О ЗАМЯТИИ
 СЧ ЯЧВСТ-В1(М7)
 СДА 64+16
 И П7
 УМН П12R
 СЧМР 64
 СДА 64-1
 УИ М17
 СДА 64-8
 ЗП РЯ2           НАЧАЛО ПК НА БУФЕРЕ
 СЧ Е48П41-4
 И СТВУ-В1(М7)
 НТЖ СТВУ-В1(М7)
 ИЛИ РЯ2
 ЗП СТВУ-В1(М7)
 УИА 3
 СЧ ЯЧВСТ-В1(М7)
 СДА 64+2
 И П37
 УИ М16
 СЧ Е48(М16)
 НТЖ ЯЧВ1
 ЗП ЯЧВ1          ОТКНЗ ОТ ТРАКТА
 СЧ ТИПУСТ-В1(М7)
 ПО ПЕРПК1        С ФСМ
 МОДА БУФПР-В1(М7)
 СЛИА (М17)
 УИА -19(М15)
ПЕРПК МОДА ПОЛЕПК-В1(М7)
 СЧ 19(М15)
 ЗП (М17)
 МОДА ПЕРПК
 ЦИКЛ (М15)
ПЕРПК1 УИА -9(М15)
 УИИ М17(М7)
 УИА -ДЛЗМ(М16)
 СЧИ М2
 СДА 64-2
 ИЛИ Е18
 ЗП РЯ2
ВОСТИР МОДА 777(М17)
 СЧ (М15)
 УИ М13(М15)      ВОССТ.М2-М13
 МОДА ВОСТИР
 ЦИКЛ (М15)
 СЧ ТУС+19
 УИ М7
 СЧИ М2
 И П1777
 УИ М2
 СЛИ М2(М7)
 СЧИ М3
 И П1777
 УИ М3
ВОСТРЯ МОДА 778(М7)       ВОССТ.РАБ.ЯЧЕЕК ДЛЯ ПРОДОЛЖЕНИЯ ВВОД
 СЧ ДЛЗМ(М16)
 МОДА ДЛЗМ(М16)
 ЗП ЯЧВСТ-В1(М7)
 МОДА ВОСТРЯ
 ЦИКЛ (М16)
 СЛИ М3(М7)        ВОССТ.М2 И М3
 СЧ ЯЧВСТ-В1(М7)
 И П177
 ИЛИ РЯ2
 ИЛИ НОММБ
 ПВ ФИЗОБМ(М16)   ЧТ.ПРЕД.МАТЕРИАЛА
 УИА ВЫБ1-В1(М16)
 СЛИ М16(М7)
 СЧ ЯЧВСТ-В1(М7)
 И Е33
 ПЕ ВЫЗВ6А-В1(М7)
 ПБ (М16)
ФАМИЛ НОП ,
 СЧ 0
 ЗП СОИ-В1(М7)
 СЧ ШИФР-В1(М7)
 НТЖ СПЕЦШ
 ПО ШВФ
 НТЖ  СПЕЦШ+1
 ПЕ НЕШВФ
ШВФ НОП ,
 СЧ ШИФР-В1(М7)
 СДА 64+32
 СДА 64-32
 ЗП ШИФР-В1(М7)
 СЧ Е1
 ЗП СОИ-В1(М7)
НЕШВФ НОП ,
 СЧ СТВУ-В1(М7)
 И Е26
 ЗП И14           И14:=0
 ПО ФАМИ  НЕ С МБ
 СЧИ М14
 ЗП И14
 СЧ 0
 ПВ ТБУФ(М15)
 СДА 64-8
 УИ М14
ФАМИ СЧИ М14
 СДА 64-2
 ИЛИ КУСФАМ
 СЛЦ НОММЛ1
 ЗП РЯ3
 УИА -2(М12)
ВФ СЧ НКЛЮЧ
 ПО ПОСТАР  РАБОТАЕМ ПО СТАРОМУ
 СДА 64-8
 ЗП ПРКЛЮЧ
 СЧ НКЛЮЧ
 И Е16П1
 ПВ ИЗ10В8(М15)
 ЗП НКЛЮЧ
 И П777
 СДА 64-1
 УИ М12
 СЧ НКЛЮЧ
 СДА 64+9
 И П3  10,11
 ПО ЧТКТЛ
 НТЖ Е1
 ПО ЧТКТЛ1 ЧИТАЕМ 2 КАТ.
 СЧ Е26
ЧТКТЛЛ СЛЦ РЯ3
 ЗП РЯ3
ЧТКТЛ СЧ РЯ3
 ПВ ФИЗОБМ(М16)
 УИА 9(М16)
 ПБ ПРНО1
ЧТКТЛ1 СЧ Е25
 ПБ ЧТКТЛЛ
ПОСТАР СЧ РЯ3
 ПВ ФИЗОБМ(М16)
 УИА 3
 УИИ М15(М14)      НАЧ.БУФЕРА
 УИА 9(М16)        ЗАПИШИ ФАМИЛИЮ
 УИА -511(М17)
ВФ1 СЧ 1(М15)
 ЗП РЯП+1         2-Е СЛОВО ФАМИЛИИ
 УИА ВФ7-В4(М13)
 НТЖ ШИФР-В1(М7)
 И Е33П48
 ПО ВФ21
 СЛИА 2(М15)
 МОДА ВФ1
 ЦИКЛ (М17)         НА 512 ФАМИЛИЙ
 СЧ РЯ3
 СЛЦ Е25
 ЗП РЯ3
 МОДА ПОСТАР
 ЦИКЛ (М12)
 ПБ ВЫДОШ-В1(М7)  ВСЕ ТРИ ПРОСМ. 0
ВФ21 СЧ РЯП+1
 И Е25П32
 ПО ВФ2
 ПБ ВЫДОШ-В1(М7)
ПРНО1 УИА 3
 УИИ М15(М12)
 СЛИ М15(М14)
 СЧ (М15)
 ПО ВЫДОШ-В1(М7)  В КАТ. ПУСТО
 СЧ 1(М15)
 ЗП РЯП+1
 УИА ВФ7-В4(М13)
 НТЖ ШИФР-В1(М7)
 И Е33П48
 ПО ВФФА НАШЛИ ФИО
 ПБ ВЫДОШ-В1(М7)   ФЕДОТ, ДА НЕ ТОТ
ВФФА СЧ РЯП+1
 НТЖ ПРКЛЮЧ
 И Е25П32
 ПЕ ВЫДОШ-В1(М7)  ФЕДОТ,ДА НЕ ТОТ
ВФ2 СЧ (М15)
 ЗП РЯП
 УИА -255(М15)
 СЧ
ОЧБУФ МОДА 768(М7)       БУФЕР ПОД РЕДАКТИР.
 ЗП 255(М15)
 МОДА ОЧБУФ
 ЦИКЛ (М15)
 МОДА РОСПЕЧ
 ПВ (М16)
 СЧ ГОД
 ЗП 768+ГОДМ0(М7) ЯЧ ГОД В МАС0
 СЧ ВРЕМЯ
 ЗП 768+ВРЕМ0(М7) ВРЕМЯ ВВОДА В МАС0
 СДА 64+10
 СДА 64-23
 ЗП ЯЧКАТ+7-В1(М7) ВРЕМЯ ВВОДА В Б.К.
 СЧ РЯП+2
 ПО ВФ7
 СДА 64+16
 НТЖ П47
 ПЕ ВЫДОШ-В1(М7)
 СЧ РЯП+2
 И Е16П1
 ЗП 768+СТПМ0(М7) СТАН.ПАСП.В МАС0
 СЧИ М14
 СДА 64-2
 ИЛИ НОММЛ1
 СЛЦ КУСПАС
ВФ4А ЗП РЯ3
 ПВ ФИЗОБМ(М16)   ЧТ.СТПАСП
ВФ4Б УИА 3
 УИА (М16)
 МОДА (М15)
 СЧ (М14)
 УИ М17
 МОДА ВФ7
 ПИО  (М17)  КОНЕЦ СТАНД ПАСПОРТОВ
 СДА 64+32
 НТЖ 768+СТПМ0(М7)
 ПО ВФ5           НАШЛИ СТПАСП
 СЛИ М15(М17)
 СЧИ М15
 И Е11
 ПО ВФ4Б          В ТОЙ ЖЕ ЗОНЕ
 СЧИ М15
 И П1777
 УИ М15
 СЧ РЯ3
 СЛЦ Е25
 ПБ ВФ4А          НА СЛ.ЗОНУ
ВФ5 НОП
 СЛИА 1(М16)        СТПАСП НА БУФЕР
 СЛИА -1(М17)
 СЛИА 1(М15)
 СЧИ М15
 И Е11
 ПО ВФ6           В ТОЙ ЖЕ ЗОНЕ
 УИИ М15(М16)
 СЧ РЯ3
 СЛЦ Е25
 ПВ ФИЗОБМ(М16)   СЛЕД.ЗОНА
 УИИ М16(М15)
 УИА (М15)
ВФ6 УИА 3
 МОДА (М15)
 СЧ (М14)
 МОДА (М16)
 ЗП 768+127(М7)
 СЧИ М17
 НТЖ Е1
 ПЕ ВФ5           НЕ ВЕСЬ СТПАСП
 СЧ П3
 СДА 64-13
 ИЛИ ЯЧВСТ-В1(М7)
 ЗП ЯЧВСТ-В1(М7)  Е14-ЕСТЬ СТПАСП,Е15-ВЫБОР ИЗ СТПАСП
 СЧ СЧСИМ-В1(М7)
 ЗП СЧСИМК-В1(М7)
 СЧИ М4
 ЗП ПОЛЕПК-В1(М7) СОХР.М4
 УИА (М4)
ВФ7 СЧ РЯП
 РЗБ =Х’3Ф3Ф3Ф3Ф3Ф3Ф’
 СЛЦ =П’,,,,,,’
 ЗП ЯЧФИО-В1(М7)  1-Е 6 СИМВОЛОВ ФАМИЛИИ
 СЧ РЯП
 СДА 64-36
 ЗП РЯП
 СЧ РЯП+1
 СДА 64-24
 СДА 64+12
 ИЛИ РЯП
 РЗБ =Х’3Ф3Ф3Ф3Ф3Ф3Ф’
 СЛЦ =П’,,,,,,’
 ЗП ЯЧФИО+1-В1(М7) 2-Я ПОЛОВИНА ФАМИЛИИ
 СЧ НОМЕРЗ-В1(М7)
 ПЕ ВФ8           ФОРМИР.ЗАДАЧИ
 СЧ ИНФСЛК-В1(М7)
 ПВ ФИЗОБМ(М16)   ЧТ.СЛОЙКИ
ВФ8 СЧ И14
 ПО ВФ9   ЛИСТА НЕТ
 СЧИ М14
 СДА 64-2
 ИЛИ Е19
 ПВ ТБУФ(М15)
 СЧ И14
 УИ М14
ВФ9 УИА 3
 УИА ЛА65Б-В4(М13)
 СЛИА НАЧLМ0(М2)
 СЧ ЯЧОТЛ-В1(М7)
 ИЛИ Е31           ПРИЗ:ВЫДАЧИ НЕ УЧИТЫВАТЬ
 ЗП ЯЧОТЛ-В1(М7)
 ПБ ЗАСИМВ-В1(М7)
ПРИХ НОП
 СЧ НОМЕРЗ-В1(М7)
 УИ М4
 И ЕЗП
 ПО ЗАСЛОЙ
 МОД ТУС-1(М4)
 УИА (М6)
 СЧ ШИФРМ(М6)
 ЗП АВТОР-В1(М7)
 СЧ ПРЕДЕЛ
 И ЕПЗФ
 ПО ЗАСЛОЙ
 СЧ 0
 ЗП (М6)
 СЧИ М1
 СДА 64-2
 ИЛИ Е19
 ПВ ТБУФ(М15)
 ПБ ОСВЗ-В1(М7)
ЗАСЛОЙ НОП
 СЧ 0
 ПВ ТБУФ(М15)
 УИА 3
 СДА 64-8
 УИ М14           БАЗА СЛОЙКИ
 СДА 64-2
 ИЛИ КУССЛ-В1(М7)
 ИЛИ НОММБ1
 ЗП РЯСЛК-В1(М7)
 ПВ ФИЗОБМ(М16)
 УИА 3
 УИА 768(М2)
 СЛИ М2(М7)
 СЧ Е35
 ЗП ЯЧВСТ-В1(М7)
 ЗП ПОВТЗП-В1(М7)
 УИА (М13)
 СЧ НОМЕРЗ-В1(М7)
 ПЕ ОТЭКФ
 СЧ НУСВУ
 И Е2
 НТЖ Е2
 ЗП ТИПУСТ-В1К(М7) 0-ПЛ;2-/ПК
 СЧ НУСВУ
 И Е1
 ЗП УСТ-В1К(М7)
 ПБ ДАЙБУФ-В1К(М7)
П47 КОНД В’47’          З-ЗАКАЖИ,А-АSSIGN(АСN)
П63 КОНД В’63’
РЯ ЭКВ ВОЗВР+3
РЯ2 ЭКВ ВОЗВР+4
КУСПАС КОНД М24В’443’М17В’1’
КУСФАМ КОНД М24В’440’М17В’1’
ИНФЗАМ КОНД М17В’1’В’33’     6ТР3С ЗАМ
ПЕРЕКЛ ПБ БЛОК
 КОНК П’БЛО’
 ПБ ЛИСТ
 КОНК П’РАG’
 ПБ ВХОД
 КОНК П’ВЕG’
 ПБ РОС
 КОНК П’WRI’
 ПБ ВРЕ
 КОНК П’ТIМ’
 ПБ ТРА
 КОНК П’ТRА’
 ПБ АЦП
 КОНК П’LРR’
 ПБ МЛАРХ
 КОНК П’ТАР’
 ПБ АВОС
 КОНК П’ЕМЕ’
 ПБ СЛУЖ
 КОНК П’СЛУ’
 ПБ ТЕЛЕ
 КОНК П’ТЕL’
 ПБ СПЕЦ
 КОНК П’SРЕ’
 ПБ ФИЗ
 КОНК П’РНУ’
 ПБ МДАРХ
 КОНК П’DIS’
 ПБ ЖДЕ
 КОНК П’WАI’
 ПБ ЛИСТ
 КОНК П’ЛИС’
 ПБ ВХОД
 КОНК П’ВХО’
 ПБ РОС
 КОНК П’РОС’
 ПБ ВРЕ
 КОНК П’ВРЕ’
 ПБ ТРА
 КОНК П’ТРА’
 ПБ ПРОВКН        РАЗДЕЛ ВЫВОД ИГНОРИР.
 КОНК П’ВЫВ’
 ПБ АЦП
 КОНК П’АЦП’
 ПБ МЛАРХ
 КОНК П’ЛЕН’
 ПБ АВОС
 КОНК П’АВО’
 ПБ ЖДЕ
 КОНК П’ЖДЕ’
 ПБ ЛА64    ИГНОР
 КОНК П’ПРИ’
 ПБ ТЕЛЕ
 КОНК П’ТЕЛ’
 ПБ ГРИФ
 КОНК П’ГРИ’
 ПБ ПОВТ
 КОНК П’ПОВ’
 ПБ ПЕЧ
 КОНК П’ПЕЧ’
 ПБ ПРОВКН        РАЗДЕЛ ГРАФ ИГНОРИР.
 КОНК П’ГРА’
 ПБ СПЕЦ
 КОНК П’СПЕ’
 ПБ ФИЗ
 КОНК П’ФИЗ’
 ПБ ИНКО
 КОНК П’ИНК’
 ПБ МДАРХ
 КОНК П’ДИС’
 ПБ ОДИН
 КОНК П’ОДИ’
 ПБ МАШИНА
 КОНК П’МАШ’
 ПБ ЛА64   ШИФР ИГНОР
 КОНК П’ШИФ’
 ПБ ОЗУ
 КОНК П’ОЗУ’
 ПБ КОП
 КОНК П’КОП’
 ПБ БАЦ
 КОНК П’БАЦ’
 ПБ ГЛАВ
 КОНК П’ГЛА’
 ПБ КТЧ
 КОНК П’КТЧ’
ЧИСЛОР ЭКВ *-ПЕРЕКЛ-1
РОСПЕЧ УИА 1-LПЕЧ(М17)
 СЧ ВСЕЕД
РОСПЕ1 МОДА 768(М7)
 ЗП ПЕЧМ0-1+LПЕЧ(М17)
 МОДА РОСПЕ1
 ЦИКЛ (М17)
 ПБ (М16)
*
КТЧ СЧ ШКАНУВ-В1(М7)
 НТЖ Е1
 ЗП ШКАНУВ-В1(М7)
 ПБ ЛА64
*
СЛУЖ СЧ НОМЕРЗ-В1(М7)
 ПО ЧУЖОЕ
 И ЕЗП
 ПО СЛУ
 СЧ ЯЧСЛУЖ(М6)
 И ЕСЛУЖ
 ПО ЧУЖОЕ
СЛУ СЧ ШКАНУВ-В1(М7)
 ИЛИ Е13
 ЗП ШКАНУВ-В1(М7)
 ПБ ПРОВКН
*
ПЕЧ МОДА РОСПЕЧ
 ПВ (М16)
 МОДА 768(М7)
 УИА (М10)
 УИА 0(М11)
 УИА ПЕЧ1-В4(М13)
ПЕЧДАЛ УИА -48(М12)
 ПБ ЗАСИМВ-В1(М7)
ПЕЧ1 СЛИА 8(М12)
 НТЖ =В’115’
 ПО ЛА65           КОНЕЦ РАЗДЕЛА
 НТЖ ДРОМБ
 ПЕ НЕРОМБ
 УИА ’17’(М15)    РОМБ ЗАМЕНИЛИ ПРОБЕЛОМ
НЕРОМБ НОП
 СЧ R-В1(М7)
 ВЧ П140
 ПО ЛА88А         НЕДОПУСТИМЫЙ СИМ.
 СЧ П377
 СДА 64(М12)
 НТЖ ВСЕЕД
 И ПЕЧМ0(М10)
 ЗП ПЕЧМ0(М10)
 СЧИ М15
 СДА 64(М12)
 ИЛИ ПЕЧМ0(М10)
 ЗП ПЕЧМ0(М10)
 МОДА ЗАСИМВ-В1(М7)
 ПИНО (М12)
 СЛИА 1(М10)
 СЛИА 1(М11)
 УИИ М16(М11)
 СЛИА -LПЕЧ(М16)
  МОДА ПЕЧДАЛ
 ПИНО (М16)
 ПБ ЛА64
ОДИН СЧ СТПАСП-В1(М7)
 ИЛИ Е31
 ЗП СТПАСП-В1(М7)
 ПБ ПРОВКН
БЛОК СЧ Е28
 ИЛИ ЯЧКАТ+6-В1(М7)
 ЗП  ЯЧКАТ+6-В1(М7)
 ПБ ПРОВКН
БАЦ СЧ Е9
 ИЛИ Е14
 НТЖ ВСЕЕД
 И ШКАНУВ-В1(М7)
 ИЛИ Е15   - ПРО
 ИЛИ Е5  ПРИЗНАК БАЦ
 ЗП ШКАНУВ-В1(М7)
 СЧИ М15
 НТЖ =В’115’
 ПО ПРОВКН     БАЦ БЕЗ ПАРАМ.
БАЦ2 УИА БАЦ1-В4(М13)
 ПБ ЗАСИМВ-В1(М7)
БАЦ1 НТЖ Е6
 ПЕ Z1
 СЧ ШКАНУВ-В1(М7)
 ИЛИ Е14       ПРИЗНАК АЦ
 ЗП ШКАНУВ-В1(М7)
 ПБ БАЦ2
Z1 СЧИ М15
 НТЖ =В’115’
 ПО ПРОВКН
 ПБ МЛ20
КОП УИА КОПИ-В4(М13)
 ПБ ЗАСИМВ-В1(М7)
КОПИ И П7
 СДА 64-1
 НТЖ ШКАНУВ-В1(М7)
 И П16
 НТЖ ШКАНУВ-В1(М7)
 ЗП ШКАНУВ-В1(М7)
 ПБ ЛА64
*
МАШИНА НОП
 СЧ R-В1(М7)
 НТЖ =В’115’
 ПЕ МАШИН   НОМЕР
 СЧ ГОД
 ПБ МАШ
МАШИН УИА МАШИ-В4(М13)
 ПБ ЗАСИМВ-В1(М7)
МАШИ СЧ R-В1(М7)
 СДА 64+3
 ПЕ ЛА88А
 СЧ R-В1(М7)   НОМ ЭВМ
МАШ СДА 64-45
 НТЖ ЯЧКАТ+7-В1(М7)
 И Е46П48
 НТЖ ЯЧКАТ+7-В1(М7)
 ЗП ЯЧКАТ+7-В1(М7)
 И Е46П48
 ПО ЛА64
 СЧ ЯЧКАТ+6-В1(М7)
 ИЛИ Е33
 ЗП ЯЧКАТ+6-В1(М7)
 СЧ ЯЧКАТ+ЯЧЗПЕР-В1(М7)
 ИЛИ ЕЗПЕР
 ЗП ЯЧКАТ+ЯЧЗПЕР-В1(М7)
 ПБ ЛА64
ЛА65Б СЛИА -’45’(М15)
 УИА 3
 МОДА ФОРМЕ
 ПИО (М15)         НА РЕДАКТ.МАССИВА;Е-КОНЕЦ ПАСПОРТА
 СЧ 0
 ЗП R1-В1(М7)
 УИА -2(М12)
 СЧ Е48
 ЗП R2-В1(М7)     ПРИЗН.ЧТО СИМВ.В R2 НЕТ
 УИА ПРИЕМ4-В4(М13) А ВОЗВРАТА ПОСЛЕ ВЫБОРКИ СИМВОЛА
ПРИЕМ4 СЧ R1-В1(М7)
 СДА 64-8
 ИЛИ R-В1(М7)
 ЗП R1-В1(М7)
 МОДА ЗАСИМВ-В1(М7)
 ЦИКЛ (М12)         ПРИЕМ 4-Х СИМВОЛОВ
 УИА ДОПРОБ-В4(М13)
 СЧ ЯЧВСТ-В1(М7)
 ИЛИ Е35
 ЗП ЯЧВСТ-В1(М7)  ЛЮБОЙ СИМВОЛ
ДОПРОБ ИЛИ Е8
 НТЖ Е8
 ЗП R-В1(М7)
 УИ М15
 НТЖ П17
 ПО ПРОБЕЛ        СИМВОЛ-ПРОБЕЛ
 НТЖ =В’102’
 ПО ПРОБЕЛ
 НТЖ =В’63’
 ПЕ ЗАСИМВ-В1(М7) НЕ НАДЧЕРКИВАНИЕ
ПРОБЕЛ СЧ ЯЧВСТ-В1(М7)
 НТЖ Е35
 ЗП ЯЧВСТ-В1(М7)
 УИА -ЧИСЛОР(М16)
ПОИСКР МОДА ПЕРЕКЛ        ОПРЕДЕЛ.РАЗДЕЛА
 СЧ ЧИСЛОР(М16)
 НТЖ R1-В1(М7)
 И Е1П24
 МОДА ПЕРЕКЛ
 ПО ЧИСЛОР(М16)   НАШЛИ РАЗДЕЛ
 МОДА ПОИСКР
 ЦИКЛ (М16)
ЧУЖОЕ НОП
 УИА 17(М16)       ЧУЖОЕ СЛОВО
 ПБ ВЫДОШ1
ОЗУ ЗП R1-В1(М7)
 УИА ОЗУ1-В4(М11)
 ПБ ТРА1
ОЗУ1  СЧ R1-В1(М7)
 ВЧОБ П7777
 ПЕ ЛА88А
 СЧ R1-В1(М7)
 ЗП ШКЛИСТ-В1(М7)
 ПБ ПРОВКН
ЛИСТ СЧ Е48
 ЗП R1-В1(М7)     ПРИЗН.ЗАПЯТОЙ
ЛИСТ1 СЧ Е48
 ЗП R2-В1(М7)     НЕ БЫЛО ЦИФР
 УИА ЛИСТ2-В4(М13)
 ПБ ЗАСИМВ-В1(М7) ЗА СИМВОЛОМ
ЛИСТ2 СДА 64+3
 ПЕ НЕВОСМ        НЕ ВОСЬМ.ЦИФРА
 СЧ R2-В1(М7)
 СДА 64-3
 ИЛИ R-В1(М7)
 ЗП R2-В1(М7)     УПАК.ЦИФР
 ПБ ЗАСИМВ-В1(М7) ЗА СИМВОЛОМ
НЕВОСМ СЧ R2-В1(М7)
 РЖА ’13’
 ПО ЛИСТ3     ЦИФР НЕ БЫЛО
 СДА 64+5
 ПЕ ЛА88А         ТАКОГО ЛИСТА НЕТ
 СЧ R1-В1(М7)
 И Е48
 ПО МИНУС         ПРЕДЫД.СИМВОЛ -
 СЧ R2-В1(М7)
 ЗП R1-В1(М7)
ФОРМШК УИ М17           НОМЕР ЛИСТА
 СЧ Е48(М17)
ФОРМЛ ИЛИ ШКЛИСТ-В1(М7)
 ЗП ШКЛИСТ-В1(М7) ФОРМ.ШКАЛЫ ЛИСТОВ ЗАДАЧИ
МИНУС СЧ R1-В1(М7)     ОБРАБ.ИНФОРМ.Л1-Л2
 НТЖ R2-В1(М7)
 ПО ЛИСТ3
 СЧ R1-В1(М7)
 ВЧ R2-В1(М7)
 ПО ЛА88А         НЕВЕРНОЕ ЗАДАНИЕ ИНФ.О ЛИСТАХ
 СЧ R1-В1(М7)
 СЛЦ Е1
 ЗП R1-В1(М7)
 ПБ ФОРМШК
ЛИСТ3 СЧ R-В1(М7)
 НТЖ П13
 ПО ЛИСТ1         СИМВОЛ-МИНУС
 СЛИА -’115’(М15)
 МОДА ЛА65
 ПИО (М15)         НАДЧЕРКИВАНИЕ
 СЛИА ’100’(М15)
 МОДА ЛИСТ
 ПИО (М15)         ЗАПЯТАЯ
 ПБ ЛА88А         НЕДОПУСТ.СИМВОЛ
ВХОД ЗП R2-В1(М7)
 СЧ ЯЧКАТ+6-В1(М7)
 И Е28
 ПЕ ВЫДОШ0        ’ВХО’ ПОСЛЕ ’БЛО’
 УИА -5(М12)
 УИА ЛА81-В4(М13)
 ПБ ЗАСИМВ-В1(М7)
ЛА81 СДА 64+3
 ПЕ ЛА83          ЦИФРА НЕ ВОСЬМЕР.
 СЧ R2-В1(М7)
 СДА 64-3
 ИЛИ R-В1(М7)
 ЗП R2-В1(М7)     УПАК.А ВХОДА
 МОДА ЗАСИМВ-В1(М7)
 ЦИКЛ (М12)
ВЫДОШ0 УИА 5(М16)        НЕВ.А ВХОДА
ВЫДОШ1 УИА ЛА64-В4(М13)
 ПБ ВЫДОШ-В1(М7)  НА ВЫДАЧУ ОШИБКИ
ЛА83 СЧ ЯЧРОСП-В1(М7)
 И П77777
 НТЖ ЯЧРОСП-В1(М7) ОС.СТАРОГО А ВХОДА
 ИЛИ R2-В1(М7)
 ЗП ЯЧРОСП-В1(М7)
 ПБ ПРОВКН
РОС УИА РОСП-В4(М13)
 ПБ ЗАСИМВ-В1(М7)
РОСП ВЧ П3
 ПО ЛА88А         СИМВОЛ>2:ОШИБКА
 СЧ Е46П48
 И ЯЧРОСП-В1(М7)
 НТЖ ЯЧРОСП-В1(М7)
 ИЛИ Е48(М15)
 ЗП ЯЧРОСП-В1(М7) ПРИЗ.РОСПИСИ
 ПБ ЛА64
ВРЕ УИА -6(М12)
 СЧ ГОД
 И П7
 СДА 64-42
 ИЛИ ЯЧРОСП-В1(М7)
 ЗП ЯЧРОСП-В1(М7)
 СЧ УСТ-В1(М7)
 СДА 64-41
 ИЛИ ЯЧРОСП-В1(М7)
 ЗП ЯЧРОСП-В1(М7)
 УИА ВРЕМ-В4(М13)
 ПБ ЗАСИМВ-В1(М7)
ВРЕМ ВЧ П12
 ПО ЛА89          ЦИФРА>9
 СЧ R2-В1(М7)
 СДА 64-4
 ИЛИ R-В1(М7)
 ЗП R2-В1(М7)
 МОДА ЗАСИМВ-В1(М7)
 ЦИКЛ (М12)
ЛА88А УИА 3(М16)        НЕДОПУСТ.СИМОЛ
 ПБ ВЫДОШ1        НА ВЫДАЧУ ОШИБКИ
ЛА89 УИА -5(М17)       ПОДСЧЕТ ВРЕМЕНИ
 СЧ ЯЧКАТ-В1(М7)
 И =В’3777777’В’-1’
 ЗП ЯЧКАТ-В1(М7)  ОЧИСТ.СТАРОГО ВРЕМЕНИ
 СЧ 0
 ЗП R1-В1(М7)
ЛА89А СЧ R2-В1(М7)
 ПО ЗАПВРЕ
 СДА 64+4
 ЗП R2-В1(М7)
 СЧМР
 СДА 64+44
 МОДА ВРЕМПР
 УМН 5(М17)
 СЧМР 64
 СЛЦ R1-В1(М7)
 ЗП R1-В1(М7)
 МОДА ЛА89А
 ЦИКЛ (М17)
ЗАПВРЕ НОП
 СЧ R1-В1(М7)
 СДА 64+5
 ИЛИ Е1
 ИЛИ ЯЧКАТ-В1(М7)
 ЗП ЯЧКАТ-В1(М7)
 ПБ ПРОВКН
ТРА ЗП R1-В1(М7)
 СЧ ЯЧКАТ-В1(М7)
 И =М20В’777’В’-1’
 ЗП ЯЧКАТ-В1(М7)  ОЧИСТКА СТАРОГО ЧИСЛА ТРАКТОВ
 УИА ЛА96Б-В4(М11) А ВОЗВ.ИЗ П/П ИЗ 10 В 8
 ПБ ТРА1
АЦП ЗП R1-В1(М7)
 УИА ЛА150-В4(М11)
ТРА1 УИА ТРАК-В4(М13)
 ПБ ЗАСИМВ-В1(М7)
ТРАК УИА -11(М12)      НА 12 ЦИФР
 ВЧ П12
 ПО ЛА149А        ЦИФРА НЕ ДЕСЯТИЧНАЯ
 СЧ R2-В1(М7)
ТРАК1 СДА 64-4
 ИЛИ R-В1(М7)
 ЗП R2-В1(М7)
 ПБ ЗАСИМВ-В1(М7) ЗА СИМВОЛОМ
ЛА96Б СЧ R1-В1(М7)
 ВЧ Е10
 ПО ЛА88А
 СЧ R1-В1(М7)
 СДА 64-20
 ИЛИ ЯЧКАТ-В1(М7)
 ЗП ЯЧКАТ-В1(М7)
 ПБ ПРОВКН
АВОС УИА (М11)
 СЧ R-В1(М7)
 НТЖ =В’115’
 ПЕ АВ            ЕСТЬ ИНФОРМ.К СЛОВУ АВОСТ
 СЧ Е5
 ЗП ЯЧАВОС-В1(М7) ВХОД В 20-Ю
 ПБ ЛА65          НА ДАЛЬН.РЕДАКТ.
АВ УИА АВ3-В4(М12)   А ВЫХОДА 1-ЫЙ РАЗ
 СЧ 0
 ЗП ЯЧАВОС-В1(М7)
 ПБ АВ1
АВ5 СЛИА 1(М15)
 МОДА АВ6
 ПИНО (М15)      НЕ С
 УИА АВ5А-В4(М12) ЗА НОМЕРОМ СЕКТОРА
АВ1 СЧ Е48
 ЗП R2-В1(М7)
 УИА АВОСТ-В4(М13)
 ПБ ЗАСИМВ-В1(М7) ЗА СИМВОЛОМ
АВОСТ УИА 2(М16)        ОШИБКА В АВОСТЕ;НОМЕР ТЕКСТА
 СДА 64+3
 МОДА (М1)
 ПЕ (М12)         НЕ ВОСЬМ.ЦИФРА
 СЧ R2-В1(М7)
 СДА 64-3
 ИЛИ R-В1(М7)
 ЗП R2-В1(М7)
 ПБ ЗАСИМВ-В1(М7)
АВ5А СЧ R2-В1(М7)
 СДА 64+2
 ПЕ ВЫДОШ1       НОМЕР СЕКТОРА>3:ОШИБКА
 СЧ R2-В1(М7)
 СДА 64-6
 ИЛИ Е48         ПРИЗН.ОБМЕНА СЕКТОРОМ
АВ2 ИЛИ ЯЧАВОС-В1(М7)
 ЗП ЯЧАВОС-В1(М7) КУС ДЛЯ ВЫЗОВА ПРОГР.АВОСТН.СИТУАЦИИ
АВ3 СЛИА -’41’(М15)
 МОДА АВ4
 ПИНО (М15)         НЕ Б
 СЛИА 4(М11)        ПРИЗНАК БУКВЫ Б
 УИА АВ3А-В4(М12)
 ПБ АВ1           ЗА НОМЕРОМ И НАПРАВЛ.МБ
АВ3А СЧ R2-В1(М7)
 СДА 64+3
 ПО ВЫДОШ1        НА ОШ-КУ:НАПР.=0
 ВЧ П3
 ПО ВЫДОШ1        НАПРАВЛ>2;ОШИБКА
 СЧ R2-В1(М7)
 СДА 64-12
 ПБ АВ2           НА ПРИФОРМ.К КУС
АВ4 СЛИА -’21’(М15)
 МОДА АВ5
 ПИНО (М15)         НЕ Т
 СЛИА 2(М11)        ПРИЗНАК Т
 УИА АВ4А-В4(М12)
 ПБ АВ1           ЗА НОМЕРОМ ТРАКТА
АВ4А СЧ R2-В1(М7)
 СДА 64+5
 ПЕ ВЫДОШ1        ОШИБКА:НОМЕР ТРАКТА>37
 СЧ R2-В1(М7)
 ИЛИ Е40
 ПБ АВ2           НОМЕР ТРАКТА И ПРИЗНАК ЧТЕНИЯ К КУС
АВ6 СЛИА ’21’(М15)
 МОДА АВ7
 ПИНО (М15)         НЕ А
 СЛИА 1(М11)        ПРИЗН.АДРЕСА
 УИА АВ6А-В4(М12)
 ПБ АВ1           ЗА А ВХОДА В АВОСТНУЮ ПРОГРАММУ
АВ6А СЧ R2-В1(М7)
 СДА 64+15
 ПЕ ВЫДОШ1        А ВХОДА>77777:ОШИБКА
 СЧ R2-В1(М7)
 СДА 64-24
 ПБ АВ2           НА ПРИФОРМ.А К КУС
АВ7 СЛИА -’55’(М15)
 МОДА ВЫДОШ1
 ПИНО (М15)         НЕДОП.СИМВОЛ
 СЛИА -7(М11)       СИМВ.НАДЧЕРКИВ.
 МОДА ВЫДОШ1
 ПИНО (М11)         НЕ ВСЕ КОМПОНЕНТЫ АВОСТ НАЛИЦО
 ПБ ЛА65          НА ДАЛЬН.РЕДАКТ.
МДАРХ НОП
МЛАРХ НОП
 СЧ ЯЧКАТ+6-В1(М7)
 И Е28
 УИА 16(М16)
 ПЕ МЛ21          ’ЛЕН’ ОСЛЕ ’БЛО’
МЛ УИА (М11)         =0-ПРИЗ.);=1-ПРИЗ.(;=2-ПРИЗН.-
 УИА 0(М12)
СКБАЦ СЧ 0
 ЗП ДЛС
МЛ1 СЧ Е48
 ЗП R2-В1(М7)     НЕ ПРИНИМ.СИМВОЛОВ
 ЗП Р4
 УИА ЛЕНТ-В4(М13)
 ПБ ЗАСИМВ-В1(М7) ЗА СИМВОЛОМ
ЛЕНТ НТЖ =В’115’
 ПО МЛ19          НАДЧЕРКИВАНИЕ
 СЧ R-В1(М7)
 СДА 64+5
 ПЕ МЛ18АР
 СЧ R-В1(М7)
МЛ2 ВЧ П12
 ПО МЛ3Б НЕ ЦИФРА
 СЧ R2-В1(М7)
 СДА 64-4
 ИЛИ R-В1(М7)
 ЗП R2-В1(М7)     УПАКОВКА ЦИФР
 УИА МЛ2-В4(М13)
 ПБ ЗАСИМВ-В1(М7)
МЛ3Б НОП
 СЛИА -’41’(М15)
 МОДА СЕГМБ
 ПИО (М15)
 СЛИА -’20’(М15)
 МОДА СЕГМД
 ПИО (М15)
 СЛИА ’61’(М15)
МЛ3 СЧ R2-В1(М7)
 НТЖ Е48
 ПО МЛ20          ОШ-КА:НЕТ СИМВОЛОВ
 СЧИ М11
 И Е2
 ПО МЛ12          ПРЕД.РАЗДЕЛИТЕЛЬ НЕ МИНУС
*ПОДВОДА НЕТ
 СЧ 0
МЛ3А ИЛИ R1-В1(М7)
 ЗП R1-В1(М7)     НОМЕР КУСКА ДЛЯ ПОДВОДА
МЛ4 СЧ ДЛС
 ПЕ МЛ4А СЕГМЕНТ
 СЛИА -11(М15)
 МОДА МЛ11
 ПИО (М15)         РАЗДЕЛИТ.-
 СЛИА -7(М15)
 МОДА МЛ5
 ПИНО (М15)         НЕ (
 МОДА МЛ20
 ПИНО (М11)         ОШ-КА:( ИДЕТ НЕ ЗРАЗУ ЗА );()-РАЗДЕЛ.
 СЛИА 1(М11)        СЛЕД.БУДЕТ НОМЕР БОБИНЫ
 ПБ МЛ1           ЗА СИМВОЛОМ
МЛ4А СЛИА -’22’(М15)
МЛ5 СЛИА -1(М15)
 МОДА МЛ20
 ПИНО (М15)         НЕДОПУСТ.СИМВОЛ
 СЧИ М11           СИМ.)-ЭТО КОНЕЦ ИНФ.ОБ ОДНОЙ МЛ
 И Е1
 ПО МЛ20          НОМЕРА БОБИНЫ НЕ БЫЛО:ОШ-КА
 УИИ М10(М7)
МЛ6 СЧИ М12
 ВЧ П3
 ПЕ МЛ7
 СЛИА 1(М10)
 УИ М12
 ПБ МЛ6           ПОДСЧ.МЕСТА ДЛЯ ЗАНЕС.НОМЕРА БОБИНЫ
МЛ7 СЛИ М12(М1)       ЗАНЕС.В КАТАЛОГ НОМЕРА БОБИНЫ
 СЧ R1-В1(М7)
 РЗБ КМЛ-В4(М12)
 ЗП R2-В1(М7)
 СЧ ЯЧКАТ+1-В1(М10)
 ИЛИ КМЛ-В4(М12)
 НТЖ КМЛ-В4(М12)
 ИЛИ R2-В1(М7)
 ЗП ЯЧКАТ+1-В1(М10)
 ПБ МЛ
МЛ11 СЧИ М11
 И Е1
 ПО МЛ20          РАЗД.-ВПЕРЕДИ РАЗД.(:ОШ-КА
 ИЛИ Е2
 УИ М11
 ПБ МЛ1           ЗА СИМВОЛОМ
МЛ12 СЧИ М11
 И Е1
 ПЕ МЛ16          ЕСТЬ НОМЕР БОБИНЫ
 УИА -1(М17)
 СЧ R2-В1(М7)
 ИЛИ П167
 НТЖ П167
 ПЕ МЛ20          НЕВЕРНОЕ НАПРАВЛ.И МГ
 СЧ R2-В1(М7)
 СЛЦ Е5
 И Е7
 ПО МЛ20          НЕВЕРН.НОМЕР НАПРАВЛ.
МЛ13 СЧ R2-В1(М7)
 СБР П167
 СДА 64+2
 ЗП R1-В1(М7)     НОМЕР НАПР.И МГ
 СДА 64+40
 ЗП МЛРВБЩ-В1(М7) НАПР.И МГ В МЛ.РАЗРЯДАХ
 УИА -5(М5)        ДЛЯ ПОИСКА СВ.МЕСТА ИЛИ ТАКОЙ ЖЕ МЛ
 СЧ П77
 СДА 64-40
 ЗП РЯ
МЛ14 МОДА (М17)
 СЧ ЯЧМЛ+1-В1(М7)
 И РЯ
 ПО МЛ15          СВОБ.МЕСТО
 НТЖ R1-В1(М7)
 ПО МЛА
 СЧ РЯ
 СДА 64+8
 ЗП РЯ
 СЧ R1-В1(М7)
 СДА 64+8
 ЗП R1-В1(М7)
 СЛИА 1(М12)        ЧИСЛО МЛ
 МОДА МЛ14
 ЦИКЛ (М5)
 МОДА МЛ13
 ЦИКЛ (М17)
 ПБ БОЛДОП
МЛА ЗП Р4
 СЧ РЯ
 СДА 64-2
 ИЛИ РЯ
 ЗП РЯ
 МОДА (М17)
 ИЛИ ЯЧМЛ+1-В1(М7)
 НТЖ РЯ
 ИЛИ R1-В1(М7)
 МОДА (М17)
 ЗП ЯЧМЛ+1-В1(М7)
 СЧ ЯЧВСТ-В1(М7)
 СДА 64+44
 ПЕ МЛ4           БЫЛИ ОШИБКИ ВВОДА
 МОД МЛРВБЩ-В1(М7)
 СЧ Е48-’30’
 И ШКАНУВ-В1(М7)
 ПО МЛ4
 НТЖ ШКАНУВ-В1(М7)
 ЗП ШКАНУВ-В1(М7)
 УИА ИМЕНА+’1400’(М16)
 СЛИ М16(М7)
МЛА1 СЧ (М16)
 СДА 64+42
 НТЖ МЛРВБЩ-В1(М7)
 ПО МЛА2
 СЧ (М16)
 СДА 64+24
 И П77
 УИ М17
 СЛИ М16(М17)
 ПБ МЛА1
МЛА2 СЧ (М16)
 И Е42П1
 ЗП (М16)
 СДА 64+21
 И П7
 ПЕ МЛ20 УЖЕ ЕСТЬ БАЦ,БПИ,...
 ПБ МЛ4
СЕГМБ НОП
 УИА СЕГМБ1-В4(М13)
 ПБ ЗАСИМВ-В1(М7)
СЕГМБ1 МОДА КОРСЕГ
 ПВ (М10)
 СЧ Е47(М16)      7 Р. КВАНТА ЯЧМЛ - ПРИЗ. СЕГМЕНТА НА МБ
 ИЛИ ЯЧМЛ-В1(М17)
 ЗП ЯЧМЛ-В1(М17)
 ЗП ДЛС           ДЛС ≠ 0 ДЛЯ МЛ4
 СЧ Е48           ТОЛЬКО 16-ЫЙ РАЗРЯД - КУСОК НА МБ
 ЗП R1-В1(М7)
 ПБ МЛ4
СЕГМД НОП
 СЧ R2-В1(М7)
 ЗП ДЛС
 ПО МЛ20
 УИА МЛ18Б-В4(М13)
 ПБ ЗАСИМВ-В1(М7)
МЛ18Б СЧ ДЛС
 И =В’27’В’-1’
 ПЕ МЛ20 ЧИСЛО КУСКОВ>17
 СЧ ДЛС
 СБР П27
 ПО МЛ20
 СДА 64+11
 ЗП R1-В1(М7)
 МОДА СЕГМД1
 УИА (М10)
*
КОРСЕГ НОП
 УИИ М17(М7)
 УИИ М16(М12)
 СЧИ М12
 ВЧОБ П5
 ПО МЛДИ
 СЛИА 1(М17)
 СЛИА -6(М16)
МЛДИ СЧИ М16
 СДА 64-3
 УИ М16
 ПБ (М10)
*
СЕГМД1 НОП
 СЧ Е48(М16)
 ИЛИ ЯЧМЛ-В1(М17)
 ЗП ЯЧМЛ-В1(М17)
 СЧ Р4
 ПО ДИСК1 УЖЕ БЫЛА ЭТА МЛ
 СЧ R1-В1(М7)
 СДА 64+33
 СЛЦ Р3
 ЗП Р3 +К ЧИСЛУ ЗОН НА РАБ МД
 ПБ ДИСК1
ДЛС КОНД В’0’
МЛ15 СЧ R1-В1(М7)
 СЛИ М17(М7)
 ИЛИ ЯЧМЛ+1-В1(М17)
 ЗП ЯЧМЛ+1-В1(М17) НОМЕР МАТЕМ.МЛ
 ПБ МЛ4
МЛ16 УИИ М16(М11)
 УИИ М17(М12)
 СЧ 0
 ЗП R1-В1(М7)
 УИА -11(М12)
 УИА ПЕР1-В4(М11)
 ПБ ЛА149А        НА ПЕРЕВОД В ВОСЬМ.НОМЕР
ПЕР1 УИИ М11(М16)
 УИИ М12(М17)
 НТЖ 0
 ПО МЛ20
 СДА 64+12
 ПЕ МЛ20          НОМЕР БОБИНЫ≥4096
 СЧ R1-В1(М7)
 УИА (М17)
 И Е12
 ПО БОБ МЛ
 СЧ R1-В1(М7)
 ВЧОБ NБЛ
 ПЕ БОБ МЛ
 УИА 1(М17)
БОБ СЧ R1-В1(М7)
 ИЛИ Е13           ПРИЗНАК БОБИНЫ
 СДА 64-33
 ЗП R1-В1(М7)
ДИСК1 СЧ МЛРВБЩ-В1(М7)
 УИ М16           НАПР.И МГ
 СЧ Е48-8(М16)
 ИЛИ УКАЗДВ-В1(М7)
 МОДА ФОРМШД
 ПИНО (М17)         ДИСК
 НТЖ Е48-8(М16)
ФОРМШД ЗП УКАЗДВ-В1(М7) ШКАЛА СООТВ.МЛ К МД
 ПБ МЛ4
МЛ18АР НОП
 СЧИ М11
 НТЖ Е1
 ПЕ МЛ18
 СЧ Е1
 ЗП ПРИЗАР
 УИИ М3(М2)        М3:=А(ИМЕНИ ОБЛАСТИ)
 СЧ Е26
 ЗП (М3)
 СЛИА 1(М2)
 СЧИ М2
 И П177
 ПО БОЛДОП
ИМЯАР СЛИА 1(М2)
 СЧИ М2
 И П177
 ПО БОЛДОП        МАС0 > 200
 СЧ Е25
 СЛЦ (М3)
 ЗП (М3)
 УИА -48(М6)
СИМИМ СЧ R-В1(М7)
 ВЧ =В’115’
 ПО МЛ20
 СЧ R-В1(М7)
 ВЧ Е6
 ПО ХОРАР
 СЧ =М33В’77745’  0-9 . /
 И Е48(М15)
 ПО КИМ
ХОРАР СЧ R-В1(М7)
 СЛИА 8(М6)
 СДА 64(М6)
 ИЛИ (М2)
 ЗП (М2)
 УИА ИМЯАР-В4(М13)
 МОДА ЗАСИМВ-В1(М7)
 ПИО (М6)
 УИА СИМИМ-В4(М13)
 ПБ ЗАСИМВ-В1(М7)
КИМ СЧ П377
 СДА 64+8(М6)
 ИЛИ (М2)
 ЗП (М2)
КИМ1 СЛИА -П’+’(М15)
 МОДА ИМЯПЛЮ
 ПИО (М15)
 СЛИА П’+’-П’-’(М15)
 МОДА КОНИМ
 ПИНО (М15)
 УИА ИМЯМИ1-В4(М13)
 ПБ ЗАСИМВ-В1(М7)
ИМЯМИ1 ВЧ =В’115’
 ПО МЛ20
 СЧ R-В1(М7)
 ВЧ Е6
 ПО ЗПБАЦ
 СЧ
ПЛЮМИН ИЛИ Е41
 ИЛИ (М3)
 ЗП (М3)
 УИА 2(М11)
 СЧ Е48
 ЗП R2-В1(М7)
ЧЗОН2 НОП
 СЧ R-В1(М7)
 ВЧ П12
 ПО НЕЦИФ
 СЧ R-В1(М7)
 И Е4
 ПЕ МЛ20 НЕ 8-Я ЦИФРА
 СЧ R2-В1(М7)
 СДА 64-3
 ИЛИ R-В1(М7)
 ЗП R2-В1(М7)
 УИА ЧЗОН2-В4(М13)
 ПБ ЗАСИМВ-В1(М7)
НЕЦИФ НОП
 СЛИА 1(М12)
 СЧ R2-В1(М7)
 ВЧ Е1
 ПЕ МЛ20 <1 ЗОНЫ
 ЗП R2-В1(М7)
 СДА 64+16
 ПЕ МЛ20 >200000 ЗОН
 СЧ R2-В1(М7)
 ИЛИ (М3)
 ЗП (М3)
 ПБ КИМ1
ЗПБАЦ СЧ
 ЗП R2-В1(М7)
ЗПБАЦ1 СЧ R2-В1(М7)
 СДА 64-8
 ИЛИ R-В1(М7)
 ЗП R2-В1(М7)
 УИА ЗПБАЦ2-В4(М13)
 ПБ ЗАСИМВ-В1(М7)
ЗПБАЦ2 ВЧ =В’115’
 ПО МЛ20
 СЧ R-В1(М7)
 ВЧ Е6
 ПО ЗПБАЦ1
 УИА -LДШБАЦ+1(М13)
ЗПБАЦ3 СЧ R2-В1(М7)
 МОДА LДШБАЦ-1(М13)
 НТЖ ДШБАЦ
 ПО ЗПБАЦ4
 МОДА ЗПБАЦ3
 ЦИКЛ (М13)
 ПБ МЛ20
ЗПБАЦ4 СЛИА LДШБАЦ(М13)
 СЧИ М13
 ВЧ П5
 ПО ЭТОЗП
 СЧ 0
ЗПБАЦ6 ИЛИ Е8-1(М13)
 ИЛИ ШКАНУВ-В1(М7)
 ЗП ШКАНУВ-В1(М7)
 СЧИ М13
 СДА 64-21
ЗПБАЦ7 ИЛИ Е40
 ИЛИ (М3)
 ЗП (М3)
 ПБ КИМ1
ЭТОЗП РЖА 7
 ПЕ ЗПДА
 СЛИА -1(М13)
 СЧ Е14
 ПБ ЗПБАЦ6
ЗПДА СЧ 0
 ПБ ЗПБАЦ7
БОЛДОП УИА 14(М16)
 ПБ МЛ21       ЛЕНТ > 12
ИМЯПЛЮ УИА ИМЯПЛ1-В4(М13)
 ПБ ЗАСИМВ-В1(М7)
ИМЯПЛ1 СЧ Е17
 ПБ ПЛЮМИН
КОНИМ СЛИА П’-’-П’)’(М15)
 МОДА МЛ20
 ПИНО (М15)
ФОРИМ1 НОП
 СЧ ДЛС
 ПЕ МЛ20          НА КУСКАХ НЕЛЬЗЯ
 СЧ МЛРВБЩ-В1(М7)
 СДА 64-42         НУ
 ИЛИ (М3)
 ЗП (М3)
 МОД МЛРВБЩ-В1(М7)
 СЧ Е48-’30’
 ИЛИ ШКАНУВ-В1(М7)
 ЗП ШКАНУВ-В1(М7)
 СЧ 0
 ЗП ПРИЗАР
 СЛИА 1(М2)
 СЧИ М2
 И П177
 ПО БОЛДОП        МАС0 > 200
 ПБ МЛ
МЛ18 СЧ R2-В1(М7)
 СДА 64-8
 ИЛИ R-В1(М7)
 ЗП R2-В1(М7)     ПРИФОРМ.БУКВЫ
 УИА МЛ17-В4(М13)
 ПБ ЗАСИМВ-В1(М7) ЗА СИМВОЛОМ
МЛ17 НТЖ =В’115’
 ПО МЛ20          ОШИБКА:НАДЧЕРК.
 СЧ R-В1(М7)
 СДА 64+5
 ПЕ МЛ18          СИМВОЛ-БУКВА
 СЧ R2-В1(М7)
 НТЖ СЗП
 ПО МЛ17А        ЕСЛИ ЗП
 СЧ R2-В1(М7)
 НТЖ СWR
 ПЕ МЛ20                НЕ WR, ОШИБКА
 ЗП R2-В1(М7)
МЛ17А СЧИ М11
 И Е2
 ПО МЛ20          МИНУСА НЕ БЫЛО
 СЧ Е48
 ПБ МЛ3А          ПРИЗН.ЗАПИСИ НА МЛ
ГЛАВ НОП
 ЗП ЯЧВСТ1-В1(М7)     ПОД ШИФР ГЛАВНОЙ ЗАДАЧИ
 СЧ ЯЧКАТ+ЯЧГЛАВ-В1(М7)
 ИЛИ ЕГЛАВ
 ЗП ЯЧКАТ+ЯЧГЛАВ-В1(М7)
 ПБ ЖДЕМВ    НА ВЫБОР ШИФРА
ЖДЕ ЗП ЯЧВСТ1-В1(М7) ПОД ШИФР ОЖИДАЕМОЙ ЗАДАЧИ
 СЧ ЯЧКАТ+6-В1(М7)
 ИЛИ Е32
 ЗП ЯЧКАТ+6-В1(М7)    ПРИЗ. "ЖДЕМ" УБИРАЕТСЯ В КЗ
ЖДЕМВ НОП    ВЫБОР ШИФРА
 УИА (М12)
 УИА ЖДЕМ-В4(М13)
 ПБ ЗАСИМВ-В1(М7) ЗА СИМВОЛОМ
ЖДЕМ ВЧ П12
 ПО ПАКЕТ1        НЕ ДЕСЯТИЧНАЯ ЦИФРА
 СЧ ЯЧВСТ1-В1(М7)
 СДА 64-4
 ИЛИ R-В1(М7)
 ЗП ЯЧВСТ1-В1(М7) ФОРМИР.ШИФРА ОЖИДАЕМОЙ ЗАДАЧИ
 СЛИА 1(М12)
 ПБ ЗАСИМВ-В1(М7)
ПАКЕТ1 СЧ R-В1(М7)
 НТЖ П16
 ПО ЖЧАС
 УИА 1(М16)
 СЧ ЯЧВСТ1-В1(М7)
 ПО ВЫДОШ1        НЕДОП.ШИФР
 НТЖ СВЯЗЬТ
 ПО ВЫДОШ1
 НТЖ СВЯЗЬТ
 СЛИА -12(М12)
 МОДА ЖДУШИФ
 ПИО (М12)         ЦИФР=12
 СЛИА 6(М12)
 МОДА ЖЧА
 ПИНО (М12)         СИМВ.В ШИФРЕ ≠6 И ≠12
 СДА 64-24
 ЗП ЯЧВСТ1-В1(М7)
ЖДУШИФ СЧ ШКАНУВ-В1(М7)
 ИЛИ Е16           ЗАДАЧА ЖДЕТ ШИФР
 ЗП ШКАНУВ-В1(М7)
 ПБ ЛА64
МЛ19 МОДА ЛА65
 ПИО (М11)         ОШИБОК НЕТ
МЛ20 УИА 3(М16)        НОМ.ТЕКСТА:НЕДОПУСТ.СИМВОЛ
МЛ21 УИА МЛ22-В4(М13)
 ПБ ВЫДОШ-В1(М7)  НА ВЫДАЧУ ОШИБКИ
МЛ22 СЛИА -’23’(М15)
 МОДА МЛ
 ПИО (М15)         ВСТРЕТ.)
 НТЖ =В’115’
 ПЕ ЗАСИМВ-В1(М7)
 ПБ ЛА65          ПРОПУСТ ДО НАДЧЕРК.
ЗАТЕЛЕ СЧ R2-В1(М7)
 СДА 64-33
 ИЛИ ЯЧОТЛ-В1(М7)
 ЗП ЯЧОТЛ-В1(М7)  А БУФЕРА ДЛЯ ОТЛАДЧИКА
ПРОВКН СЧИ М15
 НТЖ =В’115’
 ПЕ ЛА88А         ЧУЖОЙ СИМВОЛ В РАЗДЕЛЕ
 ПБ ЛА64
ЖЧАС УИА 100(М12)
 ПБ ЗАСИМВ-В1(М7)
ЖЧА СЧИ М12
 СДА 64+7
 ПЕ ВЫДОШ1
 СЧ ЯЧВСТ1-В1(М7)
 И Е48П25
 ПЕ ВЫДОШ1
ЛА64 УИА ЛА64-В4(М13)
 СЛИА -’115’(М15)
 МОДА ЗАСИМВ-В1(М7)
 ПИНО (М15)         ПРОПУСК ДО НАДЧЕРК.
ЛА65 УИА ЛА65Б-В4(М13)
 ПБ ЗАСИМВ-В1(М7) ЗА СИМВОЛОМ
ФОРМЕ СЧ ЯЧВСТ-В1(М7)
 И Е15
 ПО НЕСТП         НЕТ СТПАСП
 НТЖ ЯЧВСТ-В1(М7)
 ЗП ЯЧВСТ-В1(М7)
 СЧИ М4            СОХР.М4 ДЛЯ СТПАСП
 СДА 64-33
 ИЛИ СТПАСП-В1(М7)
 ЗП СТПАСП-В1(М7)
 СЧ ПОЛЕПК-В1(М7)
 УИ М4
 ПБ ЛА65
НЕСТП СЧ ШКЛИСТ-В1(М7)
 И Е48П17
 ЧЕД 0             ’ЛИС’
 ЗП R1-В1(М7)
 СЧ ШКЛИСТ-В1(М7)
 И П7777          ОЗУ
 ВЧ R1-В1(М7)
 ПО НЕСТП1
 СЧ 0
НЕСТП1 СЛ R1-В1(М7)     МАХ(ЛИС,ОЗУ)
 НТЖ 0
 ПО ВЫДОШ2        ОЗУ=0
 ВЧ Е1
 ЗП R1-В1(М7)     ЧИСЛО ЛИСТОВ-1
 ИЛИ ЯЧКАТ+6-В1(М7)
 ЗП ЯЧКАТ+6-В1(М7) ЧИСЛО ЛИСТОВ -1
 СЧ ЯЧКАТ-В1(М7)
 СДА 64+20
 И П777
 ЗП R2-В1(М7)     ЧИСЛО ТРАКТОВ
 СЧ
 ЗП ШКЛИСТ-В1(М7)
 СЧ ЯЧОТЛ-В1(М7)
 СДА 64+33
 ПО НЕФОАЛ
 СДА 64+10
 УИ М17
 УИА НЕСТП2-В4(М16)
 ПБ ПРОЗУ-В1(М7)
НЕСТП2 ПО  НЕФОАЛ БУФ ОТЛ НЕ В ЧУЖ ЛИСТЕ
 УИА 5(М16)
 УИА НЕФОАЛ-В4(М13)
 ПБ ВЫДОШ-В1(М7)  НА ВЫДАЧУ ОШИБКИ
НЕФОАЛ СЧ ЯЧРОСП-В1(М7)
 И П77777
 ПО ВЫДОШ2        А ВХ=0
 СДА 64+10
 УИ М17
 УИА НЕСТП3-В4(М16)
 ПБ ПРОЗУ-В1(М7)
НЕСТП3 ПО ВЫХОД АДР ВХ НЕ В ЧУЖ ЛИСТЕ
ВЫДОШ2 УИА 5(М16)
ВЫДОШ3 УИА ВЫХОД-В4(М13)
 ПБ ВЫДОШ-В1(М7)  НА ВЫДАЧУ ОШИБКИ
ТЕЛЕ СЧ ЯЧКАТ+6-В1(М7)
 ИЛИ Е32           ПРИЗ.ЖДЕТ
 ЗП ЯЧКАТ+6-В1(М7)
 И Е28
 УИА 17(М16)
 ПЕ ВЫДОШ3        ’ТЕЛ’ ПОСЛЕ ’БЛО’
 СЧ СВЯЗЬТ
 ЗП ЯЧВСТ1-В1(М7) ПРИЗН.ТЕЛЕГР.КАНАЛОВ:ОЖИД.СВЯЗИ
 СЧ ЯЧОТЛ-В1(М7)
 СДА 64-15
 СДА 64+15
 ЗП ЯЧОТЛ-В1(М7)  ОЧ.СТАРОГО А БУФ.ОТЛАДЧИКА
 СЧ R-В1(М7)
 НТЖ =В’115’
 ПО ПРОВКН        НЕТ ИНФ.
 УИА -5(М12)
 УИА АТЕЛЕ-В4(М13)
 ПБ ЗАСИМВ-В1(М7)
АТЕЛЕ СДА 64+3          ФОРМ.АБУФ ДЛЯ ОТЛАДЧИКА
 ПЕ ЗАТЕЛЕ        НЕ ЦИФРА
 СЧ R2-В1(М7)
 СДА 64-3
 ИЛИ R-В1(М7)
 ЗП R2-В1(М7)
 МОДА ЗАСИМВ-В1(М7)
 ЦИКЛ (М12)
 УИА 5(М16)        >5 СИМВ.В АДРЕСЕ
 ПБ ВЫДОШ1
ПРИЗ СЧ R2-В1(М7)
 СДА 64-3
ПРИЗ1 ИЛИ R-В1(М7)
 ЗП R2-В1(М7)     УПАКОВ.СИМВ.
 МОДА ЗАСИМВ-В1(М7)
 ЦИКЛ (М12)         НА 2 СИМВ.
 ПБ ЛА88А         ВЫД.ОШИБКИ
ВЫХОД УИА 14(М16)       ЧИСЛО РЕСУРСОВ>ДОПУСТИМОГО
 СЧ ПРЕДЕЛ
 И П777
 ВЧ R2-В1(М7)
 ПЕ ВЫХ           ЧИСЛО ТРАКТОВ>ДОПУСТИМОГО
 СЧ МД5
 ВЧ Р3
 ПЕ ВЫХ ЧИСЛО КУСКОВ>ДОПУСТИМ
 СЧ Р3
 СДА 64-10
 ИЛИ ЯЧКАТ+7-В1(М7)
 ЗП ЯЧКАТ+7-В1(М7)
 СЧ МАХОЗУ
 ВЧ Е1
 ВЧ R1-В1(М7)
 ПО ВЫХ1
ВЫХ УИА ВЫХ1-В4(М13)  ТРАКТОВ ИЛИ ЛИСТОВ>ДОПУСТИМОГО
 ПБ ВЫДОШ-В1(М7)  НА ВЫДАЧУ ОШИБКИ
ВЫХ1 СЧ ЯЧКАТ+5-В1(М7)
 СДА 64+16
 И П377
 ПЕ ВЫХОД1        МЕТРЫ АЦПУ ЗАДАНЫ В ПАСПОРТЕ
 СЧ ШКАНУВ-В1(М7)
 И Е15
 ПЕ ВЫХОД1
 СЧ П7
 СДА 64-16
 ИЛИ ЯЧКАТ+5-В1(М7)
 ЗП ЯЧКАТ+5-В1(М7) 7 М.БУМАГИ-СТАНДАРТ
ВЫХОД1 НОП
 МОД НОМЕРЗ-В1(М7)
 СЧ ТУС-1
 УИ М16
 РЖА ’13’
 ПЕ ВЫХОД2
 СЧ ЯЧФГЛ(М16)
 И ЕФГЛ
 ПО ВЫХОД2      НЕ ФОРМ.С ГЛАВНОЙ
 СЧ И(М16)
 СДА 64+21
 И П77
 ПО ШИФЯ     НЕТ ГЛАВНОЙ
 УИ М16       НОМЕР ГЛАВНОЙ
 СЧ ТУСП-1(М16)
 УИ М16
ШИФЯ СЧ ШИФРМ(М16)
 ЗП ЯЧВСТ1-В1(М7)
 СЧ ЯЧКАТ+ЯЧГЛАВ-В1(М7)
 ИЛИ ЕГЛАВ
 ЗП ЯЧКАТ+ЯЧГЛАВ-В1(М7)
 СЧ ЯЧКАТ+6-В1(М7)
 ИЛИ Е32
 НТЖ Е32
 ЗП ЯЧКАТ+6-В1(М7)
 СЧ ЯЧОТЛ-В1(М7)
 ИЛИ Е32
 ЗП ЯЧОТЛ-В1(М7)     + "ИНК"
ВЫХОД2 НОП
 СЧ ЯЧВСТ-В1(М7)
 СЛЦ Е8
 ЗП ЯЧВСТ-В1(М7)  НОМЕР МАССИВА+1
ЛА160Г СЧ ЯЧКАТ-В1(М7)
 СДА 64-28
 ПЕ ЛА160Д        ВРЕМЯ ЗАКАЗАНО
 УИА 18(М16)       НЕТ ВРЕМЕНИ
ЕУЧЕ1 НОП ____
 УИА ЛА160Д-В4(М13)
 ПБ ВЫДОШ-В1(М7)  НА ВЫДАЧУ ОШИБКИ
ЛА160Д УИА 768(М3)
 СЛИ М3(М7)
 СЧ КЛПУСК
 ЗП КЛМ0(М3)
 УИА ЛА172-В6(М13)
 УИА НАЧАЛО-В6(М16)
 СЛИ М16(М1)       ВХОД ПОСЛЕ ВЫЗОВА В6В7
 ПБ ВЫЗВ6А-В1(М7) НА ВЫЗОВ В6В7
ЛА149 СЧ R1-В1(М7)     ПЕРЕВОД В ВОСЬМ.ЧИСЛА ДВОИЧНО-ДЕСЯТ.
 УМН С1
 СЧМР 64
 ЗП R1-В1(М7)
ЛА149А СЧ R2-В1(М7)
 СДА 64-4
 ЗП R2-В1(М7)
 СЧМР
 СЛЦ R1-В1(М7)
 ЗП R1-В1(М7)
 МОДА ЛА149
 ЦИКЛ (М12)         НА 12 ЦИФР
 МОДА (М1)
 ПБ (М11)         ВЫХОД
ЛА150 СДА 64+7
 ПО ЛА150А         <128 М
 СЧ Е8
 ЗП R1-В1(М7)      128 МЕТРОВ
ЛА150А СЧ ЯЧКАТ+5-В1(М7)
 И О23П17
 ЗП ЯЧКАТ+5-В1(М7)
 СЧ R1-В1(М7)
 ПО АЦПУ0
 СДА 64-16
 ИЛИ ЯЧКАТ+5-В1(М7)
 ЗП ЯЧКАТ+5-В1(М7)
 СЧ ШКАНУВ-В1(М7)
 И Е9   - ПРИЗНАК УСТ. БАЦ
 ПЕ ПРОВКН
 СЧ ВСЕЕД
 НТЖ Е15
 И ШКАНУВ-В1(М7)
 ПБ АЦПВ
АЦПУ0 СЧ Е15
 ИЛИ ШКАНУВ-В1(М7)
АЦПВ НОП 0
 ЗП ШКАНУВ-В1(М7)
 ПБ ПРОВКН
ВРЕМПР КОНД М47В’1’В’62’
 КОНД М47В’1’В’764’
 КОНД М47В’1’В’5670’
 КОНД М47В’1’В’72460’
 КОНД М47В’1’В’537440’
 КОНД М47В’1’В’6673500’
ГРИФ ЗП ЯЧГРИФ-В1(М7)
 СЧ ЯЧОТЛ-В1(М7)
 ИЛИ Е31
 ЗП ЯЧОТЛ-В1(М7)  НЕ ПОДЛЕЖИТ УЧЕТУ
 СЧ R-В1(М7)
 НТЖ =В’115’
 ПО ЛА65          НЕТ ИНФ.К РАЗДЕЛУ ГРИФ
 УИА -4(М12)
 УИА ГРИФЦ-В4(М13)
 ПБ ЗАСИМВ-В1(М7)
ГРИФЦ ВЧ П12
 ПО ГРНЕЦ         НЕ ЦИФРА
 СЧ ЯЧГРИФ-В1(М7)
 СДА 64-4
 ИЛИ R-В1(М7)
 ЗП ЯЧГРИФ-В1(М7)
ГРИФЦ1 МОДА ЗАСИМВ-В1(М7)
 ЦИКЛ (М12)
 ПБ ЛА88А         ОШИБКА:ЧИСЛО ЦИФР>4
ГРНЕЦ НТЖ Е1
 ПЕ ЛА88А         НЕ МИНУС
 УИА ТИПЗ-В4(М13)
 ПБ ЗАСИМВ-В1(М7)
ТИПЗ СЧ ЯЧГРИФ-В1(М7)
 СДА 64-8
 ИЛИ R-В1(М7)
 СДА 64-24
 ЗП ЯЧГРИФ-В1(М7) ТИП ЗАДАЧИ
 УИА -3(М12)
 УИА ТИПМ-В4(М13)
 ПБ ЗАСИМВ-В1(М7)
ТИПМ НТЖ П13
 ПЕ ЛА88А         НЕ МИНУС
 СЧ ПРОБ6
 ЗП R1-В1(М7)
 УИА ГРИФС-В4(М13)
 ПБ ЗАСИМВ-В1(М7)
ГРИФС НТЖ =В’115’
 ПО КГРИФ         КОНЕЦ ИНФОРМАЦИИ К РАЗДЕЛУ ГРИФ
 СЧ R1-В1(М7)
 СДА 64-8
 ИЛИ R-В1(М7)
 ЗП R1-В1(М7)
 ПБ ГРИФЦ1        ЗА СИМВОЛОМ
КГРИФ СЧ R1-В1(М7)
 НТЖ ПРОБ6
 ПО ЛА88А         НЕТ ТИПА ГРИФА
 СЧ R1-В1(М7)
 И Е1П24
 ИЛИ ЯЧГРИФ-В1(М7)
 ЗП ЯЧГРИФ-В1(М7) ТИП ГРИФА
 СЧ ЯЧОТЛ-В1(М7)
 ИЛИ Е31
 НТЖ Е31
 ЗП ЯЧОТЛ-В1(М7)  С ГРИФ ПОДЛЕЖИТ УЧЕТУ
 ПБ ЛА65
ПОВТ СЧ ЯЧКАТ+6-В1(М7)
 НТЖ Е30
 ЗП ЯЧКАТ+6-В1(М7) ПРИЗНАК ПОВТОРА ЗАДАЧИ ПРИ СБОЕ ЭВМ
 ПБ ЛА65
ФИЗ УИА -2(М12)
 УИА ФИЗО-В4(М13)
 ПБ ЗАСИМВ-В1(М7)
ФИЗО НТЖ =В’115’
 ПО ЛА88А
 СЧ R-В1(М7)
 ВЧ П3
 ПО ЗАСИМВ-В1(М7) ПРОПУСК ДО ЦИФРЫ
 СЧ R2-В1(М7)
 УИА ФИЗО1-В4(М13)
 ПБ ПРИЗ1
ИНКО СЧ ЯЧОТЛ-В1(М7)
 НТЖ Е32
 ЗП ЯЧОТЛ-В1(М7)  ПРИЗ.ИНКО
 ПБ ПРОВКН
ФИЗО1 СДА 64+3
 ПО ПРИЗ          ВОСЬМЕР.ЦИФРА
 СЧ R2-В1(М7)
 ПО ЛА88А         ПАРАМ.=0
 И Е48
 ПЕ ЛА88А         <2 ЦИФР
 СЧ =М21В’37’В’-1’
 И ЯЧОТЛ-В1(М7)
 ЗП ЯЧОТЛ-В1(М7)
 СЧ R2-В1(М7)
 СДА 64-21
 ИЛИ ЯЧОТЛ-В1(М7)
 ЗП ЯЧОТЛ-В1(М7)  НУ С 22Р.
 ПБ ПРОВКН
СПЕЦ СЧ ЯЧОТЛ-В1(М7)
 НТЖ Е27
 ЗП ЯЧОТЛ-В1(М7)  ПРИЗ.СПЕЦ
 ПБ ЛА65
ПРОБ6 КОНД Х’ФАФА’
ОТЭКФ УИ М17
 МОД ТУС-1(М17)
 СЧ 0
 ЗП АВЫБОР-В1К(М7)
 УИ М16
 СЧ ТУС-1(М17)
 И Е48
 ПЕ НЕПУСК
 СЧ -1(М16)
 НТЖ ПУСКАЧ
 ПЕ НЕПУСК
 СЧ -2(М16)
 ЗП КЛПУСК
НЕПУСК НОП
 МОДА ЧУЖЛИС-В1К(М7)
 УИА (М15)
 СЧИ М15
 СДА 64-24
 ЗП ТИПУСТ-В1К(М7)
 СДА 64+24
 ПВ ЗАПВЫХ(М15)
 СЧ ТУС-1(М17)
 УИ М17          М17 := АДР.ИПЗ
 И Е48
 ПО ВЫБФОР-В1К(М7)
 СЧ АВЫБОР-В1К(М7)
 СДА 64+39
 НТЖ П772-В1К(М7)
 ПЕ НИКМБ
 ЗП ПОЛЕПК+7-В1К(М7)
 СЧ АВЫБОР-В1К(М7)
 СДА 64+36
 И П7
 ПО ЧУЖЛИС-В1К(М7)
 НТЖ П7
 ПО ЧУЖЛИС-В1К(М7)
 СЧ АВЫБОР-В1К(М7)
 СЛЦ Е37
 И Е39
 ПЕ ФОРМД
 СЧ АВЫБОР-В1К(М7)
 СБР КСБРМБ
ИЗФМД ЗП ПОЛЕПК+4-В1К(М7)
 СЧ АВЫБОР-В1К(М7)
 СДА 64+15
 И П77
 СДА 64-38
 СЛЦ ПОЛЕПК+4-В1К(М7)
 ЗП ПОЛЕПК+5-В1К(М7)
 И Е1
 ПЕ ЧУЖЛИС-В1К(М7)
 СЧ 9(М17)
 И П1777
 ЗП СДВИГ-В1К(М7)
 СЧ П1777
 ЗП АВЫБОР-В1К(М7)
 СЧ СТВУ-В1К(М7)
 ИЛИ Е26
 ЗП СТВУ-В1К(М7)
НИКМБ НОП
 УИА 1024
 СЧ (М16)
 УИА 3
 ЗП РЯВЫБ-В1(М7)  СЛОВО МАССИВА
 СДА 64+24
 НТЖ ПТК-В1(М7)
 ПЕ НИКМБ1-В1(М7)
 СЧ СТВУ-В1(М7)
 ИЛИ Е28            ПРИЗ.РЕДАКТИРОВАНИЯ
 ЗП СТВУ-В1(М7)
 СЧ РЯВЫБ-В1(М7)
 И П77777
 ЗП АПЕРЕД-В1(М7)  ДЛЯ ОШИБОК ВВОДА
 СЧ РЯВЫБ-В1(М7)
 СДА 64+16
 И П377
 УИ М16
 ПО ФОРС2-В1(М7)  НЕТ ТЕРМ.
 ВЧОБ П60
 ПЕ ФОРС2-В1(М7)
 СЧ ШКАС
 ИЛИ ШКУСТР
 И Е48-1(М16)
 ПО ФОРС2-В1(М7)
 И ШКАС
 ПЕ ОТАС
 СЧ ГОД
 СДА 64-45
 ИЛИ ЯЧКАТ+7-В1(М7)
 ЗП ЯЧКАТ+7-В1(М7)
 СЧИ М16
 ПБ ОТАС1
ОТАС СЧ ТСЛ-1(М16)
 СДА 64+АТ
 ИЛИ Е9
ОТАС1 СДА 64-29
 ИЛИ ЯЧКАТ-В1(М7)
 ЗП ЯЧКАТ-В1(М7)
 ПБ ФОРС2-В1(М7)
ФОРМД НОП
 СЧ АВЫБОР-В1К(М7)
 СДА 64+33
 НТЖ СЕГМБИ(М17)
 И П77
 ПЕ НЕСЕГБ
 СЧ АВЫБОР-В1К(М7)
 СДА 64+21
 И П3777
 ЗП ПОЛЕПК+4-В1К(М7) НАЧ.ЗОНА
 СДА 64+8
 ПЕ ЧУЖЛИС-В1К(М7)
 СЧ П3
 СДА 64-8
 ИЛИ ПОЛЕПК+4-В1К(М7)
 СДА 64-38
 ПБ ИЗФМД
НЕСЕГБ НОП
 СЧ АВЫБОР-В1К(М7)
 СДА 64-17
 И М38В17
 ПБ ИЗФМД
М38В17 КОНД М38В’1777’
ПУСКАЧ КОНД П’ПУСКАЧ’
КЛПУСК КОНД В’0’
 ЛИТЕР
Е33П48 КОНД М32Х’ФФФФ’
КМЛ КОНД М32В’137777’
 КОНД М16В’137777’
 КОНД В’137777’
ДШБАЦ НОП
 КОНД П’БПЛ’      1
 КОНД П’БГП’      2
    КОНД П’БПИ’      3
    КОНД П’БАЦ’      4
    КОНД П’БАЦА’     5
СЗП КОНД П’ЗП’       6
СWR КОНД П’WR’       7
LДШБАЦ ЭКВ *-ДШБАЦ
П167 КОНД В’167’
СВЯЗЬТ КОНД Х’999999999999’
И14 ПАМ 1
ПРИЗАР ПАМ 1
ТИПАНУ ПАМ 1
О23П17 КОНД В’-1’М16В’377’
П140 КОНД В’140’
ДРОМБ КОНД В’32’
КСБРМБ КОНД М21В’370037’
 М
СОСТАВ ВНЕШ (СЕГМБИ,СЕГМБ),ЕПЗФ
ДИСП70 ВНЕШ ТАЙМЕР,ТБУФ13,ТУС,П37,П377,ВСЕЕД
СПЕЦШ КОНД Х’131109021952’
 КОНД Х’001010871А40’
В1К ВНЕШ СОИ
В1К ВНЕШБ ОТКРМ
 Е
ДИСКИ ВНЕШ Е16П1,П11
ДИСП70 ВНЕШ АВОСТА,П60,ТУСП,ЯЧЗФ,ЕЗФ
В1К ВНЕШ ОСВЗ
В1К ВНЕШ ВОСЬМИ,ЯЧМЛ,ЯЧАВОС,ЯЧРОСП,ЯЧФИО,ЯЧОТЛ
В1К ВНЕШ ЯЧГРИФ,УКАЗДВ,РЯСЛК,УСТ4,ВЫЗВ6А,БУФПР,ИНВ3В4
В1К ВНЕШ НОМБАС,ДЛЗМ,ТИПУСТ,ЗАСИМВ,(В1,В1К)
В1К ВНЕШ R,В1К,СДВИГ,(КУССЛ,ИНФСЛК),ШКАНУВ,ВЫДОШ,ЯЧВСТ
В1К ВНЕШ СЧСИМ,ЯЧКАТ,ШКЛИСТ,ВЫБ1,ПРОЗУ
В1К ВНЕШ НОМЕРЗ,ПОЛЕПК,СТВУ,СТПАСП,МЛРВБЩ,ЯЧВСТ1,УСТ,R2,R1
В1К ВНЕШ ЧУЖЛИС,ШИФР,ПОВТЗП,(ИНФСЛК,РЯСЛК)
В1К ВНЕШ ДАЙБУФ
В1К ВНЕШ ВЫБФОР,АВЫБОР,П772,РЯВЫБ,ПТК,НИКМБ1,АПЕРЕД,ФОРС2
В1К ВНЕШ СЧСИМК
ДИСП70 ВНЕШ ИЗ10В8,(П12R,R),ЯЧВ1,П1777,НОММЛ1,П177,НОММБ,Е24П1
ДИСП70 ВНЕШ Е16П32,ПРЕДЕЛ,(С1,R),ГОД,ТСЛ,Е48П43,Р3,Р4,П27,МД5
ДИСП70 ВНЕШ Е44П42,(Е1П24,Е24П1),Е48П25,Е42П1,НОММБ1,П7,ЗАХЗАН
ДИСП70 ВНЕШ П77,П777,(П13,ПР13),П17,П12,Е46П48,РЯВВ1,П77777
ДИСП70 ВНЕШ НУСВУ
ДИСП70 ВНЕШ П5,П30,П3,ТБУФ,ВРЕМЯ,П3777
ДИСП70 ВНЕШ NБЛ,ЕЗП,МАХОЗУ,П7777
СОСТАВ ВНЕШ ЯЧЗПЕР,ЕЗПЕР,АТ
КАЧКА ВНЕШ П16,Е48П17
МОТТ ВНЕШ ШКАС,ШКУСТР
ВЗУ ВНЕШ ФИЗОБМ
КИТ ВНЕШ Е48П41
В6В7 ВНЕШ НАЧАЛО,ЛА172,(В6,В6В7)
БОЭК1 ВНЕШ ЗАПВЫХ
ОСА ВНЕШ ЯЧВСС,ОСА
 ЭКВИВ (НАЧИНЗ,0),(РЯП,ВОЗВР),(РЯ4,ВОЗВР+5),(РЯ3,ВОЗВР+6)
 ЭКВИВ (В4,В3В4),(ШКАНУ,27),(КЛМ0,28)
 ЭКВИВ (СТПМ0,29),(УПРМ0,30),(ГОДМ0,31),(ВРЕМ0,32)
 ЭКВИВ (ПЕЧМ0,33),(LПЕЧ,5)  МАССИВ ПЕЧАТИ 5 ЯЧ.
 ЭКВИВ (НАЧLМ0,ПЕЧМ0+LПЕЧ),(ИМЕНА,НАЧLМ0)
 ВХОД ГОДМ0,ВРЕМ0,ПЕЧМ0,LПЕЧ
КИТ ВНЕШ (Е25П32,Е32П25)
СОСТАВ ВНЕШ ЯЧСЛУЖ,ЕСЛУЖ,ШИФРМ,ЯЧГЛАВ,ЕГЛАВ,ЯЧФГЛ,ЕФГЛ
В1К ВНЕШ АВТОР
ДИСП70 ВНЕШ И
 ВХОД СТПМ0,УПРМ0
 ВХОД ИМЕНА,ШКАНУ,ПРИХ,КЛМ0
 ФИНИШ
*ЧП
         ФИНИШ ,             В ВАШЕМ ФИНИШЕ ОШИБКА ИЛИ ЕГО НЕТ

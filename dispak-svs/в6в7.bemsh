В6В7 СТАРТ 4096
 Б
 М
 Е
 УПОТР В6В7(М1)
ВУВВД НОП
 РЖА 3
 СЧ ПОРСЧТ
 ЗП ПОРВВО
 СЧ МГРП
 И Е16
 ПО РЕЖСЧТ
 СЧ ПОРОТЛ
 ЗП ПОРВВО
РЕЖСЧТ НОП ,
 СЧИ М14
 СДА 64-2
 ИЛИ ИНФЗОН
 СЛЦ НОММЛ1
 ЗП РЯ            КУС ШИФРОВ
 СЛЦ ЗОН710 РАЗНОСТЬ МЕЖДУ 710-545
 ЗП РЯ2 КУС ЧТ. КАТ. ЛИМ.
 СЧ ГОД
 И П7
 НТЖ ШИФР-В1(М7)
 НТЖ ЕАРХ
 ПЕ НЕАРХ
 СЧ ЯЧКАТ+6-В1(М7)
 ИЛИ Е26
 ПБ ЗПК
НЕАРХ НОП ,
 СЧ СОИ-В1(М7)
 ПО НЕШВФ
 СЧ СПЕЦКГ
 ЗП ППР
 ПБ НЕРАБ
НЕШВФ НОП ,
 СЧ ПРЕДЕЛ
 И Е32
 ПО НЕРАБ   НЕ РАБОТ. СБ. С.
 АIF (NОТ ∧АС).АС21
 СЧ ШИФР-В1(М7)
 НТЖ ПКОЛЯ
 ПЕ ПОВТОР
 СЧ ЯЧКАТ+7-В1(М7)
 ИЛИ Е44П42
 ЗП ЯЧКАТ+7-В1(М7)
 ПБ НЕРАБ
ПКОЛЯ КОНД Х’419999000100’
ПОВТОР СЧ РЯ2
 ПВ ФИЗОБМ(М16)
 УИА 3
 СЧ ШИФР-В1(М7)
 СБР МАСКО(М14)
 ЗП РЯ2
 НТЖ =М40Х’41’
 ПО ОТД2
 СЧ РЯ2
 И =М44В’17’
 ПО ОТД2
 НТЖ =М44Х’9’
 ПО ОТД2
 НТЖ =М44Х’9’
 ПБ ОТД1
ОТД2 СЧ РЯ2
ОТД1 СДА 64+40
 ПВ ИЗ10В8(М15)
 ЗП РЯ2
 СДА 64-2
 УИ М15
 СЛИ М15(М14)
 АGО .АС20
.АС21 АNОР
 СЧ РЯ2
 ПВ ФИЗОБМ(М16) ЧИТАЕМ КАТ. ЛИМ.
 УИА 3
 СЧ ШИФР-В1(М7)
 НТЖ ПКОЛЯ(М14)
 ПО НЕРАБ
 СЧ ШИФР-В1(М7)
 СБР МАСКО(М14) МАСКА ДЛЯ ОПРЕД.ОТД.
 СДА 64+40
 ПВ ИЗ10В8(М15)
 ЗП РЯ2
 СДА 64-2
 УИ М15
 СЛИ М15(М14)
 СЧ СМНАОТ(М15)
 ЗП БУМАГА
 И Е25
 ПО НЕРА  ОТД. НЕ РАБ.С БЮДЖЕТОМ
.АС20 АNОР
 СЧ РЯ2
 СДА 64-15
 И НОМО  ВЫД. ИМЕННО НОМЕР
 ИЛИ ЯЧКАТ+7-В1(М7)
 ЗП ЯЧКАТ+7-В1(М7)
 АIF (∧АС).АС18
 СЧ ШИФР-В1(М7)
  СБР МАСКД(М14)  МАСКА ДЕЖУРНОЙ
 НТЖ ВА99(М14)
 УИА 1(М17)
 ПО ЗАНОМ  ДА ЗАДАЧА ДЕЖУРН.
.АС18 АNОР
 АIF (NОТ ∧АС).АС19
ДТ ЭКВ 179
 АGО .АС17
.АС19 АNОР
 ЭКВИВ (ДТ,180),(ПКОЛЯ,’640’)
.АС17 АNОР
 УИА 1-ДТ(М15)
 УИА 3(М17)   ПРИЗ.ЭКСПР
СТРОКА СЧ ШИФР-В1(М7)
 И МАСКЭ(М14)  МАСКА ЭКСПР.ЗАДАЧИ
 МОДА (М14)
 НТЖ ТБШЭЗ+ДТ-1(М15)
 ПО ДАЭКС    ЭКСПРЕС
 МОДА СТРОКА
 ЦИКЛ (М15)
 АIF (NОТ ∧АС).АС13
 УИА -63(М15)
СВЕРХЭ СЧ ШИФР-В1(М7)
 И МАСКЭ(М14)
 МОДА (М14)
 НТЖ ЭКСВЫС+63(М15)
 ПО ДАВЫЭК
 МОДА СВЕРХЭ
 ЦИКЛ (М15)
.АС13 АNОР
 УИА 2(М17)   ЛИМИТ
ДАЭКС НОП
 АIF (∧АС).АС14
ЗАНОМ СЧ ШИФР-В1(М7)
 СБР МАСКИ(М14)
 НТЖ ВА98(М14)
 ПЕ ЗАНОМ1
 СЧ ЯЧКАТ-В1(М7)
 И =М9В’3777’
 УИА -63(М15)
 ПЕ НЕШТР
.АС14 АNОР
ЗАНОМ1 НОП 0
 СЧ ЯЧКАТ+6-В1(М7)
 ИЛИ Е45(М17)
 ИЛИ Е27 БЛОКИРОВ.
 ЗП ЯЧКАТ+6-В1(М7)
 АIF (∧АС).АС12
 СЧ БУМАГА
 И Е24П1
 ЗП РЯ2
 СЧ ЯЧКАТ+5-В1(М7)
 СДА 64+16
 И П377
 УМН К144-В1(М7)
 СЧМР 64
 ВЧОБ РЯ2
 УИА 20(М16)   НЕТ БУМ.В БЮДЖ
 ПЕ НЕТМА
 УИА (М16)
 УИА -31(М15)      ДЛЯ ПОИСКА ЗАМОР.
СТРОК СЧ ШИФР-В1(М7)
 И МАСКАЗ(М14)  МАСКА ЗАМОР.ЗАДАЧИ
 МОДА (М14)
 НТЖ ЗАМОР(М16) ИЩЕМ ЗАМОР.
 ПО ДАЗАМ
 СЛИА 2(М16)
 МОДА СТРОК
 ЦИКЛ (М15)
.АС12 АNОР
 СЧ НЕИЩ(М14)
 И Е43
 УИА -63(М15)
 АIF (NОТ ∧АС).АС10
 ПО НЕРАБ
 АGО .АС9
.АС10 АNОР
 ПО НЕШТР
 СЧ Е28(М17)
 И БУМАГА
 ПЕ НЕШТР   НЕ ПРОВ.ВРЕМЯ
.АС9 АNОР
 СЛИ М17(М14)
 СЧ ВРЕД-1(М17)
 И Е24П1
 ЗП РЯ2
 УИА 0(М16)
 ВЧ ВРЕМЯ
 ПЕ ПРО
 СЛИА 1(М16)
ПРО СЧ ВРЕД-1(М17)
 СДА 64+24
 ЗП РЯ3
 ВЧ ВРЕМЯ
 ПЕ ПРОК
 СЛИА 2(М16)
ПРОК СЧ РЯ2
 ВЧ РЯ3
 ПЕ ХОДЫ
 СЧИ М16
 НТЖ Е1
 АIF (NОТ ∧АС).АС08
 ПО НЕВКЛ
 АGО .АС8
.АС08 АNОР
 ПО НЕТОВР
НЕШТР СЧ ШИФР-В1(М7)
 И МАСКШ(М14)
 МОДА (М14)
 НТЖ ШТРАФ+63(М15)
 ПО НЕРБ
 МОДА НЕШТР
 ЦИКЛ (М15)
.АС8 АNОР
НЕРАБ СЧ РЯ
 ПВ ЕNQ(М15)      ЗАХВАТ ДИСКОВ
ПОВТШ СЧ РЯ
 ПВ ФИЗОБМ(М16)   ЧТ.ШИФРОВ ЗАДАЧ
 СЧ РЯ
 СЛЦ Е25
 ЗП РЯ2
 УИА -126(М6)
 СЧ ЯЧВСТ1-В1(М7)
 НТЖ ЖДЕМТТ
 УИА 3
 УИА (М13)
 ПО ТЕ1           ТЕРМИНАЛЬНАЯ ЗАДАЧА
 СЧ НОМЕРЗ-В1(М7)
 ПО НЕТЕЛЕ
 НТЖ П30
 ПО ТЕ            ФОРМ.ОТ ПР
 СЧ ЯЧКАТ+6-В1(М7)
 И Е26
 ПЕ ТЕ
НЕТЕЛЕ СЧ СТПАСП-В1(М7)
 И Е31
 ПО НТ
 УИА -126(М16)
НЕТЕ СЧ ШИФР-В1(М7)
 МОДА (М14)
 НТЖ 127(М16)
 ПО ЕСТЬЗД
 СЧ ШИФР-В1(М7)
 МОДА 256(М14)
 НТЖ 127(М16)
 ПО ЕСТЬЗД
 МОДА НЕТЕ
 ЦИКЛ (М16)
НТ ПАМ
 СЧ ЯЧКАТ+7-В1(М7)
 И Е44П42
 НТЖ Е44П42
 ПО НЕТ
 СЧ ППР
 ПЕ ВЕР1
 УИА -64(М16)
ПЛАН МОДА КПЛАН(М14)
 СЧ 64(М16)
 ПО ПЛА
 НТЖ ШИФР-В1(М7)
 ПО НАШЛИ
 ЗП РЯ4
 СЧ Е46П48
 ИЛИ Е45
СЛЦШ ЗП РЯ3
 И РЯ4
 ПО ПРОП
 НТЖ ШИФР-В1(М7)
 СБР РЯ3
 НТЖ Е46П48
ПРОП ПЕ ПЛА
 СЧ РЯ3
 СДА 64+4
 ПЕ СЛЦШ
НАШЛИ МОДА КПЛАН+1(М14)
 СЧ 64(М16)
ВЕР1 СБР КГ
 СДА 64+13
 ИЛИ ЯЧКАТ+5-В1(М7)
 ЗП ЯЧКАТ+5-В1(М7)
 СДА 64-11
 НТЖ ЯЧКАТ+7-В1(М7)
 И Е44П42
 НТЖ ЯЧКАТ+7-В1(М7)
 ЗП ЯЧКАТ+7-В1(М7)
 ЗП ППР
УХ СЧ НОМЕРЗ-В1(М7)
 ПО ДА
 И ЕЗП
 ПО НЕТ
ДА НОП
 СЧ ЯЧВСТ1-В1(М7)
 ИЛИ ППР
 ПЕ НЕТ    ЗАД.ЖДУ ТЕЛ КАТ ПРИО
 СЧ ПОРВВО
 И П37
 СДА 64-2
 УИ М16
 СЛИ М6(М16)   РЕЗЕРВИР.МЕСТО В НОМ
НЕТ МОДА (М14)
 МОДА (М13)
 СЧ 127(М6)
 ПО ТЕЛЕ1         ЕСТЬ СВ.МЕСТО
 МОДА НЕТ
 ЦИКЛ (М6)
 МОДА НЕТМСТ
 АIF (NОТ ∧АС).АС103
 ПБ (М13)
 АGО .АС102
.АС103 АNОР
 ПИНО (М13)
.АС102 АNОР
 УИА -126(М6)      НА 2-Й КАТАЛОГ
 УИА 256(М13)      ОБЫЧНАЯ ЗАДАЧА
 СЧ РЯ2
 СЛЦ Е25
 ЗП РЯ2
 СЧ ЯЧВСТ1-В1(М7)   ВО 2 СО ЖДЕМ НЕ ПОМЕЩАТЬ
 ПЕ НЕТМСТ
 СЧ ’601’(М14)
 ПЕ НЕТ
 СЧ ПОРВВО
 И П37
 СДА 64-2
 ЗП ’601’(М14)
 ПБ НЕТ
НЕРА  СЧ НЕИЩ(М14)
 И Е41
 ПО НЕРАБ   РАЗРЕШЕНО РАБОТАТЬ ВСЕМ
 СЧ Е27
 АIF (NОТ ∧АС).АС7
НЕРБ ИЛИ Е27
 АGО .АС6
.АС7 АNОР
НЕРБ ИЛИ Е47 ШТР КТО НЕ ПОДКЛ К БЮДЖ
.АС6 АNОР
 ИЛИ ЯЧКАТ+6-В1(М7)
 ЗП ЯЧКАТ+6-В1(М7)
 ПБ НЕРАБ
ПЛА СЛИА 2(М16)
 МОДА УХ
 ПИО (М16)
 ПБ ПЛАН
ХОДЫ СЧИ М16
 НТЖ Е2
 АIF (NОТ ∧АС).АС5
 ПО НЕРАБ
НЕВКЛ СЧ ЯЧКАТ+6-В1(М7)
 ПБ НЕРБ
ДАВЫЭК СЧ ЯЧКАТ+6-В1(М7)
 ИЛИ Е47
 ЗП ЯЧКАТ+6-В1(М7)
 ПБ ЗАНОМ1
 АGО .АС4
.АС5 АNОР
 ПО НЕШТР
НЕТОВР СЧ НЕИЩ(М14)
 И Е28
 ПО НЕРБ
 ПБ НЕВОВР
ДАЗАМ СЧИ М16
 СДА 64-9 ПОЛУЧАЕМ
 ИЛИ ЯЧКАТ+5-В1(М7)  С11 ПО 16 РР
 ЗП ЯЧКАТ+5-В1(М7)
 СЧ ЯЧКАТ+6-В1(М7)
 ИЛИ Е48
ЗПК ЗП ЯЧКАТ+6-В1(М7)
 ПБ НЕРАБ
.АС4 АNОР
ЗОН710 КОНД М24В’143’
НОМО КОНД М15В’377’
 АIF (NОТ ∧АС).АС2
ЭКСВЫС ЭКВ 768
 АGО .АС3
.АС2 АNОР
ШТРАФ ЭКВ 768
.АС3 АNОР
ТЕ СЧ ЯЧКАТ+7-В1(М7)
 И Е46П48
 ПЕ ТЕ2
 СЧ ГОД
 СДА 64-45
ТЕ2 ИЛИ Е43     СФ. ОС=РЕАЛ
ТЕ1 ИЛИ Е44
 АIF (∧АС).АС1
 ИЛИ Е42
.АС1 АNОР
 ИЛИ ЯЧКАТ+7-В1(М7)
 ЗП ЯЧКАТ+7-В1(М7)
 СЧ ЯЧКАТ+6-В1(М7)
 ИЛИ Е33
 ЗП ЯЧКАТ+6-В1(М7) ВКЛ.НА МАШИНЕ.НОМЕР ЗАНЕСЕН
 ПБ НЕТЕЛЕ
ЕСГ СЧ ЯЧКАТ+6-В1(М7)
 ИЛИ Е34
 ЗП ЯЧКАТ+6-В1(М7)
 ПБ НМ
ГР ЭКВ ’1710’
ТЕЛЕ1 СЛИА 127(М6)       НОМЕР В КАТАЛ.ШИФРОВ
 СЧ ’601’(М14)
 ЗП СО6+1-В1(М7)
 СЧИ М13
 ЗП СО13-В1(М7)
 СЧИ М6
 ЗП СО6-В1(М7)
 УИА -23(М16)   ГРИФ
НГ МОДА ГР(М14)
 СЧ 23(М16)
 НТЖ ШИФР-В1(М7)
 ПО ЕСГ
 МОДА НГ
 ЦИКЛ (М16)
НМ НОП 0
ПОВТБ СЧ РЯ2
 ПВ ФИЗОБМ(М16)   ЧТ.БОЛЬШ.КАТАЛОГА
 УИА 3
 СЧИ М13
 СДА 64+1
 УИ М12
 СЛИ М12(М6)       НОМЕР ШИФРА:≥200-ТЕРМИН.ЗАДАЧИ
 СЧИ М6
 СДА 64-3
 УИ М16
*****ВО 2 Б.К. ТАКЖЕ ПРИСУТ.
*** НОМЕР
ПОВТВ СЧИ М12
 СДА 64-40
 ИЛИ ЯЧКАТ-В1(М7)
 ЗП ЯЧКАТ-В1(М7)  НОМЕР ШИФРА
****РАЗДЕЛЕНИЕ НА КЛАССЫ И ПРИСВ. ПРИОРИТЕТОВ
ТПОР УИА 0(М15)
 СЧ ЯЧКАТ+7-В1(М7)
 И Е44П42
 ПЕ ЗАНПРИ  ЭТО РЕАЛЬН.,СЛУЖ.ТЕРМ.
 СЧ ПОРВВО
 И Е8
 ПО ТП НЕ ВЫД. ФОРМ. С ТЕРМИНАЛА
 СЧ НОМЕРЗ-В1(М7)
 ПО ТП НЕ ФОРМ
 СЧ Е44
 ПБ ЗАНПРИ
ТП СЧ ЯЧКАТ-В1(М7)
 СДА 64-28
 ЗП ЯЧКУ
 НЕД 0
 УИ М15
 СЧ ВСЕЕД
 СДА 64(М15)
 НТЖ ВСЕЕД
 И ВРКЛАС
 ЧЕД 0
 УИ М15
 СЧ ПОРВВО
 И Е11
 ПО ДИФКЛ
 СЧ 0
 ПБ КДИФ
ДИФКЛ МОДА ДИФ0
 ПИО (М15)
 СЧ Е48-1(М15)
 РЗБ ВРКЛАС
 СДА 64-1
 ИЛИ ЯЧКУ
 НЕД 0
 СЧМР 0
 И Е46П48
 НТЖ Е46П48
 СДА 64+7
 ПБ КДИФ
ДИФ0 СЧ ЯЧКУ
 НЕД 0
 СДА 64+1
 СДА 64-38
КДИФ ЗП РЯ4
 СЧИ М15
 СДА 64-41
 ИЛИ РЯ4
ЗАНПРИ ИЛИ ЯЧКАТ+7-В1(М7)
 ЗП ЯЧКАТ+7-В1(М7) ПРИЗН.ОТЛАД.ЗАДАЧИ
***ВО 2 Б.К. ТОЖЕ
*** ОЧЕРЕДЬ
 УИИ М11(М14)
 СЧ ЯЧКАТ+7-В1(М7)
 СДА 64-3
 СДА 64+26
 НТЖ П77777
 ЗП РЯ4           ВЕС ЗАДАЧИ
ПРИТ СЧ 7(М11)
 И П1777
 УИ М12
 ПО ПРИТ1         1-Я ЗАДАЧА
 СЛИ М12(М14)
 СЧ 7(М12)
 СДА 64-3
 СДА 64+26
 НТЖ П77777
 РЖА 3
 ВЧ РЯ4
 ПЕ ПРИТ1         ОЧЕРЕДЬ МЕНЬШЕ
 УИИ М11(М12)      А КАТАЛОГА
 ПБ ПРИТ
ПРИТ1 НОП
 СЧИ М12
 И П1777
 ИЛИ ЯЧКАТ+7-В1(М7)
 ЗП ЯЧКАТ+7-В1(М7)
 СЧ ШКАНУВ-В1(М7)
 И Е48П17
 ПО ПРА
 СЧ ЯЧКАТ+6-В1(М7)
 ИЛИ Е25
 ЗП ЯЧКАТ+6-В1(М7)
 СЧ ЯЧКАТ-В1(М7)
 ИЛИ Е40
 ЗП ЯЧКАТ-В1(М7)
ПРА СЧИ М16
 НТЖ 7(М11)
 И П1777
 НТЖ 7(М11)
 ЗП 7(М11)
 СЛИ М16(М14)
 СЧ ЯЧКАТ+6-В1(М7)
 СДА 64+34
 И П177
 ПО НЕТТТ
 СЧ ЯЧКАТ+6-В1(М7)
 ИЛИ Е32
 НТЖ Е32
 ЗП ЯЧКАТ+6-В1(М7)
НЕТТТ СЧ НОМЕРЗ-В1(М7)
 ПЕ ОБЗНП-В1(М7)    ФЗ
 СЧ ТИПУСТ-В1(М7)
 СДА 64-34
 ИЛИ ЯЧКАТ+5-В1(М7)
 ЗП ЯЧКАТ+5-В1(М7)
ВВПК СЧ ЯЧВСТ-В1(М7)
 И Е17П28-В1(М7)
 СЛЦ Е17
 СДА 64-20
 ИЛИ ЯЧКАТ+5-В1(М7)
 ЗП ЯЧКАТ+5-В1(М7) ЧИСЛО ПК ВВОДА
ОБЗНПК УИА -7(М15)
 ПБ НУЖДА-В1(М7)
ОШВВД НОП
 СЧ ЯЧВСТ-В1(М7)
 ПБ ИИ-В1(М7)
СБОЙ СЧ СТВУ-В1(М7)
 СДА 64+27
 И П6
 УИ М17           2-НЕТ КНЦ ПК;4-СЧСИ
 МОДА -2(М17)
 СЧ СБКС
 ЗП БУФПР+4-В1(М7)
 МОДА -1(М17)
 СЧ СБКС
 ЗП БУФПР+5-В1(М7)
ОСВМЛ СЧ ЯЧКАТ+5-В1(М7)
 И П1777
 ПО ВОЗВРТ        НЕ БЫЛО ЗАПИСИ НА МЛ
 УИ М3
 СЧИ М14
 СДА 64-2
 ИЛИ НОММЛ1
 СЛЦ ИНФЗОН        КУС ВЫЗОВА КАТАЛ.ЗОН ВВОДА
 ЗП РЯ
 ПВ ЕNQ(М15)      ЗАХВАТ ОБЩ.ДИСКА
ПОВТОС СЧ РЯ
 ПВ ФИЗОБМ(М16)   ВЫЗОВ КАТАЛОГА ЗОН
 УИА 3
 АIF (∧ВS ЕQ 0).Н1
 СЧ 512(М14)
 НТЖ 513(М14)
 ПО ЛОВ
 СТОП * ЕСТЬ ОТКРЫТАЯ СРЕДИ ЗОН 0-100
ЛОВ  НОП
.Н1 АNОР
ОСВ1 НОП
 СЧИ М3
 И П37
 УИ М17
 И Е1
 УИ М12
 СЧИ М3
 СДА 64+1
 УИ М11
 СДА 64+4
 УИ М16
 СЛИ М11(М14)
 СЧ 512(М11)
 СД КСО(М12)
 И П1777
 УИ М15
 СЧ 512(М11)
 ИЛИ Е48П25(М12)
 НТЖ Е48П25(М12)
 ЗП 512(М11)
 СЛИ М16(М14)
 СЧ 512(М16)
 НТЖ Е48(М17)
 ЗП 512(М16)
 УИИ М3(М15)
 МОДА ОСВ1
 ПИНО (М15)
 СЧ РЯ
 ПВ ФИЗЗП(М16)     ЗАПИСЬ КАТАЛОГА ЗОН
 СЧ РЯ
 ПВ DЕQ(М15)      ОСВ.ОБЩЕГО ДИСКА
ВОЗВРТ СЧ ЯЧВСТ-В1(М7)  ВОЗВРАТ ТРАКТА ВВОДА
 И П177
 СДА 64+2
 УИ М16
 ПО ОСВЗН-В1(М7)
 УИА ’2003’
 СЧ Е48(М16)
 ИЛИ ЯЧВ1
 ЗП ЯЧВ1
 ПБ ОСВЗН-В1(М7)  НА ОТКАЗ ОТ ЗАДАЧИ ВВОДА
ВВДЗАД МОДА ФСМ
 СЧ (М15)
 СДА 64-24
 ИЛИ (М16)
 ЗП (М16)
 СЧ 0
 ЗП ПРИЗН1-В1(М7)
 ПБ ВЫДТ1
ВЫДТ ПЕ ВЫДТТ1
 УИА ВВД-В6(М16)
 СЧ Е30
 ЗП ПРИЗН1-В1(М7)
 СЧ ТИПУСТ-В1(М7)
 УИ М15
 ПБ ВЫДТТ1
ЗАМ УИА 3
 СЧ ЯЧВ1
 ПЕ ЗАМ1          ОДНО ЗАМЯТИЕ
 УИА ЗАМЯ2-В7(М16)
 МОДА ОСВМЛ
 УИА (М17)
ВЫДТТ1 УИА -159(М5)
 СЧ Е48П41
РОСП МОДА БУФПР-В1(М7)  ПОД ОШ.ВВОДА
 ЗП 159(М5)
 МОДА РОСП
 ЦИКЛ (М5)
 СЛИ М16(М1)
 СЧ ТИПУСТ-В1(М7)
 УИ М15           ВИД УСТР-ВА ВВОДА
 СЧ НОМЕРЗ-В1(М7)
 ПЕ ВЫДТ1         ОТ ЭК.ФОРМИР.
 СЧ ПРИЗН1-В1(М7)
 ПЕ ВВДЗАД
 СЧ ПУСТО
 ЗП БУФПР+1-В1(М7)
 МОДА ФСМ
 СЧ (М15)
 ИЛИ УСТ-В1(М7)    НОМЕР УСТР-ВА ВВОДА
 СДА 64-8
 ПБ ВЫДТФ
ВЫДТ1 СЧ ЯЧКАТ+7-В1(М7)
 И Е44П42    КЛАСС
 СДА 64+1
 ИЛИ СКОБКА
 ЗП БУФПР+1-В1(М7)
 СЧ ЯЧКАТ-В1(М7)
 РЗБ =П’003770’
ВЫДТФ ИЛИ (М16)
 ЗП БУФПР-В1(М7)
 СЧ ШИФР-В1(М7)
 РЗБ ПРОБ6
 ЗП БУФПР+2-В1(М7)
 СЧ ШИФР-В1(М7)
 СДА 64-24
 РЗБ ПРОБ6
 ЗП БУФПР+3-В1(М7)
 СЧ ЯЧВСТ-В1(М7)
 И Е34
 ПО ВЫДТ2         ЕСТЬ МЕСТО НА БУФЕРЕ ВВОДА
 СЧ НЕТБФ         ТЕКСТ:НЕТ МЕСТА
 ЗП БУФПР-В1(М7)
 СЧ НЕТБФ+1
 ЗП БУФПР-В1+1(М7)
 ПБ ФОРМБ4
ВЫДТ2 СЧ СТПАСП-В1(М7)
 И Е33
 ПЕ ФОРМБ         ЗАДАЧА ВВЕДЕНА
 СЧ ЯЧВСТ-В1(М7)
 СДА 64+44
 ПО ФОРМБ4        НЕТ ОШ ВВОДА
 УИ М13           ЧИСЛО ОШ.ВВОДА
 УИИ М12(М7)
 УИА -4(М16)
 СЛИА 9(М5)
ФОРОШ МОДА ЗАГОЛ
 СЧ 4(М16)
 МОДА БУФПР-В1(М7)
 ЗП (М5)
 СЛИА 1(М5)
 МОДА ФОРОШ
 ЦИКЛ (М16)
 СЧ ШИФР-В1(М7)
 РЗБ ПРОБ6
 МОДА БУФПР-В1(М7)
 ЗП (М5)
 СЧ ШИФР-В1(М7)
 СДА 64-24
 РЗБ ПРОБ6
 МОДА БУФПР-В1(М7)
 ЗП 1(М5)
 СЛИА 4(М5)
ФОРОШ1 СЧ ОШИБКИ-В1(М12)
 РЗБ =П’77777’
 МОДА БУФПР-В1(М7)
 ЗП (М5)          АВВД
 СЧ ОШИБКИ-В1(М12)
 СДА 64+16
 СЛЦ Е1
 И П7777
 УИА -1(М11)
 ПВ ПЕРЕВ(М15)
 ИЛИ =М32П’  ’
 МОДА БУФПР-В1(М7)
 ЗП 1(М5)         НОМЕР ПК
 СЧ ОШИБКИ-В1(М12)
 СДА 64+8
 И П177
ФОРОШ2 ЗП РЯ            НСТР И НСИМВ
 СЛИА 1(М11)
 ВЧ П12
 ПО ФОРОШ2        ОПР.НСТР И НСИМВ
 СЧ РЯ
 ПЕ ФОРОШ3        ≠0
 СЧ Е9
 ЗП РЯ            10
ФОРОШ4 СЧИ М11
 ПВ ПЕРЕВ(М15)    НСТР
 УИИ М15(М5)
 СЛИ М15(М7)
 СДА 64-24
 ИЛИ РЯ
 ИЛИ =П’ 00 00’
 ЗП БУФПР-В1+2(М15)
 СЧ ОШИБКИ-В1(М12)
 И П377
 СДА 64-33
 РЗБ =П’77777’
 СДА 64-16
 ИЛИ =П’ 000  ’
 ЗП БУФПР-В1+3(М15)
 СЧ ОШИБКИ-В1(М12)
 СДА 64+28
 И П37
 УИ М16           ВИД ОШ.∧2
 МОДА ОШВВВ
 СЧ (М16)
 ЗП БУФПР-В1+4(М15)
 СЛИА 9(М5)
 СЛИА 1(М12)
 СЛИА -1(М13)
 МОДА ФОРОШ1
 ПИНО (М13)         ЕСТЬ ОШ.
 ПБ (М17)
ФОРОШ3 СЛИА 1(М11)
 ПБ ФОРОШ4
ФОРМБ УИА -3(М11)       НОМЕРА БОБИН НА ВЫДАЧУ ТТ
 УИА 0(М12)
ФОРМБ1 МОДА 3(М11)
 СЧ ЯЧКАТ+1-В1(М7)
 ПО ФОРМБ4        ВСЕ БОБИНЫ
 ЗП РЯ
ФОРМБ2 СЧ РЯ
 ПО ФОРМБ3        СЛОВО ВСЕ
 СДА 64-16
 ЗП РЯ
 СЧМР
 ЗП R-В1(М7)
 И Е13
 ПО ФОРМБ2         КУСОК
 СЧ R-В1(М7)
 И П7777
 ЗП R-В1(М7)
 УИА -ЧИСЛОБ(М15)
ПОИСКБ СЧ R-В1(М7)
 НТЖ ТЗНМЛ+ЧИСЛОБ(М15)
 И П7777
 ПО ФОРМБ2        БОБИНА УСТАНОВЛЕНА
 МОДА ПОИСКБ
 ЦИКЛ (М15)
 СЧ R-В1(М7)
 ПВ ПЕРЕВ(М15)
 ИЛИ =М32П’  ’
 МОДА (М12)
 ЗП БУФПР+4-В1(М7)
 СЛИА 1(М12)
 ПБ ФОРМБ2
ФОРМБ3 МОДА ФОРМБ1
 ЦИКЛ (М11)
ФОРМБ4 НОП ,
 СЛИА 9(М5)
 ПБ (М17)
СБОЙВУ УИА СБОЙВ-В7(М16)
 МОДА СБОЙ          ФОРМ.ТЕКСТА:СБОЙ ВУ
 УИА (М17)
 ПБ ВЫДТТ1
СБКС КОНД П’2НЕТ К’
 КОНД П’НЦА ПК’
 КОНД П’ СЧСИ ’
 КОНД М24П’00↑’
ЗАМ1ПК МОДА ВОЗВРТ
 УИА (М17)
ЗАМ2 УИА ЗАМЯ-В7(М16)
 ПБ ВЫДТТ1
ЗАМ1 СЧ ШИФР-В1(М7)
 ПО ЗАМ1ПК        НА 1-ОЙ ПК
 СЧ ЯЧВСТ-В1(М7)
 СДА 64+37
 И П3
 ПО НЕА3          ВВОД НЕ А3
 СЧ
 ЗП МЛРВБЩ-В1(М7)
 УИА -24(М12)
 УИА -4(М11)       НАЧАТЬ С НОВ.СЛОВА
НЕА3 МОДА 768(М7)
 УИА (М4)
 СЧИ М4
 СДА 64-2
 ЗП РЯ
 СЧ ЯЧВСТ-В1(М7)
 И П177
 ИЛИ НОММБ
 ИЛИ РЯ
 ПВ ФИЗОБМ(М16)   ЗАПИСЬ АБЗАЦА С ОТРЕД.МАССИВОМ
 УИА 3
 СЧ ИНФЗАМ
 ИЛИ НОММБ
 ИЛИ РЯ
 ЗП РЯ3
 ПВ ФИЗОБМ(М16)   ЧТ.ИНФ.О ЗАМЯТИИ
 УИА 3
 УИА -ДЛЗМ(М17)
ЗАМ3 МОДА ДЛЗМ(М17)     СОХР.РАБ.ЯЧ-ЕК И ИР ПРИ ЗАМЯТИИ
 СЧ ЯЧВСТ-В1(М7)
 МОДА 10(М4)
 ЗП ДЛЗМ(М17)
 МОДА ЗАМ3
 ЦИКЛ (М17)
 УИА -9(М15)
ЗАМ4 СЧИ М13(М15)
 МОДА 9(М4)
 ЗП (М15)
 МОДА ЗАМ4
 ЦИКЛ (М15)
 СЧ РЯ3
 НТЖ Е18
 ПВ ФИЗОБМ(М16)
 УИА 3
 МОДА ОСВЗН-В1(М7)
 УИА (М17)
 ПБ ЗАМ2
ЭКФМ КОНД П’2Ф000-’
ВВД КОНД П’20000-’
ФСМ КОНД М8П’Л’    МЕЖДУ ФСМ И УВВК ОДНО СЛОВО
ОШВВ КОНД П’7ОШ00 ’
УВВК КОНД М8П’В’
ЗАМЯ2 КОНД П’72З00 ’
ЗАМЯ КОНД П’7ЗМ00 ’
СБОЙВ КОНД П’7СБ00 ’М46В’1’
СКОБКА КОНД М32П’0 ’М24В’143’М16В’143’М8В’143’В’143’
ПУСТО КОНД М40В’143’М32В’143’М24В’143’М16В’143’М8В’143’В’143’
 АIF (∧ВS ЕQ 0).ВЫКУФ
 АGО .ЖД
.ВЫКУФ АNОР
ВЫКЛУФ СЧ НОМЕРЗ-В1(М7) БЛОК ГАШ.РАБОТЫ С УСТР-МИ ВВОДА
 ПЕ ВЫКЛУХ        ОТ ЭК.ФОРМИР.
 УИА ’2003’
 СЧ УСТ-В1(М7)
 УИ М16           НОМЕР УСТР-ВА
 СЧ ТИПУСТ-В1(М7)
 ПО ПП
 СЧ Е44(М16)
ПП1 И МГРП
 ПО ВЫКЛУС
 ПВ ТВ67А(М15)
 СЧ ТИПУСТ-В1(М7)
 ПО ВЫКЛУС        С ФСМ
 СЧ Е22(М16)
 ПВ ГАШПРП(М15)
ВЫКЛУС СЧ
 УВВ ’10’(М16)     ВЫКЛ.ФСМ
ВЫКЛУХ УИА 3
 ПБ (М12)
ПП СЧ Е42(М16)
 ПБ ПП1
.ЖД АNОР
ЖДЕМТТ КОНД Х’999999999999’
ЗАГОЛ КОНД П’АВВД ’
 КОНД П’  НПК ’
 КОНД П’НСТ НС’
 КОНД П’ СИМ  ’
 КОНД П’ШИФР  ’
ОШВВВ НОП 0
ЕКОНЕЦ КОНД П’ЕКОНЕЦ’
 КОНД П’ ШИФР ’   1
 КОНД П’АВОСТ ’   2
 КОНД П’СИМВОЛ’   3
 КОНД П’МАРКЕР’   4
 КОНД П’Ч ЛИСТ’   5
 КОНД П’ЧИСЛО ’   6
 КОНД П’КОМАНД’   7
 КОНД П’КОНСТ.’   8
 КОНД П’З ШИФР’   9
 КОНД П’НЕ ИР ’  10
 КОНД П’НЕ КОП’  11
 КОНД П’К ЧЕТ ’  12
 КОНД П’ЦИФРА ’  13
 КОНД П’РЕСУРС’  14
 КОНД П’НЕТ В ’  15
 КОНД П’БОБИНА’  16
 КОНД П’ЧУЖ СЛ’  17
 КОНД П’ВРЕМЯ ’  18
 КОНД П’УЧЕТ  ’     19
 КОНД П’НЕТ МА’     20
НЕТБФ ТЕКСТ П’2БУФ ВВД ЗП↑’
ЕАРХ ШИФАР
ИНФЗАМ КОНД М17В’1’В’33’   6ТР3С ЗАМ
 ЛИТЕР
А2А СЧ Е37
 ИЛИ ЯЧВСТ-В1(М7)
 ЗП ЯЧВСТ-В1(М7)  ЛЮБОЙ СИМВОЛ
 УИА -4(М10)
 УИА А2А0-В6(М13)
 ПБ ЗАСИМВ-В1(М7)
А2А0 МОДА ЗАСИМВ-В1(М7)
 ЦИКЛ (М12)         ПРОПУСК 8-И ПУСТЫХ СИМВОЛОВ ПОСЛЕ А2
А2А1 СЧ
 ЗП МЛРВБЩ-В1(М7)
 ЗП R2-В1(М7)
 УИА -1(М12)
ЛА162 УИА А2-В6(М13)
 ПБ ЗАСИМВ-В1(М7)
А2 МОДА (М12)
 СЧ R2-В1(М7)
 СДА 64-8          В МЛРВБЩ-СТРВБЩ,В R2-МЛРВБЩ СТРОКИ ПК
 ИЛИ R-В1(М7)
 МОДА (М12)
 ЗП R2-В1(М7)
 МОДА ЗАСИМВ-В1(М7)
 ЦИКЛ (М10)
 УИА -4(М10)
 МОДА ЗАСИМВ-В1(М7)
 ЦИКЛ (М12)
 НТЖ МЛРВБЩ-В1(М7)
 ПЕ А2А2
 СЧ R2-В1(М7)
А2А2 ПО А2А1          ПРОПУСК ПУСТОЙ СТРОКИ
 СЧ R2-В1(М7)
 И Е1
 ПО ЛА165         НЕ КОНЕЦ А2
 СЧ ЯЧВСТ-В1(М7)
 ИЛИ Е44
 НТЖ Е37
 ПБ РЕДА4В
РЕДА3А СЧ R1-В1(М7)
 СЛЦ Е1
 ЗП R1-В1(М7)     А ВВОДА
 УИА -4(М11)
 СЧ МЛРВБЩ-В1(М7)
 НТЖ Е40
 ПО КОНА3         ПРИЗН.КОНЦА А3
 СЧ
 ЗП МЛРВБЩ-В1(М7)
 УИА -24(М12)
ПРИМА4 УИА А5А-В6(М13)
 ПБ ЗАСИМВ-В1(М7) ПРИЕМ А3
А5А СЧ ЯЧВСТ-В1(М7)
 И Е38
 ПЕ А5Б           ПО А3 ВВОД
 СЧ ТИПУСТ-В1(М7)
 ПО А5Б
 СЧ R-В1(М7)
 ПО А5Б           ПУСТЫШКА
 НТЖ П177
 ПО А5А2          СИМВОЛ 177 ЭКВ ПУСТЫШКЕ
 СЧ R-В1(М7)
 ЧЕД
 И Е1
 УИА А5Б-В6(М13)
 ПО НЕДОПС
 СЧ R-В1(М7)
 НТЖ =В’340’
 ПЕ А5В
 СЧ Е8
А5А2 ЗП R-В1(М7)
 УИ М15
А5Б СЧ R-В1(М7)
 ПБ А3Б
А5В НТЖ Е1
 НТЖ П37
 ПЕ А5Б
 СЧ Е8
 ИЛИ П17
 ПБ А5А2
КОНА3 СЛИА 1(М12)
 МОДА НЕПОВТ
 ПИО (М12)         КОНЕЦ А3
 ПБ ПРИМА4
ЛА210 СЛИА 4(М15)
 МОДА ЛА211
 ПИНО (М15)         НЕ Б
 УИА -5(М12)
ЛА171А УИА Б-В6(М13)
 ПБ ЗАСИМВ-В1(М7) ПРИЕМ Б
Б СЧ (М2)
 СДА 64-8
 ИЛИ R-В1(М7)
 ЗП (М2)
 МОДА ЗАСИМВ-В1(М7)
 ЦИКЛ (М12)
 ПБ ЛА178А
ЛА188 СЛИА -’15’(М15)
 МОДА ЛА203
 ПИНО (М15)         НЕ Ч
 УИА 1(М11)        НЕТ ТОЧКИ
 СЧ Ч10
 ЗП R1-В1(М7)
 УИА ЧИС-В6(М13)
 УИА (М5)
 СЧ
 ЗП R2-В1(М7)
 ЗП (М2)
 МОДА ЛА202
 УИА (М16)
 СЧИ М16
 СДА 64-24
 ЗП 4(М7)
БЫЛПМ СЧ (М2)
 ПЕ ЛА184         СИМВОЛ ПЛЮС(МИНУС) НЕ ПЕРВЫЙ
ЛА190 РЖА 3
 ПБ ЗАСИМВ-В1(М7)
ЧИС ВЧ П12
 ПЕ ЦИФРА         ≤9
 НТЖ
 ПО БЫЛПМ         +
 НТЖ П5
 ПО ЗАСИМВ-В1(М7) ПРОПУСК
 НТЖ П3
 ПО СИМВ20        СТЕПЕНЬ
 НТЖ П7
 ПЕ НЕМИНУ
 СЧ R1-В1(М7)
 ЗНАК Е41
 ЗП R1-В1(М7)     СИМВОЛ-
 ПБ БЫЛПМ
НЕМИНУ НТЖ Е2
 ПЕ НЕЗАПЯ        НЕ ЗАПЯТАЯ
ЗАПЯТА МОДА ЛА184
 ПИО (М11)         БЫЛ РАЗДЕЛИТЕЛЬ УЖЕ
 УИА (М11)         ПРИЗНАК.ИЛИ,
 ПБ ЛА190
НЕЗАПЯ НТЖ П7
 ПО ЗАПЯТА
 СЛИА -’75’(М15)
 МОДА СИМВ20
 ПИО (М15)
 СЧ (М2)
 РЖА 2
 ДЕЛ R1-В1(М7)
 ЗП (М2)
ЛА200 УИА ЛА172-В6(М5)
 ПБ БЗРМ
СИМВ20 РЖА 2
 СЧ (М2)
 ПЕ СИМВ21
 СЧ Ч10
СИМВ21 ДЕЛ R1-В1(М7)
 ЗП (М2)
 УИА ЛА196-В6(М13)
БЫЛПЛМ СЧ R2-В1(М7)
 ПО ЛА190         НЕ БЫЛО ПЛЮСА И МИНУСА
ЛА184 УИА 3(М16)        НЕДОПУСТ.СИМВОЛ
 ПБ ЛА184А
ЛА196 ВЧ П12
 ПО НЕЦИФР
 СЧ R2-В1(М7)
 УМН П12R
 СЧМР 64
 СЛЦ R-В1(М7)
 ЗП R2-В1(М7)
 ПБ ЛА190
НЕЦИФР НТЖ
 ПО БЫЛПЛМ        +
 НТЖ Е1
 ПЕ НЕМИН
 УИА 1(М5)         -
 ПБ БЫЛПЛМ
НЕМИН СЧ R2-В1(М7)
 УИ М17
НЕПЕРЕ МОДА ЛА200
 ПИО (М17)         СТЕПЕНЬ ВСЯ
 СЛИА -1(М17)
 РЖА 0
 СЧ (М2)
 МОДА (М5)
 УМН Ч10
 ЗП (М2)
 ПБ НЕПЕРЕ
ЛА202 УИА 6(М16)        ПЕРЕПОЛН.АУ
 ПБ ЛА184А
ЦИФРА РЖА  2
 СЧ R-В1(М7)
 ИЛИ Л64
 СЛ (М2)
 УМН Ч10
 ЗП (М2)
 МОДА ЛА190
 ПИНО (М11)         ЦЕЛОЕ ЧИСЛО
 СЧ R1-В1(М7)
 УМН Ч10
 ЗП R1-В1(М7)
 ПБ ЛА190
ЛА203 СЛИА ’25’(М15)
 МОДА ЛА206
 ПИНО (М15)         НЕ В
 УИА (М12)
ПРИВ УИА ПРИЕМВ-В6(М13)
 ПБ ЗАСИМВ-В1(М7)
ПРИЕМВ НТЖ П17
 ПО ПРИВ          ПРОПУСК ПРОБЕЛА
 УИА ЛА172-В6(М5)
 СЧ R-В1(М7)
 СДА 64+3
 ПЕ ВВС           НЕ ЦИФРА
 СЧ (М2)
 СДА 64-3
 ИЛИ R-В1(М7)
 ЗП (М2)          НАКОПЛ.ВВОДНОГО СЛОВА
 СЛИА 1(М12)
 ПБ ЗАСИМВ-В1(М7)
ВВС СЧИ М12
 ВЧ П12
ВВС1 ПО ВВС2          >9 СИМВОЛОВ
 СЧ (М2)
 И П77777
 ЗП (М2)
 СЧ ЯЧВСТ-В1(М7)
 И Е8П16         НОМЕР МАССИВА
 СДА 64-8
 ИЛИ (М2)
 ЗП (М2)
 СЧ (М3)
 И Е47
 СДА 64-1
 ИЛИ (М2)
 ЗП (М2)          В НОВОЕ В НАЧАЛО МАССИВА
ЛА205 УИИ М3(М2)        А ВВ.СЛОВА В АБЗАЦЕ
 ПБ БЗРМ
ВВС2 НТЖ Е4
 ПЕ ЛА186         ≠18 СИМВОЛАМ
 СЧ (М2)
 СДА 64+27
 ЗП (М2)
 ПБ ВВС1
ЛА206 СЛИА -’17’(М15)
 МОДА ЛА209
 ПИНО (М15)         НЕ СИМВОЛ С
 УИА -15(М12)
ЛА207 УИА С-В6(М13)
 ПБ ЗАСИМВ-В1(М7)
С НТЖ П17
 ПО ЛА207         ПРОПУСК ПРОБЕЛА
 СДА 64+5
 ПО ЛА207А        НЕ БУКВА
 СЧ НОМЕРЗ-В1(М7)
 ПЕ ЛА200         ОТ ФОРМ.ЗАДАЧИ
 УИА 8(М16)        ЧИСЛО ЦИФР<16
 ПБ ЛА184А
ЛА207А СЧ R-В1(М7)
 СДА 64+3
 ПО ЛА208         ЦИФРА<8
 УИА 3(М16)
 УИА ЛА208-В6(М13)
 ПБ ВЫДОШ-В1(М7)  ВЫДАЧА ОШ-КИ
ЛА208 СЧ (М2)
 СДА 64-3
 ИЛИ R-В1(М7)
 ЗП (М2)
 МОДА ЛА207
 ЦИКЛ (М12)
 ПБ ЛА178А
ЛА209 СЛИА ’14’(М15)
 МОДА ЛА210
 ПИНО (М15)         НЕ Е
 СЧ (М3)
 ИЛИ Е47
 ЗП (М3)          КОНЕЦ МАССИВА В ВВОДНОЕ СЛОВО
 СЧ ЯЧВСТ-В1(М7)
 СЛЦ Е8
 ЗП ЯЧВСТ-В1(М7)  НОМЕР МАССИВА+1
 И Е36
 ПЕ ФОРМ          ФОРМИР.1-ГО ТИПА
 ПБ ЛА172А
ЛА211 СЛИА 1(М15)
 УИА -9(М11)       ДЛЯ АО-КОНЕЦ ИНФ.ПРИ М11=0
 МОДА ЛА212
 ПИО (М15)         СИМВОЛ:А
ЛА211А СЧ R-В1(М7)
 НТЖ П17
 ПО ЛА172А        ПРОПУСК ПРОБЕЛА
 УИА ЛА211Б-В6(М13)
НЕДОПС УИА 3(М16)        НЕДОП.СИМВОЛ
 ПБ ВЫДОШ-В1(М7)  НА ВЫДАЧУ ОШИБКИ
ДОБУКВ СДА 64+5
 ПЕ ЛА172         БУКВА
ЛА211Б УИА ДОБУКВ-В6(М13)
 ПБ ЗАСИМВ-В1(М7)
ЛА212 УИА А-В6(М13)
 ПБ ЗАСИМВ-В1(М7)
А СДА 64+3
 ПЕ ЛА211А        НЕ ЦИФРА
 МОДА ПЕРКЛА
 ПБ (М15)
ПЕРКЛА УИА -5(М12)       А0-АВТОКОД ИТМ
 ПБ А0
А1 УИА -5(М12)       А1-ТЕКСТОВАЯ ИНФОРМАЦИЯ
 ПБ ЛА218
 УИА -7(М12)       А2-АЛЬГИБР
 ПБ А2А
 СЧ Е38           А3-ОБРАЗ ПК
 ПБ А3
А4 УИА -5(М12)       А4-5-И ДОРОЖЕЧНАЯ ПЛ
 ПБ А4А
 СЧ Е39           А5-ОБРАЗ ПК В КОДАХ УПП
 ПБ А3
А6 ПБ ЛА211А        А6-ЗАПРЕЩ.
А7 ПБ ЛА211А        А7-ЗАПРЕЩ
А3 УИА -4(М11)
 ИЛИ ЯЧВСТ-В1(М7)
 ЗП ЯЧВСТ-В1(М7)
 УИА -4(М6)
 УИА -24(М12)
 СЧ (М3)
 И П77777
 ЗП R1-В1(М7)     А ВВОДА
 СДА 64-33
 ЗП РЕЖА5-В1(М7) АДРЕС ВВОДА
 СЧ
 ЗП R2-В1(М7)     ЧИСЛО ПОВТ.СИМВОЛА
 ЗП МЛРВБЩ-В1(М7) СИМВ.ДЛЯ СРАВН.
 СЧ НОМЕРЗ-В1(М7)
 ПО А3А           НЕ ФОРМИРОВАНИЕ
 СЧ
 ЗП СЧСИМ-В1(М7)
 ПБ ПРИМА4
А3А СЧ СТПАСП-В1(М7)
 И Е15
 ПЕ ПРИМА4        А3 ИЗ СТАНД.ПАСПОРТА
 СЧ ТИПУСТ-В1(М7)
 ПО ПРИМА4        ВВОД С ПЛ
А3В УИА А3В-В6(М13)
 СЧ СЧСИМ-В1(М7)
 СДА 64+11
 НТЖ П17
 ПЕ ЗАСИМВ-В1(М7) НЕ КОНЕЦ СТРОКИ
 ПБ ПРИМА4
А3Б НТЖ ПОВТЗП-В1(М7)
 ПЕ НЕПОВТ        ОТЛИЧ.СИМВ.
 СЧ R2-В1(М7)
 НТЖ П377
 ПО НЕПОВТ        ЧИСЛО ОДИН.СИМВ.=377
 СЧ R2-В1(М7)
 СЛЦ Е1
 ЗП R2-В1(М7)     ЧИСЛО ПОВТОРОВ
 СЧ R2-В1(М7)
 НТЖ П30
 ПЕ РЕДА3
 СЧ R-В1(М7)
 НТЖ Е7
 ПО КОНЕЦ
РЕДА3 СЧ МЛРВБЩ-В1(М7)
 СДА 64-8
 ИЛИ R-В1(М7)
 ЗП МЛРВБЩ-В1(М7) ДЛЯ ПРОВ.НА КОНЕЦ А3
 МОДА ПРИМА4
 ЦИКЛ (М11)         НА 5 СИМВ.
 УИА РЕДА3А-В6(М13)
 СЧ (М3)
 ИЛИ Е44
 ЗП (М3)          В ВВ.СЛОВО ПРИЗН.А3
 СЧ R1-В1(М7)
 СДА 64+10
 ПБ ПРОВЧЛ        НА ПРОБУ ЧУЖ.ЛИСТА
НЕПОВТ СЧ R2-В1(М7)
 ПО ЗАНОСН        НЕТ ПОВТОРЕН.
 УИА НЕПОВТ-В6(М5)
 ИЛИ Е9
НЕПОВ ЗП РЯ
 СЧ (М2)
 СДА 64-9
 ИЛИ РЯ
 ЗП (М2)          УПАКОВ.В СЛОВО
 СЧ
 ЗП R2-В1(М7)
 МОДА (М1)
 МОДА (М5)
 ЦИКЛ (М6)          НА 5 СИМВ.ПО 9 РАЗР.
 УИА -4(М6)
 ПБ БЗРМ
А4А СЧ Е37
 ИЛИ ЯЧВСТ-В1(М7)
 ЗП ЯЧВСТ-В1(М7)  ЛЮБОЙ СИМВОЛ
ЛА222 УИА ЛА222В-В6(М13)
 ПБ ЗАСИМВ-В1(М7)
ЛА218 УИА ЛА218А-В6(М13) ПРИЕМ А1
 ПБ ЗАСИМВ-В1(М7) ЗА СИМВОЛОМ
ЛА172А УИА ЛА172-В6(М13)
 ПБ ЗАСИМВ-В1(М7) ЗА СИМВОЛОМ
ЛА172 НОП ------
 МОДА ЧУЖЛИС-В1(М7)
 УИА (М15)
 СЧИ М15
 ПВ ЗАПВЫХ(М15)
 РЖА 3
 СЧ ЯЧВСТ-В1(М7)
 И Е36
 ПЕ ФОРМ          ФОРМИР.1-ГО ТИПА
 СЧ R-В1(М7)
 УИ М15           СИМВОЛ
 СЛИА -’52’(М15)
 МОДА ЛА188
 ПИНО (М15)         НЕ К
 УИА -1(М12)
 УИА 8(М10)
 УИА -8(М11)
ЛА175 УИА КОМ-В6(М13)
 ПБ ЗАСИМВ-В1(М7)
КОМ СДА 64+5
 ПЕ ЛА182         БУКВА
 СЧ R-В1(М7)
 НТЖ П17
 ПО ЛА175         ПРОПУСК ПРОБЕЛА
 СЧ R-В1(М7)
 СДА 64+3
 ПО ЛА176         ЦИФРА ВОСЬМ.
 УИА 13(М16)       СИМВОЛ>7
 УИА ЛА176-В6(М13)
 ПБ ВЫДОШ-В1(М7)  ВЫДАЧА ОШИБКИ
ЛА176 СЛИ М10(М11)      1-Я ЦИФРА ИР
 МОДА ЛА179
 ПИНО (М10)         НЕТ
 СЧ R-В1(М7)
 СДА 64+1
 УИА -1(М10)
 ПО ЛАС
 УИА 10(М16)       ЗАПР.ИП
 УИА ЛАС-В6(М13)
 ПБ ВЫДОШ-В1(М7)
ЛАС СЧ (М2)
 СДА 64(М10)
ЛА178 ИЛИ R-В1(М7)
 ЗП (М2)          ПРИФОРМ.СИМВОЛА К КОМАНДЕ
 УИА 8(М10)
 МОДА ЛА175
 ЦИКЛ (М11)         НА 1-Ю ПОЛОВИНУ КОМАНДЫ(СЛОВА)
 УИА -8(М11)
 МОДА ЛА175
 ЦИКЛ (М12)         НА 2-Ю
ЛА178А УИА ЛА172А-В6(М5)
 ПБ БЗРМ          НА ЗАПИСЬ В АБЗАЦ
ЛА179 СЛИА -2(М10)
 МОДА ЛА181
 ПИНО (М10)         НЕ 1-Я ЦИФРА КОП
 СЧ R-В1(М7)
 СДА 64+2
 УИА -2(М10)
 ПО ЛАС
 УИА 11(М16)       НЕСУЩ.КОД ОПЕРАЦИИ
 УИА ЛАС-В6(М13)
 ПБ ВЫДОШ-В1(М7)
ЛА181 УИА -3(М10)
 ПБ ЛАС
ЛА182 СЛИА 8(М11)
 МОДА ЛА186
 ПИНО (М11)         НЕ 9 ЦИФР
 МОДА ЛА200
 ПИО (М12)         ОДНА ПОЛОВИНА КОМАНДЫ
 СЛИА -’56’(М15)
 МОДА ЛА186
 ПИНО (М15)         НЕ О:НА ОШИБКУ
 УИА -2(М12)
ЛА183 УИА ЗАН-В6(М13)
 ПБ ЗАСИМВ-В1(М7)
ЗАН СЧ R2-В1(М7)
 СДА 64-8
 ИЛИ R-В1(М7)
 ЗП R2-В1(М7)
 МОДА ЗАСИМВ-В1(М7)
 ЦИКЛ (М12)
 СДА 64-24
 СДА 64+24
 НТЖ СЛНЕЦ
 ПЕ ЛА184         НЕДОПУСТ.СИМВОЛ
КОНЕЦ СЧ Е47           ПРИЗНАК КОНЦА ВСЕХ МАССИВОВ
 ИЛИ Е46
 ИЛИ (М3)
 ЗП (М3)
 СЧ Е42
 ИЛИ ЯЧВСТ-В1(М7)
 ЗП ЯЧВСТ-В1(М7)
 ПБ ЛА226
ЛА186 УИА 7(М16)        ЧИСЛО СИМВ. 18
ЛА184А УИА ЛА200-В6(М13)
 ПБ ВЫДОШ-В1(М7)
ЛА165 СЧ МЛРВБЩ-В1(М7)
 И Е23
 ПО ЛА167         НЕТ МАРКЕРА
 СЧ R2-В1(М7)
 СБР РАЗКА         КОНТР.РАЗРЯДЫ
 ЗП РЯ
 СЧ R2-В1(М7)
 СБР РАЗАЛ
 ЗП R2-В1(М7)     В ВИДЕ ЛЕВОЙ КОМАГДЫ
 ЧЕД 0
 СДА 64-43
 НТЖ РЯ
 ПО ЛА168         ВЕРНЫЙ КОНТРОЛЬ
 УИА 12(М16)
ЛА166 УИА ЛА168-В6(М13)
 ПБ ВЫДОШ-В1(М7)
ЛА167 УИА 4(М16)        НЕТ МАРКЕРА
 ПБ ЛА166
ЛА168 СЧ МЛРВБЩ-В1(М7)
 СБР АДРАЛ
 СДА 64+33
 УИА А2А1-В6(М5)
 ПЕ ЛА204А        АДРЕС,Т.Е.ВВОДНОЕ СЛОВО
 СЧ ЯЧВСТ-В1(М7)
 И Е44
 ПО ЛА170         ПРАВАЯ КОМАНДА
 НТЖ ЯЧВСТ-В1(М7)
 ЗП ЯЧВСТ-В1(М7)  ПРИЗН.ПРАВ.КОМАНДЫ
 СЧ R2-В1(М7)
 ЗП (М2)
 ПБ А2А1
ЛА170 СЧ Е44           ПРИЗНАК ЛЕВОЙ КОМАНДЫ
 ИЛИ ЯЧВСТ-В1(М7)
 ЗП ЯЧВСТ-В1(М7)
 СЧ R2-В1(М7)
 СДА 64+24
 ИЛИ (М2)
 ЗП (М2)
 ПБ БЗРМ
ЛА218А СЧ R1-В1(М7)
 СДА 64-8
 ИЛИ R-В1(М7)
 ЗП R1-В1(М7)
 И П77777
 НТЖ КОНБЦ
 ПЕ ЛА221         НЕ КОНЕЦ А1
 СЧ (М2)
ЛА219 СДА 64-8
 ЗП (М2)          ДОТАЛКИВАНИЕ
ЛА219А МОДА ЛА219
 ЦИКЛ (М12)
 ПО ЛА172А        ПРОПУСК СЛОВА С РОМБИКОМ
 ПБ ЛА178А
ЛА221 СЧ (М2)
 СДА 64-8
 ИЛИ R-В1(М7)
 ЗП (М2)
 МОДА ЗАСИМВ-В1(М7)
 ЦИКЛ (М12)
 УИА А1-В6(М5)
 ПБ БЗРМ
ЛА222В СЧ (М2)          ПРИЕМ А4
 СДА 64-8
 ИЛИ R-В1(М7)
 ЗП (М2)
 СЧ R-В1(М7)
 НТЖ П37
 ПО ЛА222Д        СИМВОЛ-ПРИЗНАК КОНЦА
 УИА -5(М11)
ЛА222А МОДА ЛА222
 ЦИКЛ (М12)         НА 6 СИМВ.
 УИА А4-В6(М5)
 ПБ БЗРМ
ЛА222Д МОДА ЛА222А
 ЦИКЛ (М11)         НЕ КОНЕЦ А4
 СЧ ЯЧВСТ-В1(М7)
 НТЖ Е37
 ЗП ЯЧВСТ-В1(М7)  УНИЧТ.ПРИЗН.А0
 СЧ (М2)
ЛА222Г МОДА ЛА222Б
 ЦИКЛ (М12)
 УИА КОНЕЦ-В6(М5)
 ПБ З2
ЛА222Б СДА 64-8
 ПБ ЛА222Г
ЗАНОСН УИА РЕДА3-В6(М5)
 СЧ R-В1(М7)
 ЗП ПОВТЗП-В1(М7) ДЛЯ СРАВНЕНИЯ
 МОДА НЕПОВ
 ПИНО (М12)         НЕ КОНЕЦ А3
 УИА ЗАНОСН-В6(М5)
 УИИ М17(М6)
 СЛИА 4(М17)
 МОДА НЕПОВ
 ПИНО (М17)         ДОДВИГ СЛОВА В КОНЦЕ А3
РЕДА4Б СЧ П7
 СДА 64-36
РЕДА И ЯЧВСТ-В1(М7)
 НТЖ ЯЧВСТ-В1(М7)  УНИЧТ.ПРИЗНАКА А3
РЕДА4В ЗП ЯЧВСТ-В1(М7)
 ПБ ЛА172А        НА РЕДАКТ.СЛЕД.ИНФ.
А0 СЧ (М3)
 ЗП МЛРВБЩ-В1(М7) А НАЧ.ВВОДА ДЛЯ АВТОК.ИТМ
 СЧ П377
 ЗП R-В1(М7)
ПРИ4 СЧ R-В1(М7)
 ЗП R1-В1(М7)     ПРЕДЫД.СИМВОЛ
 УИА ПРИ4Д-В6(М13)
 ПБ ЗАСИМВ-В1(М7) ЗА СИМВОЛОМ
ПРИЕМ4 СЧ R1-В1(М7)
 СДА 64-8
 ИЛИ R-В1(М7)
 НТЖ КОНБЦ
 ПЕ ПРИ4          НЕ КОНЕЦ ИНФ.К А0(АВТ.ИТМ)
 СЧ (М2)
ПРИ4А СДА 64-8
 МОДА ПРИ4А
 ЦИКЛ (М12)
 ПО ПРИ4Б
 УИА ПРИ4Б-В6(М5)
 ПБ З2
ПРИ4Б СЧ Е2
 УИА ПРИ4В-В6(М5)
ЛА204А ЗП (М2)
 ПБ ВВС           ВВ.СЛОВО
ПРИ4В СЧ АВВОДА-В1(М7)
 СДА 64+33
 ЗП R2-В1(М7)
 СДА 64+10
 ЗП РЯ
 СЧ МЛРВБЩ-В1(М7)
 И Е15П11
 СДА 64+4
 ИЛИ РЯ
 УИА ПРИ4Г-В6(М5)
З2 ЗП (М2)
 ПБ БЗРМ
ПРИ4Г СЧ R2-В1(М7)
 ЗП (М2)          3-Я ЯЧ-КА ДЛЯ АВТОК.ИТМ
 УИА ЛА172А-В6(М5)
 ПБ БЗРМ
ПРИ4Д СЧ R1-В1(М7)
 НТЖ П377
 ПО ПРИЕМ4        ПУСТОЙ СИМВОЛ
 РЖА 3
 СЧ R1-В1(М7)
 НТЖ П37
 ПО ПРИ4И         СИМВОЛ=:;НА ПРОБУ СКЛЕИВАНИЯ
 СЧ R1-В1(М7)
 ВЧ П140
 ПЕ ПРИ4Ж         СИМВОЛ<140
 ВЧ Е6
 УИА 16(М10)       НОМЕР СТРОКИ В ТАБЛ.СИМВ.
ПРИ4Е УИ М16           НОМЕР СИМВ.В СТРОКЕ
 МОДА ИТАБЛ
 СЧ (М10)
 СБР КОНСБР+6(М16)
 СДА 64+40
 ЗП РЯ            СИМВОЛ В КОДЕ АВТОК.ИТМ
 СЧ (М2)
 СДА 64-8
 ИЛИ РЯ
 ЗП (М2)
 МОДА ПРИЕМ4
 ЦИКЛ (М12)         НА 6 СИМВОЛОВ
 УИА -5(М12)
 УИА ПРИЕМ4-В6(М5)
 ПБ БЗРМ
ПРИ4Ж УИА (М10)
 СЧ R1-В1(М7)
ПРИ4З ВЧ П6            ПОИСК НОМ.СИМВОЛА И СТРОКИ В ТАБЛ.ИТМ
 ПЕ ПРИ4Е
 СЛИА 1(М10)
 ПБ ПРИ4З
ПРИ4И УИА 16(М10)       СКЛЕЙКА СИМВ.
 СЧ R-В1(М7)
 УИА -5(М16)
 НТЖ П37
 ПО ПРИ4К         СИМВ.:
 НТЖ ПР13
 УИА -6(М16)
 ПО ПРИ4К         СИМВ:°
 УИА -4(М16)
 НТЖ П12
 ПО ПРИ4К         СИМВ.>
 НТЖ П3
 УИА -3(М16)
 ПЕ ПРИ4Ж         НЕ СИМВ. ,Т.Е.НЕТ СКЛЕЙКИ
ПРИ4К СЧ П377          СКЛЕИВ.СИМВ.В ОДИН
 ЗП R-В1(М7)
 СЧИ М16
 ПБ ПРИ4Е
ВЫБОРС СЧ АВЫБОР-В1(М7)
 СЛЦ Е1
 ЗП АВЫБОР-В1(М7) А МАССИВА
 УИ М16
 МОДА ЧУЖЛИС-В1(М7)
 ПИО (М16)
 СЧ СТВУ-В1(М7)
 И Е26
 ПО ВЫБОР     НЕ С МБ
 СЧИ М16
 И П1777
 ПЕ СЛУЖ    НЕ КОНЕЦ ЛИСТА
 СЧ СТВУ-В1(М7)
 ИЛИ Е22
 ЗП СТВУ-В1(М7)   1 ТИП
 ПБ НИКМБ1-В1(М7)  С МБ
ВЫБОР СЧ НОМЕРЗ-В1(М7)
 И ЕЗП
 ПО СЛУЖ          СЛУЖ.ЗАДАЧА
 УИА ’2000’
СЛУЖ СЧ (М16)
 УИА 3
 ЗП R1-В1(М7)
 ПБ (М13)
ДОСИМВ НТЖ П17
 ПО ЗАСИМВ-В1(М7)
 НТЖ =В’102’
 ПО ЗАСИМВ-В1(М7)
ДОСИМ СЧ СОХИ13-В1(М7)
 УИ М13
 ПБ ВЫБ6-В1(М7)
НАЧАЛО СЧ П37
 СДА 64-21
 И ЯЧОТЛ-В1(М7)
 ПО ЛА160А   НЕТ ФИЗО
 СЧ ЯЧМЛ-В1(М7)
 И Е48П41
 СДА 64+25
 ИЛИ ЯЧОТЛ-В1(М7)
 ЗП ЯЧОТЛ-В1(М7)  1-Е МУ ДЛЯ ФИЗОБМЕНА
ЛА160А НОП
 СЧИ М2
 ВЧ Е1
 И П177
 СДА 64-24    L МАС0
 ИЛИ Е48
 ИЛИ Е47
 ЗП (М3)
 СЧ ЯЧВСТ-В1(М7)
 ИЛИ Е33
 ЗП ЯЧВСТ-В1(М7)  ПРИЗН.РЕДАКТ.МАССИВА
НАЧАЛ СЧ ЯЧВСТ-В1(М7)
 И Е14
 ПО НЕСТП         НЕТ СТПАСП
 СЧ ЯЧВСТ-В1(М7)
 ИЛИ Е15
 ЗП ЯЧВСТ-В1(М7)  ПРИЗ.ДОБАВКИ К СТПАСП
 СЧИ М4
 ЗП ПОЛЕПК-В1(М7) СОХР.М4
 СЧ СТПАСП-В1(М7)
 СДА 64+33
 УИ М4
 ПБ ЛА172А        ЗА СИМВОЛОМ
НЕСТП СЧИ М13
 ЗП СОХИ13-В1(М7)
 УИА ДОСИМВ-В6(М13) ПРОПУСК НАДЧЕРКИВАНИЯ ПОСЛЕ Е
 СЧ НОМЕРЗ-В1(М7)
 ПО ЗАСИМВ-В1(М7) НЕ ФОРМИРОВАНИЕ
 СЧ СТВУ-В1(М7)
 И Е28
 ПЕ ЗАСИМВ-В1(М7) ОТ ЭК.ФОРМИР.2-ГО ТИПА
 СЧ ЯЧВСТ-В1(М7)
 ИЛИ Е36
 ЗП ЯЧВСТ-В1(М7)  ПРИЗ.ФОРМ.1-ГО ТИПА
 СЧ П377
 ЗП R-В1(М7)
 ПБ ДОСИМ
ФОРМ МОДА ВЫБОРС
 ПВ (М13)
 НТЖ ЕКОНЕЦ
 ПО КОНЕЦ         КОНЕЦ ФОРМИРОВАНИЯ
 И Е48П41
 ПЕ ФОРМ1         НЕ Е
 УИА -’14’(М15)
 ПБ ЛА209
ФОРМ1 СЧ R1-В1(М7)     ФОРМ.ВВС
 И П77777
 ЗП (М2)
 УИА (М12)
 УИА ФОРМ2-В6(М5)
 ПБ ВВС
ФОРМ2 МОДА ВЫБОРС
 ПВ (М13)         ЗА СЛОВОМ
 НТЖ КМАСИВ
 ПО ФОРМ          СЛЕДОМ ВВС ИЛИ Е,ИЛИ ЕКОНЕЦ
 СЧ R1-В1(М7)
 ЗП (М2)
БЗРМ СЧИ М2
 ЗП РЯ
 СЧИ М3
 НТЖ РЯ
 ПО ЛА224         ВВОДНОЕ СЛОВО
 СЧ (М3)
 И Е47
 УИА ЛА224-В6(М13)
 ПО ЛА223         НЕ ПРИЗНАК Е
 НТЖ (М3)
 ЗП (М3)          УНИЧТ.ПРИЗН.Е
 УИА 15(М16)
 ПБ ВЫДОШ-В1(М7)  ПОСЛЕ Е НЕ ВВ.СЛОВО
ЛА223 СЧ (М3)
 СДА 64+24
 СЛЦ (М3)
 СДА 64-33
 ЗП АВВОДА-В1(М7)
 СЧ (М3)
 СЛЦ Е25
 ЗП (М3)
 СЧ АВВОДА-В1(М7)
 СДА 64+43
ПРОВЧЛ УИ М17           НОМЕР ЛИСТА
 РЖА 3
 УИА ПР1-В6(М16)
 ПБ ПРОЗУ-В1(М7)
ПР1 ПО ЛА223А ЛИСТ ЕСТЬ
 СЧ ЯЧВСТ-В1(М7)
 И Е43
 ПЕ ВЫХОДЛ        ОБ ЭТОМ ЛИСТЕ БЫЛА ВЫДАЧА НА ТТ
 УИА 5(М16)        ЧУЖОЙ ЛИСТ
 СЧ Е43
 ИЛИ ЯЧВСТ-В1(М7)
 ЗП ЯЧВСТ-В1(М7)
 И =М37В’3’
 ПО ВЫДОШ-В1(М7)  НЕ А3
 СЧ ЯЧВСТ-В1(М7)
 И Е8П16
 НТЖ Е8
 ПО ВЫДОШ-В1(М7)  1-ЫЙ МАССИВ И С А3:ЧУЖ.ЛИСТ-ОШ-КА
ЛА223А СЧ ВСЕЕД
 НТЖ Е43
 И ЯЧВСТ-В1(М7)
 ЗП ЯЧВСТ-В1(М7)
ВЫХОДЛ МОДА (М13)
 ПБ (М1)
ЛА224 СЛИА 1(М2)
 СЧИ М2
 И П377
 ПО ЛА226         АБЗАЦ ЗАПОЛНЕН
ЛА225 СЧ R-В1(М7)
 УИ М15
 МОДА (М1)
 ПБ (М5)          ВЫХОД ИЗ БЛОКА ЗАПИСИ В МАССИВ
ЛА226 СЧ (М3)
 СДА 64+24
 СЛЦ (М3)
 УИ М13
 СЧ ЯЧВСТ-В1(М7)
 СДА 64+44
 ПО ЛА227         НЕ БЫЛО ОШИБОК ВВОДА
 СЧ (М3)
 И Е46
 ПЕ ОШВВД         КОНЕЦ ВВОДА
ЛА230 СЧ ЯЧВСТ-В1(М7)
 И Е8П16         НОМЕР МАССИВА
 СДА 64-8
 ЗП РЯ
 УИА 3
 УИИ М3(М7)
 СЛИА 768(М3)
 УИИ М2(М3)
 СЛИА 1(М2)
 СЧИ М13
 ИЛИ РЯ
 ИЛИ Е45
 ЗП (М3)          НОВОЕ ВВОДНОЕ СЛОВО
 УИА -254(М16)
ЛА230А СЧ
 МОДА (М16)
 ЗП 254(М2)
 МОДА ЛА230А
 ЦИКЛ (М16)         ОЧИСТКА АБЗАЦА ПОД ОТРЕДАКТ.МАССИВ
 ПБ ЛА225         НА ДАЛЬН.РЕДАКТИРОВАНИЕ
ЛА227 СЧ ЯЧВСТ-В1(М7)
 И П177
 ИЛИ НОММБ
 ЗП РЯ
 МОДА 768(М7)
 УИА (М16)
 СЧИ М16
 СДА 64-2
 ИЛИ РЯ
 ПВ ФИЗОБМ(М16)
 СЧ ЯЧВСТ-В1(М7)
 И П3
 НТЖ П3
 ПО ЛА231А        ТРАКТ ПОЛНЫЙ
 СЧ ЯЧВСТ-В1(М7)
 СЛЦ Е1
 ЗП ЯЧВСТ-В1(М7)  НОМЕР СЕКТОРА+1
 СЧ (М3)
 И Е46
 ПО ЛА230         НЕ КОНЕЦ ВВОДА
ЛА231А УИА -30(М3)
 СЧ 0
 УИА 3
 ЗП И14    ЛИСТ НЕ БРАЛИ
НЕОСД СЧ ЯЧВСТ-В1(М7)
 И Е42
 ПЕ КОНЕЦВ
 СЧ СТВУ-В1(М7)
 И Е26
 ПО КОНЕЦВ   НЕ С МБ
 СЧИ М14
 ЗП И14
 СЧ 0
 ПВ ТБУФ(М15)
 СДА 64-8
 УИ М14
КОНЕЦВ СЧИ М14
 СДА 64-2
 ИЛИ ИНФЗОН
 СЛЦ НОММЛ1
 ЗП РЯ
 ПВ ЕNQ(М15)      ЗАХВАТ ДИСКА
ПОВТ СЧ РЯ
 ПВ ФИЗОБМ(М16)   ЧТ.С МБ КАТАЛОГА ЗОН
 УИА 3
 УИА 1(М15)
 СЧ ЯЧКАТ+5-В1(М7)
 И П1777
 ПЕ ЗП2           БЫЛА ЗАПИСЬ НА МЛ
 МОДА ЯЧКАТ+5-В1(М7)
 УИА (М2)
 СЛИА -512(М2)
 АIF (∧ВS ЕQ 0).ЗП1
ЗП1 СЧ 0
 УИА -’727’(М16)
ЗП3 ЗП КОЛВО
 СЧ Е40
 ИЛИ Е16
 МОДА (М14)
 И ’1767’(М16)
 ЧЕД
 СЛЦ КОЛВО
 МОДА ЗП3
 ЦИКЛ (М16)
 ВЧ Е9
 ВЧ Е7
 ПЕ ЗП     ЗАНЯТО ВВОДОМ < 500 ЗОН
 СЧ НОМЕРЗ-В1(М7)
 ПО НЕМЕ  НЕТ МЕСТА
 И Е6
 ПЕ НЕМЕ  НЕТ МЕСТА
ДИСП70 ВНЕШ КОЛВО
.ЗП1 АNОР
ЗП МОДА (М14)
 СЧ 512+30(М3)
 И Е48П17
 НТЖ Е48П17
 ПО ПОИСКЗ
 НЕД П77777
 УИ М16
 МОДА 512+30(М3)
 СЧ (М14)
 ИЛИ Е48(М16)
 МОДА (М14)
 ЗП 512+30(М3)
 СЛИА 30(М3)
 СЧИ М3
 СДА 64-5
 УИ М3
 СЛИ М3(М16)
 СЧИ М3
 И Е1
 УИ М16
 СЧИ М3
 СД КСН(М15)
 ИЛИ 512(М2)
 ЗП 512(М2)
 СЧИ М3
 СДА 64+1
 УИ М15
 СЧ Е16
 СД КСН(М16)
 СЛИ М15(М14)
 ИЛИ 512(М15)
 ЗП 512(М15)
 СЧ РЯ
 ПВ ФИЗЗП(М16)   ЗП КАТ.ЗОН
 СЧ РЯ
 ПВ DЕQ(М15)      ВОЗВР.ДИСКА
 СЧИ М14
 СДА 64-2
 ЗП РЯ
 СЧ ЯЧВСТ-В1(М7)
 И П177
 ИЛИ НОММБ
 ИЛИ Е18
 ИЛИ Е19
 ИЛИ РЯ
 ПВ ФИЗОБМ(М16)   ЧТ. ТРАКТА С МБ
 УИА 3
 УИА -ДЛЗМ+11(М16)
 СЧ СТПАСП-В1(М7)
 И Е32
 ПЕ БЫЛАЗП        НЕ 1-Я ЗОНА
 СЧ СТПАСП-В1(М7)
 ИЛИ Е32
 ЗП СТПАСП-В1(М7) ПРИЗ.НЕ ПЕРВОЙ ЗОНЫ
ПЕРЯЧК МОДА ШИФР-В1(М7)   ПЕРЕНОС ПАСП.ДАННЫХ
 СЧ ДЛЗМ-11(М16)
 МОДА (М14)
 ЗП ДЛЗМ-10(М16)
 МОДА ПЕРЯЧК
 ЦИКЛ (М16)
 СЧ ЯЧКАТ+ЯЧГЛАВ-В1(М7)
 И ЕГЛАВ
 ПО ЗПШИФГ
 СЧ ЯЧВСТ1-В1(М7)
ЗПШИФГ ЗП ШИФРГЗ(М14)
БЫЛАЗП СЧИ М3
 СДА 64-24
 СЛЦ НОММЛ2
 ИЛИ РЯ
 ПВ ФИЗОБМ(М16)   ЗАПИСЬ НА БУФЕР ВВОДА
 УИА 3
 СЧ ЯЧВСТ-В1(М7)
 ИЛИ П3
 НТЖ П3
 ЗП ЯЧВСТ-В1(М7)
 И Е42
 ПО НЕКОНЕ
 СЧ СУМВВД-В1(М7)
 СДА 64+24
 СЛЦ СУМВВД-В1(М7)
 ЗП СУМВВД-В1(М7)
 СДА 64+12
 СЛЦ СУМВВД-В1(М7)
 И П7777
 СДА 64-12
 ИЛИ ЯЧКАТ+6-В1(М7)
 ЗП ЯЧКАТ+6-В1(М7)
 ПБ ВУВВД
ПОИСКЗ МОДА ЗП
 ЦИКЛ (М3)
НЕМЕ МОДА НЕТМСТ
 УИА (М3)
ВЛ СЧ И14
 ПО (М3)
 СЧИ М14
 СДА 64-2
 ИЛИ Е19
 ПВ ТБУФ(М15)
 СЧ И14
 УИ М14
 ПБ (М3)
НЕТМСТ СЧ РЯ
 ПВ DЕQ(М15)
НЕТМС СЧ ЯЧВСТ-В1(М7)
 ИЛИ Е34
 ЗП ЯЧВСТ-В1(М7)  НЕТ МЕСТА
 ПБ ОШВВД
ЗП2 УИ М16
 СДА 64+1
 УИ М2
 СЛИ М2(М14)
 СЧИ М16
 И Е1
 УИ М15
 СЧ 512(М2)
 СД КСО(М15)
 И П1777
 ПЕ ЗП2
 АIF (∧ВS ЕQ 0).ЗП2
 ПБ ЗП1
 АGО .ЗПП2
.ЗП2 АNОР
 ПБ ЗП
.ЗПП2 АNОР
ЕСТЬЗД СЧ РЯ
 ПВ DЕQ(М15)
НЕВОВР УИА 9(М16)
НЕТМА УИА ОШВВД-В6(М13)
 ПБ ВЫДОШ-В1(М7)
НЕКОНЕ СЧ НОМЕРЗ-В1(М7)
 ПЕ ЛИСТБР   ФОРМ
 СЧ РЯСЛК-В1(М7)
 ПВ ФИЗОБМ(М16)
 УИА 3
 ПБ ЛА230
ЛИСТБР МОДА ВЛ
 ПВ (М3)
 УИА 3
 ПБ ЛА230
И14 ПАМ 1
СЛНЕЦ КОНД П’НЕЦ’
РЯ ПАМ 1
КОНБЦ КОНД В’55127’
РАЗАЛ КОНД В’36757367570’
АДРАЛ КОНД В’5277173’
РАЗКА КОНД М33В’57’
Ч10 КОНД Е’10’
Ч01 КОНД В’3654631463146315’
Е8П16 КОНД М7В’77’
ИТАБЛ КОНК К’001202003’
 КОНК К’000400400’
 КОНК К’032030411’
 КОНК К’002003406’
 КОНК К’060677017’
 КОНК К’021623067’
 КОНК К’022622457’
 КОНК К’032424476’
 КОНК К’061465041’
 КОНК К’031074066’
 КОНК К’142711723’
 КОНК К’112027147’
 КОНК К’152546321’
 КОНК К’143710322’
 КОНК К’143301605’
 КОНК К’101744636’
 КОНК К’111753225’
 КОНК К’100307215’
 КОНК К’151752701’
 КОНК К’151154716’
 КОНК К’102713222’
 КОНК К’153356320’
 КОНК К’102516606’
 КОНК К’102315214’
 КОНК К’022110631’
 КОНК К’103716224’
 КОНК К’061226471’
 КОНК К’030026074’
 КОНК К’062022042’
 КОНК К’030436465’
 КОНК К’053657135’
 КОНК К’053031443’
ИТАБ КОНД В’2007336661140’
КГ КОНД В’37175’
ППР ПАМ 1
ИНФЗОН КОНД М24В’445’М17В’1’ КАТАЛОГ ЗОН
КМАСИВ КОНД П’_!_!_!’
СПЕЦКГ КОНД В’37050’
ПОРВВО ПАМ 1
 ЛИТЕР
 ЭКВИВ (СМНАОТ,12),(НЕИЩ,12),(ВРЕД,13)
 ЭКВИВ (РЯ3,ВУВВД+1),(БУМАГА,РЯ3),(ЯЧКУ,РЯ3)
 ЭКВИВ (КПЛАН,’700’),(ЗАМОР,832),(ТБШЭЗ,580),(МАСКО,425)
 ЭКВИВ (МАСКАЗ,424),(МАСКЭ,423),(ВА99,422),(МАСКД,421)
 ЭКВИВ (МАСКШ,420),(МАСКИН,419),(ВА98,418),(МАСКИ,417)
 ЭКВИВ (РЯ4,ВУВВД+3),(РЯ2,ВУВВД+2),(ЧИСЛОБ,95)
 ЭКВИВ (В7,В6В7),(НАЧЗ,512)
ДИСП70 ВНЕШ ВРЕМЯ,Е24П1,ПРЕДЕЛ,СТПРИО,Е39П25,Е44П42,ИЗ10В8
ДИСП70 ВНЕШ ТЗНМЛ,П7777,ПЕРЕВ,ГОД,П30,ЯЧВ1,МГРП,ТВ67А,ГАШПРП
ДИСП70 ВНЕШ Е15П11,П140,П6,П12,ПР13,П1777,(Л64,ЭК9),ВСЕЕД
ДИСП70 ВНЕШ ВРКЛАС,ПОРСЧТ,ПОРОТЛ,Е24П48,Е44Е43,П177,НОММБ,
ДИСП70 ВНЕШ ТБУФ,НОММЛ1,КСО
ДИСП70 ВНЕШ КСН,Е48П25,НОММЛ2,Е46П48,НАПРМГ,П37,П377,П77777
ДИСП70 ВНЕШ П17,ЕЗП,NБЛ
ДИСП70 ВНЕШ П7,П77,П5,(П12R,R),П3,ЛИСТА
ВЗУ ВНЕШ ФИЗОБМ,ФИЗЗП
КАЧКА ВНЕШ Е48П17
ДИСКИ ВНЕШ П11,DЕQ,ЕNQ
КИТ ВНЕШ Е1П25,КОНСБР,Е48П41
МОТТ ВНЕШ ПРОБ6
СОСТАВ ВНЕШ ЕГЛАВ,ЯЧГЛАВ
В1К ВНЕШ НУЖДА,ПРИЗН1,ЯЧВСТ1,Е17П28,ОСВЗН,УСТ,БУФПР,ОШИБКИ
В1К ВНЕШ ДЛЗМ,ПОВТЗП,СУМВВД,ЗАСИМВ,(В1,В1К),R,ВЫДОШ
В1К ВНЕШ ЯЧВСТ,ЯЧКАТ,ШКЛИСТ,R2,R1,АВВОДА,МЛРВБЩ,СТВУ,ТИПУСТ
В1К ВНЕШ НОМЕРЗ,ШИФР,НИКМБ1,СЧСИМ,ОБЗНП,ПОЛЕПК,ВЫБ6,АВЫБОР
В1К ВНЕШ ЧУЖЛИС,РЯСЛК,СТПАСП,СОХИ13,РЕЖА5,ПРОЗУ,ШКАНУВ,ШИФРГЗ
В1К ВНЕШ СО13,СО6,ИИ
В1К ВНЕШ СОИ
БОЭК1 ВНЕШ ЗАПВЫХ
В1К ВНЕШ ЯЧМЛ,ЯЧОТЛ,К144
 ВХОД НАЧАЛ,ВЫДТ,РЯ
 ВХОД ОСВМЛ,СБОЙВУ,ВУВВД,ЗАМ,ОШВВД,НАЧАЛО,ЛА172,СЛУЖ,НЕМЕ,РЯ2
  ВХОД ВОЗВРТ,ВВД,ВЫДТТ1,ЭКФМ,ОБЗНПК,ОШВВ
В6 ЭКВ В6В7
 ФИНИШ
*ЧП
         ФИНИШ ,             В ВАШЕМ ФИНИШЕ ОШИБКА ИЛИ ЕГО НЕТ

 МАСRО
∧М Э47 ∧А
∧М КК ’047’,∧А
 ПАМ 0
 МЕND
РЗДСВС СТАРТ 1024
 РЖА 3
 ПБ ВХОД
*
* МОДИФИКАТОРЫ
*
М1 ЭКВ 1            РАБОЧИЙ
М2 ЭКВ 2            РАБОЧИЙ
М3 ЭКВ 3            РАБ
М4 ЭКВ 4            АДРЕС ЗАПИСИ В ТДД
М5 ЭКВ 5            ТИП МД
М6 ЭКВ 6
М7 ЭКВ 7            ДЛЯ ПРИНУДИТЕЛЬНО ДЕФ.ЗОНЫ
М10 ЭКВ 8            ПЕЧАТЬ
М11 ЭКВ 9            ЦИКЛ ДЛЯ ПОВТОРА ЧТЕНИЯ
М12 ЭКВ 10           КОД ОПЕРАЦИИ
R2 ЭКВ 11
R0 ЭКВ 12
М15 ЭКВ 13            ВХОДЫ В П/П
W ЭКВ 14
S ЭКВ 15
*
* ДОЛЖНЫ БЫТЬ ЗАДАНЫ
*
НПАК ЭКВ 1       ВОСЬМ. НОМЕР ПАКЕТА
ЛНУ ЭКВ 2        ВОСЬМ. ЛОГИЧЕСКИЙ НОМЕР УСТРОЙСТВА
ДЗОН ЭКВ 3       0 - ВЕСЬ ПАКЕТ
*             Е48, 25-47 - НАЧ ЗОНА МАТ, 1-23 - КОН. ЗОНА МАТ.
ДЕФ ЭКВ 4
*            ПЕРЕМЕННЫЕ
*
ЛИСТУП ЭКВ ’70000’        ЛИСТ 34
ЗАЯВКА ЭКВ ’72000’        ЛИСТ 35
ЛИСТЧТ ЭКВ ’74000’        ЛИСТ 36
ЛИСТЗП ЭКВ ’76000’
МОБМ ЭКВ ЗАЯВКА+’360’  МАССИВ ДЛЯ ОБМЕНА
ТХ ТЕКСТ П’000000000001000002000003000004’
 ТЕКСТ П’000005000006000007000008000009’
 ТЕКСТ П’00000А00000В00000С00000D00000Е00000F’
ТДД ТЕКСТ П’КЛЮЧРЗ000000’
*   0 СЛ: 1-48 - КЛЮЧ
*   К СЛ: 1-16-ЗОНА2,17-32-ЗОНА1;33-40-ЗАМ2,41-48-ЗАМ1
*         40,48 - ЧЕТНОСТЬ ДОРОЖКИ ДЛЯ 7,25 МГБ
ШДД КОНД (19)М40Х’FС’       РЕЗЕРВНЫЕ ДОРОЖКИ ДЛЯ 100 МГБ
ТАБЛ КОНД (152)В’0’   19*8
РЕЗЕРВ КОНД М34В’37777’      14 РЕЗ.ЗОН
 КОНД В’-1’В’1777777’        29 РЕЗ.ЗОН
ЗОНТАБ КОНД В’1744’        МД 7.25
 КОНД В’7634’              МД 29 МГБ
КОНЗОН КОНД В’1743’         СВЯЗАНЫ С ЗОНТАБ
 КОНД В’7633’
К0 КОНД В’4003146314631464’
 КОНД В’4000631463146315’
 КОНД В’4000327450327451’  R1/38
К1 КОНД В’4000000000000005’
 КОНД В’4000000000000024’
I19 КОНД В’4000000000000023’
МТДД КОНД М40Х’FF’М16Х’FFFF’
 КОНД М32Х’FF’Х’FFFF’
КВАНТ КОНД В’1’              КВАНТ ЗАПИСИ В ТДД
СЛСЛ КОНД (8)В’0’
НЗОН КОНД В’4’
ТЗОН КОНД В’0’         ФИЗ.НОМЕР ЗОНЫ
РАБЯЧ КОНД В’200000’    17Р=1 ЗП/ЧТ С ТЕГ
КЗОН ПАМ 1
АФ КОНД В’0’
АДРТУС ПАМ 1
РЯ1 ПАМ 1
РЯЛ ПАМ 1
РЯП ПАМ 1
ДР1 ПАМ 1
ДР2 ПАМ 1
ДРУ1 ПАМ 1
ДРУ2 ПАМ 1
Е1 КОНД В’1’
Е3 КОНД В’4’
Е48 КОНД В’4000000000000000’
СТЕК ЭКВ 20 !!!
ВХОД СЧ ДЕФ
 УИ М7
 УИА АВОСТ(W)
 УИА СТЕК(S)
 СЧИ W             АДРЕС ОБРАБОТКИ АВОСТА
 Э50 ’103’
 СЧ =П’Я-ТЕСТ’
 Э47 ’17’          ПОДТВЕРЖДЕНИЕ ПРАВ НА Э47
 СЧ ЛНУ           ЛОГИЧ. НОМЕР УСТ-ВА
 СДА 64-24
 ИЛИ Е48
 Э47 ’15’          ДАЙ АДРЕС ТУСЗ
 УИА НЕТРАБ(М10)
 ПО ПЕЧАТЬ        УСТ-ВА НЕТ ВООБЩЕ
 ЗП АДРТУС
 Э47 ’12’          ЧТЕНИЕ ПОЛНОРАЗР. ТУСЗ
 ЗП (S)
 СДА 64+27
 И =В’3’
 УИМ М5            ТИП МД : 0-7; 1-29; 2-100 МГБ
 И =В’177700’
 НТЖ =В’177700’
 ПЕ ПЕЧАТЬ        УСТ-ВО ДОСТУПНО КОМУ-ТО ДРУГОМУ
 СЧ =В’35’
 УИА 0(М4)         СТРОКА В ТДД
 Э50 ’137’         ФИКСАЦИЯ 35 ЛИСТА
 УИА НЕТФ(М10)
 ПЕ ПЕЧАТЬ        ЛИСТЫ НЕ ФИКСИРОВАНЫ
 УИА -1023(W)
 СЧ =В’2525252525252525’
РОСП НТЖ =В’-1’        РОСПИСЬ РАЗМ.КОДОМ ’ЛИСТЗП’
 ЗП ЛИСТЗП+1023(W)
 ЦИКЛ РОСП(W)
************************************
*   ПОДГОТОВКА СЛУЖЕБНЫХ СЛОВ В СЛСЛ
************************************
 Э50 ’114’        СЧИТ. ЯЧ.ГОД
 СБР =В’377774000007’
 СДА 64+27
 СЧМ ЛНУ
 СДА 64-21
 ИЛИ (S)
 ЗП СЛСЛ
 Э53 8            ВНЕСЕНИЕ ВРЕМЕНИ В СЛУЖ.СЛ.
 СДА 64+15
 СДА 64-27
 ИЛИ СЛСЛ
 ЗП СЛСЛ
 СЧ НПАК          ВОСЬМ.
 СДА 64-12
 ИЛИ =В’0137070700000000’ 40,41,43Р=1
* 40-СЛСЛ ПО НОВОМУ; 41-КСМ ПО НОВОМУ; 43-ЕСТЬ N ЗОНЫ
 ЗП СЛСЛ+1
 ЗП СЛСЛ+5
 Э50 ’100’         ДАЙ ШИФР
 ЗП СЛСЛ+2
 ЗП СЛСЛ+6
 МОДА (М5)
 УИА -2(W)
 ПИО М100(W)
 СЧ
 УИА -18(W)
 ПАМ
 ЗП ТДД+19(W)
 ЦИКЛ *(W)
 СЧ ЗОНТАБ(М5)
 СЛЦ Е3
 ЗП ТЗОН          ФИЗ.N ЗОНЫ
 СЧ КОНЗОН(М5)
 СЛЦ Е3
 ЗП КЗОН
 СЧ ДЗОН
 ПЕ НЕВЕСЬ        РАЗМЕТКА ПО ДИАПАЗОНУ ЗОН
 ПВ УПАК(R2)      ПОДСЧЕТ КСМ И УПАКОВКА ЛИСТЗП В МАСЗП
РАЗТДД УИА ПЛОХТ(М10)
 ПВ РЗС(R0)       РАЗМЕТКА ЗОНЫ С ТДД
 ПЕ ПЕЧАТ1       ПЛОХАЯ ЗОНА С ТДД, КОНЕЦ РАЗМЕТКИ
 ЗП ТЗОН
СЛЕДЗ ПИНО ДЕФДОР(М7)
 ПВ РЗС(R0)
 ПЕ ДЕФДОР        ДОРОЖКА ПЛОХАЯ
ДАЛЬШЕ СЧ ТЗОН
 СЛЦ Е1
 ЗП ТЗОН
 ВЧОБ КЗОН          + И 0 ВВЕРХ, - ВНИЗ
 ПО СЛЕДЗ         НА РАЗМЕТКУ СЛЕД. ЗОНЫ
 УИА НЕТДД(М11)    НЕТ ДЕФЕКТОВ ПРИ РАЗМЕТКЕ
 УИА -15(W)
 СЧ ТДД+1
 ПО Д1
 УИА ПЕЧТ(М11)     РАЗМЕТКА ЗАКОНЧЕНА НО С ДЕФЕКТАМИ
Д1 СЧ ТДД+15(W)     ПЕРЕПИСЬ ТДД НА ДИСК
 ЗП ЛИСТЗП+15(W)
 ЦИКЛ Д1(W)
 СЧ ЗОНТАБ(М5)
 СЛЦ Е3
 СДА 64-1(М5)
 ЗП АФ            ДЛЯ ЗП ТДД НА МД
 СЧ ТЗОН
 ВЧ Е1
 ЗП ТЗОН
 ПВ УПАК(R2)      ПОДСЧЕТ КСМ И УПАКОВКА ЛИСТЗП В МАСЗП
 ПВ ЗАП(R2)
 УИА ПЛОХТ(М10)
 ПЕ ПЕЧАТЬ        ВЫХОД ПРИ ПЛОХОЙ ЗП ТДД
 СЧ Е3            ЗАПИСЬ ТДД2 ВО 2ФИЗ.ЗОНУ
 СДА 64(М5)
 ЗП АФ
 ПВ ЗАП(R2)
 УИИ М10(М11)
 ПБ ПЕЧАТЬ        ПЕЧАТЬ ОБ ОКОНЧАНИИ РАЗМЕТКИ
************************************
*       Р З С
************************************
* ВХ:  ТЗОН - N ФИЗ. ЗОНЫ ДЛЯ РАЗМЕТКИ
* ВЫХ: СМ=0 - ЗОНА ХОРОШАЯ
*        =1 - ЗОНА ПЛОХАЯ
РЗС УИА ’12634’(М12)    ’56’-РАЗМЕТКА ДО КОНЦА ДОР,ОДНОС.
 СЧ ТЗОН
 СДА 64-1(М5)
 ЗП АФ
Р1 ПВ ФОБМ(R2)
 ПЕ (R0)            ЕСТЬ БНС !!
 ПИНО Р2(М5)          МД 29 МГБ
 СЧ АФ
 НТЖ Е1
 ЗП АФ
 И Е1
 ПЕ Р1
Р2 ПВ ЗАП(R2)         ЗАПИСАЛИ ЗОНУ
 ПЕ (R0)            ЕСТЬ БНС !!
 ПВ ЧТЕН(М11)
 ПЕ (R0)            ЕСТЬ БНС !!
* ПОСЛЕ ЧТЕНИЯ РАСПАКОВАННЫЙ ЛИСТ В ’ЛИСТЧТ’
 УИА -7(W)           ПРОВЕРКА СЛУЖ. СЛОВ
 ПАМ
 СЧ СЛСЛ+7(W)
 НТЖ МОБМ+7(W)
 ПЕ (R0)
 ЦИКЛ *-1(W)
 УИА -1023(W)        ПРОВЕРКА МАССИВА
 ПАМ
 СЧ ЛИСТЗП+1023(W)
 НТЖ ЛИСТЧТ+1023(W)
 ПЕ (R0)
 ЦИКЛ *-1(W)
 ПБ (R0)            ВСЕ ХОРОШО
************************************
*       З А П И С Ь
************************************
* ВХ:  АФ - АДРЕС ФАЙЛА ДЛЯ БВВ
ЗАП УИА ’12020’(М12)    КОП 50 - ЗАПИСЬ ОДНОСЕКТ.
 СЧ АФ
 ПИНО ЗАПD(М5)
 МОДА 1
ЗАПD СДА 64-36
 ЗП (S)
 СЧМР
 ИЛИ (S)
 СЧМ СЛСЛ
 И =В’777777777774’ ВНЕСЕНИЕ N ЗОНЫ В СЛУЖ. СЛОВА
 ИЛИ (S)
 ЗП СЛСЛ
 ЗП СЛСЛ+4
 УИА -7(W)           ПЕРЕПИСЬ СЛСЛ В УПАКОВАННЫЙ ЛИСТ
 ПАМ
 СЧ СЛСЛ+7(W)
 ЗП МОБМ+7(W)
 ЦИКЛ *-1(W)
************************************
*         Ф И О Б М
************************************
* ПОДГОТОВКА ЗАЯВКИ, ОБМЕН
* ВЫХ: СМ=0 - НЕТ БНС В ДР
*         1 - ЕСТЬ БНС: ДР-<ДР1,ДР2; ДРУ-<ДРУ1,ДРУ2
ФОБМ УИА -7(М1)         СЧЕТЧИК ПОВТОРОВ
ПОВЗ СЧ =В’77’         ТЕГ 77
 Э47 ’13’           УСТАНОВКА РГ ТЕГОВ
 СЧ =А(МОБМ)
 СДА 64+16
 СЧ РАЗМЕР          РАЗМЕР МАССИВА
 Э47 ЗАЯВКА+2       ДО
 И 0              ГАШЕНИЕ СМ И РМР
 Э47 ЗАЯВКА+1       ЛР
 ЗП ЗАЯВКА+7       СМ СОХР. ПОСЛЕ Э47
 Э47 ЗАЯВКА+5       ДР
 Э47 ЗАЯВКА+6       ДРУ
 СЧИ М12
 СДА 64-24          КОП;ОДНОСЕКТ;РАЗ. ДО КОНЦА ДОР.
 ЗП (S)
 СДА 64-17          ПРИЗНАК ЗП/ЧТ
 И Е48
 ИЛИ =М37В’6’       ТИП ТЕГА БЭСМ
 Э47 ЗАЯВКА+3       СО
 СЧ АДРТУС
 СДА 64
 Э47 ЗАЯВКА         АТУСЗ
 СДА 64-44
 СДА 64+6
 ИЛИ (S)
 ИЛИ РАБЯЧ          ДЛЯ ЗАМЕНЫ ДОРОЖЕК
 СЧМ АФ             В СТЕКЕ НОМЕР УСТРОЙСТВА
 ИЛИ =В’20000000000000’ ПРИЗНАК КОРРЕКЦИИ
 СДА 64+16
 ИЛИ (S)
 СЧМ АФ
 СДА 64+16
 СЧ (S)
 Э47 ЗАЯВКА+4       СПУ
 СЧ =А(ЗАЯВКА)
 Э47 ’16’           ОБМЕН
 СЧ =А(ЗАЯВКА+5)
 Э47 ’10’           СЧП ДР
 ЗП ДР1
 СЧМР
 СДА 64+32
 ЗП ДР2
 И Е1
 ПО (R2)           ВОЗВРАТ
 ЦИКЛ ПОВЗ(М1)       ПОВТОР ПРИ СБОЕ
 УИА ЗАЯВКА+6(W)    АДРЕС ДРУ
 СЧИ W
 Э47 ’10’           СЧП ДРУ
 ЗП ДРУ1
 СЧМР
 СДА 64+32
 ЗП ДРУ2
************************************
*       А Н А Л И З   Д Р  Д Р У
* ПРИХОДИМ ПОСЛЕ ПЛОХОГО ОБМЕНА (Э47 ’16’)
* ДР-<ДР1,ДР2(1-16); ДРУ-<ДРУ1,ДРУ2(1-16)
* МОГУТ ИНТЕРЕСОВАТЬ:
* ДР:   0  - БНС
*       12 - ДРУ
*       13 - ВУ НЕ ОПОЗНАНО
* ДРУ:  38 - НЕ ГОТОВ
*       37 - ВЫКЛ
*       36 - МГ ОТВЕДЕНЫ
*       35 - ТОЛЬКО ЧТЕНИЕ
 СЧ ДР2
 И =В’10000’    Е13
 ПЕ ЕДРУ
 СЧ ДР2
 И =В’20000’    Е14
 ПЕ ПЕЧДРУ        ВУ НЕ ОПОЗНАНО
 ПБ ЕND
ЕДРУ СЧ ДРУ1
 МОДА (М5)
 УИА -2(W)
 ПИО ЕД100(W)
 И =В’36000000’   35-38Р. ДРУ
 ПЕ ПЕЧДРУ        ЕСТЬ ЧТО СКАЗАТЬ
ЕND СЧ Е1
 ПБ (R2)
ЕД100 МОДА (М12)
 УИА -’12120’(W)
 ПИО ЕND(W) ПРИ ЧТЕНИИ
 И =В’16000042600000’
* 17,18,20,24,38,39,40 РАЗРЯДЫ АВАРИЙНЫЙ
 ПЕ ШАП+1         ЕСТЬ ЧТО СКАЗАТЬ
 ПБ ЕND
************************************
* Ч Т Е Н И Е  И  Р А С П А К О В К А
************************************
ЧТЕН УИА ’12120’(М12)   КОП 51 - ЧТЕНИЕ ОДНОСЕКТ
 ПАМ
 ПВ ФОБМ(R2)
 ПЕ (М11)
* РАСПАКОВКА МАССИВА ИЗ ’МОБМ’ -< ’ЛИСТЧТ’
 УИА -255(М15)          КОЛ-ВО СЛОВ
 УИА -2(М2)             СДВИГИ
 УИА ЗАЯВКА+1023(М3)    АДРЕС ЧТЕНИЯ
Ч СДА 64+16
 ЗП ЛИСТЧТ+255(М15)
 СЧИ М3
 Э47 ’11’          СЧП С ТЕГОМ 36
 ЗП ЛИСТЧТ-ЗАЯВКА(М3)
 СЛИА -1(М3)
 СЧМР
 ИЛИ ЛИСТЧТ+255(М15)
 ЦИКЛ Ч(М2)
 ЗП ЛИСТЧТ+255(М15)
 УИА -2(М2)
 ЦИКЛ Ч(М15)
 СЧ
 ПБ (М11)
************************************
*       Н Е В Е С Ь
************************************
НЕВЕСЬ СДА 64+24         СМ=ДЗОН
 И =Х’FFFF’
 СЛЦ Е3
 ЗП НЗОН          НАЧ. ФИЗ. ЗОНА
 СЧ ДЗОН
 И =Х’FFFF’
 СЛЦ Е3
 ЗП КЗОН          КОН.ФИЗ.ЗОНА
 ВЧ Е3
 ВЧОБ КОНЗОН(М5)
 УИА ВЕЛИК(М10)    ВЕЛИК НОМЕР КОНЕЧНОЙ ЗОНЫ
 ПЕ ПЕЧАТЬ
 СЧ ТЗОН          ЗОНА С ТДД ФИЗ.
 СДА 64-1(М5)
 ЗП АФ
 ПВ ЧТЕН(М11)     ЧТЕНИЕ ТДД С МД
 УИА ПЛОХТ(М10)
 ПЕ ПЕЧАТ1       ЗОНА С ТДД НЕ ЧИТАЕТСЯ
 СЧ НЗОН
 ЗП ТЗОН
 СЧ ЛИСТЧТ
 НТЖ ТДД          ПРОВЕРКА ТДД НА КЛЮЧРЗ
 ПЕ РАЗТДД        НАДО РАЗМ. ЗОНУ ТДД
************************************
* ПЕРЕПИСЬ ТАБЛИЦЫ ДЕФЕКТНЫХ ДОРОЖЕК
************************************
 ПВ УПАК(R2)      ПОДСЧЕТ КСМ И УПАКОВКА ЛИСТЗП В МАСЗП
 УИА ЛИСТЧТ+1(М1)
 УИА КВЧТ(М15)
 МОДА Е1-ТАБЛ
КВЧТ СЧ ТАБЛ
 НТЖ Е1
 ЗП ТАБЛ
 УИ W
 СЧ (М1)
 СБР МТДД(W)        48/41-ЗАМЕНА,40/25-ЗОНА
 СЛИ М1(W)
 ПО СЛЕДЗ
 ЗП РЯ1
 СДА 64+24
 И =Х’FFFF’
 ВЧ НЗОН
 ПЕ НЕВДИА        НЕ В ДИАПАЗОНЕ
 СЛ НЗОН
 ВЧОБ КЗОН
 ПО (М15)
************************************
*   НЕ В ДИАПАЗОНЕ
************************************
НЕВДИА СЧ РЯ1 ФОРМИРОВАНИЕ ШКАЛЫ РЕЗЕРВНЫХ ДОР.
 СДА 64+40
 СЧМ Е48
 МОД (S)
 СДА 64-1
 НТЖ РЕЗЕРВ(М5)
 ЗП РЕЗЕРВ(М5)
 СЧ РЯ1           48/41-ЗАМЕНА,40/25-ЗОНА
УПЛТДД СЧМ КВАНТ
 НТЖ Е1
 ЗП КВАНТ
 УИМ W
 РЗБ МТДД(W)
 ИЛИ ТДД+1(М4)
 ЗП ТДД+1(М4)
 СЛИ М4(W)
 ПБ (М15)
************************************
*   К С М    И    У П А К О В К А
************************************
* ЛИСТЗП - ЛИСТ С ИСХОДНОЙ ИНФОРМАЦИЕЙ
* МОБМ  - ЛИСТ УПАКОВАННЫЙ, УКАЗ. НА НАЧАЛО СЛУЖ.СЛОВ
УПАК УИА -1023(W)
 СЧ
 СЛЦ ЛИСТЗП+1023(W)
 ЦИКЛ *(W)        ПОДСЧЕТ КСМ
 ЗП СЛСЛ+3
 ЗП СЛСЛ+7        ЭТО  КСМ
 СЧ =В’36’        ТЕГ БЭСМ
 Э47 ’13’
 УИА 256(М15)      КОЛ-ВО ЯЧЕЕК
 УИА 256(М1)       ЧТ 0 АБЗАЦА
 УИА -2(М2)        СДВИГ ПО ЯЧЕЙКЕ
У0 СЧ ЛИСТЗП-1(М15)
 СДА 64+48
У1 СЧ ЛИСТЗП(М1)
 Э47 ЗАЯВКА(М1)
 СЛИА 1(М1)
 СЧМР
 СДА 64+32
 ЦИКЛ У1(М2)
 УИА -2(М2)
 СЛИА -1(М15)
 ПИНО У0(М15)
 ПБ (R2)
************************************
* Д Е Ф Е К Т Н А Я  Д О Р О Ж К А *
************************************
* ТЗОН - ДЕФЕКТНАЯ ЗОНА
ДЕФДОР СЧ ТЗОН
 СЧМ ДРУ2
 И =В’400’    Е9
 СДА 64+1
 ЗП РЯ1
 УИА НРЕЗД(М10)
ЕЩЕРЕЗ СЧ РЕЗЕРВ(М5)
 ПО ПЕЧАТЬ        НЕТ РЕЗЕРВНЫХ ДОРОЖЕК
 НЕД
 УИ М6           НОМЕР ЗАМЕНЫ
 СЛЦ ЗОНТАБ(М5)
 СЛЦ Е3
 ЗП ТЗОН
 СЧ Е48
 СДА 64-1(М6)
 НТЖ РЕЗЕРВ(М5)
 ЗП РЕЗЕРВ(М5)
 ПВ УПАК(R2)      ПОДСЧЕТ КСМ И УПАКОВКА ЛИСТЗП В МАСЗП
 ПВ РЗС(R0)      РАЗМЕТКА РЕЗЕРВНОЙ ЗОНЫ
 ПЕ ЕЩЕРЕЗ
 СЧ (S)
 ЗП ТЗОН          ВОССТАНОВЛЕНИЕ ТЗОН
 СЧИ М6
 ПИНО ЧЕТ(М5)
 ИЛИ РЯ1
ЧЕТ СДА 64-16
 ИЛИ ТЗОН
 СДА 64-24
 УИА ДАЛЬШЕ(М15)
 ПБ УПЛТДД
************************************
*   ПЕРЕВОД ИЗ 8 В 10/УПП
************************************
ПЕР10 УИА 48(W)
 ЗП РЯ1
 СЧ
К8 СЧМ РЯ1
 УМН =В’4001463146314632’
 ЗП РЯ1
 СЧМР 64
 УМН =В’4000000000000012’
 СДА 64-48(W)
 ИЛИ (S)
 СЛИА -8(W)
 ПИНО К8(W)
 ПБ (М15)
************************************
*       ОБРАБОТКА АВОСТА
************************************
АВОСТ УИА ТАВОСТ(М10)    ПЕЧАТЬ ТЕКСТА АВОСТ
 ПБ ПЕЧАТЬ
************************************
* ПЕЧАТЬ ПРИ ОШИБКАХ ДР, ДРУ
************************************
* СМ=Е14 - ВУН (ДР)
ПЕЧДРУ СБР =М19Х’F’М13В’1’    СМ=35-38Р ДРУ
 ЗП РЯ1
 УИА -3(М1)
 УИА ПДРУ(М10)
ЦДРУ СЧ РЯ1
 СДА 64-1
 ЗП РЯ1
 СЧМР
 ПЕ ПЕЧАТЬ
 СЛИА 3(М10)
 ЦИКЛ ЦДРУ(М1)
 ПБ ШАП
************************************
*    П Е Ч А Т Ь
************************************
ПЕЧАТ1 СЧ
 ЗП ТЗОН
ПЕЧАТЬ СЧ НПАК
 ПВ ПЕР10(М15)
 ИЛИ ТЗАГ+3
 ЗП ТЗАГ+3
 СЧ ЛНУ
 СДА 64-42
 РЗБ =П’77’
 ИЛИ ТЗАГ+6
 ЗП ТЗАГ+6
 УИА -1(W)
П1 СЧ НЗОН+1(W)
 ВЧ Е3
 ПО П2
 СЧ МТДД          ЕТХ В СТАРШЕМ БАЙТЕ
 ЗП ТЗАГ+8   НЕ ПЕЧ.ЗОНЫ,ЕСЛИ МАТ.ЗОНЫ НЕ РАЗМ.
П2 СДА 64-33
 РЗБ =П’77777’
 ИЛИ ТЗАГ+11(W)
 ЗП ТЗАГ+11(W)
 ЦИКЛ П1(W)
 Э64 ЗАГ
 СЧ ТЗОН
 ПО КОНЕЦ  РАСПЕЧАТКА ТДД
 Э64 ПЕЧ1
 СЧ ТДД+1
 ПО КОНЕЦ    НЕТ ДЕФЕКТОВ
************************************
* РАСПАКОВКА ТАБЛИЦЫ ДЕФ.ДОРОЖЕК
************************************
 УИА -27(W)
 УИА ТАБЛ(М1)
РАС СЧ ТДД+28(W)
 СБР МТДД
 СДА 64+24
 ЗП (М1)
 СЧ ТДД+28(W)
 СБР МТДД+1
 СДА 64+24
 ЗП 1(М1)
 СЛИА 2(М1)
 ЦИКЛ РАС(W)
************************************
* УПОРЯДОЧИВАНИЕ ТАБЛИЦЫ ДЕФ.ДОРОЖЕК
************************************
 УИА ТАБЛ-1(W)
У СЛИА 1(W)
 СЧ (W)
 ПО ВСЕ
 И =Х’FFFF’
 СЧМ 1(W)
 И =Х’FFFF’
 ПО ВСЕ
 ВЧ (S)
 ПО У
 УИИ М1(W)
У2 СЧ (М1)
 СЧМ 1(М1)
 ЗПМ (М1)
 ЗП 1(М1)
 СЛИА -1(М1)
 СЧ (М1)
 И =Х’FFFF’
 СЧМ 1(М1)
 И =Х’FFFF’
 ВЧ (S)
 ПЕ У2
 ПБ У
************************************
*    ПЕЧАТЬ ТАБЛИЦЫ ДЕФ. ДОРОЖЕК
************************************
ВСЕ УИА ШАПКА(М10)
 Э64 ПЕЧ1
 УИА ПЕЧЗОН(М10)
 УИА ТАБЛ(М2)
ПСТР СЧ (М2)
 И =Х’FFFF’
 ПО ВСЯТ
 ВЧ Е3
 ПО НЕПРЕД        НЕ ПРЕДЗОНЫ
 ВЧОБ
 ИЛИ =В’13000’
НЕПРЕД СДА 64-35         N ЗОНЫ 12 РАЗР.
 РЗБ =П’ 777’      ЗОНА
 ИЛИ =П’  0000’
 ЗП ПЕЧЗОН+3
 СЧ (М2)
 СДА 64+16
 СЛЦ ЗОНТАБ(М5)    ЗОНА МАТЕМ.
 СДА 64-36
 РЗБ =П’7777’      ЗАМЕНА
 ИЛИ =П’  0000’
 ЗП ПЕЧЗОН+5
 СЧ (М2)
 СЛЦ =В’7777777740000000’
 И =Х’FFFF’     ЗОНА 15 РАЗР.
 УМН К0(М5)      ВЫЧИСЛЕНИЕ N ЦИЛИНДРА И ГОЛОВКИ
 ЗП ДР2
 СЧМР 64
 УМН К1(М5)
 ПВ ПЕР10(М15)
 СЧМ ДР2
 ПВ ПЕР10(М15)
 СДА 64-24
 ИЛИ (S)
 ИЛИ =П’000.00’
 ЗП ПЕЧЗОН+1
 Э64 ПЕЧ1
 ЦИКЛ ПСТР(М2)
ВСЯТ УИА ЧЕРТА(М10)
 Э64 ПЕЧ1          ПЕЧАТЬ ЧЕРТЫ ВНИЗУ
КОНЕЦ Э74
М100 СЧ
 ЗП РАБЯЧ
 УИА -1023(W)  РОСПИСЬ ЗОНЫ
 СЧ =Х’ЕВ6DВ6DВЕВ6D’
 ЗП ЛИСТЗП+1023(W)
 ЦИКЛ *(W)
 ПВ УПАК(R2)      ПОДВЧЕТ КСМ И УПАКОВКА ЛИСТЗП В МАСЗП
 УИА -403(М4)     КОЛИЧ. ЦИЛИНДРОВ
 Э65 ’7’
 И =М24В’1’
 ПО D100          ОДНОСЕКТОРНЫЙ ФОРМАТ
 СЧ ДЗОН
 ПЕ МDIА
ЦИЛЛ УИА -18(М2)      КОЛИЧ. ГОЛОВОК
ДОР УИА ’12614’(М12)    56-РАЗМЕТКА ДО КОНЦА ДОР
 ПВ ФОБМ(R2)
 УИА *(R2)
 ПЕ НЕРАЗМ
 УИА ’12714’(М12)       57-КОНТ.ЧТЕНИЕ ДО КОНЦА ДОР.
 ПВ ФОБМ(R2)
 УИА ДОР+1(R2)
 ПЕ НЕРАЗМ
 СЧ АФ
 СЛЦ =Ф’33’
 ЗП АФ
 ЦИКЛ ДОР(М2)
 ЦИКЛ ЦИЛЛ(М4)
 УИА -10131(М4)       КОЛИЧ. ЗОН
 СЧ 0          РАЗМЕТКА ДИСКА ОКОНЧЕНА
ПЕРЕ УМН =В’4000000000000031’
 СЧМР 64
 ЗП АФ
 СЧ ТЗОН
 УИА ’12000’(М12) ЗАПИСЬ МНОГОСЕКТОРНАЯ
 ПВ ЗАПD(R2)
 УИА ДЕФЗП(М10)
 ПЕ ПЕЧАТЬ
 СЧ ТЗОН
 СЛЦ Е1
 ЗП ТЗОН
 ЦИКЛ ПЕРЕ(М4)
ПЕРЕТД УИА -180(W)       ПЕРЕПИСЬ МАССИВА РЕЗЕРВ ДОР
 УИА ’12000’(М12)
 СЧ ТДД+180(W)
 ЗП ЛИСТЗП+180(W)
 ЦИКЛ *-1(W)
 ПВ УПАК(R2)      ПОДСЧЕТ КСМ И УПАКОВКА ЛИСТЗП В МАСЗП
 СЧ =Ф’10131’
 УМН =В’4000000000000031’
 СЧМР 64
 ЗП АФ
 СЧ =Ф’10131’
 ПБ DЕ
НЕРАЗМ СЧ ШДД+18(М2)
 ПЕ Н1
 УИА НРЕЗД(М10)
 УИА ПЕЧАТЬ(М6)
 ПБ ПТДД
Н1 НЕД =В’777777777777’
 ЗП РЯ1
 МОДА (М4)
 УИА 403(W)
 СЧИ W
 МОДА (М2)
 УИА 18(W)
 СЧИМ W
 СДА 64-3
 СЛЦ РЯ1
 УИМ W
 ЗП ТАБЛ(W)
 СЧ Е48
 МОД РЯ1
 СДА 64
 НТЖ ШДД+18(М2)
 ЗП ШДД+18(М2)
 СЧ РЯ1
 СДА 64-19           НОМЕР ЗАП ДОР
 ЗП РАБЯЧ            ДЛЯ СПУ
 УИА ’13014’(М12)      58-ЗАПИСЬ В СА И РАЗМ.ДО КОН.ДОР
 ПБ ФОБМ
МDIА СДА 64+24
 И =Х’FFFF’
 СЛЦ Е3
 ЗП НЗОН
 ЗП ТЗОН
 ВЧОБ =Ф’10131’
 УИА ВЕЛИК(М10)
 ПЕ ПЕЧАТЬ
 СЧ =Ф’10131’
 УМН =В’4000000000000031’ 25
 СЧМР 64
 ЗП АФ
 УИА ’12100’(М12)
 ПВ ЧТЕН+1(М11)  ЧТЕНИЕ ТДД
 УИА ПЛОХТ(М10)
 ПЕ ПЕЧАТЬ
 СЧ ЛИСТЧТ
 НТЖ ТДД  ’КЛЮЧРЗ’
 ПЕ ПЕЧАТЬ НЕ ТДД
 УИА -180(W)
 ПАМ
 СЧ ЛИСТЧТ+180(W)
 ЗП ТДД+180(W)
 ЦИКЛ *-1(W)
 СЧ ТЗОН
 УМН =В’4000000000000031’
 СЧМР 64
 ЗП ДЕФ
 ВЧ =Ф’25’
 ЗП АФ
 ПВ ЧТЕН+1(М11) ЧТЕНИЕ N-1
 ЗП РЯЛ
 УИА -1023(W)
 ПАМ
 СЧ ЛИСТЧТ+1023(W)
 ЗП ЛИСТУП+1023(W)
 ЦИКЛ *-1(W)
 УИА -7(W)
 ПАМ
 СЧ МОБМ+7(W)
 ЗП ЗАЯВКА+107(W)
 ЦИКЛ *-1(W)
 СЧ ДЕФ
 СЛЦ =Ф’25’
 ЗП АФ
 ПВ ЧТЕН+1(М11) ЧТЕНИЕ N+1
 ЗП РЯП
 УИА -7(W)
 ПАМ
 СЧ МОБМ+7(W)
 ЗП ЗАЯВКА+117(W)
 ЦИКЛ *-1(W)
 СЧ ДЕФ
 УМН =В’4000370174076040’ R1/33
 ЗП (S) НОМЕР ДОРОЖКИ
 УМН =В’4000657120657121’ R1/19
 ЗП РЯ1
 УИ М4
 СЛИА -403(М4)
 СЧМР 64
 УМН I19
 УИ W ГОЛОВКА
 УИ М2
 СЛИА -18(М2)
 СДА 64-3
 УИМ R0
 УМН =В’4000000000000041’
 СЧМР 64
 ЗП АФ
 УИА -7(R2) ПОИСК ДЕФЕКТНОЙ ДОРОЖКИ В ТДД
МI0 МОДА 7(R2)
 СЧ ТАБЛ(R0)
 НТЖ РЯ1
 ПЕ МI1
 МОДА 7(R2)
 ЗП ТАБЛ(R0)
 СЧ Е48
 СДА 64+7(R2)
 ИЛИ ТДД(W)
 ЗП ТДД(W)
МI1 ЦИКЛ МI0(R2)
 УИА ’12614’(М12)
 ПВ ФОБМ(R2)  РАЗМЕТКА ДОРОЖКИ
 УИА *(R2)
 ПЕ НЕРАЗМ
 УИА -2(М6)
 СЧ ТЗОН
 ВЧ Е1
МI2 ЗП ТЗОН  РОСПИСЬ 3-Х ЗОН
 УМН =В’4000000000000031’
 СЧМР 64
 ЗП АФ
 СЧ ТЗОН
 УИА ’12000’(М12)
 ПВ ЗАПD(R2)
 УИА ДЕФЗП(М10)
 ПЕ ПЕЧАТЬ
 СЧ ТЗОН
 СЛЦ Е1
 ЦИКЛ МI2(М6)
 СЧ РЯЛ
 ПЕ МI3
 УИА -1023(W)
 ПАМ
 СЧ ЛИСТУП+1023(W)
 ЗП ЛИСТЗП+1023(W)
 ЦИКЛ *-1(W)
 УИА -7(W)
 ПАМ
 СЧ ЗАЯВКА+107(W)
 ЗП МОБМ+7(W)
 ЦИКЛ *-1(W)
 ПВ УПАК(R2)
 СЧ ДЕФ
 ВЧ =Ф’25’
 ЗП АФ
 УИА ’12000’(М12)
 ПВ ФОБМ(R2) ЗАПИСЬ N-1
 УИА ТТТ(М10)
 ПЕ ПЕЧАТЬ
МI3 СЧ РЯП
 ПЕ МI4
 УИА -1023(W)
 УИА ’12000’(М12)
 ПАМ
 СЧ ЛИСТЧТ+1023(W)
 ЗП ЛИСТЗП+1023(W)
 ЦИКЛ *-1(W)
 УИА -7(W)
 ПАМ
 СЧ ЗАЯВКА+117(W)
 ЗП МОБМ+7(W)
 ЦИКЛ *-1(W)
 ПВ УПАК(R2)
 СЧ ДЕФ
 СЛЦ =Ф’25’
 ЗП АФ
 ПВ ФОБМ(R2)  ЗАПИСЬ N+1
 УИА ТТТ(М10)
 ПЕ ПЕЧАТЬ
МI4 СЧ НЗОН
 ЗП ТЗОН
 СЧ РЯЛ
 ИЛИ РЯП
 ПО ПЕРЕТД
 УИА СМЕЗ(М10)
 Э64 ПЕЧ1
 ПБ ПЕРЕТД
D100 СЧ ДЗОН
 ПЕ DIА
D0 УИА ’12634’(М12)   РАЗМЕТКА ДОРОЖКИ
 ПВ ФОБМ(R2)
 УИА *(R2)
 ПЕ DЕF
 СЧ АФ
 СЛЦ =В’4’
 ЗП АФ
 ВЧ =М2Ф’7684’  404*19 ЧИСЛО ДОРОЖЕК
 ПЕ D0
 СЧ
 ЗП АФ
D1 СЧ АФ
 СДА 64+1
 ЗП ТЗОН
 УИА ’12020’(М12)
 ПВ ЗАПD(R2)
 УИА D1(R2)
 ПЕ DЕF      НЕ ЗАПИСАЛИ
 ПВ ЧТЕН(М11)
 УИА D1(R2)
 ПЕ DЕF
 СЧ АФ
 СЛЦ =В’2’
 ЗП АФ
 СДА 64+1
 ЗП ТЗОН
 ВЧ =В’36004’
 ПЕ D1
DЕND УИА -180(W)
 УИА ’12020’(М12)
 СЧ ТДД+180(W)
 ЗП ЛИСТЗП+180(W)
 ЦИКЛ *-1(W)
 ПВ УПАК(R2)
 СЧ =М1Ф’15364’
 ЗП АФ   АДРЕС ТДД
 СДА 64+1
DЕ ПВ ЗАПD(R2)
 УИА ПЛОХТ(М10)
 ПЕ ПЕЧАТЬ
 СЧ Е3
 ЗП АФ  ЗАПИСЬ В0 2 ЗОНУ
 СЧ =В’2’
 ПВ ЗАПD(R2)
 ПЕ ПЕЧАТЬ
DЕЕ УИА -18(W)
 ПАМ
 СЧМ ШДД+18(W)
 НТЖ =М40Х’FС’
 ИЛИ (S)
 ЦИКЛ *-1(W)
 УИА НЕТДД(М10)
 ПО ПЕЧАТЬ
 УИА ПЕЧАТЬ(М6)
 УИА ПЕЧТ(М10)
 ПБ ПТДД
РАСТДД ЗП НЗОН
 ЗП ТЗОН
 СЧ МОБМ
 СДА 64+21
 И =В’77’
 ЗП ЛНУ
 СЧ МОБМ+1
 СДА 64+12
 И =В’7777’
 ЗП НПАК
 СЧ ТЗАГ1
 ЗП ТЗАГ+1
 СЧ ТЗАГ1+1
 ЗП ТЗАГ+2
 ПБ DЕЕ
DЕF СЧ АФ  ВЫРАВНИВАНИЕ НА НАЧАЛО ДОРОЖКИ
 СДА 64+2
 СДА 64-2
 ЗП АФ
 СДА 64+1
 УМН К0+2
 ЗП (S)          N ЦИЛИНДРА
 СЧМР 64
 УМН К1+2
 УИ W            N ГОЛОВКИ
 СДА 64-3
 СЧМ ШДД(W)
 ПЕ DЕF1
 УИА НРЕЗД(М10)
 УИА ПЕЧАТЬ(М6)
 ПБ ПТДД
DЕF1 НЕД
 ЗП РЯ1
 СЛЦ (S)
 УИМ R0
 ЗП ТАБЛ-1(R0)
 СЧ Е48
 МОД РЯ1
 СДА 64-1
 НТЖ ШДД(W)
 ЗП ШДД(W)
 СЧ РЯ1
 СДА 64-19
 ЗП РАБЯЧ
 УИА ’13034’(М12) ПЕРЕАДРЕСАЦИЯ ОДНОС
 ПБ ФОБМ
DIА СДА 64+24
 И =Х’FFFF’
 СЛЦ Е3
 ЗП НЗОН
 ЗП ТЗОН
 ВЧ =Ф’15364’
 УИА ВЕЛИК(М10)
 ПО ПЕЧАТЬ
 СЧ =М1Ф’15364’
 ЗП АФ
 ПВ ЧТЕН(М11)   ЧТЕНИЕ ТДД
 УИА ПЛОХТ(М10)
 ПЕ ПЕЧАТЬ
 СЧ ЛИСТЧТ
 НТЖ ТДД  ’КЛЮЧРЗ’
 ПЕ ПЕЧАТЬ  НЕ ТДД
 УИА -180(W)
 ПАМ 0      ПЕРЕПИСЬ ТДД
 СЧ ЛИСТЧТ+180(W)
 ЗП ТДД+180(W)
 ЦИКЛ *-1(W)
 СЧ ДЗОН
 СДА 64+23
 НТЖ Е1
 ПО РАСТДД
 СЧ НЗОН
 УМН К0+2
 ЗП РЯ1
 СЧМР 64
 УМН К1+2
 УИ W ГОЛОВКА
 СДА 64-3
 УИ R0
 УИА -5(R2)
I3 МОДА 5(R2)
 СЧ ТАБЛ(R0)
 НТЖ РЯ1
 ПЕ I4
 МОДА 5(R2)
 ЗП ТАБЛ(R0)
 СЧ Е48
 СДА 64+5(R2)
 ИЛИ ШДД(W)
 ЗП ШДД(W)
I4 ЦИКЛ I3(R2)
 СЧ НЗОН
 НТЖ Е1
 СДА 64-1
 ЗП АФ
 СДА 64-32
 РЗБ =П’77777’
 ЗП СМЕЗ+4
 СЧ ДЕФ
 ПО I7
 СЧ НЗОН
 СДА 64+1
 СДА 64-2
 ЗП АФ
 УИА I2(R2)
 ПБ DЕF
I7 УИА СМЕЗ(М10)
 ПВ ЧТЕН(М11)  ЧТЕНИЕ ЗОНЫ НА ДЕФ ДОРОЖКЕ
 УИА -7(W)
 ЗП ДЕФ
 ПО I0
 Э64 ПЕЧ1
 ПБ I1
I0 СЧ МОБМ+7(W)
 ЗП ЗАЯВКА+107(W)
 ЦИКЛ *-1(W)  УПРЯТАЛИ СЛСЛ
I1 УИА ’12634’(М12)
 СЧ НЗОН
 СДА 64+1
 СДА 64-2
 ЗП АФ АДРЕС ФАЙЛА НАЧАЛА ДОРОЖКИ
 ПВ ФОБМ(R2)  РАЗМЕТКА ДОРОЖКИ
I2 УИА *(R2)
 ПЕ DЕF  НЕ ХОЧЕТ
 ПВ УПАК(R2)
 СЧ АФ
 СДА 64+1      N ЗОНЫ
 УИА ’12020’(М12)
 ПВ ЗАПD(R2)  ЗАПИСЬ РАЗМ КОДА
 УИА I2(R2)
 ПЕ DЕF  НЕ ЗАПИСАЛИ
 ПВ ЧТЕН(М11)  ЧТЕНИЕ ЗОНЫ
 УИА I2(R2)
 ПЕ DЕF  НЕ ЗАПИСАЛИ
 СЧ АФ
 СЛЦ =В’2’
 ЗП АФ
 СДА 64+1     N ЗОНЫ
 ПВ ЗАПD(R2)
 УИА I2(R2)
 ПЕ DЕF  НЕ ЗАПИСАЛИ
 ПВ ЧТЕН(М11)
 УИА I2(R2)
 ПЕ DЕF  НЕ ЗАПИСАЛИ
 СЧ ДЕФ
 ПЕ DЕND
 УИА -1023(W)
 ПАМ 0 ПЕРЕПИСЬ УПРЯТАННОЙ ЗОНЫ
 СЧ ЛИСТЧТ+1023(W)
 ЗП ЛИСТЗП+1023(W)
 ЦИКЛ *-1(W)
 УИА -7(W) ВОССТАНОВЛЕНИЕ СЛСЛ
 СЧ ЗАЯВКА+107(W)
 ЗП МОБМ+7(W)
 ЦИКЛ *-1(W)
 ПВ УПАК(R2)
 СЧ НЗОН
 НТЖ Е1
 СДА 64-1
 ЗП АФ
 УИА ’12020’(М12)
 ПВ ФОБМ(R2)
 ПО DЕND
 ПВ ПЕЧАТЬ(М10)
ТТТ ТЕКСТ П’НЕ МОГУ ЗАПИСАТЬ СМЕЖНУЮ’
 КОНД П’ ЗОНУ0’Х’FF’
СМЕЗ ТЕКСТ П’СМЕЖНАЯ ЗОНА НЕ ЧИТАЕТСЯ’
 КОНД В’0’  ЗДЕСЬ НОМЕР ЗОНЫ
 КОНД М40Х’FF’
ДЕФЗП ТЕКСТ П’ДЕФЕКТЫ ПРИ РОСПИС’
 КОНД П’И    0’Х’FF’
С ЭКВ 1
З ЭКВ 2
БЗ ЭКВ 4
ЗБ СЧИ С
 УМН =В’4001252525252526’ R1/12
 УИ W
 СЧМР 64
 УМН =В’4037777777777720’ D48
 СЧМ ДР1(W)
 МОД (S)
 СДА 64+45
 И =Х’F’
 СЧИМ З
 УМН =В’4002525252525253’ R1/6
 УИ W
 СЧМР 64
 УМН =В’4000000000000060’ I48
 СЛИ W(БЗ)
 ЗП РЯ1
 МОД (S)
 СЧ ТХ
 МОД РЯ1
 СДА 64-40
 ИЛИ (W)
 ЗП (W)
 СЧ ДРУ1
 СЛЦ Е1
 ЗП ДРУ1
 НТЖ =В’2’
 ПЕ ЗБ1
 ЗП ДРУ1
 МОДА 1
ЗБ1 СЛИА 1(З)
 ПБ (R2)
УСТ0 МОДА 1
УСТ УИА 8(W)
 СЧИ М10
 Э47 (W)
 ЗП ДР1
 СЧМР
 ЗП ДР2
 УИА -1(С) СЧЕТЧИК ’Х’ КОНСТАНТ
 УИА 0(З) СЧЕТЧИК БАЙТОВ ЗАПИСИ
 УИА -15(R0) ДЛИНА КОНСТАНТЫ
 СЧ
 ЗП ДРУ1
 ПАМ 0
 СЛИА 1(С)
 ПВ ЗБ(R2)
 ЦИКЛ *-1(R0)
 ПБ (М6)
ШАП Э64 ПЕЧ1
 УИА ЗАЯВКА+4(М10)
 УИА -2(М3)
 УИА СПУ-4(БЗ)
 ПАМ
 СЛИА 5(БЗ)
 ПВ УСТ(М6)
 СЛИА 1(М10)
 ЦИКЛ *-1(М3)
 СЧ =М6Ф’8’
 ЗП РАЗМЕР
 УИА ’23100’(М12) КОМ 99 ЧТЕНИЕ БУС
 ПВ ФОБМ(R2)
 УИА -7(М3)
 УИА БУС-4(БЗ)
ТТБ МОДА МОБМ+7(М3)
 УИА (М10)
 СЛИА 5(БЗ)
 ПВ УСТ0(М6)
 ЦИКЛ ТТБ(М3)
*УИА ’14100’(М12)
*ПВ ФОБМ(R2)
*ПЕ ТТС1
*УИА -2(М3)
*УИА СТА-4(БЗ)
*ТТС МОДА МОБМ+2(М3)
*УИА (М10)
*СЛИА 5(БЗ)
*ПВ УСТ0(М6)
*ЦИКЛ ТТС(М3)
ТТС1 УИА КОНЕЦ(М6)
 Э64 ПЕЧ4
ПТДД УИА -18(R2)
 УИА ТАБЛ-8(R0)
ПТ1 СЛИА 8(R0)
 УИА -7(М4)
ПТ2 МОДА 7(М4)
 СЧ (R0)
 ПО ПТ3
 ЗП (S)
 УМН I19
 СЧМР 64
 МОДА 18(R2)
 УИА (W)
 СЧИМ W
 СЛЦ (S)
 СДА 64-1
 ВЧ =В’4’
 СДА 64-33         ЭТО ЗОНА
 РЗБ =П’777770’
 ИЛИ =Х’F’
 ЗП БУФ+2 ТЕКСТ ЗОНЫ
 СЧИ W
 ПВ ПЕР10(М15)
 ЗПМ ДР1
 ПВ ПЕР10(М15)
 СДА 64-24
 ИЛИ ДР1
 ИЛИ =П’000.00’
 ЗП БУФ+4
 Э64 ПЕЧ3
ПТ3 ЦИКЛ ПТ2(М4)
 ЦИКЛ ПТ1(R2)
 ПБ (М6)
************************************
РАЗМЕР КОНД М6В’1420’
ЗАГ КОНК А(ТЗАГ)
 КОНК А(ТЗАГ)
 КОНД М20В’12’       ПРОПУСК 2-Х СТРОК
*
ПЕЧ1 ЗП (М10)
 ЗП (М10)
 КОНД М20В’10’
ПЕЧ3 ЗП БУФ
 ЗП 1
 КОНД М20В’12’
ПЕЧ4 ЗП СПУУ
 ЗП 1
 КОНД М20В’12’       ПРОПУСК 2-Х СТРОК
БУФ КОНД П’00ДЕФ.’М40В’173’М32В’63’
 ТЕКСТ П’ ЗОНА=000000ЦИ.ГО=000000’
 КОНД М40Х’FF’
ТЗАГ1 ТЕКСТ П’ЗМЕЧЕН ТОМ  ’
ТЗАГ КОНД П’0000РА’М40В’214’М32В’214’М24В’173’М16В’60’
 ТЕКСТ П’ЗМЕТКА ТОМА ’
 КОНД П’000000’М40В’143’М32В’143’
 ТЕКСТ П’ НА УСТРОЙСТ’
 КОНД П’ВЕ 000’М16В’143’
 КОНД М40В’214’М32В’173’М24В’60’М16В’31’М8В’174’В’42’
 КОНД П’000РАЗ’М40В’214’М32В’173’М24В’65’
 ТЕКСТ П’МЕЧЕНЫ 00000-00000’
 КОНД П’ ЗОНЫ0’В’377’
*
НЕТРАБ ТЕКСТ П’УСТРОЙСТВО ДОСТУПНО ИЛИ ВЫКЛЮЧ’
 КОНД П’ЕНО  0’Х’FF’
*
НЕТФ ТЕКСТ П’ЛИСТ НЕ ФИКСИРУЕТС’
 КОНД П’Я    0’Х’FF’
*
НЕТДД КОНД П’00Д Е ’М40В’173’М32В’65’
 ТЕКСТ П’Ф Е К Т О В   Н Е ’
 КОНД П’Т !  0’Х’FF’
*
ПЛОХТ ТЕКСТ П’ПЛОХАЯ ЗОНА С ТАБЛИЦЕЙ ДЕФЕКТНЫХ ДОР’
 КОНД П’ОЖЕК 0’Х’FF’
*
ТАВОСТ ТЕКСТ П’АВОСТ В ПРОГ’
 КОНД П’РАММЕ0’Х’FF’
*
ВЕЛИК ТЕКСТ П’НЕВЕРНО ЗАДАН ДИАПАЗОН З’
 КОНД П’ОН   0’Х’FF’
*
ПЕЧТ КОНД П’00РАЗМ’М40В’173’М32В’70’
 ТЕКСТ П’ЕТКА ЗАКОНЧЕ’
 КОНД  П’НА 000’М16В’377’
*
НРЕЗД ТЕКСТ П’ИСЧЕРПАНЫ РЕЗЕРВНЫЕ ЗОНЫ’
 КОНД М40Х’FF’
НЕРАЗ ТЕКСТ П’ДОРОЖКА НЕ РАЗМЕЧА’
 КОНД П’ЕТСЯ 0’Х’FF’
*
ШАПКА КОНД П’000ТАБ’М40В’214’М32В’173’М24В’65’
 ТЕКСТ П’ЛИЦА ДЕФЕКТНЫХ ЗОН’
 КОНД М40В’214’М32В’173’М24В’60’М16В’115’М8В’174’В’40’
 КОНД П’000I Ц’М40В’214’М32В’173’М24В’60’
 ТЕКСТ П’ИЛИНДР, I ДЕФЕКТНАЯ I  ЗОНА  I’
 КОНД П’000I Г’М40В’214’М32В’173’М24В’60’
 ТЕКСТ П’ОЛОВКА  I ЗОНА(МАТ) I ЗАМЕНЫ I’
 КОНД П’     0’В’214’
ЧЕРТА КОНД М40В’173’М32В’60’М24В’115’М16В’174’М8В’40’В’377’
*
ПЕЧЗОН КОНД П’000I  ’М40В’143’М32В’173’М24В’60’
 КОНД В’0’
 КОНД П’0  I  ’М40В’143’
 КОНД П’  0000’    ДЕФ.ЗОНА
 КОНД П’   I00’М8В’143’В’143’
 КОНД П’  0000’    ЗАМЕНА
 КОНД П’  I000’М16В’377’
*
ПДРУ КОНД П’00НЕТ ’М40В’173’М32В’66’
 КОНД П’ГОТОВН’
 КОНД П’ОСТИ 0’В’377’
 КОНД П’00УСТР’М40В’173’М32В’66’
 КОНД П’. ВЫКЛ’
 КОНД П’ЮЧЕНО0’В’377’
 КОНД П’00М.Г.’М40В’173’М32В’66’
 КОНД П’ ОТВЕД’
 КОНД П’ЕНЫ 00’М8В’377’
 КОНД П’00ТОЛЬ’М40В’173’М32В’66’
 КОНД П’КО ЧТЕ’
 КОНД П’НИЕ 00’М8В’377’
 КОНД П’00ВУ Н’М40В’173’М32В’66’
 КОНД П’Е ОПОЗ’
 КОНД П’НАНО 0’В’377’
СПУУ КОНД П’00СБОЙ’М40В’173’М32В’7’
 КОНД П’ АППАР’
 КОНД П’АТУРЫ0’В’214’
СПУ ТЕКСТ П’  СПУ:’
 КОНД (3)П’00 00 ’
 КОНД П’00 000’В’214’
 ТЕКСТ П’   ДР:’
 КОНД (3)П’00 00 ’
 КОНД П’00 000’В’214’
 ТЕКСТ П’  ДРУ:’
 КОНД (3)П’00 00 ’
 КОНД П’00 000’В’214’
БУС ТЕКСТ П’  БУС:’
 КОНД (3)П’00 00 ’
 КОНД П’00 000’В’214’
 КОНД П’      ’
 КОНД (3)П’00 00 ’
 КОНД П’00 000’В’214’
 КОНД П’      ’
 КОНД (3)П’00 00 ’
 КОНД П’00 000’В’214’
 КОНД П’      ’
 КОНД (3)П’00 00 ’
 КОНД П’00 000’В’214’
 КОНД П’      ’
 КОНД (3)П’00 00 ’
 КОНД П’00 000’В’214’
 КОНД П’      ’
 КОНД (3)П’00 00 ’
 КОНД П’00 000’В’214’
 КОНД П’      ’
 КОНД (3)П’00 00 ’
 КОНД П’00 000’В’214’
 КОНД П’      ’
 КОНД (3)П’00 00 ’
 КОНД П’00 000’Х’FF’
*СТА ТЕКСТ П’ СТАТ:’
*КОНД (3)П’00 00 ’
*КОНД П’00 000’В’214’
*КОНД П’      ’
*КОНД (3)П’00 00 ’
*КОНД П’00 000’В’214’
*КОНД П’      ’
*КОНД (3)П’00 00 ’
*КОНД П’00 000’В’214’
*КОНД М40Х’FF’
 ФИНИШ ,

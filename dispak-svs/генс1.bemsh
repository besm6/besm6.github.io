 МАСRО
 ОБММБ ∧КУС,∧АЗАЯВ
 СЧИ ∧АЗАЯВ
 ЗП Р16
 СЧИ М10
 ЗП ЗМ14    УПРЯТ. М10
 СЧ ∧КУС
 СДА 64+12
 И П37
 УИ М10   ПАРАМ. ПВВМБ
 СЧ ∧КУС
 ПВ ПВВМБ(13)
 ПАМ 3
 МОДА КО∧SNDХ
 УИА (13)
 ПБ ЖДИПР
КО∧SNDХ НОП
 УИА ’2003’
 УИ ∧АЗАЯВ
 СЧ ’500’
 ЗП Я500
 СЧ ЯЧ500
 ЗП ’500’
 МОДА М1∧SNDХ
 ДК ’36’,0
М1∧SNDХ НОП
 ВОСЛМБ ∧КУС,∧АЗАЯВ
 СЧ Я500
 ЗП ’500’
 МОДА М2∧SNDХ
 ДК ’36’,0
М2∧SNDХ НОП
 СЧ Р16
 УИ ∧АЗАЯВ
 СЧ ЗМ14
 УИ М10   ВОССТ.  М10
 МЕND
 МАСRО
 ОБММД ∧КУС,∧АЗАЯВ
 СЧИ ∧АЗАЯВ
 ЗП Р16
 СЧИ М10
 ЗП ЗМ14      УПРЯТ.  М10
 СЧ ∧КУС
 СДА 64+12
 И П37
 УИ М10    ПАРАМ ПВВМД
 СЧ ∧КУС
 ПВ ПВВМД(13)
 ПАМ 3
 МОДА КО∧SNDХ
 УИА (13)
 ПБ ЖДИПР
КО∧SNDХ НОП
 УИА ’2003’
 УИ ∧АЗАЯВ
 СЧ ’500’
 ЗП Я500
 СЧ ЯЧ500
 ЗП ’500’
 МОДА М1∧SNDХ
 ДК ’36’,0
М1∧SNDХ НОП
 ВОСЛМД ∧КУС,∧АЗАЯВ
 СЧ Я500
 ЗП ’500’
 МОДА М2∧SNDХ
 ДК ’36’,0
М2∧SNDХ НОП
 СЧ Р16
 УИ ∧АЗАЯВ
 СЧ ЗМ14
 УИ М10    ВОССТ.  М10
 МЕND
 МАСRО
 ВОСЛМБ ∧КУС,∧АЗАЯВ
 СЧ ∧КУС
 ЗП (∧АЗАЯВ)
 СЧИ ∧АЗАЯВ
 ПВ ВОСЛМБ(13)
 ПАМ 3
 МЕND
 МАСRО
 ВОСЛМД ∧КУС,∧АЗАЯВ
 СЧ ∧КУС
 ЗП (∧АЗАЯВ)
 СЧИ ∧АЗАЯВ
 ПВ ВОСЛМД(13)
 ПАМ 3
 МЕND
ГЕНС1 СТАРТ ’60000’
 Б
 Е
 М
∧С SЕТВ 1   ИМЕЕТ ДР.СМЫСЛ,ЧЕМ ДЛЯ ОБЩИХ ДИСКОВ СВС
*∧Ф SЕТВ 0
*∧НИ SЕТВ 0
 GВLВ ∧Р,∧МВВ
∧МВВ SЕТВ 0
∧Р SЕТВ 0
************
 УПОТР ГЕНС1(М6)
 УИА -1(М17)
 УИА Н(М10)
*ОПРЕДЕЛЕНИЕ УСТАНОВЛЕННЫХ ТОМОВ
 СЧ НОМРД
 И Е24П1
 ЗП МДБ(М10)
 И =М12В’77’
 СДА 64-29
 ИЛИ ДЛТ
 СДА 64+12
 И Е39П25
 ИЛИ МДБ(М10)
 ЗП МДБ(М10)
 УИА ’30’(М14)
 СЧ
 ЗП КУСR
 ЗП ЕСТЬРД
НАПРД УИА -3(М17)
НАПД1 УИА -7(М11)
УСТРД НОП  ЦИКЛ ПО УСТРОЙСТВАМ
 ПВ ОПРОСД(М15)
 И Е48Е47
 ПО ОБХ1  НЕ ДОСТУПЕН
 СЧ КУСR
 ИЛИ =М24В’100’М17В’13’
ОБМСС НОП
 ЗП КУСR1
 СЧ 0
 ЗП 1(М14)    1-ОЕ СЛОВО СС =0
 ОБММД КУСR1,М16     ОБМЕН
 СЧ ОТВЕТ ДР
 ПО ХОР ОШ ОБМЕНА НЕТ
 СЧ КУСR1
 И Е32
 ПЕ ОБХ1  СЧИТАНО 2 ЗОНЫ
 СЧ КУСR1
 СЛЦ Е31  200-Я ЗОНА
 ПБ ОБМСС ПОПРОБУЕМ 2-Ю ЗОНУ
ХОР НОП
 СЧ ’500’
 ЗП Р500
 СЧ КЧ500
 ЗП ’500’
 МОДА Х1
 ДК ’36’,0
Х1 НОП
 СЧ К2003
 УИ ’21’
 СЧ 1(М14)  1-ОЕ СЛОВО СС
 И Е39П25
 НТЖ КЛЮЧ(М10)
 ПЕ ОБХ  КЛЮЧА НЕТ
 СЧ КУСR
 СБР НАПРМГ(М10)
 СДА 64+42
 УИ М15     N НАПР. И УСТР.
 СЧ 1(М14)
 И Е24П13(М10)  НОМЕР ПАКЕТА
 СДА 64+12
 МОДА (М10)
 ЗП ТЗНМД(М15)
 НТЖ НОМРД   НОМЕР РАБ.МД
 И Е12П1
 ПЕ НЕРД  НЕ РАБ. МД
 СЧ Е1
 ЗП ЕСТЬРД   РАБ. МД СТОИТ
НЕРД НОП
 СЧ КГОД
 И П7777(М10)  МАКС. НОМЕР ОБЩ. ПАКЕТА
 МОДА (М10)
 ВЧ ТЗНМД(М15)
 ПЕ ОБХ   ПАКЕТ НЕ ОБЩИЙ
 МОДА (М10)
 СЧ ТЗНМД(М15)
 ИЛИ Е41  ПРИЗНАК ОБЩЕГО ПАКЕТА
 МОДА (М10)
 ЗП ТЗНМД(М15)
ОБХ НОП
 СЧ К2013
 УИ ’21’
 СЧ Р500
 ЗП ’500’
 МОДА О1
 ДК ’36’,0
О1 НОП
ОБХ1 СЧ КУСR
 СЛЦ Е8
 ЗП КУСR
 ЦИКЛ УСТРД(М11)
 НТЖ Е11
 СЛЦ Е40
 ЗП КУСR
 ЦИКЛ НАПД1(М17)
 И Е43
 ПЕ ВСЕ ОБА НАПРАВЛЕНИЯ ПРОВЕРЕНЫ
 СЧ Е42
 ЗП КУСR  4-Е НАПРАВЛЕНИЕ
 УИА ’40’(М14)
 ПБ НАПРД
ВСЕ НОП ----
 УИА -1(М17)
 УИА -1(М15)
 СЧ NМБ+1
 СДА 64+12
 И П7777(М10)
 ПО МБИМБП
 ЗП NБЛ(М10)
МБИМБП СЧ NМБ+1(М17) УСТАН.НОММБ,НОММБ1
 СДА 64-42
 РЗБ НАПРМГ(М10)
 МОДА (М10)
 ИЛИ ЕМБФ
 МОДА (М10)
 ЗП НОММБ+1(М17)
 ЦИКЛ МБИМБП(М17)
 СЧ NМБ
 СДА 64+6
 И П7777(М10)
 ПЕ ЛИСОП
 СЧ ТРАКТЫ
 СДА 64+9
 И П77
ЛИСОП ЗП МАХОЗУ(М10)
 СЧ МАЛКАТ(М10)
 ИЛИ НОММБ(М10)
 ЗП МАЛКАТ(М10)
 УИА -’60000’(М14)
МЛМДС СЧ КУСГП
 СБР =М39В’7’М7В’7’
 РЗБ НАПРМГ(М10)
 ЗП РОСМБ
 СЛЦ К
 МОДА (М10)
 ЗП НОММЛ+2(М15)
 ЦИКЛ МЛМДС(М15)
 СБР НАПРМГ(М10)
 ИЛИ НЛОГ(М10)
 ЗП НЛОГ(М10)
 СДА 64+6
 ИЛИ НЛОГ(М10)
 ЗП НЛОГ(М10)
 ЗП НФИЗ(М10)
 СЧ НОММЛ+1(М10)
 СЛЦ Е31
 ЗП НОММЛ+1(М10)
 СБР НАПРМГ(М10)
 СДА 64+42
 УИ М15    НАПР.,УСТР-ВО СИС МД
 ЗП НАПРКЧ  ДЛЯ Ч
 МОДА (М10)
 СЧ ТЗНМД(М15)
 ЗП НАПРКЧ+1    ДЛЯ КЧ
 И Е40П1
 ИЛИ Е47    ПРИЗНАК СИСТЕМНОГО ТОМА
 МОДА (М10)    ЗАНЕСЕНИЕ СИС ТОМА В ТЗНМД
 ЗП ТЗНМД(М15)
МЛВ1А НОП
 СЧ ЕСТЬРД
 ПЕ МЛВ1Р
 СЧ НОМРД
 И =М24В’77777777’
 ЗП НОМРД
МЛВ1Р НОП
 СЧ НОМРД
 СДА 64+18
 И П77        ЧИСЛО КУСКОВ
 ЗП РАБМД(М14)
 УИА LШЗНМБ(М15)
 СЧИ М15
 ВЧ ГРАНБ
 ВЧ РАБМД(М14)
 ПО ВЛЕЗ
 СЛ РАБМД(М14)
 ЗП РАБМД(М14)
ВЛЕЗ НОП
 СЧ НОМРД
 И =В’-1’М18В’77’ ГАШУ ЗАДАННОЕ ЧИСЛО КУСКОВ
 ЗП НОМРД
 СЧ РАБМД(М14)
 СДА 64-18
 ИЛИ НОМРД
 ЗП НОМРД
МЛВ1 СЧ НОМРД
 И П7777(М10)
 ЗП РАБМД(М14)
 СЧ ДЛТ
 СДА 64+12
 И Е39П25
 ИЛИ РАБМД(М14)
 ЗП РАБМД(М14)
 СЧ НОМРД
 СДА 64+12
 И П77
 СДА 64-29
 СЛЦ РАБМД(М14)
 ЗП РАБМД(М14)
 СЧ NМЛ+1
 И П7777(М10)
 ЗП НОММЛ(М10)
 СЧ СЛОЙК
 СДА 64+12
 И П7777(М10)
 ЗП НОМСВ(М14)
 СЧ СЛОЙК
 И Е39П25
 ИЛИ НОМСВ(М14)
 ЗП НОМСВ(М14)
 СЧ 0
 ЗП КУСЛСВ(М10)
 МОДА МРВП  -МГРП В АДАП
 СЧ
 ЗП МАСРВП(М10)
 СЧ СЛОЙК
 И Е47П1(М10)
 ЗП БОБСВЗ(М10) МЛ ДЛЯ СВЯЗИ
 СЧ ТРАКТЫ
 ЗП ПРЕДЕЛ(М10)
 СЧ НОМРД
 УИА ’14000’(М15)
 СДА 64+24
 И П7777(М10)
 ПО РД
 ЗП НОМЛД-’74000’(М15)
 СЧ НОМРД
 СДА 64+36
 И П37 НАЧ КУСОК
 СДА 64-29
 СЛЦ НОМЛД-’74000’(М15)
 ЗП НОМЛД-’74000’(М15)
 СЧ ДЛТ
 И Е39П25
 СЛЦ НОМЛД-’74000’(М15)
 ЗП НОМЛД-’74000’(М15)
 СЧ НОМРД
 СДА 64+42
 ЗП МД5(М10)
 УИ М17
 СЧ ВСЕЕД
 СДА 64-48(М17)
 ЗП КУСКИ-’74000’(М15)
РД ПАМ 0
 СЧ КГОД
 И Е12П1
 ЗП NКОБ-’74000’(М15)
 СЧ КГОД
 И Е48П17
 ЗП КГОД
 УИА -1(М16)
 СЧ 0
ОПМД УИА -31(М17)
ОПМДД ЗП РМР
 НТЖ =М24В’7’
 ПВ ИНТУСЛ(М15)
 ПО ОПМДДД    ДИСК НЕ ВХОДИТ В КОНФИГ. СТАТИТ.
 СЧ Е1(М17)
 МОДА ’14000’
 ИЛИ НОМРМД+1-’74000’(М16)
 МОДА ’14000’
 ЗП НОМРМД+1-’74000’(М16)
ОПМДДД СЧ РМР
 СЛЦ Е25    ЛОГИЧ НОМЕР СЛЕД.ДИСКА
 МОДА ОПМДД
 ЦИКЛ (М17)
 МОДА ОПМД
 ЦИКЛ (М16)
 АIF (∧Р).Л1
 ПО РДС
 АGО .М1
.Л1 АNОР
 ПО ТРМД2
.М1 АNОР
 СЧ NМЛ+1
 СДА 64+43
 И П3
 ИЛИ МПРП(М10)
 ЗП МПРП(М10)
 АIF (∧Р).Л2
РДС СЧ НОМРД
 УИА (М15)
 И П7777(М10)
 ПО НЕТРМД        НЕТ РАБОЧЕГО МД
 СЧ НОМРД
 СДА 64+18
 И П37
 УИ М17 КОЛ-ВО КУСКОВ
 УИА (М14)
ТРМД СЛИА -1(М17) ЗАПОЛНЕНИЕ ШКАЛЫ ЗАНЯТОСТИ ТРАКТОВ МД
 СЧ Е48П17
 МОД ГРАНБ
 МОДА (М17)
 ЗП ШЗНМБО(М10)
 СЛИА 32(М14)
 ПИНО ТРМД(М17)
 СЧИ М14
 ЗП МАХ
НЕТРМД НОП ,
*::::::::::::::::::::::::::::::::::::::::::::::::::::::*
*:::::::::::::::::::::::::::::::::::::::::::::::::::::::*
*               ФОРМИРОВАНИЕ    ШКАЛ   ДЛЯ
*              Т  Е  Р  М  И  Н  А  Л  О  В
*::::::::::::::::::::::::::::::::::::::::::::::::::::::*
*:::::::::::::::::::::::::::::::::::::::::::::::::::::::*
 УИА 0(М17)
 УИА -62(М15)
 УИА -47(М16)
ТЕРАС НОП ,
 СЧ ШКVТ+1(М17)
 И Е48+47(М16)
 ПО НЕТТЕР
 СЧ NМБ+1
 И Е48П46
 ПЕ ДРАС
 СЧ NБЭСМ
 СДА 64-45
ДРАС ИЛИ Е16
 МОДА ТАС
 ЗП 1087(М15)
НЕТТЕР НОП ,
 ПИО СЛЕДЯП(М16)
 СЛИА 1(М16)
 ЦИКЛ ТЕРАС(М15)
 УИА -1(М17)   ТЕРМ АС:64-127
 УИА -63(М15)
СЛТЕР УИА -47(М16)
СЛИТЕ СЧ ШАС128+1(М17)
 И Е48+47(М16)
 ПО СЛ17
 СЧ NБЭСМ
 СДА 64+3
 СЧМР 0
 ИЛИ Е16
 МОДА ТАС+Н
 ЗП 127(М15)
СЛ17 ПИНО СЛ16(М16)
 СЛИА 1(М15)
 ЦИКЛ СЛТЕР(М17)
СЛ16 СЛИА 1(М16)
 ЦИКЛ СЛИТЕ(М15)
 ПБ ЗТТ           ТАС СФОРМИРОВАН
СЛЕДЯП НОП ,
 СЛИА 1(М15)
 УИА -1(М17)
 УИА -47(М16)
 ПБ ТЕРАС
*
*      ЗАНЕСЕНИЕ ТИПОВ ТЕРМИНАЛОВ В ТАБТИП
*      -----------------------------------
*
*------ ЧТЕНИЕ 751 ЗОНЫ В 21 ЛИСТ ------*
ЗТТ УИА (М4)
 УИА 21(М1)       ВЫБРАТЬ КУС 751 ЗОНЫ
 ПВ ЧТГП(М11)
*------ ДОПУСК К РАБОТЕ ПО НОВОМУ ------*
 УИА 1024(М10)    ВОССТАНОВИМ М10 ПОСЛЕ ЧТГП
*
 СЧ ВСЕЕД          ЗАПИСЬ ПРИЗНАКА ’ОLD’
 ЗП ТАБТИП+1024+5  ДЛЯ ПРОЦ-РЫ ТИПТЕР В БОНБОТЕ
*
 МОДА КЛЮ
 СЧ 0
 НТЖ Е24П1
 ПЕ ОLD          ПО СТАРОМУ
*------ ПЕРЕПИСЬ ТИПОВ ТЕРМИНАЛОВ АС ------*
 УИА -10(М16)
ПЕРТИП МОДА ТИПТЕР+14
 СЧ 10(М16)
 ЗП ТАБТИП+Н+12(М16)
 МОДА СКОТЕР
 СЧ 10(М16)
 МОДА СКОРТТ-МОТТ+КАЧ
 ЗП 10(М16)
 ЦИКЛ ПЕРТИП(М16)
*------ПЕРЕПИСЬ ДЛЯ ТЕРМ.НА ПРЯМУЮ ------*
 СЧ NБЭСМ
 СДА 64-1
 И П17
 УИ М15
 МОДА ТИПТЕР-2
 СЧ (М15)
 ЗП ТАБТИП+Н
 МОДА ТИПТЕР-1
 СЧ (М15)
 ЗП ТАБТИП+Н+1
*------  Е N D  ------*
ОLD НОП
 МОДА КАЧ
 УИА -’76000’(М16) М16 := КАЧ+СМЕЩЕНИЕ В МОТСВС
 СЧ Я71766
 ЗП НОМУС(М16)    НОМЕРА ТЕРМИНАЛОВ ДЛЯ ОПЕРАТОРСКИХ МАРШ.
 СЧ Я71767
 ЗП ШКУСТР(М16)   ТЕРМИНАЛЫ  ЕС-7920 И ЕС-7934
 СЧ Я71772
 ЗП ШКППМ(М16)    ТЕРМИНАЛЫ ЕС-7934
 СЧ 0
 ЗП ШОТА(М16)
 УИА 0(М17)
 СЧ NТАС
ЦОТ ЗП АСОТ(М16) Р.Я.
 ПО КЦОТ
 И П77
 ПО СДСТ
 СЧ Е48(М17)
 ИЛИ ШОТА(М16)
 ЗП ШОТА(М16)  ЛОГ.НОМЕРА ТЕРМ.В ОТАСЕ
СДСТ СЛИА 1(М17)
 СЧ АСОТ(М16)
 СДА 64+6
 ПБ ЦОТ
КЦОТ СЧ Я71770
 ЗП ШКОПТТ(М16)   ОПЕРАТОРСКИЕ ТЕРМИНАЛЫ
 ИЛИ ШОТА(М16)
 ИЛИ ШКУСТР(М16)
 ЗП ШКНОВ(М16)
 ЗП ШЗПРИМ(М16)   НОВЫЕ ТЕРМИНАЛЫ ЗАКРЫЛИ В ШЗПРИМ
 НТЖ ВСЕЕД
 ЗП СЛОN(М16)
 СЧ ШОТА(М16)
 ЗП ШКАС(М16)
 СЧ ШКVТ
 СДА 64+33
 ИЛИ ШКVТ2
 ПО НЕТАС
 СЧ ШГ+1024
 ИЛИ 0             ИЛИ Е46 (ПРИЕМ АС ПВВ)
 ЗП ШГ+1024
 СЧ NТАС
 ЗП АСОТ(М16)
НЕТАС НОП 0
 СЧ NМБ+1
 СДА 64+24
 И П177
 ЗП ЛИНСББ(М16)
 СЧ NМБ+1
 СДА 64+31
 И П177
 НТЖ ЛИНСББ(М16)
 ЗП ЛИНСББ+1(М16)
 СЧ NБЭСМ
 ЗП НОМАС(М16)
 СЧ NМБ+1
 СДА 64+45
 ПО СТНАС
 ЗП НОМАС(М16)
СТНАС НОП
*::::::::::::::::::::::::::::::::::::::::::::::::::::::*
*::::::::::::::::::::::::::::::::::::::::::::::::::::::*
 СЧ NБЭСМ
 И Е39П1
 ИЛИ КГОД
 ЗП ГОД(М10) МЛ.ЦИФРА ГОДА,НАЧ.N И КОЛ-ВО ОБЩ.МД
ПРОВД НОП ------
 УИА РМР(М17)
 УИА -63(М16)
 УИИ М11(М15)
 СЧ
Д29 ЗП (М17)
 ПВ ИНТУСЛ(М15)
 УИ М15
 ПО ПД29
 УВВ (М15)
 СДА 64+27
 И П3
 ПО ПД29          МД НЕ 29 И НЕ 100 МГБ
 И Е2
 УИ М1            М1 = 1 - МД 100 МГБ
 СЧ -1(М17)
 СДА 64+29
 УИ М15
 СЧМР
 СДА 64+43
 СЧМ Е32
 МОД (М17)
 СДА 64
 ЗП РЯ3
 ИЛИ МД29М+Н(М15)
 ЗП МД29М+Н(М15)
 ПИО ПД29(М1)
 СЧ РЯ3
 МОДА Г100МД
 ИЛИ (М15)
 МОДА Г100МД
 ЗП (М15)
ПД29 СЧ (М17)
 СЛЦ Е25
 ЦИКЛ Д29(М16)
*
 СЧ 0
 ЗП РЯ3
 СЧ ГРАНБ
 МОДА (М10)
 ЗП ГРАНЬБ
 ВЧОБ
 УИ М16
 СЛИА 1(М16)
 СЧ NМБ
 И П37
 УИ М15
 СЧ ТРСИСТ
 НТЖ ОТБРТР
 ЗП СЧВРБ-8(М15)  1 - ТР ПОЛЬЗОВАТ
 СЧ NМБ+1
 И П37
 УИ М15
 СЧ ТРСИСТ+1  ТР ПОЛЬЗ
 ЗП СЧВРБ-8(М15)
 УИА (М15)
ЧТРП СЧ СЧВРБ(М15) ПОДСЧЕТ ЧИСЛА ТРАКТОВ ПОЛЬЗ-ЛЯ
 И Е48П17
 МОДА (М10)
 ЗП ШЗНМБО(М15)
 МОДА МК
 ЗП БРАКТР(М15)
 ЧЕД РЯ3
 ЗП РЯ3
 СЛИА 1(М15)
 ЦИКЛ ЧТРП(М16)
 СЧ РЯ3
 ЗП ТМБ(М10)      ТМБ := КОЛ-ВО ТРАКТОВ НА МБ
 ЗП ОЗУ(М10)      ОЗУ := ТМБ
 ВЧ П27
 ПЕ МАЛО
 ЗП ОЗУ(М10)      ОЗУ := ТМБ - 27
МАЛО НОП ,
 СЧ МАХ
 ЗП ТМД(М10)      ТМД := КОЛ-ВО ТРАКТОВ НА МД
 СЛЦ ТМБ(М10)
 ЗП РЯ3
 СЧ ПРЕДЕЛ(М10)
 И П777
 ВЧОБ РЯ3
 ПО ТРАК МАХ ЧИСЛО ТРАКТОВ НА ЗАД.ДОПУСТИМО
 СЧ ВСЕЕД
 НТЖ П777
 И ПРЕДЕЛ(М10)
 ИЛИ РЯ3
 ЗП ПРЕДЕЛ(М10)
ТРАК СЧ NМБ
 СДА 64-42
 РЗБ НАПРМГ+1024
 ИЛИ КНАПР+2
ТРАК1 НОП
 ИЛИ ЕМБФ+1024
 ЗП НОММБ2+Н
 УИА МК(М5)
П36Л УИА ’14000’(М1)
 УИА ’74000’(М2)
 УИА -1(М4)
 ПВ ПЕРЕПС(М15)   ПЕРЕНОС ДИСКИ,МОТТ
 УИА 1024(М10)
*
*  ФОРМИРОВАНИЕ  Ш К А П
 СЧ =М46В’3’В’177777776’  1-2,30-57 КАНАЛЫ
 ЗП ШКАП(М10)
*
 УИА МК(М5)
 СЧ КОДТР
 ПО БЕЗРАЗ
 НТЖ Е2
 ПО БЕЗРАЗ
 НТЖ П6+1024
 ПО С3РАЗ ВЫЗОВ С 3 РАЗР.
 ПБ ПОЧП
С3РАЗ СЧ П17
 ИЛИ ММГРП(М5)  БЛОК ВЫВОДА
 ЗП ММГРП(М5)
БЕЗРАЗ СЧ МГРП(М10) СОХР КАТАЛ
 СДА 64+24
 СДА 64-24
 ИЛИ ММГРП(М5)  МЛ РАЗР МГРП СОХР
 ИЛИ Е10
 ЗП МГРП(М10) М10=2000.ПРИ ОБМ С МБ ПОРТИТСЯ
ПОЧП ПАМ 0
ПОЧП1 НОП
 СЧ 0
 УИА -3071(М15)
ОЧИПЗ МОДА ’62000’
 ЗП 3071(М15)
 ЦИКЛ ОЧИПЗ(М15)
 УИА -31(М14)
В4 УИА -1(М13)
В5 УИА 0(М4)
 УИА 1(М1) ОБНУЛЕНИЕ СЧ-КА ЗП.В СЛУЖ.СЛОВАХ
 ПВ ОБМИМБ(М15) ИНФОРМАЦИОННОГО МБ (ПЕРЕЗАПИСЬ
 СЧ КУСИМБ+1 ИНФ.ЧЕРЕЗ 20 СТР.
 НТЖ Е18
 ЗП КУСИМБ+1
 ЦИКЛ В5(М13)
 СЛЦ Е3 +1 К N ТРАКТА
 ЗП КУСИМБ+1
 ЦИКЛ В4(М14)
*
* ЧТЕНИЕ ЕСМЛ,ДМЛМБ,ХЛАМ,РЕЗОТ НА 25,26,27 ЛИСТЫ
*
 УИА 5(М1)
 УИА -2(М4)
 ПВ ЧТГП(М11)
*
 СЧ 3
 И Е32П25+1
 НТЖ Е32П25+1
 ЗП ПРРО
 ПО ИСПКВВ
*
****************************************************
*               Н   А  Ч  А  Л  О                  *
*    БЛОКА ФОРМИРОВАНИЯ   И П З  СБОЙНЫХ ЗАДАЧ     *
*                                                  *
****************************************************
*
*
*
*
*
 УИА 3(М1)
 ПВ ЧТМДВ(М11) ЧТ НА 22 Л ШИФРОВ
 УИА ’44000’(М11)
 СЧ (М11)
 ИЛИ ’400’(М11)
 ПЕ ИСПКАТ ИСПОРЧЕН КАТ.ШИФРОВ
ПРОК УИА (М1)
 УИА -1(М4)
 ПВ ЧТМДВ(М11)
ФИПЗЗ УИА БКТ+8(М3)
 УИА (М11)
 УИА -126(М4)
ФИПЗ МОДА КШ ФОРМИР.ИПЗ ЗАДАЧ,СЧИТАВШ.В МОМ СБОЯ
 МОДА (М11)
 СЧ 127(М4)
 ПО СЛЕД
 СЧ 6(М3)
 И Е31
 ПО СЛЕД НЕ В РЕШ
 СЧ 7(М3)
 СДА 64+45
 НТЖ NБЭСМ
 И П7
 ПЕ СЛЕД НЕ ТА БЭСМ
 СЧ 7(М3)
 СДА 64+17
 И П77 N КАНАЛА
МОР УИ М15
 ВЧ МИНЗ+1024
 ПЕ МУР
 ВЧ МАКЗ+1024
 ПЕ МИР
МУР СЧ ШГП+1024
 НТЖ ШКАП+1024
 НТЖ Е1
 ПО ФИП
 НЕД 0
 ПБ МОР
МИР СЧ ШГП+1024
 И Е48-1(М15)
 ПЕ МУР
ФИП1 СЧ ТУСП-1+Н(М15)
 И П77777
 УИ М17
 ПЕ ФИП2
 СЧ Е48-1(М15) ВРЕМ
 И ШКАП+Н     ПОТОМ ЭТУ ПРОВЕР УБР
 ПО СЛЕД
 СЧ РАСПП
 ИЛИ ЛИСТА3
 НТЖ Е48П17
 ПО СЛЕД
 НЕД П77777
 УИ М2
 СДА 64-10
 УИ М7
 СЧ РАСПП
 ИЛИ Е48(М2)
 ЗП РАСПП
 УИА -1023(М1)
 СЧ 0
РО МОДА (М7)
 ЗП 1023(М1)
 ЦИКЛ РО(М1)
 УИА -11(М2)
 УИА (М1)
 УИА -2(М17)
ОПРИ СЧ Е48-1(М15)
 И Е48П37+2+Н(М17)
 ПЕ ОПРИ1
 СЛИА 12(М1)
 ЦИКЛ ОПРИ(М17)
ОПРИ1 МОДА (М1)
 СЧ ТУСП+11+Н(М2)
 И П77777
 ПЕ ФИП1
 СЧИ М7
 МОДА (М1)
 ИЛИ ТУСП+11+Н(М2)
 МОДА (М1)
 ЗП ТУСП+11+Н(М2)
 СЛИА ДЛИН(М7)
 ЦИКЛ ОПРИ1(М2)
 ПБ ФИП1
ФИП2 СЧИ М15
 ПВ АДСЕК(М7)
 СЧ ИН(М1)
 И Е39П25
 ЗП РЯ1
 СЧ 5(М3)
 И П1777
 ИЛИ РЯ1
 ЗП РЯ1
 СЧ (М3)
 СДА 64+40
 СДА 64-40
 ИЛИ РЯ1
 ЗП ИН(М1)
 СЧИ М1
 И П17
 УИ М7
 СЛИ М1(М7)
 СЧ ВРОС(М1)
 ИЛИ ВРЕР(М1)
 ПО ФИП3
 СЧ ВРЕР(М1)
 ЗП 39(М17)
 СЧ ВРОС(М1)
 СДА 64+23
 ПБ ФИП4
ФИП3 СЧ (М3)
 СДА 64-5
 И Е25П1 ЗАКАЗАННОЕ ВРЕМЯ
 НТЖ ВСЕЕД
 СЛЦ Е1
ФИП4 СДА 64-23
 ЗП 33(М17) ВРЕМЯ В ИПЗ В ДОП.КОДЕ
 СЧ (М3)
 СДА 64-37
 ИЛИ 35(М17)
 ЗП 35(М17)
 СЧ (М3)
 СДА 64+11
 СДА 64-39
 ИЛИ ЯЧВР(М17)
 ЗП ЯЧВР(М17)
 СЧ 5(М3)
 СДА 64+16
 И Е8П1
 УМН ЧИСЛСТ
 СЧМР 64
 ВЧОБ Е17 УСТ.В ИПЗ ЧИСЛО СТРОК ПЕЧАТИ
 ИЛИ Е48П17 ЛИСТЫ ЗАКРЫТЬ
 ЗП 32(М17) В ОБРАТНОМ КОДЕ
 СЧ Е48П17
 ЗП 22(М17)
 СЧ =М47В’1’В’2500’
 ЗП КВНЗАД(М17)
 СЧ Е31
 ЗП АВОС(М17)
 УИА 65(М14)
 СЧИ М14
 СДА 64-15
 ЗП 23(М17)
 СДА 64-26
 ЗП 36(М17) СБОЙ - ВВЕДИ СНОВА
 УИА АВРАСП(М14)
 СЧИ М14
 ЗП 3(М17) УСТ.ВХОД В АВРАСП
 СЧ ИВ25+1024
 НТЖ Е19
 ЗП 5(М17) ДЛЯ УСТ.М21 И М27 ПРИ ВЫБОРЕ ЗАДАЧИ
 СЧ Е48-1(М15)
 ИЛИ ШГП+1024
 ЗП ШГП+1024
 СЧ ГРУП+3+1024
 ИЛИ Е48-1(М15)
 ЗП ГРУП+3+1024   ЗАДАЧА 5 КЛАССА
 СЧ ЛЕСТ+3+1024
 ИЛИ Е48-1(М15)
 ЗП ЛЕСТ+3+1024
 МОДА КШ А КАТАЛ.ШИФРОВ
 МОДА (М11) ТЕЛЕ С 0 АБЗ.
 СЧ 127(М4)
 ЗП ШИФ(М17)
СЛЕД СЛИА 8(М3)
 ЦИКЛ ФИПЗ(М4)
ФИП УИА (М4)
 СЧ ШГП+1024
 И ШКАП+1024
 МОДА ’20000’
 ЗП ШКРЗ
 УИА 4(М1)
 УИА (М4)
 УИА -МЧПЗ(М13)
 ПВ ЧТМДВ(М11) ЧТ.ЗОНЫ С ФИО НА 23Л
 УИА -2(М14)  3 ЗОНЫ С ФИО
ФАМ УИА -МЧПЗ(М12)
 УИА ФАМ1А(М7)
ФАМ1 УИА -511(М3) ПОИСК ФАМИЛИИ
 УИА ’50001’(М4)
 МОДА 1024
 МОД ТУСП+МЧПЗ+НПЗ(М12)
 УИА (М17)
 ПИО ФАМ3(М17)
ФАМ2 СЧ ШИФ(М17)
 ПО ФАМ3
 НТЖ (М4)
 И Е48П33
 ПО ФАМН ФАМИЛ НАЙДЕНА
 СЛИА 2(М4)
 ЦИКЛ ФАМ2(М3)
ФАМ3 ЦИКЛ ФАМ1(М12)
 ПИО КОНЗАД(М14)
 СЛИА 1(М14)
 СЧ КУСД1
 СЛЦ Е25 +1 К ЗОНЕ
 ЗП КУСД1
 ПВ ЧТМД(М1)
 МОД ЛСТБАЗ
 УИА (М6)
 ПБ ФАМ
ФАМН СЧ -1(М4) СБОРКА ФИО
 ЗП РЯ1
 СДА 64-36
 ЗП РЯ2 7,8 СИМВ
 УИИ М15(М12)
 СЛИА ЧПЗ+НПЗ(М15)
АДСЕК СЧИ М15
 И П17
 УИ М2
 РЖА 3
 СЧИ М15
 СДА 64+4
 ВЧОБ Е2
 СДА 64-8
 УИ М1
 СЛИ М1(М2)
 СЛИА МК(М1)
 ПБ (М7)
ФАМ1А СЧ (М4)
 СДА 64-24
 СДА 64+12
 ИЛИ РЯ2 7-12 СММВ
 РЗБ МАСКА
 СЛЦ ЗАП
 ЗП ФИО1(М1)
 СЧ РЯ1
 РЗБ МАСКА
 СЛЦ ЗАП
 ЗП ФИО(М1)
 ПИО КОНЗАД(М13)   ВСЕ ФИО ОПР-НЫ
 СЛИА 1(М13)
 ПБ ФАМ3
*
****************************************************
*               К   О  Н  Е  Ц                     *
*    БЛОКА ФОРМИРОВАНИЯ   И П З  СБОЙНЫХ ЗАДАЧ     *
*                                                  *
****************************************************
*
ИСПКАТ УИА -1023(М15)
 СЧ Е1
 ЗП ИСКАТ
 СЧ 0
ОЧКШ МОДА (М11) РОСПИСЬ ЛИСТА С ШИФРАМИ
 ЗП 1023(М15)
 ЦИКЛ ОЧКШ(М15)
 ПБ ФИПЗЗ
ИСПКВВ СЧ Е2 ПР:КАТАЛ.ВВОДА ИСПОРЧЕН
 ЗП ИСКАТ
 УИА -1023(М15)
 СЧ 0
ОЧМК МОДА МК
 ЗП 1023(М15)
 ЦИКЛ ОЧМК(М15)
 СЧ ГРАНБ
 ВЧОБ
 УИ М16
 СЛИА 1(М16)
 УИА 0(М15)
ЗАНТР МОДА (М15)
 СЧ ШЗНМБО+1024
 МОДА МК
 ЗП БРАКТР(М15)
 СЛИА 1(М15)
 ЦИКЛ ЗАНТР(М16)
 СЧ УПРМБ
 МОДА МК
 ЗП МБМК
 СЧ ГРАНБ
 МОДА МК
 ЗП УПРГРА
КОНЗАД НОП ,
 УИА 2(М1)
 УИА 0(М4)
 СЧ NМЛ+2
 МОДА МК
 ЗП КЛЮМК         ЗАПИСЬ КЛЮЧА М К
 ПВ ОБМИМБ(М15)   ЗАПИСЬ МАЛОГО КАТАЛОГА
 СЧ РАСПП
 И Е20
 ЗП ЗАП20Р
 СЧ Е20
 ИЛИ РАСПП ЛИСТ ПОД ГЕНС НЕ РАСПИС
 ЗП РАСПП
 УИА 19(М1)
 УИА (М4)
 ПВ ЧТГП(М11)
 УИА 5(М1)
 ПВ ОБМИМБ(М15)   ЗАПИСЬ ТУПР НА  03 ТРАКТ ИНФ.МБ
* ЧТЕНИЕ  БОЭК1,ВИСП,АВМД НА  7 И 10 ЛИСТЫ
 УИА 20(М1)
 УИА -1(М4)        ДВЕ ЗОНЫ
 ПВ ЧТГП(М11)
 СЧИ М6
 СДА 64+10
 УИ М1
 СЧ КУСГП
 И КОСД
 ИЛИ КУСНН+Н
 ЗП КУСНН+Н
 СЧ Е48(М1)
 ИЛИ ЛИСТА3
 ИЛИ РАСПП
 ЗП РАСПП
 УИА 1024(М1)
 УИА 0(М2)
 УИА -3(М4)   ПЕРЕПИСЬ ДИСП С 1-4 СТР.
 МОДА ПЕРЕПС       НА 0-3  СТР
 ПВ (М15)
 СЧ ГОД
 И П7
 УИ М15
 СЧ Е42(М15)
 МОДА КОНГЕН
 ИЛИ 0
 МОДА КОНГЕН        ДИСП80 НА СВОЕМ МЕСТЕ
 ЗП 0
 СЧ ШКИВ
 ИЛИ ЕСБОЙВ
 ЗП ШКИВ
 СЧ ’501’   ИЗ ДИСП70
 ЗП ДИС501
 СЧ ЯЧ501      ИЗ ГЕНС
 ЗП ’501’   ДЛЯ ОБМЕНОВ ИЗ ГЕНС
* ФОРМИРОВАНИЕ  ТАБКОД
 УИА 9(М1)
 УИА 0(М4)
 ПВ ЧТГП(М11)    ЧТЕНИЕ ТАБКОД НА 4 ЛИСТ
 СЧ ТЕГКОМ
 РЕГ ’44’
 УИА ТАБКОД(М15)
 УИА -4(М1)
ПЕРТК1 НОП ,
 УИА -127(М2)
 УИА ИСХТК(М4)
ПЕРТК2 НОП ,
 МОДА 127(М2)
 СЧ (М4)
 МОДА 4(М1)
 СБР СБ6Б
 СДА 64+40
 ЗП РАБ
 МОДА 127(М2)
 СЧ (М4)
 МОДА 4(М1)
 СБР СБРБАЙ
 СДА 64+32
 ИЛИ РАБ
 ЗП РАБ
 ЧЕД
 И Е1
 ИЛИ РАБ
 СДА 64+16
 СЧ (М15)
 КК ’032’,(М15)
 СЛИА 1(М15)
 ЦИКЛ ПЕРТК2(М2)
 ЦИКЛ ПЕРТК1(М1)
 СЧ Е5
 РЕГ ’44’
*
*
 УИА (М4)
 АIF (∧МВВ ЕQ 1).МВВ3
 УИА ’12000’(М1)
 УИА ’72000’(М2)
 АGО .МВВ4
.МВВ3 АNОР
 ПБ ПРДИМК
БЕЗСПП УИА ’72000’(М2)
.МВВ4 АNОР
 ПВ ПЕРЕПС(М15)    КИТ,КАЧКА
 УИА 0(М4)
 УИА 10(М1)
 ПВ ЧТГП(М11)   ЧТЕНИЕ АВОСТ-А НА 5 ЛИСТ
 РЕГ ’107’
 СЧ 3
 И Е8
 НТЖ Е8
 ЗП СИТУАЦ
 ПЕ НЕСИТ
 СЧ РАСПП+2
 ПО ЕСТЬ2П
 СЧ 0
 ЗП СИТУАЦ        СИТУАЦИЮ НЕ ЗАПИСЫВАТЬ
 ПБ НЕСИТ
ЕСТЬ2П НОП
 СЧ Е6
 СЛЦ ОЗУ
 ЗП ОЗУ
 СЧ Е4817Д
 ЗП РАСПП+2
НЕСИТ НОП 0
*
* ПЕРЕНОС ИНФОРМАЦИИ СВЯЗАННОЙ С НОМЕРОМ БАЗОВОЙ СЕКЦИИ  О С
*
 СЧ ПРИСОГ
 ЗП ПРИСОС
*
 СЧ СЕКОСГ
 ЗП СЕКОС         01 Р.
 СДА 64-2
 ЗП БОСТЗС        03 Р.
 СДА 64-5
 ЗП НЛОС          08 Р.
 СДА 64-36
 ЗП БОСОБМ        44 Р.
*
 СЧ ТРАКТЫ
 И Е46
 ПО НЕТСВЯ        НЕТ РАЗДЕЛА ГЕНЕРАЦИИ ОС ’СВЯЗЬ ДА’
 СЧ Е44          44-ЫЙ(8) ЛИСТ БАЗОВОЙ СЕКЦИИ
 ИЛИ РАСПП+1
 ЗП РАСПП+1       З А К Р Ы Л И
 СЧ =В’44’
 ИЛИ НЛОС
 ЗП ЛИСТСВ        ЛИСТСВ := N ЛИСТА10-1 РР
 И 0
 КК ’32’,СВСЭ
 КК ’32’,ЭСВС
*
НЕТСВЯ НОП ,
* ВЫДЕЛЕНИЕ БУФЕРНЫХ ЛИСТОВ   О С
*
 СЧ РАБМД
 И П7777
 ПО НЕТУК        УКАЧКА НЕ РАБОТАЕТ
 ПВ ДАЙЛИС(М17)
 СДА 64+2
 ЗП ДУК            ЛИСТ ДЛЯ УКАЧКИ
НЕТУК НОП ,
* ЗАПРОС ЛИСТОВ ДЛЯ СЕКТОРНОГО ОБМЕНА И ДЛЯ КОНТ.ЧТЕНИЯ С МД
 УИА -3(М15)
ЛИСТМД НОП ,
 ПВ ДАЙЛИС(М17)
 СДА 64+2
 ЗП ЛСЕК+3(М15)
 ЦИКЛ ЛИСТМД(М15)
*
*    ЗАПРОС ЛИСТА ДЛЯ СОБЫТИЙ
*
 ПВ ДАЙЛИС(М17)
 РЗБ ПРИПД+1
 ЗП ЛПРОЦ
* ЗАПРОС ЛИСТА ДЛЯ  АС ЧЕРЕЗ ПВВ И ЕС-7920
 ПВ ДАЙЛИС(М17)
 ЗП БАСЕС
*ЗАПРОС ЛИСТОВ ДЛЯ ТОП И ТБД
 ПВ ДАЙЛИС(М17)
 РЗБ ПРИПД+1
 ЗП ЛТБД          ПРИПИСКА ТОП
*
*     ++++++++++++++++++++++++++++++++++++++++++++++++++
*     +                                                +
*     +          ИНИЦИАЛИЗАЦИЯ КОНСТАНТ В ТОП-Е        +
*     +           ДЛЯ РАБОТЫ В РЕЖИМЕ ПРИПИСКИ         +
*     +                   ПОЛЬЗОВАТЕЛЯ                 +
*     +                                                +
*     ++++++++++++++++++++++++++++++++++++++++++++++++++
*
 СЧ ЕОБМ
 УИА 1026
 ЗП ЕОБММ
 УИА 1027
*
 СЧ ВИОЛП
 УИА 1026
 ЗП ВИОЛПМ
 УИА 1027
*
 СЧ ЕОТКЛ
 УИА 1026
 ЗП ЕОТКЛМ
 УИА 1027
*
 СЧ ВИОЛ
 УИА 1026
 ЗП ВИОЛМ
 УИА 1027
*
 СЧ П17777
 УИА 1026
 ЗП М17777
 УИА 1027
*
 СЧ КВВЛ
 УИА 1026
 ЗП КВВЛМ
 УИА 1027
*
 СЧ КВСЧК
 УИА 1026
 ЗП КВСЧКМ
 УИА 1027
*
 СЧ КВТВЛ
 УИА 1026
 ЗП КВТВЛМ
 УИА 1027
*
 ЭКВИВ (ВИОЛПМ,’2000’),(ВИОЛМ,’2001’),(ЕОБММ,’2002’)
 ЭКВИВ (ЕОТКЛМ,’2003’),(М17777,’2005’)
 ЭКВИВ (КВВЛМ,’2006’),(КВСЧКМ,’2007’)
 ЭКВИВ (РАБ1РМ,’2010’),(РАБ2РМ,’2014’)  РАБ ЯЧ.РЕСУРСА
 ЭКВИВ (РАБ1М,’2015’),(РАБ2М,’2016’)  РАБ.ЯЧ. Е5 ’ЗАНЯТО’
 ЭКВИВ (РАБРЭМ,’2037’),(КВТВЛМ,’2036’)
*
 ВХОД РАБРЭМ,КВТВЛМ
 ВХОД ВИОЛМ,ВИОЛПМ,ЕОБММ,ЕОТКЛМ,М17777,КВВЛМ,КВСЧКМ
 ВХОД РАБ1М,РАБ2М,РАБ1РМ,РАБ2РМ
*
*    ++++++++++++++++++++++++++++++++++++++++++++++
*
 ПВ ДАЙЛИС(М17)
 РЗБ ПРИПД+2
 ИЛИ ЛТБД
 ЗП ЛТБД           ПРИПИСКА 1-ОГО ЛИСТА ТБД
 СЧ НОМРД
 СДА 64+18
 И П77
 СЛЦ ГРАНЬБ
 ВЧОБ Е6
 ПО ОДИНЛ         ТРАКТОВ < 1024
 УИА ДВАЛИС(М17)
 ПБ ДАЙЛИС
ОДИНЛ НОП ,
 СЧ ЛТБД
 СБР ПРИПД+2
ДВАЛИС НОП ,
 РЗБ ПРИПД+3
 ИЛИ ЛТБД
 ЗП ЛТБД          ПРИПИСКА 2-ОГО ЛИСТА ТБД
 СЧ СЕКОС
 СДА 64-45
 РЗБ ПРИПД
 ИЛИ ЛТБД
 ЗП ЛТБД     ПРИП. 0-МУ ЛИСТУ ДЛЯ СЛУЖ.СЛОВ МЛ
 ЗП КАЧКА+31
 ЗП ИПОБМ+31
* ЗАПРОС ЛИСТОВ ДЛЯ СООБЩЕНИЙ  О С
 МОДА ДАЙЛИС
 ПВ (М17)
 РЗБ ПРИПД+1
 ЗП ЛТВЫД
 МОДА ДАЙЛИС
 ПВ (М17)
 РЗБ ПРИПД+2
 ИЛИ ЛТВЫД
 ЗП ЛТВЫД
*
*
*
* ФОРМИРОВАНИЕ ЛИСТОВ ОБЩЕГО ПУЛА
*
*
 СЧ ПРЕДЕЛ
 И Е31
 ПО НЕТПУЛ        НЕТ РАЗДЕЛА ’ОБЩПУ’
 МОДА ПРИПУЛ
 СЧ 0
 ПЕ НЕТПУЛ        ОБЩПУЛ ОБРАЗОВАН ДРУГИМ  СВС
 УИА -7(М15)
ЛИСПУЛ НОП
 МОДА ДАЙЛИС
 ПВ (М17)
 СДА 64+2
 МОДА ПРИПУЛ
 ЗП 7(М15)        N ЛИСТА С 37 РАЗРЯДА
 ЦИКЛ ЛИСПУЛ(М15)
НЕТПУЛ НОП ,
*
 И 0
 КК ’32’,СЕМППД
 РЕГ ’254’
 ИЛИ ЕПРМПД
 РЕГ ’54’    ОТКР.МАСК.ИЗ МПД
*
* Р О С П И С Ь  О П  И   ФОРМИРОВАНИЕ     Т З Ф С Т Р
*
*
*
 УИА -31(М15)
 СЧ Е48П17
РОСТЗС НОП
 ЗП ТЗФСТР+31(М15)
 ЦИКЛ РОСТЗС(М15)
* ПЕРЕПИСЬ    Р А С П П
 УИА -3(М15)
ПЕРРПП НОП ,
 СЧ РАСПП+3(М15)
 МОД БОСТЗС
 ЗП ТЗФСТР+3(М15)
 ЦИКЛ ПЕРРПП(М15)
*
 РЕГ ’236’
 НТЖ ВСЕЕД
 И ШКАМОГ        ШКАЛА ЗАДАННЫХ МОДУЛЕЙ(ВЫЗОВБ)
 ЗП ШКАМОД        ШКАЛА РАБОЧИХ МОДУЛЕЙ  О С
 ПВ ВКЛМОД(М15)
 СЧ =М28Х’F’
 МОД БОСТЗС
 СДА 64-16
 ЗП NМОД           NМОД := МОДУЛИ БАЗ.СЕК.ОС
 СЧ ШКАМОД
 СДА 64-16
ФОРТЗС НОП ,
 ЗП РАБ
 ПО РОСМОД
 НЕД П77777
 И П77777
 ЗП ПОСМОД
 УИ М15
 СЧ NМОД
 И Е48(М15)
 ПЕ НЕЛЬЗЯ         МОДУЛЬ БАЗОВОЙ СЕКЦИИ ОС
 СЧ 0
 ЗП ТЗФСТР(М15)   ЛИСТЫ МОДУЛЯ ДОСТУПНЫ
НЕЛЬЗЯ НОП ,
 СЧ РАБ
 НТЖ Е48(М15)
 ПБ ФОРТЗС
*
РОСМОД НОП ,
 УИА -31(М15)
 СЧ 0
 ЗП NМОД
РОСМ1 НОП ,
 СЧ ТЗФСТР+31(М15)
 НТЖ Е48П17
 ЗП РАБ
 ЧЕД
 ПО СЛЕДС         СЕГМЕНТ ЗАКРЫТ
 СЛЦ ОЗУ
 ЗП ОЗУ
РОСЛИС НОП ,
 СЧ РАБ
 НЕД П77777
 И П77777
 УИ М16
 СЛЦ NМОД
 СДА 64-38
 РЗБ ПРИПД+1
 МОДА УПР0Л
 ДК ’36’,0
УПР0Л НОП
 РЕГ ’20’          УСТАНОВКА ПРИПИСКИ 1-ОМУ МАТ.ЛИСТУ
 УИ 0
 СЧ 0
 УИА -1023(М17)
 УИА 1026
РОСЛИ1 НОП ,
 ЗП 1024+1023(М17)
 ЦИКЛ РОСЛИ1(М17)
 УИА 1027
 СЧ РАБ
 НТЖ Е48(М16)
 ЗП РАБ
 ПЕ РОСЛИС
СЛЕДС НОП ,
 СЧ NМОД
 СЛЦ Е6
 ЗП NМОД
 ЦИКЛ РОСМ1(М15)
*
 СЧ ОЗУ           ТРАКТЫ НА МБ + О П
 СЛЦ ТМД          ТРАКТЫ НА МД
 ЗП ТОБ           ТОБ := ТРАКТЫ + О П
*
* ФОРМИРОВАНИЕ ГРАНИЦ ДИАПОЗОНОВ
*   ДОСТУПНЫХ ЛИСТОВ ДЛЯ КАЧКИ
*
 СЧ  Е1            ПРИЗНАК ,ЧТО НЕЛЬЗЯ РАЗМЕЩАТЬ ЛИСТЫ МАТ.
 ИЛИ Е2            ПРИЗНАК ,ЧТО НЕЛЬЗЯ РАЗМЕЩАТЬ ТРАКТЫ
 МОД БОСТЗС
 ИЛИ ТЗФСТР
 МОД БОСТЗС
 ЗП ТЗФСТР        ОТМЕТИЛИ 0 СЕГМ.БАЗ.СЕК.ОС
*
* ДЛЯ ТБУФ
*
 СЧ СЕКОС
 СДА 64-7
 ЗП НАЧФИЛ
 ЗП ФИЛ
 СЛЦ =В’37’
 ЗП КОНФИЛ
*
* ДЛЯ ДБУФ
*
  СЛЦ Е1
 ЗП НАЧФИЛ+1
 ЗП ФИЛ+1
 СЛЦ =В’137’
 ЗП КОНФИЛ+1
*
*ДЛЯ МАТЕМАТИКА
*
 СЧ ШКАМОД
 СДА 64-16
 НЕД П77777
 И П77777
 СДА 64-5
 ЗП ФИЛ+2
 ЗП НАЧФИЛ+2
 СЧ ПОСМОД
 СДА 64-5
 СЛЦ =В’37’
 ЗП КОНФИЛ+2
*
 СЧ СМШГ
 НТЖ Е36
 ЗП СМШГ
 СЧ МГРП
 ПВ ТВ67(М15)  ГАШ.ГРП,УСТ МГРП (40,32,10)
 РЕГ ’101’
 ПВ ОТМТЕС(М15)
 СЧ ПРИСОС
 ИЛИ ПРИРЕК
 ЗП ПРИРЕК        ПРИПИСКА РЕКОРДАМ ТЕРМИНАЛОВ
 СЧ ПРИПД+3
 НТЖ ВСЕЕД
 МОДА ПРАДАП
 И 0             УБРАЛИ ПРИПИСКУ 17 ЛИСТА ОС
 ЗП ПРИПАД
 СЧ НЛОС
 ИЛИ П17
 СДА 64-38
 РЗБ ПРИПД+3
 ИЛИ ПРИПАД
 ЗП ПРИПАД         ПРИПИСКА АДАП-А
 СЧ СЕКОС
 СДА 64-45
 РЗБ ПРИПД
 ЗП ИПЗМЛ+’37’    ПРИПИСКА 0-ОГО ЛИСТА ОС
 СЧ 0
 РЕГ ’37’
 СЧ ДИС501   ВОССТ ДИСП
 ЗП ’501’
*
 СЧ Г100МД
 ЗП МД100М
 СЧ Г100МД+1
 ЗП МД100М+1
  СЧ 7
 И Е25
 ПО ОДНСЕК
* МНОГОСЕКТОРНЫЙ ФОРМАТ
 СЧ =М24Ф’10127’
 ЗП КОНЗОН+2
ОДНСЕК НОП ,
*
 ДК ’36’,БВЗ      УХОД В   Д И С П 8 0   НА   Б В З
*
* ДАЛЕЕ РАБОТАЕМ ОТ ИМЕНИ   Н О М Б О Б -А
*
АЛИСТЫ ПАМ 0
 СЧ СИТУАЦ
 ПЕ НЕТСИТ
 УИА -31(М11)
 СЧ Е43      100 ЛИСТ
 СЛЦ БОСОБМ
СИТ НОП ,
 ЗП Р3
 УИА 1027
 ЗП РМРД
 СЧ НОММЛ1
 СЛЦ КУССИТ
 ПВ ФИЗУР(М16)      ЗП СИТУАЦ
 УИА 3
 СЧ КУССИТ
 СЛЦ Е25
 ЗП КУССИТ
 СЧ Р3
 СЛЦ Е37
 МОДА СИТ
 ЦИКЛ (М11)
 СЧ 0
 МОД БОСТЗС
 ЗП ТЗФСТР+2
НЕТСИТ НОП 0
 УИА 1(М11)
 СЧ ПЕРВ
 ПЕ ББ1-У1(М7)
 СЧ Е1
 ЗП ПЕРВ
 МОДА (М11)
ВЫА СЧ П6076
 ПВ ТБУФ(М15)
АВМДС СДА 64-10
 СЛЦ НОММБ1
 МОДА (М11)
 СЛЦ ТРАВ
 ЗП Р3
 ПВ ФИЗОБМ(М16)
 СЧ Р3
 МОДА АВМЛС
 ПИНО (М11)
 УИА 3
 ПБ (М7)
АВМЛС ИЛИ Е48Е47
 ПВ ТБУФ(М15)
 СЧ КУСГП
 И НАПРМГ
 ЗП Р9
 СБР НАПРМГ
 СДА 64+42
 УИ М10
ОЖ СЧ ТЗНМД(М10)
 ПЕ НОМЕСТ
 СЧ Е6
 ПВ ТАЙМЕР(М15)
 МОДА ОЖ
 УИА (М16)
 ПБ ТБУФ13
НОМЕСТ СЧ =В’6075’     НЕРОТ
 ПВ ТБУФ(М15)
 СДА 64-8
 УИ М10
 СДА 64-2
 ИЛИ =М38В’1’М24В’464’М17В’3’
 ИЛИ Р9
 ЗП Р3
 ПВ ФИЗОБМ(М16)
 СЧ ЕНОВ
 ИЛИ ШКРАБН
 ЗП ШКРАБН
 СЧ ЕХОЛ
 ИЛИ ШКОРАБ
 ЗП ШКОРАБ
 СЧ ШКЧТЕН
 НТЖ Е30
 ЗП ШКЧТЕН
 СЧ Р3
 ИЛИ Е48Е47
 ПВ ТБУФ(М15)
 СЧ =В’6054’
 ПВ ТБУФ(М15)
 СДА 64-8
 УИ М10
 СДА 64-2
 ИЛИ =М38В’1’М24В’443’М17В’3’
 ИЛИ Р9
 ЗП Р8
 ПВ ФИЗОБМ(М16)
 СЧ ПРОГОН
 СДА 64-40
 СЛЦ =М42В’56’
 ЗП ДЛИНА-Э64(М10)
 СЧ ШКЧТЕН
 НТЖ Е41
 ЗП ШКЧТЕН
 СЧ Р8
 ИЛИ Е48Е47
 ПВ ТБУФ(М15)
 СЧ ЗАНЯТА
 ИЛИ Е16 РАЗРЕШЕНИЕ РАБОТАТЬ
 ЗП ЗАНЯТА
 ПБ ББ1-У1(М7)
ТРАВ КОНД М17В’3’М2В’24’
 КОНД М17В’3’В’144’
ПЕРВ ПАМ 1
АОБЩГ ПАМ 0
 УИА 3
 УИА ТЗНМЛ(М3)
 ПБ НЕТОБЩ-У1(М7)
ПОИСКС УИА 3
 УИА -3(М3)
 УИА -1(М15)
ВС СЧ НОММЛ1+1(М15)
 СБР НАПРМГ
 СДА 64+42
 УИ М16
 СЧ ТЗНМД(М16)
 И П7777
 ИЛИ Е47
 ЗП ТЗНМД(М16)
 МОДА ВС
 ЦИКЛ (М15)
ПМДИ СЧ РАБМД+3(М3)
 УИА -63(М15)
 ПЕ ПМД
 ЗП СБ+1(М3) РАБМД НЕТ
ПМД СЧ СБ+1(М3)
 ПО ПОИСК1
 МОД СБ+1(М3)
 СЧ 0
 НТЖ ТЗНМД+63(М15)
 И П7777
 ПЕ ПМД1
 СЧ ТЗНМД+63(М15)
 НТЖ НАПРКЧ+1
 И П7777
 ПЕ НЕМДГП
 СЧИ М15
 СЛЦ П77
 НТЖ НАПРКЧ
 И П77
 ПЕ ПМД1
НЕМДГП НОП
 МОДА ПР1
 ПИО (М15)
 УИИ М16(М15)
 СЛИА 1(М16)
ПР СЧ ТЗНМД+63(М15)
 НТЖ ТЗНМД+63(М16)
 И П7777
 ПЕ ПРО
 СЧ ТЗНМД+63(М16)
 И Е47
 ПО ПР1
 ПБ ПМД1
ПРО МОДА ПР
 ЦИКЛ (М16)
ПР1 НОП 0
 СЧ ТЗНМД+63(М15)
 И П7777
 ИЛИ Е47
 ЗП ТЗНМД+63(М15)
 СЛИА 63(М15)
 СЧИ М15
 СДА 64-42
 РЗБ НАПРМГ
 ЗП R3-НОМБОБ(М7)
 МОД СБ+1(М3)
 СЧ 0
 И ВСК1
 ИЛИ R3-НОМБОБ(М7)
 СЛЦ К
 МОД СБ+1(М3)
 ЗП 0
 СЧИ М3
 УМН П6Е48
 СЧМР 64
 УИ М16
 УИА 1027
 СЧ П77
 СДА 64-12(М16)
 НТЖ НЛОГ
 ЗП НЛОГ
 СЧИ М15
 СДА 64-12(М16)
 ИЛИ НЛОГ
 ЗП НЛОГ
 ЗП НФИЗ
 УИА 3
 СЧ 0
 ЗП СБ+1(М3)
 ПБ ПОИСК1
ПМД1 МОДА ПМД
 ЦИКЛ (М15)
 МОД СБ+1(М3)
 СЧ 0
 И П7777
 ПВ ПЕРЕВ(М15)
 ЗП УСТ+1
 МОДА УСТ
 УИА (М16)
 МОДА ВЫХ-НОМБОБ(М7)
 УИА (М15)
 УИИ ’32’(М15)
 СЧ Е23
 ПБ ТВ40
УСТ КОНД П’1НЕТ Б’
 КОНД В’0’
 КОНД П’↑     ’
ПОИСК1 МОДА ПМДИ
 ЦИКЛ (М3)
 СЧ Е47
 ИЛИ ЗАНЯТА
 ЗП ЗАНЯТА        ПР:ВСЕ СИСТ.БОБ.НАШЛИ
 УИА 3
 ПБ КОН1
*ПЕРЕПИСЬ СТРАНИЦ  *
* N СТРАНИЦЫ ЧТЕНИЯ В М1
* N СТРАНИЦЫ ЗАПИСИ В М2
* ЧИСЛО СТРАНИЦ ПЕРЕПИСИ В М4
* ВЫХОД ПО М15
*
ПЕРЕПС УИА -1023(М17)
Б2 МОДА 1023(М17)
 СЧ (М1)
 МОДА 1023(М17)
 ЗП (М2)
 МОДА Б2
 ЦИКЛ (М17)
 СЛИА 1024(М1)
 СЛИА 1024(М2)
 МОДА ПЕРЕПС
 ЦИКЛ (М4)
 ПБ (М15)
*
ДАЙЛИС НОП ,
 УИА 3(М2)
ДАЙЛИ НОП ,
 СЧ РАСПП(М2)
 И Е4817Д
 НТЖ Е4817Д
 ПЕ ЕСТЬЛ
 СЛИА -1(М2)
 ПБ ДАЙЛИ         ВСЕГДА НАЙДЕМ
ЕСТЬЛ НОП ,
 НЕД ПР7Д
 УИ М16
 СЧ РАСПП(М2)
 ИЛИ Е48(М16)
 ЗП РАСПП(М2)     ЗАКРЫЛИ ЛИСТ
 СЧИ М16
 ЗП Р3
 СЧИ М2
 СДА 64-5
 ИЛИ Р3
 ИЛИ НЛОС
 СДА 64-38         СМ := N ЛИСТА С 39 Р.
 ЗП РАБ
 РЗБ ПРИПД+1
 УИА -1023(М16)
 МОДА ВТДЛ
 ДК ’36’,0
ВТДЛ НОП ,
 РЕГ ’20’
 УИ 0
 СЧ 0
 УИА 1026
РОСДЛ НОП ,
 ЗП 1024+1023(М16)
 МОДА РОСДЛ
 ЦИКЛ (М16)
 УИА 1027
 СЧ РАБ
 ПБ (М17)
*
* КОНСТАНТЫ ДЛЯ ’ВЫДЕЛЕНИЯ ИНФОРМАЦИИ О ЛИСТЕ ’ В ТОП-Е
ВИОЛ  КОНД М34В’177’
ВИОЛП КОНД М34В’177’М20В’7777’
КВВЛ КОНД М39В’1’М20В’7777’
КВТВЛ КОНД М39В’1’М20В’7777’
КВСЧК КОНД М16В’3’
*
*
КЛСВ   КОНД  В’57600740000’
* ВСЕ ДЛЯ ФОРМИРОВАНИЯ ТАБКОД-А
ИСХТК ЭКВ ’11000’      2-АЯ ПОЛОВИНА 4-ОГО ЛИСТА
СБ6Б НОП ,
 КОНД М40В’277’
 КОНД (4)В’0’
СБРБАЙ НОП ,
 КОНД М08В’377’     УПП -> МПД
 КОНД М16В’377’     УПП -> ЕС
 КОНД М00В’377’     МПД -> УПП
 КОНД М24В’377’     ЕС  -> УПП (000-177)
 КОНД М32В’377’     ЕС  -> УПП (200-377)
ТЕГКОМ КОНД В’35’
РАБ КОНД В’0’
 АIF (NОТ ∧АС).АС3
КЛСВ1 КОНД В’61200740000’
.АС3 АNОР
ЧИСЛСТ КОНД М47В’1’В’360’
МАСКА КОНД В’1763747717637477’
ВСК1 КОНД М37В’1’М24В’7777’
К КОНД М38В’1’М18В’1’М26В’1’
МАХ ПАМ 1
Е2Е1 КОНД В’3’
Е3П1 КОНД В’7’
П137 КОНД В’137’
П17 КОНД В’17’
П27 КОНД В’27’
Е5П1 КОНД В’37’
П600 КОНД В’600’
ЗОНСВ ЭКВ ’750’
П6076 КОНД В’6076’
 КОНД В’6077’
П77 КОНД В’77’
П177 КОНД В’177’
Е8П1 КОНД В’377’
Е9П1 КОНД В’777’
Е10П1 КОНД В’1777’
Е12П1 КОНД В’7777’
Е15П1 КОНД В’77777’
Е15П11 КОНД М10В’37’
Е24П1 КОНД В’77777777’
Е25П1 КОНД В’177777777’
Е39П1 КОНД М39В’777’В’-1’
Е39П25 КОНД М24В’77777’
Е40П1 КОНД М40В’377’В’-1’
Е42П44 КОНД М41В’7’
Е48П17 КОНД М16В’37777777777’
Е48П33 КОНД М32В’177777’
Е48П46 КОНД М45В’7’
Е48Е47 КОНД М46В’3’
П6Е48 КОНД М47В’1’В’6’
Р500 ПАМ 1
КЧ500 ПБ ОБХ
КУСR1 ПАМ 1
ЕСТЬРД КОНД В’0’
К2003 КОНД В’2003’
К2013 КОНД В’2013’
СИТУАЦ ПАМ 1
КУССИТ КОНД М18В’1’М24В’1300’   СИТ. С 1400 ЗОНЫ
Я500 ПАМ 1
ЯЧ500 ЗП СМ
 ПБ ПРКЧ
ЯЧ501 ЗП СМ
 ПБ ВНЕШПР
*
Д ПАМ 1
NМОД ПАМ 1
ПОСМОД КОНД В’0’
Р9 ПАМ 1
Г100МД ПАМ 2
КОСД КОНД М39В’7’М24В’7777’М7В’7’
*
ВЗУ ВНЕШ ТАБКОД,ФИЗУР
ГЕНС ВНЕШ Р16,СМ,ПРКЧ,ВНЕШПР,ЗМ14,ЖДИПР,КУС
ДИСП70 ВНЕШ ШГ,(РМРД,РМР),ИПОБМ,(МАСРВП,МРВП)
ПВВ ВНЕШ (МРВП,МГРП),ПВВМД,ВОСЛМД,ОТМТЕС
ГЕНС ВНЕШ КУСR,ДИС501,РМР,ОТВЕТ,ЛВЗЯЛИ,НАПРКЧ
ГЕНС ВНЕШ ГРАНБ,РК,УПРМБ,МБМК,УПРГРА
ДИСП70 ВНЕШ ЛТБД,КЛЮЧ,Е24П13,ГРАНЬБ,СЕМППД
ПВВ ВНЕШ ПРАДАП,ОПРОСД,ИНТУСЛ
ПВВ ВНЕШ КОНГЕН
СОСТАВ ВНЕШ ЕСБОЙВ
ДИСП70 ВНЕШ ШКИВ
СОСТАВ ВНЕШ ЕМБФ,ЕПРМПД,АВОС,LШЗНМБ
Р8 ПАМ 1
НОМУСТ ЭКВ ’71766’ ТАБЛИЦА УСТРОЙСТВ ТТ И СОNSUL
АВМД ВНЕШ АВМД
ДИСП70 ВНЕШ АВРАСП,БВЗ,БОБСВЗ,ИВ25,ГОД,ЗАНЯТА
ДИСП70 ВНЕШ НФИЗ,НЛОГ,МД5,ТБУФ,ГРУП,ЛЕСТ,ТАБТИП
ДИСП70 ВНЕШ ТАЙМЕР,ТБУФ13,ДЛИН,ЕН,МГРП,НАПРМГ
ДИСП70 ВНЕШ НОММБ1,НОММБ2,НОММБ,НОММЛ,ЯЧВР
ДИСП70 ВНЕШ НОММЛ1,Э75,VIDI,ТСЛ,КУСЛСВ,ТАС,ДУК,Е36П25
ДИСП70 ВНЕШ Е48П37,ПРЕДЕЛ,УСТПРП,КВНЗАД,Е47П1,ШЗНМБО
ДИСП70 ВНЕШ ТВ67,ТЗФСТР,ТЗНМЛ,ЛТВЫД,ЛПРОЦ,П17777
ДИСП70 ВНЕШ МАКЗ,МИНЗ,ТУСП,ЧПЗ,ШКАП,НПЗ,ШГП,МАЛКАТ,NБЛ
ДИСП70 ВНЕШ ТЗНМД,МД29М,МПРП,МЧПЗ,ОЗУ,ТМБ,ТОБ,ШКЗЛ
ПВВ ВНЕШ ВКЛМОД,ПРИПУЛ
ДИСП70 ВНЕШ (ШИФ,ШИФРМ),(ПР7Д,П77777),ТМД
ДИСП70 ВНЕШ ТВ40,ПЕРЕВ,СМШГ,П6,П7777,МДБ,(ПРИПД,ПРИП)
ДИСП70 ВНЕШ СЕКОС,ПРИСОС,БОСТЗС,БОСОБМ,ШКАМОД,ЛКЧТМД
ДИСП70 ВНЕШ НЛОС,ЛСЕК,ИПЗМЛ,ШКЧТЕН,КУСНН,МАХОЗУ
КАЧКА ВНЕШ ФИЛ,НАЧФИЛ,КОНФИЛ
ДИСП70 ВНЕШ ПРИРЕК,ПРИПАД,БАСЕС
ДИСП70 ВНЕШ СВСЭ,ЭСВС,ЛИСТСВ
МОТТ ВНЕШ ШОТА
МОТТ ВНЕШ ШКАС,СЛОN,АСОТ,ШЗПРИМ,ШКОРАБ,ШКНОВ,ШКУСТР,ШКОПТТ
МОТТ ВНЕШ ШКРАБН,ЕНОВ,ЕХОЛ,ЛИНСББ,НОМУС
МОТТ ВНЕШ МОТТ,СКОРТТ,ШКППМ,НОМАС
Я71766 ЭКВ ’71766’       НОМЕРА ТЕРМИНАЛОВ ДЛЯ ОПЕР.МАРШР.
Я71767 ЭКВ ’71767’      ТЕРМИНАЛЫ  ЕС-7920
Я71772 ЭКВ ’71772’      ТЕРМИНАЛЫ  ЕС-7934
Я71770 ЭКВ ’71770’       ОПЕРАТОРСКЕ ТЕРМИНАЛЫ
СМЕНА1 ВНЕШ У1,НЕТОБЩ,ББ1
КИТ ВНЕШ БРАКТР,КЛЮМК,ММГРП
Э64 ВНЕШ Э64,ДЛИНА
ПЛАН2 ВНЕШ ИН,ФИО,ФИО1,ВРОС,ВРЕР
НОМБОБ ВНЕШ НОМБОБ,R3,ВЫХ
КАЧКА ВНЕШ КАЧКА,(Е4817Д,Е48П17),ШКРЗ
ВЗУ ВНЕШ ФИЗОБМ
ДИСКИ ВНЕШ РАБМД,КУСКИ,НОМЛД,НОМСВ,NКОБ,НОМРМД
ДИСКИ ВНЕШ МД100М,КОНЗОН
 ЭКВИВ (П777,Е9П1),(П37,Е5П1),(КАЧ,’16000’)
 ЭКВИВ (БКТ,’40000’),(Н,1024),(П77777,Е15П1),(ЯЧКАН,’71751’)
 ЭКВИВ (КШ,’44000’),(МК,’46000’),(П3,Е2Е1)
 ЭКВИВ (СКОТЕР,’42257’)
 ЭКВИВ (ТИПТЕР,’42226’),(КЛЮ,’42776’),(КУСГП,’70000’)
 ЭКВИВ (П7,Е3П1),(П1777,Е10П1)
ГЕНС ВНЕШ ШКVТ,NБЭСМ,NМБ,NМЛ,ТРСИСТ,ШКТТ,РАСПП
ГЕНС ВНЕШ РОСМБ,РЯ1,РЯ2,РЯ3,СЛОЙК,ОБМИМБ,ОТБРТР
ГЕНС ВНЕШ Р3,ДЛТ,ЗАП,ИСКАТ,КГОД,КНАПР,КОДТР
ГЕНС ВНЕШ СЧВРБ,СБ,НОМЛИС,ПРРО,ЛИСТА3,КОЛЛИС
ГЕНС ВНЕШ КУСИМБ,КУСД1,КУСЛИС,НОМРД,ЧТГП,ЧТМД
ГЕНС ВНЕШ ЧТМДВ,ТРАКТЫ,Е32П25,ШАС128
ГЕНС ВНЕШ ШКОПТ,NТАС,ШКТТ2,ШКVТ2,КОН1,ПРОГОН,ВСЕЕД
ГЕНС ВНЕШ ЛСТБАЗ,ЗАП20Р,СЕКОСГ,ПРИСОГ,ШКАМОГ
 ВХОД ПОИСКС,АЛИСТЫ,АОБЩГ
СОСТАВ ВНЕШ ЕОБМ,ЕОТКЛ
 ФИНИШ
*КОНЕЦ ЧАСТИ

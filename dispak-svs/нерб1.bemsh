НЕРБ1 СТАРТ ’11400’
 УПОТР НЕРБ1-’1400’(М7)
 Е
 М
ВХКОНФ НОП .             ВХОД В НЕРБ1
 УИА 3
 РЖА 3
 СЧ
 ЗП ТР3-Б(М7)
 СЧ П35
 ЗП ПРКОНФ-Б(М7)      НЕ РАВ 0 - РАБ НЕРБ1
 ЗП ТУМБ6-Б(М7)
 СЧ ПРОШУ-Б(М7)
 ПО ЦЦ
 СЧ СИ-Б(М7)
 ПБ ВХОШУ
ЦЦ МОДА ВС-Б(М7)
 ПВ (М16)      ЗА СИМВОЛОМ
ВХОШУ ЗП ПРОЦ
 НТЖ П17
 ПО ЦЦ       ПРОБЕЛ
 НТЖ П360
 ПО ДСВС     ДАННЫЙ СВС,   ДАННЫЙ АЦД
 СЧ СИ-Б(М7)
 НТЖ ПЕОТ
 ПО ЦЦ
 СЧ СИ-Б(М7)
 НТЖ СЛЭШ-Б(М7)
 ПЕ НЕСЛЕШ
 МОДА СВСТЕС
 СЧ
 НТЖ =М32В’1777’
 МОДА КОНСВС
 И
 СДА 64-6
 ЗП МСВС      МАСКА СВС
 СЧ ЕНЕРОТ
 ПВ ЗАНПРТ(М15)
 МОДА ДМСВС
 ПВ (М11)    ЗА НОМ. СВС
НЕЦ1 МОДА ВС-Б(М7)
 ПВ (М16)
НЕЦ СЧ ПРОЦ
 ВЧ П17
 ПО ВЫБ4     НОМЕР ПРОЦЕССОРА > 14
 СЧ ПРОЦ
 ИЛИ ТР4-Б(М7)
 ЗП ТР4-Б(М7)
 СЧ СИ-Б(М7)
 НТЖ П377
 ПО ДАНАЦД     ДАННЫЙ АЦД ПО УМОЛЧ.
 ПБ ВЫБТИП
НЕСЛЕШ СЧ СИ-Б(М7)
 ЗП РЯ1-Б(М7)
 ВЧ П12
 ПО ТИПУС    ДАН.СВС
 МОДА ВС-Б(М7)
 ПВ (М16)
 ВЧ Е4
 ПО НЕЦ   НЕ ЦИФРА
 СЧ ПРОЦ
 СДА 64-3
 СЛЦ Е2
 СЛЦ СИ-Б(М7)
 ЗП ПРОЦ
 ПБ НЕЦ1
ТИПУС МОДА НОМСВС
 ПВ (М15)
 УИА -2(М17)
 ПБ ТИПУС2
ВЫБТИП УИА -3(М17)
 ПБ ТИПУС2
* ОПРЕДЕЛЕНИЕ ТИПА УСТР-В
ТИПУС2 МОДА ДЕШ4+LДЕШ4-1
 УИА (М5)
 УИА -LДЕШ4+1(М15)
 УИА (М13)
ВЫБОП МОДА ВС-Б(М7)
 ПВ (М16)
 НТЖ П377
 ПО ПРОБЕ
 НТЖ П360
 ПО ПРОБЕЛ
ВЫБОП1 СЧ РЯ1-Б(М7)
 СДА 64-8
 ИЛИ СИ-Б(М7)
 ЗП РЯ1-Б(М7)
 МОДА ВЫБОП
 ЦИКЛ (М17)
ВСЛ ПБ ПОИСК-Б(М7)
ПРОБЕ НОП
 УИА 1(М13)
ПРОБЕЛ МОДА ВСЛ
 ПИО (М17)
 СЧ П17
 ЗП СИ-Б(М7)
 УИА (М17)
 ПБ ВЫБОП1
ДАНАЦД СЧИ М2
 ЗП НУСТ
 СЧ =М6В’6’     ТИП АЦД=6
 ЗП ТР5-Б(М7)
 ЗП ТР3-Б(М7) ТР3 ≠ 0 ДИР ВКЛ N (N - НОМ МАШ)
 МОДА ВСЕНУС
 УИА (М15)
 ПБ ЗАПНУС
* ВХОД ИЗ БОНБОТА, ПОСЛЕ НАХОЖДЕНИЯ ТИПА УСТ-ВА
ПР35Н МОДА ВСЕНУС
 ПИНО (М13)
* ОПРЕДЕЛЕНИЕ ЗАДАННЫХ УСТР-В
ПР35НУ УИА 1(М15)    ПРИЗНАК 8-Х  ЧИСЕЛ
 УИА 3(М6)
 МОДА ВЫБЧИ-Б(М7)
 ПВ (М17)     ВЫБОРКА ЧИСЛА
 СЧ ТР5-Б(М7)
 СДА 64+6
 И П77
 НТЖ П6
 УИ М15           0 - АЦД
 СЧ РЯ1-Б(М7)
 ПО ЗНУСТ
 МОДА ЗНУСТ
 ПИНО (М15)
 ВЧ Е1
ЗНУСТ ЗП НУСТ
 ВЧОБ П77
 ПЕ ВЫБ4    НУС > ’77’
 МОДА ХХ
 УИА (М15)
ЗАПНУС УИА (М6)
 МОДА ТР4-Б(М7)
 УИА (М16)
 СЧ НУСТ
 И Е6
 ПО ЗН     УСТР-ВА 00-37
 МОДА ТР5-Б(М7)
 УИА (М16)
 УИА -’40’(М6)
ЗН СЧ Е48
 МОД НУСТ
 СДА 64(М6)
 ИЛИ (М16)
 ЗП (М16)      ЗАПИСАЛИ  НОМЕР УСТР-ВА ПОЗИЦИОННО
 ПБ (М15)
ХХ СЧ СИ-Б(М7)
 НТЖ МИНУС
 ПО ДИАП    ЗАДАН ДИАПАЗОН УСТР-В
 СЧ СИ-Б(М7)
 НТЖ П17
 ПО ПР35НУ
 НТЖ П360
 ПО ВСЕНУС
ВЫБ4 УИА ВЫБ4Б-Б(М16)
 СЛИ М16(М7)
 ПБ ВЫХ2
ДИАП УИА 3(М6)
 УИА 1(М15)
 МОДА ВЫБЧИ-Б(М7)
 ПВ (М17)
 СЧ РЯ1-Б(М7)
 ВЧОБ П77
 ПЕ ВЫБ4     НУС > ’77’
 СЧ РЯ1-Б(М7)
 ВЧ НУСТ
 ПЕ ВЫБ4     КОН. НОМЕР УСТ. <  НАЧ. НОМЕР УСТ.
 УИ М17          ЧИСЛО УСТРОЙСТВ
 СЧ ТР5-Б(М7)
 СДА 64+6
 И П77
 НТЖ П6
 ПЕ АХ            НЕ АЦД
 СЛИА -1(М17)
АХ МОДА ХХ
 ПИО (М17)
 СЧ НУСТ
 СЛЦ Е1
 ЗП НУСТ
 МОДА ЗАПНУС
 ПВ (М15)
 СЛИА -1(М17)
 ПБ АХ
ДСВС МОДА НОМСВС
 ПВ (М15)
 СЧИ М2
 ЗП НУСТ
 СЧ =М6В’6’
 ЗП ТР5-Б(М7)
 ЗП ТР3-Б(М7)    РАЗРЕШЕНА ПОЛЬЗОВАТЕЛЮ
 МОДА ЗАПНУС
 ПВ (М15)
ВСЕНУС СЧ ТР4-Б(М7)
 ИЛИ ТР5-Б(М7)
 И Е48П17
 ПЕ ЕСТЬНУ
 СЧ Е48П17
 ИЛИ ТР4-Б(М7)
 ЗП ТР4-Б(М7)   ВСЕ УСТР.ПО УМОЛЧАН
 СЧ Е48П17
 ИЛИ Е13     ПРИЗНАК ТОГО, ЧТО ВСЕ УСТ-ВА ЗАД. ПО УМОЛЧ.
 ИЛИ ТР5-Б(М7)
 ЗП ТР5-Б(М7)
*   КОНЕЦ ОБРАБОТКИ ПРЕДЛОЖЕНИЯ
ЕСТЬНУ УИА 1(М13)
 УИА ЗАНЕС1-Б(М16)
 СЛИ М16(М7)
 СЧ ТР3-Б(М7)
 ПЕ ВЫХ2
 СЧ ТР4-Б(М7)
 И Е6
 ПЕ ВЫХ2          ДИР-ВА ОПР
 СЧ ШКОПТТ
 И Е48(М2)
 ПЕ ВЫХ2
 УИА ДЗАПРЩ-Б(М16)
 СЛИ М16(М7)
 ПБ ВЫХ2
*     ТИПЫ   УСТРОЙСТВ
ДЕШ4 КОНК В’0’
 КОНК П’МД ’
 КОНК М6В’1’
 КОНК П’МБ ’
 КОНК М6В’2’
 КОНК П’МГ ’
 КОНК М6В’3’
 КОНК П’АЦП’
 КОНК М6В’4’
 КОНК П’ЧПК’
 КОНК М6В’5’
 КОНК П’ФСМ’
 КОНК М6В’6’
 КОНК П’АЦД’
 КОНК М6В’7’
 КОНК П’ГП ’
 КОНК М6В’10’
 КОНК П’ПИ ’
 КОНК М6В’11’
 КОНК П’ПЛ ’
 КОНК М6В’12’
 КОНК П’АС ’
 КОНК М6В’13’
 КОНК П’МОП’
LДЕШ4 ЭКВ *-ДЕШ4
МИНУС КОНД П’00000-’
НУСТ КОНД В’0’
П360 КОНД В’360’
ПРОЦ КОНД В’0’
*
*
*****************************************************************
*****************************************************************
* --    П Р О Ц Е Д У Р Ы -----------*
ДМСВС СЧ МСВС         ДАЙ  СЛЕД. НОМЕР СВС
 НЕД
 ЗП ПРОЦ    НОМЕР ПРОЦЕССОРА
 МОД ПРОЦ
 СЧ Е48-1
 НТЖ МСВС
 ЗП МСВС
 ПБ (М11)      ВЫХОД ИЗ ПРОЦЕДУРЫ
*
*
*
*/////////////////////////////////////////////*
*//////                                 //////*
*//////    УЧЕТ АКТИВНОСТИ ТЕРМИНАЛОВ   //////*
*//////                                 //////*
*/////////////////////////////////////////////*
*
ВРЕЕТХ НОП
 СЧИ М2            N ТЕРМ.
 СДА 64+1
 УИ М3
 СЧИ М2
 И Е1
 УИ М5
 МОДА ТАБВРЕ-Б(М7)
 СЛИА (М3)
 СЧ Е48П25(М5)
 НТЖ ВСЕЕД
 И (М3)
 ЗП (М3)
 РЕГ ’256’         ТЕКУЩЕЕ ВРЕМЯ
 СДА 64+20
 СД КСН(М5)
 ИЛИ (М3)
 ЗП (М3)
 ПБ ОЧТЕР-Б(М7)   ВРЕМЕННО
*
*****************************************************************
*  В Ы Х О Д Ы    В    Б О Н Б О Т
ВЫХ2 НОП
 СЧ
 ЗП ИР1-Б(М7)
ВЫХ СЧ ЕНЕРОТ
 УИИ М15(М16)
 ПБ ЗАНПРТ
НОМСВС РЕГ ’250’
 НТЖ ВСЕЕД
 СДА 64-6
 НЕД П77777
 И П77777
 ЗП ПРОЦ           N СВС
 ИЛИ ТР4-Б(М7)
 ЗП ТР4-Б(М7)
 ПБ (М15)
*
*     ОСВОБОЖДЕНИЕ ПАССИВНЫХ ЛОГИЧЕСКИХ НОМЕРОВ
*     -----------------------------------------
*
ОТКЛЮЧ НОП
 УИА ’2003’
 СЧ ШЗНКТ         ПОДГОТОВКА РАБОЧЕЙ ШКАЛЫ
 ИЛИ ШКУСТР
 ИЛИ ШКОПТТ
 ИЛИ ШСВЯТ
 ИЛИ ШОТА
МОТТ ВНЕШ ШОТА
 ИЛИ ШЗПРИМ
 НТЖ ВСЕЕД
 И ШКАС
 ЗП ШОТКЛ         ГОТОВА РАБОЧАЯ ШКАЛА
 ИЛИ ШЗПРИМ
 ЗП ШЗПРИМ
 РЕГ ’256’         ФИКСИРУЕМ ВРЕМЯ
 СДА 64+20
 ЗП РЯ1-Б(М7)
 МОДА ОЧТЕР-Б(М7)
 ПВ (М4)
*
 СЧИ М2            N ТЕРМ.
 СДА 64+1
 УИ М3
 СЧИ М2
 И Е1
 УИ М5
 МОДА ТАБВРЕ-Б(М7)
 СЧ (М3)
 СБР Е48П25(М5)
 СДА 64+24         ВРЕМЯ ПОСЛ.ПРОЯВЛЕНИЯ АКТИВНОСТИ
 ВЧОБ РЯ1-Б(М7)
 ВЧ ПРЕДАК
 ПЕ ОТПРИМ-Б(М7)  ТЕРМИНАЛ АКТИВЕН
 УИА ’2003’
 СЧ Е48(М2)
 ИЛИ ШКСТАР
 ЗП ШКСТАР        ШКАЛА ПЕРЕКЛЮЧАЕМЫХ НА ДР.ЭВМ
 СЧ ЕСТАР
 ИЛИ ШКРАБН
 ЗП ШКРАБН        РАБОТУ НЕРОТУ
 СЧ 0
 МОД ТСЛ(М2)
 ЗП Н             ПЕРЕКЛЮЧИМ НА НУЛЕВУЮ ЭВМ
 ПБ ОЧТЕР-Б(М7)
*****************************************************************
*
П35 КОНД В’35’
ПРЕДАК КОНД В’1452’       15 МИНУТ
*
ДИСП70 ВНЕШ ТСЛ
НЕРОТ ВНЕШ ОТПРИМ
МОТТ ВНЕШ ШЗНКТ,ШКУСТР,ШКОПТТ,ШЗПРИМ,ШСВЯТ,ШКАС
МОТТ ВНЕШ ШОТКЛ,ШКСТАР,ЕСТАР,ШКРАБН,Н
 ВХОД ОТКЛЮЧ
ДИСП70 ВНЕШ ЗАНЯТА,ГОД,(ЕПРИК,Е13),П77777
ПВВ ВНЕШ МАСТУС,СООБЩС,(ДПС,ДСП),ТУСЗ
НЕРОТ ВНЕШ ЗАНЕС1,ПОИСК,ВЫБЧИ,ОЧТЕР
НЕРОТ ВНЕШ ПРОШУ,(ВЫБ4Б,ВЫБ4),ТР4,ТР5,ТУМБ6,(Б,НЕРОТ)
НЕРОТ ВНЕШ СЛЭШ,ТАБВРЕ
ДИСП70 ВНЕШ ВСЕЕД,ТВ40,П17,П37,П77,П377
НЕРОТ ВНЕШ РЯ1,СИ,ВС,ШКПУ,ДОП1,ИР1
ДИСП70 ВНЕШ П60,П12,П6,Е48П25,КСО,КСН
КАЧКА ВНЕШ Е48П17
МОТТ ВНЕШ ПЕОТ,НОМУС1,МСВС,ЗАНПРТ,ЗАНПАД
ПВВ ВНЕШ КОНСВС,(СВСТЕС,РЕЖИМ)
СОСТАВ ВНЕШ ЕНЕРОТ
 ВХОД ПР35Н,ВХКОНФ,ВРЕЕТХ
НЕРОТ ВНЕШ ПРКОНФ
 ВХОД ВЫБ4
НЕРОТ ВНЕШ ДЗАПРЩ,ТР3
 ФИНИШ
*КОНЕЦ ЧАСТИ
*КОНЕЦ ЧАСТИ

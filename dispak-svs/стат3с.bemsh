СТАТ3С СТАРТ ’11400’
 Б
 Е
 М
 УПОТР СТАТ3С-768(М7)
 СЧ ’4007’
 СЧ ЗАНЯТА
 И Е7
 ПО ВЫХНБ-Н(М7)
 СЧ 0
 ПВ ТБУФ(М15)
 УИА 3
 НТЖ 00
 ПО ВЫХНБ-Н(М7)   НЕТ ЛИСТОВ
 СДА 64-8
 УИ М2
 СДА 64-2
 ИЛИ КУССБ
 СЛЦ НОММЛ
 ЗП КУССБ
 ПВ ЕNQ(М15)
 СЧ КУССБ
 ПВ ФИЗОБМ(М16)   ЧТ.ЗОНЫ СТ-КИ ПО СБОЯМ
 УИА 3
 СЧ 1023(М2)
 НТЖ КЛЮЧС
 ПО РСТ1
 СЧ 0
 УИА -1022(М15)
РСТ МОДА (М2)
 ЗП 1022(М15)
 МОДА РСТ
 ЦИКЛ (М15)
 СЧ КЛЮЧС
 ЗП 1023(М2)
РСТ2 УИА -255(М16)
 УИИ М3(М2)
ПСВ СЧ (М3)
 ПО НОВЗП
 СЛИА 2(М3)
 МОДА ПСВ
 ЦИКЛ (М16)
 УИА -509(М16)
 УИИ М3(М2)
ПСВ1 СЧ 2(М3)
 ЗП (М3)
 СЛИА 1(М3)
 МОДА ПСВ1
 ЦИКЛ (М16)
НОВЗП НОП ,             НОВАЯ ЗАПИСЬ
 СЧ ГОД
 ИЛИ СУТКИ
 ЗП (М3)
 СЧ ВРЕМЯ
 СДА 64-24
 ЗП 1(М3)         НАЧАЛО РАБОТЫ ОС
 СЧ ВРЕМЯ
 ИЛИ Е14
 ИЛИ 1(М3)
ЗПСТ1  ЗП 1(М3)    ВРЕМЯ
 СЧИ М3
 ЗП ВЫЗОС-Н(М7)
 УИА -6(М16)    СТ-КА ПО ПАКЕТАМ
 ПБ ЛОЖМЕТ        МОЖНО УБРАТЬ
 УИИ М15(М2)
ЗСП СЧ 512(М15)
 ПО СВ
 НТЖ ГОД
 И КВЫД
 ПО СВ1
 СЛИА 73(М15)
 МОДА ЗСП
 ЦИКЛ (М16)
 УИА -437(М16)
 УИИ М15(М2)
ПСТ СЧ 512+73(М15)
 ЗП 512(М15)
 СЛИА 1(М15)
 МОДА ПСТ
 ЦИКЛ (М16)
 УИА -72(М16)
 СЧ 0
ПСТ1 МОДА (М15)
 ЗП 72(М16)
 МОДА ПСТ1
 ЦИКЛ (М16)
СВ СЧ ГОД
 ЗП 512(М15)
СВ1 УИИ М3(М15)
 УИА -71(М17)
 УИА -63(М16)
ППАК СЧ ТЗНМД+63(М16)
 ПО ПП
 НТЖ 513(М3)
 И П7777
 ПО ПАК
 СЧ 513(М3)
 ПО ПАК1
 СЛИА 1(М3)
 МОДА ППАК
 ЦИКЛ (М17)
 УИИ М3(М15)
ПП МОДА ППАК
 ЦИКЛ (М16)
ЛОЖМЕТ НОП ,
 СЧ КУССБ
 ПВ ФИЗЗП(М16)
 СЧ КУССБ
 ПВ DЕQ(М15)
 СЧ КУССБ
 ПВ ТБУФ(М15)
 СЧ ВРЕМЯ
 ЗП БУДСБ-Н(М7)
 ПБ ВЫХНБ-Н(М7)
РСТ1 СЧ ВЫЗОС-Н(М7)
 ПО РСТ2     ПЕРЕВЫЗОВ
 И П1777
 УИ М3
 СЛИ М3(М2)
 СЧ ГОД
 НТЖ  (М3)
 СДА 64-1
 ПО ТОЖЕ          ДАТА НЕ ИЗМЕНИЛАСЬ
 СЧ Е48
 ЗП СУТКИ
 ПБ РСТ2
ТОЖЕ НОП ,
 СЧ ВРЕМЯ
 НТЖ 1(М3)
 И Е24П1
 НТЖ 1(М3)
 ПБ ЗПСТ1
ПАК СЧ ВР5М-Н(М7)
 СДА 64-24
 СЛЦ 513(М3)
 ЗП 513(М3)
ПК УИИ М3(М15)
 ПБ ПП
ПАК1 МОДА ПАК1А
 ЦИКЛ (М17)
 ПБ ПАК
ПАК1А СЧ ТЗНМД+63(М16)
 И П7777
 ЗП 513(М3)
 ПБ ПАК
КВЫД КОНД М24В’3777’
КУССБ КОНД М24В’1741’М17В’1’
КЛЮЧС КОНД П’011088’
СУТКИ КОНД В’0’
ВЗУ ВНЕШ ФИЗОБМ,ФИЗЗП
ДИСП70 ВНЕШ ТЗНМД,ГОД,ВРЕМЯ,ТБУФ,П7777,Е24П1,НОММЛ
ДИСП70 ВНЕШ П1777,ЗАНЯТА
НОМБОБ ВНЕШ ВЫХНБ,БУДСБ,(Н,НОМБОБ),ВР5М,ВЫЗОС
ДИСКИ ВНЕШ ЕNQ,DЕQ
*
 ЭКВИВ (СТ3,СТАТ3С-768)
 ВХОД СТ3
 ФИНИШ ,
*КОНЕЦ ЧАСТИ

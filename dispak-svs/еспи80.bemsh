 Б
ЕСПИ80 СТАРТ ’10000’
 УПОТР ЕСПИ80(М7)
*************************************************************** *
 М
 Е
 АIF (∧ВS ЕQ 0).КОТ
КОТПВВ ВНЕШН ТЕРМ.КОТПВВ
.КОТ АNОР
*************************************************************** *
*ВНЕШНИЕ ПЕРЕМЕННЫЕ
ДИСП70 ВНЕШ ТУС,ТБУФ,ЗАНОСМ,БЛИСКН,ФИЗОБМ,ТВ40
ДИСП70 ВНЕШ ШИФРАЦ,ЕСАЦ1,СТУСТР,ШКПЛАН,АЗУ,НОММБ
КИТ ВНЕШ КОНСБР
*************************************************************** *
*  ВНЕШНИЕ АДРЕСА
 АIF (∧ВS ЕQ 1).С20
ОСА ВНЕШН ОСА.ОСА
СОПКА ВНЕШН ОСА.СОПКА
НАКВ ВНЕШН ОСА.НАКВ
.С20 АNОР
 АIF (∧ВS ЕQ 0).Е20
ПВВ ВНЕШ ПВВЕС,ДАЙКОТ,ТИППИ
.Е20 АNОР
*************************************************************** *
*   ВНЕШНИЕ КОНСТАНТЫ
ДИСП70 ВНЕШ П3,(Е8П1,П377),(Е10П1,П1777),ВСЕЕД
 АIF (∧ВS ЕQ 1).С21
Е32П1 ВНЕШН ОСА.Е32П1
.С21 АNОР
 АIF (∧ВS ЕQ 1).С22
П11 ВНЕШН ДИСКИ.П11
.С22 АNОР
*************************************************************** *
ИПЗ КОНД В’0’ СМ
 КОНД М41В’3’ РЖАУ
 КОНД (20)В’0’
П33 КОНД В’33’
РЯ КОНД В’0’
 АIF (∧ВS ЕQ 1).С23
ИМЯПИ КОНД Х’5D’
КВИТ КОНД П’00ПАУК’
 КОНД П’ЕСПИ’М44В’13’
 КОНД М33В’1’
 АGО .Е21
.С23 АNОР
ИМЯПИ КОНД В’0’
КВИТ КОНД В’0’
ДМ11 КОНД В’0’
СБПИ КОНД В’0’
.Е21 АNОР
КУСПИ КОНД В’400104’
ПРДЛИН КОНД В’0’
МАКЕТ КОНД В’4000020000100000’
Е15П9 КОНД В’77400’
П340 КОНД В’340’
 КОНД М47В’1’
ТСБОЙ КОНД П’4СБОЙ ’
 КОНД П’ПИ 0↑ ’
 ПАМ 1             ИСПОЛЬЗУЕТСЯ ПРИ ВЫЗОВЕ
ТНЕТГТ КОНД П’4НЕТ Г’
 КОНД П’Т ПИ 0’
 КОНД П’↑     ’
НАЧАЛО УИА ’2003’
 СЧ ШИФРАЦ
 ИЛИ Е2
 ЗП ШИФРАЦ ЕСПИ80 В РАБОТЕ
 УИА 3
 СЧ НУСВУ
 И Е28
 СДА 64+22
 ЗП НОМПИ
 СДА 64-11
 ИЛИ ТСБОЙ+1
 ЗП  ТСБОЙ+1
*
 СЧ НОМПИ
 СДА 64+5
 ИЛИ ТНЕТГТ+1
 ЗП ТНЕТГТ+1
*
 СЧ НОМПИ
 СДА 64-27
 ИЛИ ТКВ+4
 ЗП ТКВ+4
*
*
*
 УИА (М13) ЗАНЕСЕНИЕ ПЕРФОКАРТЫ-
 МОДА ФИЗОПИ С ШИФРОМ ЗАДАЧИ НА-
 ПВ (М14) АБЗАЦ ДЛЯ ПИЕС.
ГЛЦИКЛ СЧИ М13 КОЛИЧЕСТВО ОБРАБОТАННЫХ СЛОВ С ТРАКТА.
 НТЖ ПРДЛИН ДЛИНА ИНФОРМАЦИИ НА ТРАКТЕ.
 И Е10П1
 ПО КОНТРА ИНФОРМАЦИЯ С ТРАКТА ВЫБРАНА.
 УИА -23(М1)
ЗАПБУФ СЧ (М10)   ЦИКЛ-
 МОДА БУФПФК ПЕРЕПИСИ-
 ЗП 23(М1) ИНФОРМАЦИИ-
 СЛИА 1(М10) С АБЗАЦА-
 СЛИА 1(М13) ПИБЭСМ-
 МОДА ФИЗОПИ НА БУФЕР ’БУФПФК’-
 ПВ (М14) ДЛИНОЙ-
 МОДА ЗАПБУФ 24-
 ЦИКЛ (М1) ЯЧЕЙКИ.
 СЧ Е1 ЗАПИСЬ ОЧЕРЕДНОЙ-
 МОДА ФИЗОЕС ПЕРФОКАРТЫ НА-
 ПВ (М14) БУФЕРНЫЙ АБЗАЦ ДЛЯ ПИЕС.
 ПБ ГЛЦИКЛ
КОНТРА НОП ,
 СЧ Е3
 МОДА ФИЗОЕС ПИЕС ПЕРФОКАРТЫ-
 ПВ (М14) РАЗДЕЛИТЕЛЯ.
 СЧ ПРДЛИН
 И Е48
 ПО ВЫХОД
* ВЫДАЛИ ПОСЛЕДНИЮ ЗОНУ ЗАДАЧИ
 УИА -3(М10)
ПУСТО4 СЧ Е2 НА АБЗАЦ ПИЕС-
 МОДА ФИЗОЕС ЗАПИСЫВАЮТСЯ ЧЕТЫРЕ-
 ПВ (М14) ЧИСТЫЕ ПЕРФОКАРТЫ-
 МОДА ПУСТО4 ПОСЛЕ ПОСЛЕДНЕЙ-
 ЦИКЛ (М10) ПОРЦИИ ПЕРФОКАРТ.
ПРОПИ НОП ,
 УИА ’2003’
 СЧ ШИФРАЦ
 НТЖ Е2
 ЗП ШИФРАЦ
 УИА 3
 МОДА ТКВ
 УИА (М16)
 МОДА ВЫХОД
 УИА (М15)
ВЫДОПТ НОП ,
 УИИ М32(М15)
 СЧ Е23
 ПБ ТВ40
ВЫХОД НОП ,
 СЧ КУСПИ
 ПВ ТБУФ(М15)
 СЧ ШКПЛАН
 ИЛИ Е2
 ЗП ШКПЛАН ДАЙ ТРАКТЫ ДЛЯ ПИ.
 ПВ ЗАНОСМ(М16) ВРЕМЯ
 СЧ Е18
 ПБ БЛИСКН ОТКАЗ ОТ ЕСПИ80
*БЛОК ПЕЧАТИ ОБ ОШИБКАХ
ВЫДОШ НОП ,
 МОДА ВЫХОШ
 УИА (М15)
 ПБ ВЫДОПТ
ВЫХОШ СЧИ М5
 МОДА ДЕЛЮ1
 УМН
 СЧМР 64
 УИ М15
 СЧ Е48П43
 СДА 64-6(М15)
 ЗП СБПИ
 СЧ НОМПИ
 СДА 64+5
 УИ М4
 СЧ СТУСТР+4(М4)
 И СБПИ
 НТЖ СБПИ
 ПО ВЫХОШ0
 СЧ Е43-6(М15)
 СЛЦ СТУСТР+4(М4)
 ЗП СТУСТР+4(М4)  +1 В СТАТИСТИКУ ПО СБОЯМ
ВЫХОШ0 ПБ (М2)
ВЫХОШ1 СЧ ШИФРАЦ
 ИЛИ Е26           СБОЙ ПИ
 ЗП ШИФРАЦ
 ПБ ВЫХОД
*************************************************************** *
*П/П ПОДКАЧКИ (ПОСЕКТОРНО) ИНФОРМАЦИИ С МБ ДЛЯ ПИБЭСМ
*М10-БАЗА АБЗАЦА ДЛЯ СЧИТАННОИ ИНФОРМАЦИИ;М13-КОЛИЧЕСТВО
*ОБРАБОТАННЫХ СЛОВ С ТРАКТА;М14-ВОЗВРАТ ИЗ П/П;М11,М12,М15,М17-
*РАБОЧИЕ МОДИФИКАТОРЫ (РМ),ИСПОЛЬЗУЕМЫЕ ТОЛЬКО ПРИ ОБРАБОТКЕ ИН Ф
*С НУЛЕВОГО СЕКТОРА;М16-РМ.
ФИЗОПИ МОДА ФИЗПИ1
 ПИНО (М13) КУС ОБМЕНА СФОРМИРОВАН
 СЧ Е1
 ПВ ТБУФ(М15) ДАИ АБЗАЦ ДЛЯ ПИБЭСМ
 СДА 64-8
 УИ М10 АДРЕС АБЗАЦА ДЛЯ ПИБЭСМ
 СДА 64-2
 ИЛИ КУСПИ 21 ТРАКТ С ИНФОРМАЦИЕЙ ДЛЯ ПИБЭСМ
 ИЛИ НОММБ
 ЗП КУСПИ КУС ЧТЕНИЯ СЕКТОРА С 21 ТРАКТА
ФИЗПИ1 СЧИ М13
 И Е8П1
 ПЕ (М14) ИНФОРМАЦИЮ С АБЗАЦА ЕЩЕ НЕ ВЫБРАЛИ,ВЫХОД
 СЧИ М13
 СДА 64+8
 И П3
 ИЛИ КУСПИ КУС ЧТЕНИЯ+НОМЕР СЕКТОРА
 ПВ ФИЗОБМ(М16) ЧТЕНИЕ ОЧЕРЕДНОГО СЕКТОРА
 УИА 3
 СЧИ М13
 ПО ФИЗПИ2 СЧИТАЛИ НУЛЕВОЙ СЕКТОР
 СЛИА -256(М10)
 ПБ (М14) СЕКТОР НЕ НУЛЕВОИ,ВЫХОД
ФИЗПИ2 СЧ (М10) 48Р=1-ПОСЛЕДНИЙ ТРАКТ;1:10Р-КОЛИЧЕСТВО СЛОВ
 ЗП ПРДЛИН С ИНФОРМАЦИЕЙ НА ТРАКТЕ
 СЧ 1(М10) ШИФР ЗАДАЧИ
 РЗБ =П’      ’
 ЗП ТКВ+1          1- 6 ЦИФРЫ ШИФРА
 СЧ 1(М10)
 СДА 64-24
 РЗБ =П’      ’
 ЗП ТКВ+2          7-12 ЦИФРЫ ШИФРА
 СЧ ШИФРАЦ
 И Е14
 ПЕ ПРОПИ ДИРЕКТИВА ОПЕРАТОРА ’ПРО ПИ’
*ПЕРФОРАЦИЯ ШИФРА ЗАДАЧИ В НАЧАЛЕ ОЧЕРЕДНОЙ ПОРЦИИ ПФК
ШИФР УИА -3(М17)
 СЛИА 2(М13)
 СЧ ТКВ+1
 ЗП РЯ
ШИФР1 МОДА БУФПФК 24 ЯЧЕЙКИ ДЛЯ ПФК В МАКЕТЕ ПИБЭСМ
 УИА (М11)
 СЧ РЯ
 СДА 64-8
 ЗП РЯ
 СЧМР
 УИ М16 ОЧЕРЕДНАЯ ЦИФРА ШИФРА
 УИА -5(М15)
ШИФР2 МОДА ЦИФРЫ
 СЧ (М16)
 СБР КОНСБР+5(М15)
 СДА 64+8(М12)
 ИЛИ 3(М11)
 ЗП 3(М11)
 СЛИА 2(М11)
 МОДА ШИФР2
 ЦИКЛ (М15) НА ШЕСТЬ СТРОК ПФК
 СЛИА 10(М12)
 МОДА ШИФР1
 ЦИКЛ (М17) НА ЧЕТЫРЕ ЦИФРЫ ШИФРА
 СЧ
 УИА -9(М17)
ЧИСТ10 МОДА БУФПФК+14
 ЗП 9(М17)
 МОДА ЧИСТ10
 ЦИКЛ (М17)
 СЛИА 2(М10)
 ПБ ФИЗОЕС
*КОНЕЦ П/П ФИЗОПИ
*************************************************************** *
*П/П ПЕРЕВОДА ИЗ ПИБЭСМ В ПИЕС С ВЫДАЧЕЙ НА ЕСПИ
*ВХОД:СМ=Е1-ЗАПИСЬ НА АБЗАЦ ЕСПИ ОДНОЙ ПФК С ПЕРЕВОДОМ,
*СМ=Е2-ЧИСТАЯ ПФК,СМ=Е3-ВЫТАЛКИВАНИЕ БУФЕРА.
*М11-БАЗА АБЗАЦА ДЛЯ ПИЕС,М12-КОЛИЧЕСТВО ПФК В АБЗАЦЕ,
*М14-ВОЗВРАТ ИЗ П/П,М1:М6,М15:М17-РМ.
ФИЗОЕС УИ М1
 АIF (∧ВS ЕQ 1).С5
 ПЕ ФИЗЕС1 ОБРАЩЕНИЕ НЕ ИЗ ФИЗОПИ
 СЧ Е1
 ПВ ТБУФ(М15) ДАЙ АБЗАЦ ДЛЯ ПИЕС
 СДА 64-8
 УИ М11 АДРЕС АБЗАЦА ДЛЯ ПИЕС
 УИА (М12) ЧИСЛО ПФК:=0
 АGО .Е3
.С5 АNОР
 МОДА БУФ
 УИА (М11)
.Е3 АNОР
 УИА 3
 АIF (∧ВS ЕQ 1).С4
ФИЗЕС1 СЧИ М1
 И Е3
 ПЕ ФИЗЕС9 БЕЗУСЛОВНОЕ ВЫТАЛКИВАНИЕ БУФЕРА
 АGО .Е2
.С4 АNОР
 УИА -26(М15)
 СЧ 0
ЧИСТ МОДА (М11)
 ЗП 26(М15)
 МОДА ЧИСТ
 ЦИКЛ (М15)
 СЧИ М1
 И Е3
 ПО МНОП
 СЧ ВСЕЕД
 ЗП 26(М11)
 ЗП 12(М11)
 СДА 64-32
 ЗП 13(М11)
 СЧ ВСЕЕД
 СДА 64+16
 ЗП 25(М11)
 ПБ ФИЗЕС8
МНОП НОП
.Е2 АNОР
 СЧИ М1
 И Е2
 ПЕ ФИЗЕС8 ЧИСТАЯ ПФК
 АIF (∧ВS ЕQ 1).С3
 СЧИ М12
 ПЕ ФИЗЕС2 ПЕРЕВОД ИЗ ПИБЭСМ В ПИЕС
 УИА -255(М15)
ЧИСТКА МОДА (М11)    ОБНУЛЕНИЕ
 ЗП 255(М15)         АБЗАЦА
 МОДА ЧИСТКА         ПОД
 ЦИКЛ (М15)          ПИЕС.
.С3 АNОР
*БЛОК ПЕРЕВОДА ИНФОРМАЦИИ ИЗ МАКЕТА ПИБЭСМ В ПИЕС
ФИЗЕС2 УИА -11(М1)
 МОДА БУФПФК
 УИА (М2)
ФИЗЕС3 СЧ (М2)
 СДА 64-8
 ЗП (М2) А(I,48:9)=А(I,40:1)
 СЧ 1(М2)
 СДА 64-16
 ЗП 1(М2) А(I+1,48:17)=А(I+1,32:1)
 СЧМР
 И Е8П1
 ИЛИ (М2)
 ЗП (М2) А(I, ):=А(I, )V(А(I+1,40:33)’СДВ’+32)
 СЛИА 2(М2)
 МОДА ФИЗЕС3
 ЦИКЛ (М1)
 УИА (М1) I:=0
 УИА (М3) К:=0
 УИА -1(М4) 2 РАЗА ЦИКЛ
ФИЗЕС4 УИА -15(М5) 16 РАЗ ЦИКЛ
ФИЗЕС5 СЧИ М3
 НТЖ П33
 ПО ФИЗЕС8 ПЕРЕВОД ОДНОЙ ПФК ЗАКОНЧЕН
 СЧ МАКЕТ
 ЗП РЯ
 УИИ М2(М1) J:=I
 УИА -1(М6) 2 РАЗА ЦИКЛ
ФИЗЕС6 СЧ РЯ
 СДА 64+2
 ЗП РЯ
 УИА -5(М15) 6 РАЗ ЦИКЛ
ФИЗЕС7 МОДА (М2) J
 СЧ БУФПФК
 РЗБ РЯ
 МОДА (М3) К
 ИЛИ (М11)
 МОДА (М3)
 ЗП (М11)
 МОДА (М2)
 СЧ БУФПФК
 СДА 64-3
 МОДА (М2)
 ЗП БУФПФК
 СЧ РЯ
 СДА 64+1
 ЗП РЯ
 СЛИА 2(М2) J:=J+2
 МОДА ФИЗЕС7
 ЦИКЛ (М15) КОНЕЦ1;
 МОДА ФИЗЕС6
 ЦИКЛ (М6) КОНЕЦ2;
 СЛИА 1(М3) К:=К+1
 МОДА ФИЗЕС5
 ЦИКЛ (М5) КОНЕЦ3;
 УИА 1(М1) I:=1
 МОДА ФИЗЕС4
 ЦИКЛ (М4) КОНЕЦ4; КОНЕЦ БЛОКА ПЕРЕВОДА.
ФИЗЕС8 НОП ,
 СЧИ М11
 ЗП ДМ11
 ИЛИ =М44В’6’М38В’4’М30Х’61’М20В’32’
* ОПИСАНИЕ ПАРАМЕТРОВ СМ. В ПВВ
 МОД НОМПИ
 УИА  ТИППИ(М11)
 ПВ ПВВЕС(13)
 КК ’33’,5(13)
 ЗП СБПИ       (63-16 РР ДР) -> В РАБ.ЯЧ
 СЧИ 13
 ПВ ДАЙКОТ(13)   КО
 РЗБ КОТПВВ   В ТЕРМ
 ЗП КВИТ
 СЧ ДМ11
 УИ М11
*
*  В М5 ФИКСИРУЕТСЯ ТИП СБОЯ ДЛЯ СТАТИСТИКИ:
*  1 -ОТКЛОНЕНИЕ КОМАНДЫ (ОК)
*  2 -ТРЕБУЕТСЯ ВМЕШАТ.ОПЕРАТОРА (ТВО)
*  3 -СБОЙ КАНАЛА (СБК)
*  4 -СБОЙ В ОБОРУДОВАНИИ (СБО)
*  8 - НЕТ ГОТОВНОСТИ УСТРОЙСТВА
*
 СЧ КВИТ
 ПО ХОР
 УИА 8(М5)  НЕ ГОТОВ
 МОДА ТНЕТГТ
 УИА (М16)
 СЧ СБПИ
 И Е2
 ПО СБ0
 СЧ КВИТ
 И Е47
 ПО СБ0
 МОДА ВЫХОШ1
 УИА (М2)
 ПБ ВЫДОШ
СБ0 МОДА ТСБОЙ
 УИА (М16)
 МОДА СБ0А
 УИА (М15)
 ПБ ВЫДОПТ
СБ0А УИА -3(М6)
 УИА 1(М5)
СБ1 СЧ КВИТ
 И Е45(М6)
 ПО СБ2    НЕТ ЭТОГО ВИДА СБОЕВ
 МОДА ВЫХОШ
 ПВ (М2)
СБ2 СЛИА 1(М5)
 МОДА СБ1
 ЦИКЛ (М6)
 ПБ ВЫХОШ1
ХОР НОП ,
 СЧ Е1
 СЛЦ АЗУ+1
 ЗП АЗУ+1 КОЛИЧЕСТВО ВЫДАННЫХ ПФК НА ЕСПИ
 ПБ (М14)
*КОНЕЦ П/П ФИЗОЕС
БУФПФК КОНД (14)В’0’
ЦИФРЫ КОНД В’1714170360741474’ 0
 КОНД В’1701403026034030’ 1
 КОНД В’7774007400741474’ 2
 КОНД В’1714140317003377’ 3
 КОНД В’0157754615414014’ 4
 КОНД В’1714140377140374’ 5
 КОНД В’1714170377140074’ 6
 КОНД В’1403003003001777’ 7
 КОНД В’1714170317141474’ 8
 КОНД В’1700147760741474’ 9
БУФ КОНД (27)В’0’
ТКВ КОНД П’4     ’
 КОНД (2)В’0’       ШИФР ЗАДАЧИ
 КОНД П’ КВ ПИ’
 КОНД П’ 0↑   ’     НЕ РАЗДЕЛЯТЬ С ТКВ
НОМПИ КОНД В’0’         N ПИ
ДИСП70 ВНЕШ НУСВУ,Е48П43
МОТТ ВНЕШ ДЕЛЮ1
*КОНЕЦ ЧАСТИ

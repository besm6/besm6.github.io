 МАСRО
∧М ЧТСЕМ
∧М КК ’33’,СЕМППД
 МЕND
 МАСRО
∧М ЗПСЕМ
∧М КК ’32’,СЕМППД
 МЕND
СЕТЬ СТАРТ ’12335’
ЭКСЕТЬ НОП ,
*
* М1 - НОМЕР ЗАДАЧИ
* М3 - АДРЕС ИПЗ
 СЧ (М3)
 МОДА ПАРОЛЬ
 НТЖ 0
 ПЕ НЕПАР
*
***********************************************************
*                                                         *
*            Э К - К О Д        0 5 0  0 2 2 2            *
*                                                         *
* ВХОД  :                                                 *
*  СМ = П’ЬЛОРАП’                                         *
*     ЗАДАЧА ОБ’ЯВЛЯЕТ СЕБЯ ТРАНС.СТАНЦИЕЙ ВИРТ.ТЕРМИНАЛОВ*
* КОДЫ ОТВЕТА: НОРМА,ЕСТЬТС                               *
***********************************************************
*
 УИА ЕСТЬТС(М14)
 СЧ НЗТСВТ
 ПЕ ВЫХЭК
 СЧИ М1
 ЗП НЗТСВТ
 ПБ НОРВЫХ
*
*
***********************************************************
*                                                         *
*            Э К - К О Д        0 5 0  0 2 2 2            *
*                                                         *
* ВХОД  :                                                 *
*        СМ =                                             *
*             48-43 РР. - N РАБОТЫ                  NРАБ  *
*             42-37 РР. - N ВИРТ.ТЕРМИНАЛА          NВТ   *
*             36-34 РР. - ТИП ТЕРМИНАЛА             ТИПТ  *
*             33-31 РР. - КОД ОТВЕТА                КОТ   *
*             30-25 РР. - КОД ФУНКЦ.КЛАВИШЫ         КФК   *
*             24-16 РР. - ЧИСЛО СЛОВ ПРИНЯТЫХ С В.Т. LПР  *
*             15- 1 РР. - АДРЕС ИНФОРМАЦИИ          АИНФ  *
*                                                         *
* ВЫХОД :                            N РАБОТЫ      ИМЯ    *
*   М16 =  0  -   Н О Р М А          0 1 2 3 4 5  НОРВЫХ  *
*   М16 =  1  -   УЖЕ ЕСТЬ Т.С.В.Т     ’ЬЛОРАП’   ЕСТЬТС  *
*   М16 =  2  -   НЕТ ЛОГ.ЛИНИЙ        +          НЕСЛОН  *
*   М16 =  3  -   В.Т. В ЗАДАЧЕ                +  ВТВЗАД  *
*   М16 =  4  -   В.Т. НЕТ               + + +    ВТНЕТ   *
*   М16 =  5  -   НЕТ ЗАКАЗА               +      НЗАК    *
*   М16 =  6  -   НЕ БЫЛО ВЫДАЧИ             +    НЕТВЫД  *
*   М16 =  7  -   ПРИЕМ ЗАПРЕЩЕН         +        ПРЗАКР  *
*   М16 = 10  -   ЕСТЬ ЗАКАЗ                   +  ЕСТЬЗК  *
*   М16 = 11  -   НЕ ОКОНЧЕН.ВЫДАЧА            +  НЕОКВД  *
*   М16 = 12  -   НЕВЕРНЫЙ ТИП ТЕРМ.   +          НЕТТИП  *
*   М16 = 13  -   НЕТ МЕСТА В БУФ.         +      НЕТМЕС  *
*                                                         *
***********************************************************
*
 ЭКВИВ (НОРМА,0),(ЕСТЬТС,1),(НЕСЛОН,2),(ВТВЗАД,3)
 ЭКВИВ (ВТНЕТ,4),(НЗАК,5),(НЕТВЫД,6),(ПРЗАКР,7)
 ЭКВИВ (ЕСТЬЗК,8),(НЕОКВД,9),(НЕТТИП,10),(НЕТМЕС,11)
 СТРН ,
********************************
НЕПАР НОП ,
 СЧИ М1
 НТЖ НЗТСВТ
 ПЕ ЗАПЭК
 СЧ (М3)
 СДА 64+42
 УИ М16
 СДА 64+3
 ПЕ ЗАПЭК         ТАКОЙ РАБОТЫ НЕТ
 УИА ЧУЖЛИС(М15)
 СЧИ М15
 ПВ ЗАПВЫХ(М15)
 СЧ (М3)
 СДА 64+36
 И П77
 УИ М2
ПЛОХВТ НОП ,
 УИА ВТНЕТ(М14)
 ПО ПЛОХО
 ВЧОБ П60
 ПЕ ПЛОХО
 УИА 0(М14)        М14 = 0 - НВТ ЗАДАН ВЕРНО
 СЛИА -1(М2)        М2 -   NВТ-1
 СЧ ШКВТ
 И Е48(М2)
 ПО ПЛОХВТ
ПЛОХО НОП ,
 СЧ (М3)            СМ ЗАДАЧИ
 ПБ ДЕШРАБ(М16)
* ДЕШИИФРАТОР РАБОТ
ДЕШРАБ НОП ,
  СЧ ШКВТ
 ПБ РАБ0ВТ
  УИА 1027
 ПБ РАБ1ВТ
  СЧ ШКВЫД
 ПБ РАБ2ВТ
  СЧ ШРАБВТ
 ПБ РАБ3ВТ
  СЧ ШКВЫД
 ПБ РАБ4ВТ
  СЧ ШКВТ
 ПБ РАБ5ВТ
  СЧ (М3)
 ПБ РАБ6ВТ
  СЧ (М3)
 ПБ РАБ7ВТ
*
*
***********************************************************
*                                                         *
*              N   Р А Б О Т Ы  = 0 0                     *
*                   О П Р О С                             *
*  ВХОД :                                                 *
* ВЫХОД:                                                  *
*              СМ = ШКАЛА В.Т ,ПО КОТОРЫМ ЕСТЬ ЗАКАЗЫ     *
*                              ДЛЯ Т.С.В.Т.               *
*             РМР = ШКАЛА ВИРТ. ТЕРМИНАЛОВ                *
* КОДЫ ОТВЕТА: НОРМА              00                      *
***********************************************************
*
РАБ0ВТ НОП ,
 ЗП 2(М3)         РМР = ШКВТ
 СЧ ШРАБВТ
 ЗП (М3)          СМ  = ШКАЛА РАБОТ ДЛЯ Т.С.
 ПБ НОРВЫХ
 СТРН ,
*
***********************************************************
*                                                         *
*              N   Р А Б О Т Ы  = 0 1                     *
*           ЗАПРОС ЛОГИЧЕСКОЙ ЛИНИИ ДЛЯ В.Т.              *
*  ВХОД : ТИПТ                                            *
* ВЫХОД:                                                  *
*          СМ  = НОМЕР ЛОГИЧЕСКОЙ ЛИНИИ                   *
* КОДЫ ОТВЕТА: НОРМА,НЕСЛОН       01                      *
***********************************************************
*
РАБ1ВТ НОП ,
 СЧ (М3)
 СДА 64+33
 И П3
 УИ М13           М13 = ТИП ТЕРМИНАЛА
 УИА ЕVТ(М15)
 СЧ (М3)
 СДА 64+35
 И Е1
 ПО НЕЕС
 УИА 0(М15)
 УИА НЕТТИП(М14)
 ПИНО ВЫХЭК(М13)
НЕЕС НОП
 УИА НЕСЛОН(М14)   НЕТ ЛОГ.ЛИНИЙ
 СЧ СЛОN
 НЕД ,
 ПО ВЫХЭК
 ЗП (М3)          СМ: N ЛОГ.ЛИНИИ
 УИ М16
 СЛИА -1(М16)
 СЧ (М15)
 ЗП ТСЛ(М16)
 СЧ ШКВТ
 ИЛИ Е48(М16)
 ЗП ШКВТ
 ИЛИ ШКУСТР
 ЗП ШКУСТР
 СЧ СЛОN
 НТЖ Е48(М16)
 ЗП СЛОN
 СЧ ШКВИД(М13)
 ИЛИ Е48(М16)
 ЗП ШКВИД(М13)    ТИП ТЕРМИНАЛА
 СЧ ШVD130
 ИЛИ ШНР
 ЗП ШНР
 ПВ ПИЛОТ(М15)
НОРВЫХ НОП ,
 УИА НОРМА(М14)
 ПБ ВЫХЭК
 СТРН ,
*
***********************************************************
*                                                         *
*              N   Р А Б О Т Ы  = 0 2                     *
*       ПЕРЕДАЧА ИНФОРМАЦИИ ПРИНЯТОЙ С ВИРТ.ТЕРМИНАЛА     *
*  ВХОД : NВТ,КФК,КОТ,LПР,АИНФ                            *
*        ПРИМЕЧАНИЕ: В ПОСЛЕДНЕМ СЛОВЕ ПОСЛЕ ’КОНЕЧНОГО’  *
*                    БАЙТА ДОЛЖНЫ БЫТЬ ТОЛЬКО НУЛЕВЫЕ     *
*                    БАЙТЫ.                               *
*                                                         *
*                                                         *
* КОДЫ ОТВЕТА: НОРМА,ВТНЕТ,ПРЗАКР 02                      *
***********************************************************
*
РАБ2ВТ НОП ,
 ПИНО ВЫХЭК(М14)    НЕТ В.Т.
 УИА ПРЗАКР(М14)
 ИЛИ ШЗПРИМ
 И Е48(М2)
 ПЕ  ВЫХЭК          ПРИЕМ ЗАКРЫТ
 СЧ (М3)
 СДА 64+15
 И П777
 УИ М5            М5 - КОЛ-ВО СЛОВ ПРИНЯТЫХ С В.Т.
 ЗП 2(М3)
 УИИ М7(М1)        М7 = М1 - НОМЕР ЗАДАЧИ
 МОД (М3)
 УИА (М4)          М4-АИНФ
 СЧ (М3)
 СДА 64+31-19
 И КОТ
 ПЕ ЕСТЬК
 СЧ Е20
ЕСТЬК НОП ,
 ИЛИ ТСЛ(М2)
 ЗП ТСЛ(М2)
 И КОТ
 НТЖ Е20
 ПЕ КОНПРИ        КОТ≠10
 МОД ТСЛ(М2)
 УИА (М1)          М1-АДРЕС 1-ОГО РЕКОРДА
 СЧ ШКЭ71
 И Е48(М2)
 ПЕ ВТЭ71         ДЛЯ Э71
 СЧ РАЗРЕК
 ЗП ТСВ(М1)
ВТЭ71 НОП ,
 СЧ ТСВ(М1)
 И П777
 УИ М6            М6- КОЛ-ВО СЛОВ В Э71
 ВЧ 2(М3)
 ПО LПРБОЛ
 УИИ М5(М6)
LПРБОЛ НОП ,
 СЧ (М3)
 СДА 64+24
 И П77           КФК
 ЗП ТСВ(М1)
 СЛИА Н(М1)
ПЕРПР НОП ,
 УИА 0
 СЧ (М4)
 УИА 3
 ЗП (М1)
 СЛИА 1(М1)
 СЛИА 1(М4)
 СЛИА -1(М5)
 ПИО КОНПРИ(М5)
 СЧИ М1
 И П37
 ПЕ ПЕРПР         В РЕКОРДЕ ЕСТЬ ЕЩЕ МЕСТО
 ПВ ПОИСК(М6)
 УИА 3
 ПБ ПЕРПР
*
КОНПРИ НОП ,
 УИИ М1(М7)        М1 = НОМЕР ЗАДАЧИ
 УИА 1027
 СЧ ЖДУРЕК
 ИЛИ Е48(М2)
 НТЖ Е48(М2)
 ЗП ЖДУРЕК
 СЧ ЕЗАКП
 ИЛИ ШКОРАБ
 ЗП ШКОРАБ
 СЧ Е48(М2)
 ИЛИ ШЗПРИМ
 ЗП ШЗПРИМ
 СЧ Е48(М2)
 ИЛИ ШКЗАПР
 ЗП ШКЗАПР
 УИА 3
 ПБ НОРВЫХ
 СТРН ,
*
***********************************************************
*                                                         *
*              N   Р А Б О Т Ы  = 0 3                     *
*   ЗАПРОС ИНФОРМАЦИИ ДЛЯ ВЫДАЧИ НА ВИРТ.ТЕРМИНАЛ         *
*  ВХОД : NВТ,АИНФ,LПР.                                   *
*     ПРИМЕЧАНИЕ:                                         *
*      1.ПРИ NТВ= 0 ,ВЫДАЕТСЯ ЗАКАЗ ДЛЯ ДЛЯ МЛАДШЕГО В.Т. *
*                    ИЗ В.Т. ,ШКАЛА КОТОРЫХ ЗАДАНА НА РМР.*
*      2.ПРИ LПР≠ 0, LПР -  РАЗМЕР БУФЕРА У ЗАДАЧИ.      .*
*            ЕСЛИ РАЗМЕР БУФЕРА У ЗАДАЧИ МЕНЬШЕ ЧЕМ       *
*            РАЗМЕР ВЫДАВАЕМОЙ ИНФОРМАЦИИ,ТО ЗАПРОС       *
*             НЕ УДОЛЕТВОРЯЕТСЯ(КОД ОТВЕТА=НЕТМЕС).       *
* ВЫХОД:                                                  *
*         СМ :                                            *
*                       48-43 РР - NВТ                    *
*                          42 Р  - ЗАКАЗ ОТ ЗАДАЧИ(Э71)   *
*                          40 Р  - ПРИЕМ Э71              *
*                          38 Р  - СЕКРЕТНЫЙ ПРИЕМ        *
*                          37 Р  - ТЕСТОВЫЙ РЕЖИМ         *
*                       18-10 РР - L СЛОВ ВЫДАЧИ          *
*                        9- 1 РР - L СЛОВ ПРИЕМА          *
*                    АИНФ  - ... -  ТЕКСТ ВЫДАЧИ          *
*          В ТЕСТОВОМ РЕЖИМЕ ТЕКСТ ЗАКАНЧИВАЕТСЯ НУЛЕВЫМ  *
*          БАЙТОМ, ИНАЧЕ - БАЙТОМ ’377’                   *
*        РМР :                                            *
*                       ШКАЛА В.Т.( 48 Р- 1-ЫЙ В.Т.);     *
*                          ИМЕЕТ СМЫСЛ ПРИ NВТ= 0         *
* КОДЫ ОТВЕТА: НОРМА,ВТНЕТ,НЗАК,НЕТМЕС 03                 *
***********************************************************
*
РАБ3ВТ НОП ,
 ПИО ВТЗАД(М14)    НОМЕР ЗАДАН
 ПИНО ВЫХЭК(М2)     ЗАДАН НЕВЕРНО
 УИА НЗАК(М14)
 И 2(М3)
 ПО ВЫХЭК         НЕТ ЗАКАЗОВ
 НЕД П77777
 УИ М2            НОМЕР ВТ
ВТЗАД НОП
 МОД (М3)
 УИА (М17)         АДРЕС ИНФ В ПАМЯТИ ЗАДАЧИ
 СЧ ТСЛ(М2)
 УИ М4            БАЗОВЫЙ РЕКОРД
 УИА 1027
 СЧ ШРАБВТ
 УИА НЗАК(М14)
 И Е48(М2)
 ПО ВЫХЭК         НЕТ ЗАКАЗА ПО ЭТОМУ ВТ
 СЧ (М3)
 СДА 64+15
 И П777
 ЗП 2(М3)         РМР:=РАЗМЕР БУФЕРА У ЗАДАЧИ
 УИ М14
 СЧ ТСВ(М4)
 ЗП (М3)          ЗАГОЛОВОК
 ПИО КОЛНЕТ(М14)   У ЗАДАЧИ РАЗМЕР БУФЕРА НЕ ЗАДАН
 СДА 64+9
 И П777
 ВЧОБ 2(М3)
 УИА НЕТМЕС(М14)
 ПЕ ВЫХЭК         РАЗМЕР БУФЕРА У ЗАДАЧИ МАЛ
КОЛНЕТ НОП ,
 СЧ Е48(М2)
 НТЖ ШРАБВТ
 ЗП ШРАБВТ
 УИА 3
ПОРЕК НОП .             ЦИКЛ ПО ЦЕПОЧКЕ РЕКОРДОВ
 СЧ ДЛ(М4)        ДЛИНА РЕКОРДА
 ВЧОБ
 УИ М5            ДЛЯ ЦИКЛА
 УИИ М6(М4)
ПЕРЕС НОП .             ЦИКЛ ПО СЛОВАМ РЕКОРДА
 СЧ Н(М6)
 УИА 0
 ЗП (М17)
 УИА 3
 СЛИА 1(М6)
 ЦИКЛ ПЕРЕС(М5)
 МОД ВН(М4)
 УИА (М4)          ССЫЛКА ВНИЗ
 ПИНО ПОРЕК(М4)     ЕСТЬ ЕЩЕ РЕКОРДЫ
 СЧ ТСЛ(М2)
 И ЕПРИГ
 ПО НЕПРИЕ        НЕ ПРИЕМ
* ПРИ ПРИЕМЕ ПОДТВЕРЖДАТЬ ВЫДАЧУ ПРИГЛАШЕНИЯ
*          Н Е   Н А Д О ! ! !
 УИА 1027
 СЧ ШКВЫД
 НТЖ Е48(М2)
 ЗП ШКВЫД
 СЧ ШЗПРИМ
 ИЛИ Е48(М2)
 НТЖ Е48(М2)
 ЗП ШЗПРИМ
 СЧ ТСЛ(М2)
 И ГАШТСЛ
 ЗП ТСЛ(М2)
 УИА 3
НЕПРИЕ НОП ,
 СЧ ШРАБВТ
  ПО НОРВЫХ        ЗАКАЗОВ БОЛЬШЕ НЕТ
  ЗП 2(М3)         РМР := ШКАЛА ЗАКАЗОВ
  УИА НОРВЫХ(М16)
 СЧ НЗТСВТ
 ИЛИ ЕРАБВТ
 ПБ ЗАПВЕТ
 СТРН ,
*
***********************************************************
*                                                         *
*              N   Р А Б О Т Ы  = 0 4                     *
*     ПОДТВЕРЖДЕНИЕ ОКОНЧАНИЯ ВЫДАЧИ НА ВИРТ.ТЕРМИНАЛ     *
*  ВХОД :NВТ ,КОТ                                         *
* КОДЫ ОТВЕТА: НОРМА,ВТНЕТ,НЕТВЫД 04                      *
***********************************************************
*
РАБ4ВТ НОП ,
 НТЖ ШРАБВТ
 И ШКВЫД
 УИА НЕТВЫД(М14)
 И Е48(М2)
 ПО ВЫХЭК
 УИА 1027
 СЧ (М3)
 СДА 64+31-19
 И КОТ
 ПЕ КЕСТЬ
 СЧ Е20
КЕСТЬ НОП ,
 ИЛИ ТСЛ(М2)
 ЗП ТСЛ(М2)
 УИИ М15(М2)
 ПВ КОНВЫД(М16)
 УИА 3
 ПБ НОРВЫХ
 СТРН ,
*
*
***********************************************************
*                                                         *
*              N   Р А Б О Т Ы  = 0 5                     *
*            ОТКАЗ ОТ ЛОГИЧЕСКОЙ ЛИНИИ В.Т.               *
*  ВХОД :  NВТ                                            *
* КОДЫ ОТВЕТА: ВТНЕТ,ВТВЗАД,ЕСТЬЗК,НЕОКВД 05              *
***********************************************************
*
РАБ5ВТ НОП ,
 ПИНО ВЫХЭК(М14)    NВ.Т. ЗАДАН НЕВЕРНО
 И Е48(М2)
 УИА ВТНЕТ(М14)
 ПО ВЫХЭК         ТЕРМИНАЛ НЕ ВИРТ.
 УИА ВТВЗАД(М14)
 СЧ ШЗНКТ
 ИЛИ ШЗНКТ1
 И Е48(М2)
 ПЕ ВЫХЭК         В.Т. В ЗАДАЧЕ
 СЧ ШРАБВТ
 ИЛИ ШЗПРИМ
 И Е48(М2)
 УИА ЕСТЬЗК(М14)
 ПЕ ВЫХЭК
 СЧ ШКВЫД
 И Е48(М2)
 УИА НЕОКВД(М14)
 ПЕ ВЫХЭК
 УИА 1027
 СЧ Е48(М2)
 НТЖ ВСЕЕД
 ЗП (М3)
 И ШКУСТР
 ЗП ШКУСТР
 И ШКВТ
 ЗП ШКВТ
 УИА -3(М14)
ЧИСШК НОП ,
 СЧ (М3)
 И ШКВИД+3(М14)
 ЗП ШКВИД+3(М14)
 ЦИКЛ ЧИСШК(М14)
 СЧ 0
 ЗП ТСЛ(М2)
 СЧ Е48(М2)
 ИЛИ СЛОN
 ЗП СЛОN
 ПБ НОРВЫХ
 СТРН ,
*
*
***********************************************************
*                                                         *
*              N   Р А Б О Т Ы  = 0 6                     *
*              С  В  О  Б  О  Д  Н  А                     *
*  ВХОД :                                                 *
* ВЫХОД:                                                  *
***********************************************************
*
РАБ6ВТ НОП ,
*
*
*
***********************************************************
*                                                         *
*              N   Р А Б О Т Ы  = 0 7                     *
*              С  В  О  Б  О  Д  Н  А                     *
*  ВХОД :                                                 *
* ВЫХОД:                                                  *
***********************************************************
*
РАБ7ВТ НОП ,
*
*
ЗАПЭК НОП ,
 УИА 36(М17)
 ПБ АВОСТ
*
ЧУЖЛИС НОП ,
 УИА 16(М17)
АВОСТ НОП ,
 УИИ М15(М3)
 ПВ АВОСТА(М16)
 ПБ ВЫХ222
*
ВЫХЭК НОП ,
 СЧИ М14
 ЗП 8(М3)        М16 ЗАДАЧИ
 ПБ ВЫХ222
 СТРН ,
*
*///////////////////////////////////////////////////////
*//////                                           //////
*//////   Э К  -  К О Д   0 0  0 5 0   1 2 0 0    //////
*//////                                           //////
*//////              РАБОТА С  П П Д              //////
*//////                                           //////
*//////                                           //////
*///////////////////////////////////////////////////////
*
*
*   СЕМППД :
*           63-56(48-41) РР. - ШКАЛА СВОБОДНЫХ БУФЕРОВ
*           55-48(40-33) РР. - ССЫЛКА ДЛЯ СВСППД
*           47-40(32-25) РР. - ССЫЛКА ДЛЯ ППДСВС
*              32(   17)  Р. - СЕМАФОРНАЙ БИТ
*              24(    9)  Р. - СВС РАБОТАЕТ
*              16(    1)  Р. - ППД РАБОТАЕТ
*   СВСППД :
*           63-16(48- 1) РР. - ССЫЛКИ ДЛЯ 0-3 БУФЕРОВ
*   СВСППД +1:
*           63-16(48- 1) РР. - ССЫЛКИ ДЛЯ 4-7 БУФЕРОВ
*   ССЫЛКА :
*              8 Р. = 0 - КОНЕЦ ЦЕПОЧКИ
*              4 Р. - ТЕК.БУФ.ЕСТЬ ПРОДОЛЖЕНИЕ ПРЕД.БУФ.
*            3-1 РР.-  НОМЕР БУФЕРА (0-7)
*
*                 СВСППД  ЭКВ  СЕМППД+1
*
*////////////////////////////////////////////////////////
*
 ЭКВИВ (НЕТЗАГ,1),(ЗАНСЕМ,2),(НЕТПУС,3),(НЕТИНФ,4)
 ЭКВИВ (НЕТБУФ,5),(ОШППД,6),(ОШКОЛ,7)
 ЭКВИВ (ЗАДЕРЖ,’77777’)
*
ЭКППД НОП ,
 УИ М16
 ВЧ Е3
 ПЕ ЗАПЭК         ЗАПРЕЩЕННЫЙ ЭК-КОД
 НТЖ 0
 ПО ППДР4         РАБОТА N 4
 НТЖ П3
 ПО ППДР1         РАБОТА N 1
 УИА ЧУЖЛИС(М15)
 СЧИ М15
 ПВ ЗАПВЫХ(М15)
*
* ФИКСАЦИЯ ЛИСТА ЗАДАЧИ
*
 СЧ (М3)
 И П37
 ЗП 2(М3)
 НТЖ П37
 УИ М7            М7 := ОБРАТНЫЙ КОД N ЛИСТА
 СЧ 2(М3)
 СДА 64-10
 УИ М6
 СЧ 2(М3)
 И П3
 УИ М5
 СЧ 2(М3)
 СДА 64+2
 НТЖ П7
 УИ М15
 УИА 1026
 СЧ 20(М6)
 УИА 1027
 СЧ 32(М3)
 ИЛИ Е48(М7)
 ЗП 32(М3)
 УИА 3
 МОДА (М3)
 СЧ 24(М15)
 СБР ПРИП(М5)
 СДА 64+2
 УИА 0(М2)         ЛИСТ ЗАДАЧИ НА 1-ЫЙ ЛИСТ
 ПВ ПРУ12(М10)
*
 СЧ П77
 РЕГ ’44’          ТЕГ 77
 ПИО ППДР0(М16)    РАБОТА N 0
 ПВ ЗАХСЕМ(М13)
 ПЕ ВЫХППД
 СЧ БУФППД
 УИА 1(М2)
 ПВ ПРУ12(М10)    БУФЕР ППД НА 2-ОЙ ЛИСТ
 СЧИ М16
 НТЖ Е2
 ПО ППДР2          РАБОТА N 2
 ПБ ППДР3          РАБОТА N 3
*
********************************************************
*                                                      *
*            З А Г Р У З К А     П  П  Д               *
*                                                      *
* В Х О Д :                                            *
*          СМ =                                        *
*              48-46 РР. = 0                           *
*               5-1  РР. = НОМЕР ЛИСТА С ПРОГР.ППД     *
*                                                      *
* В Ы Х О Д:                                           *
*            КОТ: НОРМА,НЕТПУС                         *
*                                                      *
********************************************************
*
ППДР0 НОП ,
 СЧ БУФППД
 ПЕ ЕСТБУФ        БУФЕР ППД ЕСТЬ
 СЧ Е2
 ПВ ДБУФ(М15)     БУФ.ППД НА 2-ОЙ ЛИСТ
 ЗП БУФППД
 ПБ ПРОДР0
ЕСТБУФ НОП ,
 УИА 1(М2)
 ПВ ПРУ12(М10)    БУФ. ППД НА 2-ОЙ ЛИСТ
ПРОДР0 НОП ,
 УИА 3
 СЧ БУФППД
 СДА 64+36
 УИ М11           М11:= N ЛИСТА,ДЛЯ ПУСППД
 СЧ БАЙТ
 ИЛИ Е9            СВС РАБОТАЕТ
 КК ’32’,СЕМППД
 И 0
 КК ’32’,СВСППД
 КК ’32’,ППДСВС
 УИА СЕМППД(М15)
 СЧИ М15
 СДА 64-36
 ИЛИ БОСОБМ
 СДА 64+20
 УИА 2
 ЗП ’2000’+4      АДРЕС СЕМППД
 УИА -511(М16)
 УИА 0(М15)
 УИА ’4000’(М17)
ПЕРППД НОП ,
 СЧ ’2001’(М16)
 СДА 64+32
 ИЛИ ’2000’(М16)
 ЗП ’2000’
 СЧ ’2001’(М16)
 СДА 64+32
 СЧ ’2000’
 КК ’032’,(М17)
 ЦИКЛ ПЕРППД(М16)
 УИА 3
 СЧ КПППД
 ПВ ПУСППД(М15)
 УИА НОРМА(М14)
 ПО ВЫХППД
 УИА НЕТПУС(М14)
 ПБ ВЫХППД
*
********************************************************
*                                                      *
*              О П Р О С         П  П  Д               *
*                                                      *
* В Х О Д :                                            *
*          СМ =                                        *
*              48-46 РР. = 1                           *
*                                                      *
* В Ы Х О Д:                                           *
*            КОТ: НОРМА,НЕТЗАГ,ЗАНСЕМ;                 *
*            ПРИ КОТ=НОРМА, СМ = 1 Р.= 1 - ППД РАБОТАЕТ*
*                                2 Р.= 1 - ЕСТЬ КАДР   *
*                                                      *
********************************************************
*
ППДР1 НОП ,
 ЧТСЕМ
 И Е1            РАБОТАЕТ ЛИ ППД
 ЗП (М3)
 ПВ ЗАХСЕМ(М13)
 ПЕ ВЫХЭК
 ЧТСЕМ
 И Е48П25
 ИЛИ Е9            СВС РАБОТАЕТ
 ЗПСЕМ
 И КОНСБР+3      Е32П25
 ПО НЗППД
 СЧ Е2            ППД ЧТО-ТО ПРИСЛАЛ
НЗППД НОП ,
 ИЛИ (М3)
 ЗП (М3)
 УИА НОРМА(М14)
 ДК ’36’,ВЫХЭК
*
********************************************************
*                                                      *
*              Ч Т Е Н И Е  ИЗ   П  П  Д               *
*                                                      *
* В Х О Д :                                            *
*          СМ =                                        *
*              48-46 РР. = 2                           *
*               5- 1 РР. = НОМЕР ЛИСТА                 *
*                                                      *
* В Ы Х О Д:                                           *
*            КОТ: НОРМА,НЕТИНФ,ЗАНСЕМ;                 *
*                                                      *
********************************************************
*
ППДР2 НОП ,
 УИА НЕТИНФ(М14)
 УИА ’2000’(М17)   М17 := ’2000’ АДРЕС ЗАПИСИ
СЛБППД НОП ,
 ЧТСЕМ
 СДА 64+24
 И П377
 ПО ВЫХОСВ        НЕТ ИНФОРМАЦИИ ППД->СВС
 И П7
 УИ М13
 УИ М4            М4 := N БУФЕРА
 УИА 0(М5)
 И Е3
 ПО ПЕРЯЧП        БУФ В ПЕРВОЙ ЯЧЕЙКИ СВСППД
 УИА 1(М5)
 СЛИА -4(М13)
ПЕРЯЧП НОП ,
 СЧ СВСППД(М5)
 И  БАЙТ(М13)
 ЗП (М3)
 НТЖ СВСППД(М5)
 ЗП СВСППД(М5)
 СЧ (М3)
 СБР БАЙТ(М13)
 СДА 64+16
 ЗП (М3)
 ЧТСЕМ
 ИЛИ БАЙТ+2
 НТЖ БАЙТ+2
 ИЛИ Е48(М4)       ОСВОБОДИЛИ БУФЕР
 ИЛИ (М3)          НОВАЯ ССЫЛКА НА НАЧ.ЦЕПОЧКИ ППД->СВС
 ЗПСЕМ
 СЧИ М4
 СДА 64-7          NБУФ * 128
 УИ М16
 УИА -127(М15)
 УИА 2
ПЕРПС НОП ,
  СЧ ’4000’(М16)
 ЗП (М17)
 СЛИА 1(М16)
 ЦИКЛ ПЕРПС(М15)
 УИА 3
 УИА НОРМА(М14)
 ЧТСЕМ
 И БАЙТ+2
 ПО ВЫХРП2
 И Е32
 ПЕ СЛБППД        СЛЕД.БУФ. ПРОДОЛЖЕНИЕ КАДРА
 СЧ Е1            СМ= 1 - ЕСТЬ ЕЩЕ КАДРЫ
ВЫХРП2 НОП ,
 ЗП (М3)
 ПБ ВЫХОСВ
*
********************************************************
*                                                      *
*              З А П И С Ь  В    П  П  Д               *
*                                                      *
* В Х О Д :                                            *
*          СМ =                                        *
*              48-46 РР. = 3                           *
*              17- 7 РР. = КОЛ.ЯЧЕЕК (< ИЛИ = ’2000’)  *
*               5- 1 РР. = НОМЕР ЛИСТА                 *
*                                                      *
* В Ы Х О Д:                                           *
*            КОТ: НОРМА,ОШКОЛ,ЗАНСЕМ,НЕТБУФ,ОШППД;     *
*                                                      *
********************************************************
*
ППДР3 НОП ,
 УИА ОШКОЛ(М14)
 СЧ (М3)
 СДА 64+6
 И П3777
 ЗП (М3)
 ПО ВЫХОСВ        КОЛ-ВО = 0
 СДА 64+7
 УИ М6            М6 = ЧИСЛО БУФЕРОВ
 ВЧОБ Е4
 ПЕ ВЫХОСВ        КОЛ-ВО > ’2000’
 СЧ (М3)
 И П177
 ПО КРАТНО
 СЛИА 1(М6)
КРАТНО НОП ,
 СЧИ М6
 ЗП (М3)
 УИА НЕТБУФ(М14)
 ЧТСЕМ
 И БАЙТ
 ЧЕД
 ВЧ (М3)
 ПЕ ВЫХОСВ        НЕТ БУФЕРВ
 СЧ Е4
 ЗП (М3)          СМ- ЕСТЬ БУФЕР
 УИА 0(М16)        М16= 0 АДРЕС ЧТЕНИЯ
 УИА 0(М5)         М5 := 0 , ПЕРВОЕ СЛОВО СВСППД
СЛБСВС НОП ,
 УИА ОШППД(М14)
 ЧТСЕМ
 И БАЙТ
 ПО ВЫХОСВ        ППД РАБОТАЕТ БЕЗ СЕМАФОРА
 НЕД П77777
 УИ М4            М4 - НОМЕР БУФЕРА
  СДА 64-7
 УИ М17           М17- АДРЕС ЗАПИСИ
 СЛИА ’4000’(М17)
 ЧТСЕМ
 НТЖ Е48(М4)
 ЗПСЕМ ,          ЗАНЯЛИ БУФЕР
 И БАЙТ+1
 ЗП 2(М3)
 СДА 63+32
 ПЕ ЦЕСТЬ         УЖЕ ЕСТЬ ЦЕПОЧКА
 СЧИ М4
 ИЛИ (М3)
 СДА 64-32
 ИЛИ 2(М3)
 ЗПСЕМ
 ПБ НПЕРСП        НАЧАЛО ПЕРЕПИСИ СВС->ППД
ЦЕСТЬ НОП ,
 И П7
 УИ М13
 И Е3
 ПО ПЕРСВС
 СЛИА -4(М13)
 УИА 1(М5)         М5 := 1 , ВТОРОЕ СЛОВО СВСППД
ПЕРСВС НОП ,
 СЧ СВСППД(М5)
 СБР БАЙТ(М13)
 СДА 64+40
 ПЕ ЦЕСТЬ
*ВСТАВЛЯЕМ N БУФЕРА В КОНЕЦ ЦЕПОЧКИ
 СЧИ М4
 ИЛИ (М3)
 СДА 64-40
 РЗБ БАЙТ(М13)
 ИЛИ СВСППД(М5)
 ЗП СВСППД(М5)
*
НПЕРСП НОП ,
 УИА -127(М15)
 УИА 2
ПЕРСП НОП ,
 СЧ ’2000’(М16)
 ЗП (М17)
 СЛИА 1(М16)
 ЦИКЛ ПЕРСП(М15)
 УИА 3
 СЧ Е8
 ИЛИ Е4
 ЗП (М3)          СМ - ЕСТЬ БУФ. И ПРОДОЛЖЕНИЕ КАДРА
 СЛИА -1(М6)
 ПИНО СЛБСВС(М6)    НУЖЕН ЕЩЕ БУФЕР
 УИА НОРМА(М14)
 ПБ ВЫХОСВ
*
********************************************************
*                                                      *
*         З А К О Н Ч И Т Ь  Р А Б О Т У  П П Д        *
*                                                      *
* В Х О Д :                                            *
*          СМ =                                        *
*              48-46 РР. = 4                           *
*                                                      *
********************************************************
ППДР4 НОП ,
 УИА НОРМА(М14)
 СЧ БУФППД
 ПО ВЫХЭК
 ИЛИ Е19
 УИА ВЫХЭК(М15)
 ПБ ДБУФ
*********************************************************
*
* ЗАХВАТ СЕМАФОРА
*
ЗАХСЕМ НОП ,
  УИА НЕТЗАГ(М14)
 СЧ БУФППД
 ПО ПЛОППД         ППД НЕ ЗАГРУЖЕН
 УИА ЗАДЕРЖ(М15)
ЖДУСЕМ НОП ,
 ЧТСЕМ
 И Е17
 ПО (М13)         СВОБОДЕН
 УИ 0
 СЛИА -1(М15)
 УИ 0
 ПИНО ЖДУСЕМ(М15)
 УИА ЗАНСЕМ(М14)
ПЛОППД СЧ Е1
 ПБ ВЫХППД         СЕМ.ЗАНЯТ
*
**************************************************
*
ВЫХОСВ НОП ,
 ЧТСЕМ
 НТЖ Е17           УБРАЛИ СЕМ.БИТ
 ИЛИ Е9            СВС РАБОТАЕТ
 ЗПСЕМ
ВЫХППД НОП ,
 УИА 1027
 СЧ 32(М3)
 НТЖ Е48(М7)
 ЗП 32(М3)
 УИА 3
 ДК ’36’,ВЫХЭК
*********************************************************
*
ПАРОЛЬ КОНД П’ЬЛОРАП’
*********************************************************
*
 ВХОД ЭКСЕТЬ,ЕРАБВТ,ЭКППД
********************************
ДИСП70 ВНЕШ ТСЛ,ТБЛ,ГУС,ТУС,ЗАПВЕТ,ВППС,АВОСТА,П3
ДИСП70 ВНЕШ П60,П7,П77,П77777,ВСЕЕД,П377,П777,П37,П177,П3777
МОТТ ВНЕШ ШЗПРИМ,ШКВЫД,СЛОN,ШКУСТР,ШКВИД,ПИЛОТ,ШНР,ШVD130
ДИСП70 ВНЕШ СЕМППД,СВСППД,ППДСВС,БУФППД,КПППД
ДИСП70 ВНЕШ ПРИП,ДБУФ,БОСОБМ,Е48П25,БАЙТ
ХЛАМ ВНЕШ ПРУ12
ПВВ ВНЕШ ПУСППД
КИТ ВНЕШ Е48П41,КОНСБР,
МОТТ ВНЕШ ЕПРИГ,ГАШТСЛ
МОТТ ВНЕШ ШЗНКТ,ШЗНКТ1,ДЛ,ВН,ТСВ,Н,КОТ,ПОИСК,РАЗРЕК
МОТТ ВНЕШ ШКЗАПР,ЖДУРЕК,ШКОРАБ,ЕЗАКП,КОНВЫД,ШКЭ71
ТЕРМ ВНЕШ ЗС,ШКВТ,ШРАБВТ,НЗТСВТ
РЕЗАРХ ВНЕШ ВЫХ222
КИТ ВНЕШ Е48П42
БОЭК1 ВНЕШ ЗАПВЫХ
СОСТАВ ВНЕШ АТ
********************************
 Е
ЕРАБВТ ЭКВ Е40
 ЭКВИВ (ЕVТ,Е16)
 М
 ФИНИШ ,

*
*
*
*********************************************************
*******                                           *******
*******                Е26                        *******
*******                NЗАД= 27(134)              *******
*******                ТУС = 2151                 *******
*******                                           *******
*********************************************************
*
*
*
НОМБОБ СТАРТ ’10000’
 УПОТР НОМБОБ(М7)
 Б
 М
 Е
 КОНД В’0’
 КОНД М41В’3’ РЖА 3
 КОНД (3)В’0’
 КОНД М15В’2003’
 КОНД (14)В’0’
 КОНД В’0’
 КОНД В’0’
К2 КОНД В’37777777777’
 КОНД (3)В’0’        СВБОДНО
R1 КОНД В’0’
R3 КОНД В’0’
МСК2 КОНД В’1603400’
ТМЛ4 КОНД П’1ПР.МГ’М46В’1’
ПАР КОНД В’0’
 ПАМ 1    ПРИП
 КОНД В’37777777777’ КОПИЯ К2 (26 ЯЧ.ИПЗ =40 ЯЧ.ИПЗ)
 КОНД М47В’1’
ТМЛА КОНД П’1ВКЛМГ’
 ПАМ 1    СВ-НА
 КОНД В’0’ 44-Я ЯЧ.ПОРТИТСЯ П0И ПЕЧ.ТТ(1÷21РР)
ТМЛ1 КОНД П’  0000’
 КОНД П’ - 00,’
ПРИЗ КОНД В’1’
* ДЛЯ ТЕРМИНАЛЬНОГО РАСПИСАНИЯ
 СЧ ФЛАТЕР
 И Е24П1
 ПО НАЧ
 ВЧ ВРЕМЯ
 ПО НАЧ
 СЧ ФЛАТЕР
 И БЛР
 ПЕ НАЧ
 УИА 1027
 СЧ ФЛАТЕР
 ИЛИ Е47
 ЗП ФЛАТЕР
 СЧ ШКИВ
 ИЛИ ЕПУСК
 ЗП ШКИВ
 УИА 3
НАЧ УИА -1(М2)
 СЧ ЕЗАР
 ЗП МИН
 УИА 1-КГРУП(М16)
МИНИ СЧ КМИН
 И Е1(М16)
 ПО ПМИ2 КЛ БЕЗ МИН
 УИА -МЧПЗ+1(М15)
 УИА 1(М14)
 ЗП R1
ПМИН СЧ ГРУП+КГРУП-1(М16)
 И ШКРЗ
 И Е48+МЧПЗ+НПЗ(М15)
 ПО ПМИ1
 МОД ТУСП+МЧПЗ+НПЗ(М15)
 СЧ 33
 СДА 64+24
 ВЧ R1
 ПЕ ПМИ1
 УИИ М14(М15)
 СЛ R1
 ЗП R1
ПМИ1 МОДА ПМИН
 ЦИКЛ (М15)
 СЧ Е48+МЧПЗ+НПЗ(М14)
 ИЛИ МИН
 ЗП МИН
ПМИ2 МОДА МИНИ
 ЦИКЛ (М16)
 УИА (М12)
 ПБ ОБХОДИ
ДЛЯУПР ВЧ Е1
 ЗП БУДУПР
ВСЕУПР НОП
 УИА (М5)
 УИА АВМД(М15)
ВУЗ МОДА (М5)
 СЧ БУР+2-АВМД(М15)
 ПО ВАЗ
 МОДА ВОЗ
 УИА (М16)
 УИИ ’32’(М16)
 МОДА (М5)
 МОДА БУР+2-АВМД(М15)
 УИА (М16)
 ПБ ПЧТ
ВОЗ СЧ 0
 ЗП (М16)
ВАЗ СЧИ М5
 НТЖ П30
 СЛИА 8(М5)
 ПЕ ВУЗ
*
***************************************************
*                                                 *
*               ПЕЧАТЬ ОШИБОК  М Б                *
*                                                 *
***************************************************
*
 УИА 0(М5)
СЛЕДМБ НОП ,
 УИА 1027
 СЧ БУФМБ(М5)
 ПО ПУСТО
*
 СЧ БУФМБ(М5)
 СДА 64+45
 УИ М16
 МОДА ТИПОМБ
 СЧ -1(М16)
 ЗП БПМБ+1
*
 СЧ БУФМБ(М5)
 СДА 64-5
 УИА 1(М15)
 МОДА ШУП           N МБ (1 ЦИФРА)
 ПВ (М14)
 СДА 64-32
 ИЛИ БПМБ+1
 ЗП БПМБ+1
*
 СЧ БУФМБ(М5)
 СДА 64+44
 И Е1
 УИ М16
 МОДА МБЗП
 СЧ (М16)
 ЗП БПМБ+2
*
 СЧ БУФМБ(М5)
 СДА 64-33
 СДА 64+1
 УИА 4(М15)
 МОДА ШУП           АДРЕС ФАЙЛА (4 ЦИФРЫ)
 ПВ (М14)
 СДА 64+16
 ИЛИ =П’ АФ=00’
 ЗП БПМБ+3
*
 СЧ R1
 СДА 64-32
 ИЛИ =П’00 ОП=’
 ЗП БПМБ+4
*
 СЧ БУФМБ(М5)
 СДА 64-13
 РЗБ =П’703077’
 ИЛИ =П’0 0 00’
 ЗП БПМБ+5
*
 СЧ БУФМБ(М5)
 СДА 64-13-11
 РЗБ =П’777000’
 ИЛИ =П’000↑ДР’
 ЗП БПМБ+6
*
 СЧ БУФМБ(М5)
 СДА 64+45
 УИА 0(М14)
 НТЖ Е1
 ПО ДРМБ
 НТЖ Е3
 ПЕ НЕДРУ
 УИА 1(М14)
ДРМБ НОП ,
 СЧ БПМБ+6
 НТЖ =М16В’36’      ПРОБЕЛ ВМЕСТО ↑
 ЗП БПМБ+6
 МОДА КДР
 СЧ (М14)
 ЗП БПМБ+7
 КК ’33’,БУФМБ+1(М5)
  УИА 4(М15)
 МОДА ШУП           СТ. 4 ЦИФРЫ ДР(ДРУ)
 ПВ (М14)
 ИЛИ БПМБ+7
 ЗП БПМБ+7
 КК ’33’,БУФМБ+1(М5)
 СДА 64-16
 УИА 6(М15)
 МОДА ШУП
 ПВ (М14)           6 ЦИФР ДР(ДРУ)
 ЗП БПМБ+8
*
 КК ’33’,БУФМБ+1(М5)
 СДА 64-40
 ЗП R3
 КК ’33’,БУФМБ+1(М5)
 СЧМР
 СДА 64+8
 ИЛИ R3
 УИА 6(М15)
 МОДА ШУП           МЛ. 6 ЦИФР ДР(ДРУ)
 ПВ (М14)
 ЗП БПМБ+9
*
НЕДРУ НОП ,
 СЧ 0
 ЗП БУФМБ(М5)
 МОДА ПУСТО
 УИА (М16)
 УИИ М32(М16)
 МОДА БПМБ
 УИА (М16)
 ПБ ПЧТ
ПУСТО НОП ,
 УИА 3
 СЛИА 2(М5)
 СЧИ М5
 НТЖ Е5
 ПЕ СЛЕДМБ
*
*****************************************************
*
* Г Р У П П А  (ЦУП)
*
 УИА -15(М5)
ЦУП НОП ,
 СЧ ШКЖДУ+15(М5)
 ПО НЕТЦУП
 СЧ ГЛГРУП+15(М5)
 ПО ФОРЦУП
НЕТЦУП НОП ,
 МОДА ЦУП
 ЦИКЛ (М5)
*
 СЧ 0
 ЗП ЧПОД
 ЗП R1
 УИА КУСП-Н(М5)
 МОДА ВНОР
 УИА (М16)
ВЫЗАСЧ СЛИ М5(М7)
 СЧИ М7
 СДА 64-2
 ИЛИ (М5)
 СЛЦ НОММБ1
 ПБ ФИЗОБМ
ВНОР СЧ 0
 ЗП РМЛ
 СЧ ЗАНЯТА
 И Е47
 ПО ВГЕНС
ГОТЕС МОДА КРМЛ
 СЧ 0
 ИЛИ ДЛЯНБ
 ЗП РМЛ
 СЧ 0
 ЗП ДЛЯНБ
 СЧ РМЛ
 ПБ Б-У1(М7)
ОБХОДИ СЧ РАБЕ16
 ПО ВСЕУПР
 СЧ БУДУПР
 ПЕ ДЛЯУПР
 СЧ ШКИВ
 ИЛИ Е16
 ЗП ШКИВ
 ПБ ВСЕУПР
МГЗАН СЧ ТЗНМЛ-24(М3)
 НТЖ R3
 И П7777
 ПЕ МГЗЫ
 СЧ ТЗНМЛ-24(М3)
 И О40
 ЗП ТЗНМЛ-24(М3)
 ПБ МЛ2А
МЛ6 СЧ ТМЛ+1
 СЛЦ Е3
 ЗП ТМЛ+1 КОНЕЦ ТЕКСТА
 ПБ ВТ
МЛ2АБ УИА 3
 СЧ ПРИЗ
 ПО ВМЛ1-У1(М7)
 ПБ МЛ2А
ВМЛ2 УИИ М10(М4)
 СЛИА ’67’(М10)
 СЧИ М10
 НТЖ П7
 УИ М10
 УИИ М3(М12)
 СЛИ М3(М10)
 УИА ’2003’
 СЧ ТЗНМЛ-24(М3)
 ПО ВМЛ1-У1(М7)
 И ПРСВЛ
 ПО МДПМ1
 СЧ ТЗНМЛ-24(М3)
 ИЛИ Е40
 ЗП ТЗНМЛ-24(М3)
 СЧ ТМЛА
 ЗП ТМЛ-1
ВТЕК СЛИА -24(М10)
 МОДА МЛ6
 УИА (М11)
 ПБ МЛ3Е
МДПМ1 ЗП ТЗНМЛ-24(М3) ПОЧИСТ ЯЧ
 СЧ Е1
 ЗП ПАР
 ПБ ВМЛ1-У1(М7)
МЛЕ НОП ,
 СЧ КФКУС
 ЗП ТМЛ
 УИИ М4(М3)
 СЧИ М4
 СДА 64-42
 РЗБ НАПРМГ
 УИА -3(М11)
 ИЛИ ТМЛ
 ЗП ТМЛ
ОПРОС СЧ ТМЛ
 СЛЦ Е26Е25
 ЗП ТМЛ
 ПВ ФИЗОБМ(М16)
 ЗП R3
 НТЖ ВСЕЕД
 ПО ВТЕ
МЛ2Д МОДА МЛ2В
 ПИО (М16)        ОПОЗНАНИЕ СОСТОЯЛОСЬ
 СЧИ М16
 НТЖ Е5
 ПО МЛ2АБ        ФИКТИВНАЯ РАБОТА НОМБОБА
 СЧ ТЗНМЛ-24(М3)
 И ПРСВЛ
 ПЕ МЛ2В         МГ ЗАНЯТ
 СЧ Е13
 ИЛИ ТЗНМЛ-24(М3) УСТАНОВКА ПРИЗНАКА ЛЕНТЫ ЕС
 ЗП ТЗНМЛ-24(М3)
 СЧИ М3
 И П7
 НТЖ П7
 УИ М1
 СЧИ М3
 СДА 64+3
 УИ М16
 СЧ МГЗ-3(М16)
 ИЛИ Е8(М1)
 ЗП МГЗ-3(М16)
 УИА 7(М1)
 СЧИ М4
 СДА 64-42
 РЗБ ДВАС
 МОДА ТТЕС
 УИА (М16)
 ИЛИ -1(М16)
 ЗП (М16)
 МОДА МЛ2АБ
 УИА (М15)
 УИИ ’32’(М15)
 СЧ Е23
 ПБ ТВ40
МЛ1ЕС СЧ РЗМГЕС
 И Е48-8(М13)
 ПО МЛЕ          МГ НЕ ДЛЯ РАЗМЕТКИ ЕС
 УИА ’2003’
 СЧ РЗМГЕС
 СДА 64+32
 ИЛИ Е13
 ЗП R3
 И П7777
 ВЧОБ NБЛ
 ПО ЛЕ
 СЧ R3
 ИЛИ ЕЛЕН
 ЗП R3
ЛЕ СЧ ТЗНМЛ-24(М3)
 ПЕ МГЗАН        МГ ЗАНЯТ
 СЧ ШРЕС
 НЕД 0
 УИ М1          НОМЕР ЗАДАЧИ РАЗМЕТКИ
 СЧИ М3
 ЗП ПРОМ
 СЧ R3
 И Е12П1
 СДА 64-6
 ИЛИ ПРОМ
 МОД ТУСП-1(М1)
 ЗП 0            НА СМ РАЗМЕТКИ
 МОД ТУСП-1(М1)
 СЧ ’25’         СОДЕРЖИМОЕ М1
 СДА 64+3
 И П3
 СДА 64-19
 ИЛИ R3
 ЗП R3
 УИИ М13(М3)
 УИА -2(М16)
 ПБ РАЗМ-У1(М7)
МЛ2В СЧ R3
 ПЕ МЛ2
ПОВОПР МОДА ОПРОС
 ЦИКЛ (М11) ОПРОСИТЬ 10 РАЗ
ВТЕ СЧИ М4
 СДА 64-42
 МОДА ФОРМТ
 ПВ (М11)
 СЛЦ Е3
 ЗП ТМЛ
ВТ МОДА МЛ2АБ
 УИА (М15)
 УИА ТМЛ-1-НОМБОБ(М16)
 ПБ МЛТТ
МЛ2 И Е12
 ПО МЛ2Б
 СЧ R3
 ВЧОБ NБЛ
 ПО ПОВОПР
 СЧ R3
 ИЛИ ЕЛЕН
 ЗП R3
МЛ2Б СЧ ТЗНМЛ-24(М3)
 УИИ М11(М2)
 СЛИ М11(М7)
 И Е47
 ПО МДПО
 СЧ НМД-Н(М11)
 И Е48-24(М10)
 ПЕ МДПО
 СЧ Е48-24(М10)
 ИЛИ ПР-Н(М11)
 ЗП ПР-Н(М11)
МДПО СЧ ТЗНМЛ-24(М3)
 И ПРСВЛ
 ПЕ МГЗАД
 СЧ R3
 ИЛИ ЕН
 ИЛИ Е47
 ЗП ТЗНМЛ-24(М3)
 УИА 1027
 СЧ АЗУ
 СЛЦ Е1
 ЗП АЗУ ПОДСЧЕТ ЧИСЛА УСТАНОВ.БОБИН
 ЗП ПАР
 СЧ Е48-24(М10)
 ИЛИ НОВМЛ
 ЗП НОВМЛ
 ПБ ВЫВОД
МГЗАД СЧ ТЗНМЛ-24(М3)
 НТЖ R3
 И П7777
 ПО СВП
 МОДА МЛ
 ПИО (М12)
МГЗЫ СЧ Е40
 ИЛИ ТЗНМЛ-24(М3)
 ЗП ТЗНМЛ-24(М3)
 СЧ Е48-24(М10)
 ИЛИ НОВМЛ
 ЗП НОВМЛ
 СЧ R3
 ИЛИ Е47
 МОДА НТЗНМЛ-24-Н(М7)
 ЗП (М3)
 УИИ М11(М2)
 СЛИ М11(М7)
 СЧ Е48-24(М10)
 ИЛИ ДР-Н(М11)
 ЗП ДР-Н(М11)
 СЧ ПР-Н(М11)
 ИЛИ Е48-24(М10)
 НТЖ Е48-24(М10)
 ЗП ПР-Н(М11)
 ПБ ВТЕК
МЛ СЧ ТЗНМЛ-24(М3)
 И Е39
 ПО МГЗЫ
 ПБ ВТЕК
СВП СЧ ПЕРЕ2
 ИЛИ Е48-24(М3)
 НТЖ Е48-24(М3)
 ЗП ПЕРЕ2
СОВП СЧ ТЗНМЛ-24(М3)
 И О40
 ИЛИ Е47
 ЗП ТЗНМЛ-24(М3)
ВЫВОД СЧ Е48-24(М10)
 ИЛИ R1
 ЗП R1 НОВЫЙ МГ (48Р.R1-3 НАПР.0 МГ)
МЛ2А НОП 0
 СЧ РМД(М2)
 НТЖ РМД1(М2)
 И К2
 ПЕ МЛ1А-У1(М7)
 УИА ’2003’
 СЧ Е6
 ИЛИ ШКИВ
 ЗП ШКИВ
 СЧ R1
 ПО ДАР
 НЕД П77777
 УИ М10
 МОДА ТМЛ-1
 УИА (М17)
 МОДА МЛ4
 УИА (М11)
МЛ3Е СЧ ТЗНМЛ(М10)
 И П7777
 ПВ ПЕРЕВ(М15) 8-10
 ИЛИ ТМЛ1
 ЗП 1(М17)
 УИИ М16(М10)
 СЛИ М16(М12)
 СЛИА 24(М16)
 СЧИ М16
 СДА 64-42
ФОРМТ РЗБ МСК2
 ИЛИ ТМЛ1+1
 ЗП 2(М17)
 ПБ (М11)
МЛ4 СЧ Е3
 СЛЦ 2(М17)
 ЗП 2(М17) ПРИФОРМ.КОНЕЦ
 СЧ ТМЛ
 И Е41П1
 ЗП ТМЛ
 МОДА ДАР
 УИА (М15) А ВОЗВР.
 УИА ТМЛ-НОМБОБ(М16)
МЛТТ СЛИ М16(М7)
 УИИ ’32’(М15)
ПЧТ СЧ Е23
 ПБ ТВ40
ПОИС6 СЧ ЗАНЯТА
 И Е47
 ПО ПОИСК
 ПБ ВЫХ
ЗВ2 ПАМ 0
*************************************************
*   ПРОВЕРКА  ЗАВИСАНИЯ       М     П    Д      *
*************************************************
 СЧ ВРЕМПД
 ПО НЕЗАВИ С
 РЕГ ’256’
 ВЧ ВРЕМПД
 ВЧ ДЕЛЬТА
 ПЕ НЕЗАВИ
 УИА 1027
 СЧ ШКРАБН
 ИЛИ ЕЗАВИС
 ЗП ШКРАБН        РАБОТА ПО ЗАВИСАНИЮ  М П Д
 СЧ ШКОРАБ
 ИЛИ ЕХОЛ
 ЗП ШКОРАБ        РАБОТА: ВЫЗОВ НЕРОТ-А
 УИА 3
*************************************************
НЕЗАВИ НОП
 УИА 3
 УИА -47(М15)
ЗВ1 СЧ ВППС+47(М15)
 УИА 1027
 ПО ЗВ1А    НЕТ БУД
 РЖА 19
 ПЕ ЗВ1А    ОБРЫВ
 СЛЦ Е46
 ЗП ВППС+47(М15)
 ПЕ ЗВ1А
 СЧ ШКОЛА    БУД
 И Е1(М15)
 ПЕ АС762    ПРИН.ПЕРЕКЛ
 СЧ ШЗПРИМ
 ИЛИ Е1(М15)
 ЗП ШЗПРИМ
 СЧ ШБУДИЛ
 ИЛИ Е1(М15)
 ЗП ШБУДИЛ
 СЧ ШКОРАБ
 ИЛИ ЕЗВ
 ЗП ШКОРАБ   БУД
ЗВ1А УИА 3
 МОДА ЗВ1
 ЦИКЛ (М15)
ОПРОС1 СЧ ЗАКАЗ
 УИА 1027
 ПЕ ВЫХОД
 СЧ ШЗЗОБМ
 ПЕ ВЫХОД
 СЧ Е40
 ЗП ШЗЗОБМ
 УИА ФИЗО60(М15)
 УИА ИПЗМЛ(М16)
 СЧ Е42
 ИЛИ Е5
 ЗП (М16)
 СЧИ М15
 ЗП 3(М16)
 СЧ 0
 ЗП 2(М16)
 СЧ ШГ
 ИЛИ Е39
 ЗП ШГ
ВЫХОД УИА 3
 ПВ ЗАНОСМ(М16)
 СЧ ШРНОМБ
 НТЖ Е48
 ПЕ ВЫ9          НЕРСТ НЕ ТРЕБУЕТСЯ
 ЗП ШРНОМБ
 ПБ СТАТ1
ВЫ9 СЧ ВРЕМЯ
 ВЧ БУДСБ
 ВЧАБ ВР5М
 ПЕ ВЫХНБ
 МОДА СТАТ3С-СТ3(М7)
 УИА (М16)
 УИА КУССТ3-Н(М5)
 ПБ ВЫЗАСЧ
ВЫХНБ СЧ Е26
 ПБ ОТКАЗН
*
АС762 НТЖ ШКОЛА
 ЗП ШКОЛА
 СЧ ШКВЫД
 ИЛИ Е1(М15)
 ЗП ШКВЫД
 СЧ 0
 ЗП ВППС+47(М15)
 ПБ ЗВ1А
*
 КОНД В’0’ ПЕРЕД ТМЛ
ТМЛ КОНД П’  0000’
 КОНД П’ - 00,’
 КОНД В’0’
ИНФ1 КОНД В’0’
ПРЗП КОНД В’0’ ПР ЗП НА МД ДЛЯ МЛ ТЗНМЛ
НОВМЛ ПАМ 1
О40 КОНД В’-1’М39В’1’
*
*
ДАР НОП
 СЧ ПАР
 ПО ББ1-У1(М7)
 СЧ 0
 ЗП ПАР
 СЧ СОБА40
 ПВ ЗАПВЕТ(М16)
 СЧ ШАРСИС
 ИЛИ Е26
 ЗП ШАРСИС
 ПБ ББ1-У1(М7)
ЕСТЬБЭ УИИ М16(М4)
 УИИ М4(М12)
 УИИ М12(М16)
ЕСТЬБ СЧИ М4
 СЛЦ П30
 ЗП Р2 СТАРЫЙ МГ
 СДА 64+3
 УИ М16 НАПР
 СЧ 0
 ЗП ПРБ
 СЧ Р2
 СДА 64-42
 РЗБ НАПРМГ
 ЗП Р5 СТАРЫЙ
 СЧИ М12
 СЛЦ П30
 ЗП Р3 НОВЫЙ
 СДА 64-42
 РЗБ НАПРМГ
 ЗП Р4
************************:
 СЧ ’7776’
 УИА 1027
 СЧ Р2
 ПВ ОСВМЛ(М15)
 СЧ Р3
 ПВ ЗАНМЛ(М15)
ПВВ ВНЕШ ОСВМЛ,ЗАНМЛ
*********************:
 СЧ Р2
 И П7
 НТЖ П7
 УИ М15
 СЧ 0
 ЗП НОВД+1
 ЗП ТЗОНМЛ(М4)
 ПВ ДЛЯДИС(М10)
 СЛИ М11(М3)
 ПВ ОЧ3И6(М10)
 СЧ НОВД
 УИ М10
 СЛИ М10(М3)
 УИА 1027
 СЧ К
 СДА 64(М15)
 НТЖ ВСЕЕД
 И СОЛ-3(М16)
 ЗП СОЛ-3(М16)
СМЛ СЧ ШОЧМГ(М4)
 ЗП Р-1
 ЗП Р7
ЗАММЛ СЧ Р7
 ПО ПЕРЕЛ2
 НЕД 0
 УИ М1
 СЧ Е32(М12)
 ИЛИ СМЕМЛ
 ЗП СМЕМЛ
 СЧ Е48-1(М1)
 НТЖ Р7
 ЗП Р7
 СЧ НЕТНАП
 НТЖ Е23
 И ОЧКУС-1(М1)
 ИЛИ Р4
 ЗП ОЧКУС-1(М1)
 СЧ ВСЕЕД
 НТЖ Е48-1(М1)
 И ЗОЧ2-5(М2)
 ЗП ЗОЧ2-5(М2)
 ПБ ЗАММЛ
ПЕРЕЛ2 СЧ ШОЧНАП-3(М2)
 ИЛИ Р-1
 НТЖ Р-1
 ЗП ШОЧНАП-3(М2)
 СЧ ШКЗЗП-5(М2)
 ИЛИ Р-1
 НТЖ Р-1
 ЗП ШКЗЗП-5(М2)
 СЧ ПЕРЕ2
 ИЛИ Е48(М12)
 НТЖ Е48(М12)
 ИЛИ Е48(М4)
 НТЖ Е48(М4)
 ЗП ПЕРЕ2
ПРЛ2 МОДА НОВИЛ
 ПИНО (М13)
 СЧ ШОЧМГ(М12)
 ЗП ШОЧМГ(М4)
 СЧ Р-1
 ЗП ШОЧМГ(М12)
 СЧ КВТ
 И ТЗНМЛ(М4)
 ЗП Р
 СЧИ М14
 ПО СММЛ
 СЧ КВТ
 И ТЗНМЛ(М12)
СММЛ ЗП ТЗНМЛ(М4)
 СЧ Р
 ЗП ТЗНМЛ(М12)
 ПБ СМЛ1
НОВИЛ УИА 1(М14) 2-Й РАЗ
 СЧ НМЛ
 НТЖ Е48(М4)
 ЗП НМЛ
 УИА (М13)
 СЧ ПРБ
 ЗП ДР
 ПБ ЕСТЬБЭ
ПЕРЕМЛ МОДА (М4)
 СЧ НТЗНМЛ
 НТЖ ТЗНМЛ(М12)
 И П7777
 ПО ЕСТЬБ
ПЕМ СЧИ М12
 НТЖ П7
 УИ М2
 СЧ РМЛ1
 ИЛИ Е32(М2)
 ЗП РМЛ1
НВМЛ1 СЧ НМЛ
 НТЖ Е48(М12)
 ЗП НМЛ
 ПБ ПМЛ1-У1(М7)
СМЛ1 НОП ,
 УИА -46(М1)
 УИА 3
ПОМ НОП ,
 СЧ ШКРЗ
 И Е48+46(М1)
 ПО НЕТЗАД
 МОД ТУСП+46(М1)
 УИА (М11)        М11 := АДРЕС ИПЗ
 УИА 45(М16)
 УИА -3(М2)
ПО1 УИА -2(М4)
 СЛИА -3(М16)
ПО1А УИА -1(М17)
 УИИ М15(М7)
 УИА 1027
ПО МОДА  (М11)
 СЧ ОБЩТОМ+3(М2)
 СД КТОМ+2(М4)
 МОДА 1(М17)
 НТЖ Р2
 И П77
ПО3 ПО ЭТОТ
 СЛИА -1(М15)
 МОДА ПО
 ЦИКЛ (М17)
ПО2 СЛИА -3(М16)
 МОДА ПО1А
 ЦИКЛ (М4)
 МОДА ПО1
 ЦИКЛ (М2)
НЕТЗАД НОП ,
 МОДА ПОМ
 ЦИКЛ (М1)
 УИА 3
 СЧ ПРИ
 ПО ПО4
ОБЩСТО СТОП ’1150’     ОБЩИХ ЛЕНТ БЫТЬ НЕ ДОЛЖНО
*ПБ ЗК2В*
ПО4 СЧ И10
 УИА 3
 УИ М12
 СЧ 0
 ЗП R1-Н(М7)
 ПБ НВМЛ1
ЭТЛЕ СЧ УКАЗД(М11)
 И Е32(М13)
 ПЕ ПО3 МД
 СЧ Р3-Н(М15)
 СД КТОМЛ+2(М4)
 ЗП Б1+7
ЭТЛ СЧ П77
 СД КТОМЛ+2(М4)
 НТЖ ВСЕЕД
 МОДА (М11)
 И ОБЩТОМ+3(М2)
 ИЛИ Б1+7
 МОДА (М11)
 ЗП ОБЩТОМ+3(М2)
 ПБ ПО2
НВМЛ4 ПАМ
 СЧ СМЕМЛ
 ИЛИ ВЫМГ
 ПО ЗАНИ
 УИА 1027
 СЧ ШЗЗОБМ
 ПЕ ЗАН1
 СЧ Е39
 ПВ ВШГ(М15)
 УИА ВХСМЕ(М16)
 СЧИ М16
 ЗП ИПЗМЛ+3
 СЧ 0
ЗАН1 ИЛИ Е1
 ЗП ШЗЗОБМ
ЗАНИ УИА 3
 ПБ ПОИС6-Н(М7)
ЭТОТ СЧИ М2
 СЛЦ П3
 СДА 64-2
 ЗП Р
 СЧИ М4
 СЛЦ П3
 ИЛИ Р
 ЗП Р НОМ СТР,СТОЛБЦА ОБЩТОМ
 УИА -15(М13)
ЭТТ УИА 4(М10)
ЭТД МОДА (М11)
 СЧ ШЗНМБ+15(М13)
 СДА 64+8(М10)
 НТЖ Р
 И П17
 ПО ЭТДИ
 МОДА ЭТД1
 ПИО (М10)
 УИА (М10)
 ПБ ЭТД
ЭТД1 МОДА ЭТТ
 ЦИКЛ (М13)
ЭТДИ СЛИА 15(М13)
 СЛИ М13(М13)
 МОДА ЭТЛЕ
 ПИО (М10)
 СЛИА 1(М13)
 ПБ ЭТЛЕ
ВЫХ СЧ ВРЕЗП
 СЛЦ Е43
 УИА 1027
 ПЕ СТАТ
СТАТ1 НОП ,
 СЧ ЗАНЯТА
 И Е7
 ПО В             НЕ НАБРАНА ДИРЕКТИВА ’ВРЕ’
 МОДА НАЧСТ-УН(М7)
 УИА (М16)
 СЧ ’5000’
 УИА КУСПН-Н(М5)
 ПБ ВЫЗАСЧ       ВЫЗОВ И ПЕРЕХОД В СТАТ1
КНЦСТ МОДА СТАСУТ-УНТ(М7)
 УИА (М16)
 УИА КУСНЕР-Н(М5)
 ПБ ВЫЗАСЧ       ПЕРЕХОД В СТАТ2
КНЦСТТ СЧ 0
СТАТ ЗП ВРЕЗП
В СЧ ВРЕМЯ
 ВЧАБ БУДСМЕ
 ВЧАБ КОН
 ПЕ ВОЗ1
 СЧ ШКИВ
 ИЛИ Е25
 ЗП ШКИВ
 СЧ Е3
 ЗП ТУМБ6
 СЧ ВРЕМЯ
 ЗП БУДСМЕ
ВОЗ1 СЧ 0
 ЗП ПРЗП
 ПБ ЗВ2
*
ФОРЦУП НОП
 УИИ М15(М5)
 СЛИА 16(М15)
 СЧИ М15
 СДА 64-24
 СЛЦ ПАСЦУП
 ЗП ЗАДЦУП+1
 МОДА ЗАДЦУП
 УИА (М16)
 СЧИ М16
 Э50 ’7701’
 ПБ НЕТЦУП
*
ПАСЦУП КОНД П’ЗГ0,Е ’М16П’F’
ЗАДЦУП ТЕКСТ П’ШИФР  419907061989’
 КОНД В’0’
 КОНД П’ЕКОНЕЦ’
**************************************
ШУП НОП ,
 ЗП R3
 СЧ 0
ШУПС НОП ,
 ЗП R1
 СЧ R3
 СДА 64+46
 УИ М16
 СЧ R3
 СДА 64+44
 И П3
 СДА 64-3          * НА 8
 УИ М17
 МОДА ШЦИФРЫ
 СЧ (М16)
 СДА 64+16(М17)
 И П377
 ИЛИ R1
 ЗП R1
 СЛИА -1(М15)
 МОДА (М14)
 ПИО (М15)         ВЫХОД := СМ - КОД В УПП
 СЧ R3
 СДА 64-4
 ЗП R3
 СЧ R1
 СДА 64-8
 ПБ ШУПС
*
ШЦИФРЫ КОНД М16П’3210’
       КОНД М16П’7654’
       КОНД М16П’ВА98’
       КОНД М16П’FЕDС’
БПМБ   КОНД П’2ОШ МБ’
 ПАМ 9
      КОНД М40П’↑’       НЕ РАЗДЕЛЯТЬ С БПМБ
ТИПОМБ КОНД П’=0 ДР ’
       КОНД П’=0 КЧ ’
       КОНД П’=0 КС ’
       КОНД П’=0 СС ’
       КОНД П’=0 ДРУ’
МБЗП КОНД П’ ЗП.00’М8В’143’В’143’
     КОНД П’ ЧТ.00’М8В’143’В’143’
КДР КОНД П’ =0000’
    КОНД П’У=0000’
 ЛИТЕР
*
НТЗНМЛ КОНД (32)В’0’ НЕ РАЗД С НТЗНМД
НМЛ КОНД В’0’ НЕ РАЗД С НМД
НМД КОНД (2)В’0’
КФКУС КОНД М20В’1’М17В’1’М4В’1’
КОН КОНД В’257620’
ПРЛ КОНД В’0’ НЕ РАЗД С ПР
ПР КОНД (2)В’0’
ДРЛ КОНД В’0’ НЕ РАЗД С ДР
ДР КОНД (2)В’0’ ВЫКЛ МД,ПРИНАДЛ ЗАД
ПРОМ ПАМ 1
И10 КОНД В’0’
ПРБ КОНД В’0’
КВТ КОНД М30В’777777’В’7717777’М39В’1’
ПРИ КОНД В’0’
НОВД КОНД (4)В’0’
КУСП КОНД М17В’1’М10В’3’М2В’21’В’0’    СМЕНА1
КУСПН КОНД М17В’1’М10В’3’М2В’21’В’1’    СТАТ1
КУСНЕР КОНД М17В’1’М10В’3’М2В’21’В’2’    СТАТ2
КУССТ3 КОНД М17В’1’М10В’3’М2В’21’В’3’    СТАТ3С
ВР5М КОНД В’35230’    5 МИН.
ВЫЗОС ПАМ 1
БУДСБ ПАМ 1
КУССТ ПАМ 1
Б1 ПАМ 1
Р ПАМ 1
Р1 ПАМ 1
Р2 ПАМ 1
Р3 ПАМ 1
Р4 ПАМ 1
Р5 ПАМ 1
Р7 ПАМ 1
 ПАМ 3
ТЕКНТР ТЕКСТ П’1ВЫКЛ КОНТ-Р’
 ПАМ 1
ДВАС КОНД П’000077’
 КОНД П’1МГ-00’
ТТЕС КОНД П’1МГ-00’
 КОНД П’ ЛЕНТА’
 КОНД П’ ЕС.ДА’
 КОНД П’Й НОМЕ’
 КОНД П’Р↑0000’
*
 ЭКВИВ (Н,НОМБОБ),(ДЕЛЬТА,Е19)
*
АВМД ВНЕШ АВМД,БУР
БОЭК1 ВНЕШ ВХСМЕ
ВЗУ ВНЕШ ФИЗОБМ,ФИЗО60
ВИСП ВНЕШ ВШГ
ГЕНС ВНЕШ ПОИСК,(ВГЕНС,А)
ДИСКИ ВНЕШ КТОМ,КТОМЛ
ДИСП70 ВНЕШ ТВ40,Е26Е25,ЧПОД,АЗУ,Е24П1,ГРУП,ОТКАЗН
ДИСП70 ВНЕШ КГРУП,КМИН,ВППС,ЗАНОСМ,Е41П1,РАБЕ16,БУДУПР
ДИСП70 ВНЕШ ШЗЗОБМ,РОЙ,ПЕРЕВ,ВЫМГ,ИПЗМЛ,ТУСП,НПЗ,МЧПЗ,МИН
ДИСП70 ВНЕШ ЕЛЕН,NБЛ,ПРСВЛ,ПР14,НОММБ1,БУДСМЕ,ЗАНЯТА,ТУМБ6
ДИСП70 ВНЕШ П7777,П77777,П77,П7,ТБУФ,РМД,РМД1,ТЗНМЛ,ВРЕМЯ
ДИСП70 ВНЕШ ВРЕЗП,ВСЕЕД,ШКИВ,НАПРМГ,ОБЩТОМ,УКАЗД,К,П30
ДИСП70 ВНЕШ РМЛ,РМЛ1,П17,П3,СОЛ,ТЗОНМЛ,ШОЧМГ,ОЧКУС,ЗОЧ2
ДИСП70 ВНЕШ ШОЧНАП,ШКЗЗП,ЕН,ЗАПВЕТ,ШАРСИС,СОБА40,НЕТНАП,ШРНОМБ
ДИСП70 ВНЕШ ШГ,(Е12П1,П7777),ШРЕС,ДЛЯНБ,МГЗ
ДИСП70 ВНЕШ ЕЗАР,БУФМБ,П377
ДМЛМБ ВНЕШ ДЛЯДИС,ОЧ3И6
ЕСМЛ ВНЕШ КРМЛ
КАЧКА ВНЕШ ШКРЗ,Е48П17
КИТ ВНЕШ ЗАКАЗ,РЗМГЕС,ПЕРЕ2,СМЕМЛ
МОТТ ВНЕШ ШКОЛА,ШКОРАБ,ШЗПРИМ,ШКВЫД,ФЛАТЕР,ШКРАБН,ЕЗАВИС
МОТТ ВНЕШ ЕХОЛ,ЕЗВ,ВРЕМПД,ШБУДИЛ
СТАТ1С ВНЕШ УН,НАЧСТ
СТАТ2С ВНЕШ УНТ,СТАСУТ
СТАТ3С ВНЕШ СТАТ3С,СТ3
СКОР1 ВНЕШ БЛР
СМЕНА1 ВНЕШ ВМЛ1,У1,ББ1,ПМЛ1,МЛ1А,Б,РАЗМ
СОСТАВ ВНЕШ ШЗНМБ,ЕПУСК
ГРУППА ВНЕШ ШКЖДУ,ГЛГРУП
*
 ВХОД ВМЛ2,ПЕМ,КНЦСТ,КНЦСТТ,КУССТ,НТЗНМЛ,ДАР
 ВХОД МЛ1ЕС,ПРЛ,ДРЛ,ЕСТЬБ,ПРЗП,ПРИЗ,НОВД
 ВХОД НВМЛ4,МСК2,И10,ВЫВОД,ТЕКНТР,ВЫХ
 ВХОД R3,ОБХОДИ,ВЫХОД,ТМЛ,ТМЛ4,К2,ПЧТ,НВМЛ1
 ВХОД ОБЩСТО,Р1,МГЗАН,НМЛ,ПЕРЕМЛ,НОВМЛ,ДР
 ВХОД БУДСБ,МЛ2В,МЛ2АБ,R1,ВР5М,ВЫХНБ,ВЫЗОС
 ВХОД В
* МЕТКА ’КЕСМЛ’ НУЖНА В НПЛАН2 ДЛЯ ОБЩИХ ЛЕНТ,НА СВС
*     ЕЕ ЗНАЧЕНИЕ   Н Е  С У Щ Е С Т В Е Н О.
 ЭКВИВ (КЕСМЛ,0)
 ВХОД КЕСМЛ
*
*
 ПАМ 256        НЕРЕЗИДЕНТЫ
*КОНЕЦ ЧАСТИ

 МАСRО
∧М БЛПР
∧М УИА ’2003’
 МЕND
**********          **********
 МАСRО
∧М ОТПР
∧М УИА 3
 МЕND
**********          **********
 МАСRО
∧М ВХ
∧М НОП
 ВХОДН ∧М
 МЕND
**********          **********
 МАСRО
∧М КОНДВ ∧А
∧М КОНД ∧А
 ВХОДН ∧М
 МЕND
**********          **********
 МАСRО
∧М ПАМВ ∧А
∧М ПАМ ∧А
 ВХОДН ∧М
 МЕND
**********          **********
ПЛАН СТАРТ ’10000’
* ИСПОЛЬЗУЮТСЯ МАКРОПЕРЕМЕННЫЕ: ВS
 Б
 Е
 М
Б ЭКВ ПЛАН
М ЭКВ М7
 УПОТР ПЛАН(М7)
* ЯЧЕЙКА 00 (00)          СУММАТОР
 ПАМ 1
* ЯЧЕЙКА 01 (01)          РК
 КОНД М41В’7’
* ЯЧЕЙКИ 02-25 (02-21)    РЕГИСТРЫ
 ПАМ 20
* ЯЧЕЙКА 26 (22)          В 48-17Р ДОЛЖНА БЫТЬ = 40(32)
 КОНД В’0’
* ЯЧЕЙКА 27 (23)          ПРИЗНАК КОМАНДНОЙ СВЕРТКИ
 КОНД М47В’1’
**ЯЧЕЙКИ*30-37*(24-31)****П*Р*И*П*И*С*К*А**************
* ЯЧЕЙКА 30 (24)
 ПАМ 1
* ЯЧЕЙКА 31 (25) [ПОЧТА]  НОМЕР ЗАДАЧИ ДЛЯ Э64
 ПАМ 1
* ЯЧЕЙКИ 32-35 (26-29)
 ПАМ 4
* ЯЧЕЙКИ 36-37 (30-31)    ПРИПИСКА ДЛЯ Э64
 КОНД В’714244’
ПРИП2Л ПАМ 1
* ЯЧЕЙКА 40 (32)          В 48-17Р ДОЛЖНА БЫТЬ = 26(22)
*                         В 16- 1Р - ЧИСЛО СТРОК ДЛЯ Э64
 КОНД В’100000’
* ЯЧЕЙКА 41 (33)          ВРЕМЯ
 КОНД М47В’1’
* ЯЧЕЙКИ 42-43 (34-35)
ЗОНАОЛ КОНДВ М24В’7770’М17В’1’
ЗОНПАР КОНДВ М24В’0600’М17В’1’
* ЯЧЕЙКА 44 (36)          ИСПОЛЬЗУЕТСЯ В ТВ40
 ПАМ 1
* ЯЧЕЙКИ 45-47 (37-39)
ШКАНМК КОНДВ М1В’177777’
 КОНД М17В’177777’
 КОНД М33В’77777’
* ЯЧЕЙКА 50 (40)          НАЧАЛО НЕРЕЗИДЕНТА
 СЧ 0
 ПВ ТБУФ(М15)     Л И С Т   П Л А Н 2   И   5 4 5
 ОТПР
 СДА 64-8
 УИ М1
 СДА 64-2
 ИЛИ НОММБ1
 ИЛИ КУСПЛ2-Б(М)
 ЗП РУСПЛ2-Б(М)
 СЧИ М1
 СДА 64-2
 ИЛИ КУС545
 СЛЦ НОММЛ1
 ЗП РУС545-Б(М)
*
********** ОБРАБОТКА ОТКАЗОВ ОТ МД **********
 БЛПР
 СЧ ПРЕДЕЛ
 И ЕОТКД
 ПО НОТК
 НТЖ ПРЕДЕЛ
 ЗП ПРЕДЕЛ
 ОТПР
 УИА 0(М4)
ОТКАЗД СЧ ТЗНМД(М4)
 И ЕОТМД
 ПО НУС3          ОТ ЭТОГО НЕ ОТКАЗЫВАЛИСЬ
 УИА -МЧПЗ(М12)
ПУС2 СЧ ШКРЗ
 И Е48+МЧПЗ(М12)
 ПО НУС2
 МОД ТУСП+МЧПЗ(М12)
 УИА (М11)
 УИА -15(М16)
ПУС1 УИА 4(М5)
ПУС МОДА (М11)
 СЧ ШЗНМБ+15(М16)
 СДА 64+8(М5)
 И П17
 ПО НУС
 ЗП РАЯ-Б(М)
 УИИ М6(М16)
 СЛИА 15(М6)
 СЛИ М6(М6)
 МОДА ОТКМД
 ПИО (М5)
 СЛИА 1(М6)
ОТКМД СЧ УКАЗД(М11)
 И Е32(М6)
 ПО НУС
 СЧ МДР(М11)
 И Е32(М6)
 ПЕ НУС
 СЧ РАЯ-Б(М)
 И П3
 УИ М17
 СЧ РАЯ-Б(М)
 СДА 64+2
 УИ М15
 МОДА (М11)
 СЧ ОБЩТОМ(М15)
 СД КТОМ-1(М17)
 И П77
 ЗП РАЯ-Б(М)
 СЧИ М4
 НТЖ РАЯ-Б(М)
 ПЕ НУС
 БЛПР
 СЧ ЕОТМД
 И ТЗНМД(М4)
 НТЖ ТЗНМД(М4)
 ПБ НУС3А
НУС МОДА НУС1
 ПИО (М5)
 УИА 0(М5)
 ПБ ПУС
НУС1 МОДА ПУС1
 ЦИКЛ (М16)
НУС2 МОДА ПУС2
 ЦИКЛ (М12)
 СЧ ТЗНМД(М4)
 И ЕОТМД
 БЛПР
 ПО НУС3
 НТЖ ТЗНМД(М4)
 И КС-Б(М)
 ЗП ТЗНМД(М4)
 И Е40
 ПО НУС3
 СЧ ТЗНМД(М4)
 И Е48П42
 ПЕ НУС3
НУС3А ЗП ТЗНМД(М4)
 ОТПР
НУС3 СЛИА 1(М4)
 ОТПР
 СЧИ М4
 И Е7
 ПО ОТКАЗД
НОТК НОП
*
********** ОБРАБОТКА ЗОНЫ 545 **********
 СЧ ЗАНЯТА
 И Е7
 ПО ПОКАРА        К РАБ.НЕ ГТ
 СЧ МГРП
 И ЕСТОП
 ПЕ ПОКАРА        СТОП
 СЧ 0
 ПВ ТБУФ(М15)     Л И С Т   Д Л Я    Б . К .
 ОТПР
 СДА 64-8
 УИ М2
 СДА 64-2
 ИЛИ КУСБК
 СЛЦ НОММЛ1
 ЗП РУСБК-Б(М)
 ПВ ЕNQ(М15)
 СЧ РУСБК-Б(М)
 ПВ ФИЗОБМ(М16)   Б О Л Ь Ш О Й   К А Т А Л О Г
 ОТПР
 УИА 0(М5)         М5 - НАДО ЗАПИСАТЬ Б.К.
* ГОТОВИМ ШКАЛЫ ЗАДАЧ, ГОТОВЫХ ПО ГЛАВНОЙ
 УИА -3(М16)
 СЧ ВСЕЕД
ЦРОСГЛ МОДА 3(М16)
 ЗП ШКАГЛ-Б(М)
 МОДА ЦРОСГЛ
 ЦИКЛ (М16)
* ПРОВЕРЯЕМ, НАДО ЛИ РАБОТАТЬ С "ЖДУ ВРЕМЯ"
 УИА 0(М10)        М10 - ПРОВЕРЯТЬ "ЖДУ ВРЕМЯ"
 СЧ ВРЕМЯ
 ВЧ БУДЖ
 ВЧАБ МИН20-Б(М)
 ПЕ НЕТВР
 СЧ ВРЕМЯ
 ЗП БУДЖ
 ИЛИ ЭК9           = Л 6 4
 ДЕЛ ЧАС-Б(М)
 СЛ ЭК9
 И П37
 ПВ ПЕРЕВ(М15)
 СБР ПРОБ6
 СДА 64+24
 ЗП РАЯ1-Б(М)     РАЯ1:= ТЕК.ВРЕМЯ В 2-10 ВИДЕ
 УИА 1(М10)
НЕТВР НОП
* ЦИКЛ ПО БОЛЬШОМУ КАТАЛОГУ
Ц546 СЛИА 8(М2)
 СЧИ М2
 И П1777
 ПО К546
 СДА 64+3
 УИ М13           М13:= НОМЕР В КАТ
 И П37
 УИ М14
 СЧ БК6(М2)
 И Е31
 ПЕ Ц546          В РЕШЕНИИ
 СЧИ М13
 СДА 64+5
 УИ М15
 СЧ ЯЧГЛАВ(М2)
 И ЕГЛАВ
 ПО НГЛАВ         НЕ ЖДЕТ ГЛАВНУЮ
 СЧ РУС545-Б(М)
 ПО Е545          ЗОНА СЧИТАНА
 ПВ ФИЗОБМ(М16)   К А Т А Л О Г   Ш И Ф Р О В
 ОТПР
 СЧ 0
 ЗП РУС545-Б(М)
Е545 СЛИ М13(М1)
 УИА -МЧПЗ(М16)
ЦЗРЕШ СЧ ШКРЗ
 И Е48+МЧПЗ(М16)
 ПО КЦЗРЕШ
 МОД ТУСП+МЧПЗ(М16)
 СЧ ШИФРМ
 НТЖ ’200’(М13)
 ПО Ц546
КЦЗРЕШ МОДА ЦЗРЕШ
 ЦИКЛ (М16)
НЕГТЗ НОП
 СЧ Е48(М14)
 МОДА ШКАГЛ-Б(М)
 НТЖ (М15)
 МОДА ШКАГЛ-Б(М)
 ЗП (М15)
 ПБ Ц546
НГЛАВ СЧ БК6(М2)
 И Е32
 ПО Ц546
 МОДА НЕГТЗ
 ПИО (М10)
 СЧ РУС545-Б(М)
 ПО ЕС545
 ПВ ФИЗОБМ(М16)   К А Т А Л О Г   Ш И Ф Р О В
 ОТПР
 СЧ 0
 ЗП РУС545-Б(М)
ЕС545 СЛИ М13(М1)
 СЧ ’200’(М13)
 ПО НЕГТЗ
 И Е48П25
 ПЕ НЕГТЗ
 СЧ ’200’(М13)
 СДА 64+8
 И П37
 ЗП РАЯ-Б(М)
 СЧ ГОД
 СДА 64+24
 И П37
 ВЧОБ РАЯ-Б(М)
 ПЕ НАДО          МЕСЯЦ < ТЕК
 РЖА 7
 ПЕ НЕГТЗ         МЕСЯЦ > ТЕК
 СЧ ’200’(М13)
 СДА 64+16
 И П77
 ЗП РАЯ-Б(М)
 СЧ ГОД
 СДА 64+29
 И П77
 ВЧОБ РАЯ-Б(М)
 ПЕ НАДО          ЧИСЛО < ТЕК
 РЖА 7
 ПЕ НЕГТЗ         ЧИСЛО > ТЕК
 СЧ ’200’(М13)
 И П77
 ВЧОБ РАЯ1-Б(М)
 ПЕ НЕГТЗ         ЧАС > ТЕК
НАДО СЧ Е32
 НТЖ БК6(М2)
 ЗП БК6(М2)
 УИА 1(М5)
 ПБ Ц546
К546 СЛИА -1024(М2)
ПОКАРА НОП
*
********** ОБРАБОТКА СБОЕВ ВЫВОДА **********
 СЧ РУСПЛ2-Б(М)
 ПВ ФИЗОБМ(М16)
 ОТПР
 СЧИ М1
 СДА 64-2
 ИЛИ МАЛКАТ
 ИЛИ Е18
 ИЛИ НОММБ
 ЗП РУСМК-Б(М)
 ПВ ФИЗОБМ(М16)   М А Л Ы Й   К А Т А Л О Г
 УИА 0(М6)         М6 - ПРИЗНАК ЗАПИСИ М.К.
 ПБ ПЛАН2-ПЛАН2+512(М1)
*
********** ЗАПИСЬ ВРЕМЕН РЕШАЮЩИХСЯ ЗАДАЧ **********
ВЫХОД ВХ
 УИА 1(М16)
 СЧ РУСМК-Б(М)
 МОДА ПЕРЕВР-ПЛАН2+512(М1)
 ПВ (М12)
 МОДА СЛМК
 ПИО (М6)
 СЧ РУСМК-Б(М)
 ПВ ФИЗЗП(М16)
СЛМК ОТПР
 СЧ РУСМК1-Б(М)
 УИА 1(М16)
 ПЕ ЕМКТ1
 УИА 0(М16)
 СЧ РУСМК-Б(М)
 ИЛИ Е11Е1-Б(М)
 ЗП РУСМК1-Б(М)
ЕМКТ1 УИИ М6(М16)
 МОДА ПЕРЕВР-ПЛАН2+512(М1)
 ПВ (М12)
 МОДА СЛМК1
 ПИО (М6)
 СЧ РУСМК1-Б(М)
 ПВ ФИЗЗП(М16)
СЛМК1 ОТПР
 СЧ РУСМК1-Б(М)
 НТЖ П3
 ЗП РУСМК1-Б(М)
 УИА 0(М16)
 УИА 0(М6)
 МОДА ПЕРЕВР-ПЛАН2+512(М1)
 ПВ (М12)
 МОДА ВЫХОДИ
 ПИО (М6)
 СЧ РУСМК1-Б(М)
 ПВ ФИЗЗП(М16)
ВЫХОДИ НОП
*
********** КОНЕЦ РАБОТЫ **********
 СЧ ШКОР
 ПО НШКОР
 БЛПР
 СЧ Е11
 ИЛИ ШКИВ
 ЗП ШКИВ
НШКОР СЧ РУСПЛ2-Б(М)
 ПВ ТБУФ(М15)
 ПВ ЗАНОСМ(М16)
 СЧ Е36
 ПБ БЛИСКН
 СТРОК 5
 ЭКВИВ (БК0,0),(БК1,1),(БК2,2),(БК3,3)
 ЭКВИВ (БК4,4),(БК5,5),(БК6,6),(БК7,7)
 СТРОК 3
НЭВМ ПАМВ 1
КАНАЛЫ ПАМВ 1
КУСЗОН ПАМВ 1
МАСКЗН ПАМВ 1
ПРБЮД ПАМВ 1
ПРСПЕЦ ПАМВ 1
СВКУС ПАМВ 1
ТЕКПРТ ПАМВ 1
РУСМК1 ПАМВ 1
РУС545 ПАМВ 1
РУСПЛ2 ПАМВ 1
РУСБК ПАМВ 1
РУСМК ПАМВ 1
КУСПЛ2 КОНД М17В’3’М2В’3’
ДЛЯКВА КОНДВ В’33575’
Е11Е1 КОНДВ В’2001’
Е16Е1 КОНДВ В’100001’
МАСКА7 КОНДВ М17В’77’М45В’7’
Е30П25 КОНДВ М24В’77’
Е41Е33 КОНДВ М40В’1’М32В’1’
Е48П26 КОНДВ М25В’-1’
МИН20 КОНД F’60000’
ЧАС КОНД Е’180000’
ТОМА ПАМВ 13
* СТРУКТУРА ЯЧЕЕК "ТОМА":
*  48Р = 1  -  КУСОК       12- 1Р - НАЧАЛЬНЫЙ КУСОК
*                          16-13Р - ЧИСЛО КУСКОВ
*  47Р = 1  -  ОБЩМЛ       12- 1Р - НОМЕР ТОМА
*                          24-20Р - ФИЗ.НУ - ’30’
*                          27-25Р - N ЭВМ - 1
*  46Р = 1  -  МЛ          12- 1Р - НОМЕР ТОМА
*                          25-20Р - ФИЗ.НУ
*  ИНАЧЕ    -  МД          12- 1Р - НОМЕР ТОМА
*                          25-20Р - ФИЗ.НУ
*
* СДВИГ В 20-Й РАЗРЯД ОПРЕДЕЛЯЕТСЯ "ДТОМА" И ЗАВИСИТ
* ОТ "ЕТЗНУП" И "ЕН" (ПРИЧЕМ "ЕТЗНУП" МОЖЕТ ИМЕТЬСЯ
* В ЯЧЕЙКЕ "ТОМА")
*
МАСКА  ВХ
       КОНК   А(ЕТЗНУП-ЕТЗНУП) ..ЧТОБЫ ПОПАСТЬ В ТПС
       КОНК   А(’47777’+ЕН-ЕН) ..= НОМЕР ТОМА ОR ЕН
ДТОМА  ЭКВ   19            ДТОМА=Е1-МАХ(ЕТЗНУП,ЕН)+1
*                          (НОМЕР СТАРШЕЙ ИЗ ЕДИНИЦ)
 ВХОД ДТОМА
*
РАЯ ПАМВ 1
КУДА ВХ
РАЯ1 ПАМВ 1
Х ВХ
ТЕКТОМ ПАМВ 1
У ВХ
ТЕКНОМ ПАМВ 1
Z ВХ
ТЕКЯЧТ ПАМВ 1
АДРБУФ ПАМВ 1
ТЕКЗОН ПАМВ 1
ШКАГЛ ПАМВ 4
 СТРОК 3
КС КОНДБ М46В’1’М39В’3’В’1777777’
КС КОНДС М44В’5’М39В’3’В’7777777’
ММГРП КОНДВ В’72357777’
ЕЕЕАРХ КОНДВ В’1000004000020000’
КПРИЧ  КОНДВ В’2000010000040000’
ВЫВОД ВХ
 ТЕКСТ П’2ПЕРЕП. БУФЕРА ВЫВОДА↑’
 СТРОК 3
* ВНЕШНИЕ КОНСТАНТЫ
ПЛАН2 ВНЕШ ПЛАН2,ПЕРЕВР,ВРОС,ВРЕР
ДИСП70 ВНЕШ Е17П1,Е46П48,П17,П37,П77,П7777,П77777,П3
ДИСП70 ВНЕШ П7,П777,П177,П30,П14,ВСЕЕД,ЭК9,П1777,Е48П25
ДИСП70 ВНЕШ Е24П16,Е36П25,П5,Е44П42
КИТ ВНЕШ Е48П41,Е48П42,Е39П37,Е34П1
КАЧКА ВНЕШ Е48П17
* ПОДПРОГРАММЫ
ДИСП70 ВНЕШ ТБУФ,ТВ40,ЗАХЗАН,ГАШЗАН,БЛИСКН,ПЕРЕВ,ЗАНОСМ
ВЗУ ВНЕШ ФИЗОБМ,ФИЗЗП
ДИСКИ ВНЕШ ЕNQ,DЕQ
* ПРОЧЕЕ
ДИСП70 ВНЕШ СДВКУП,СДВТУП,ТАС,ТОБ,ШКАП,ШИФРМ,ШКИВ
ДИСП70 ВНЕШ НОММБ1,ЕЗАР,ЕБКИО,ЕДОБ,ЕКУПР,ЕН,ЯБКЗПА
ДИСП70 ВНЕШ ПРЕДЕЛ,ГОД,ТУСП,НОММЛ1,МАЛКАТ,КУСБК
ДИСП70 ВНЕШ ТЗНМЛ,ТЗНМД,ЕТЗНУП,МГРП,НОММБ,ЯЧБКИО,ЯЧКУПР
ДИСП70 ВНЕШ ШЗНМБ,ЯЧВР,УКАЗД,ОБЩТОМ,КУС545,ЯЧНОМ,NБЛ
ДИСП70 ВНЕШ ПРСВЛ,РЯПСЗ,ЕСТОП,ШКПЛАН,ШИФРАЦ,ДЛИН
ДИСП70 ВНЕШ ВРЕМЯ,БУДЖ,КВНЗАД,КСО,МДР,ОЗУ
ДИСП70 ВНЕШ МЧПЗ,ЗАНЯТА
КИТ ВНЕШ ШКОР,ШКВМК
НОМБОБ ВНЕШ КЕСМЛ
СОСТАВ ВНЕШ ЕОТКД,ЕОТМД,ЯЧГЛАВ,ЕГЛАВ,ЕЗПЕР,ЯЧЗПЕР
СОСТАВ ВНЕШ ТРЕГ3,ТРЕГ6
КАЧКА ВНЕШ ШКРЗ
ДИСКИ ВНЕШ КТОМ,КУСКИ
МОТТ ВНЕШ ПРОБ6,ШЗНКТ,ШЗПРИМ
 ФИНИШ
*КОНЕЦ ЧАСТИ

АСПВВ СТАРТ ’74474’
 Е
 М
*
*
*
*/////////////////////////////////////////////*
*//////                                 //////*
*//////    ВЫДАЧА ЕС 7920               //////*
*//////             Е19                 //////*
*//////             NЗАД =   36(164)    //////*
*//////             ТУС  = 2162         //////*
*//////                                 //////*
*/////////////////////////////////////////////*
*
ИВ7920 НОП ,             ИПЗ ВЫДАЧИ  ЕС 7920
 КОНД В’0’
 КОНД М41В’7’
 КОНД В’0’
 КОНД А(НВ7920)     М33
 КОНД В’0’
 КОНД М15В’2007’В’7’ М27,М21
 КОНД В’0’          М20
 КОНД В’0’          М17
 КОНД В’0’          М16
 КОНД В’0’          М15
 КОНД В’0’          М14
 КОНД В’0’          М13
 КОНД В’0’          М12
 КОНД В’0’          М11
 КОНД В’0’          М10
 КОНД В’0’          М7
 КОНД В’0’          М6
 КОНД В’0’          М5
 КОНД В’0’          М4
 КОНД В’0’          М3
 КОНД В’0’          М2
 КОНД В’0’          М1
КПЕРЕВ КОНД Х’110000130000’
КРЗБВ КОНД М32В’77’М24В’77’
ТЕКПОЗ ПАМ 1
ТЕКСТР ПАМ 1
СЛОВОБ ПАМ 1
ВЫДТЕС КОНД В’0’         ВЫДТЕС = 0 - ЕВТЕС СВОБОДЕН
Р7920 КОНД В’0’
АБВТЕС КОНК М20В’6’М6В’1’
 КОНК А(БВТЕС)
 КОНК М20В’6’М6В’5’
 КОНК А(БВТЕС)      НЕ РАЗДЕЛЯТЬ С АБВТЕС
СУЗ КОНД Х’4В’
 ПАМ 1
 КОНД М47В’1’       ВРЕМЯ
NСТР ПАМ 32            НОМЕР СТРОКИ * 80
КNСТР ПАМ 32            СМЕЩЕНИЕ ОТ НАЧАЛА СТРОКИ
*
* ПОДГОТОВКА ПАРАМЕТРОВ ДЛЯ СПО ПВВЕС
*
ППСПО НОП
 УИИ М14(М15)
 СЧ ЕВТЕС
 ПВ ЗАНПАД(М15)
 СЧИ М14
 СДА 64-5
 УИ М11
 СЛИА ТИПАЦД(М11)
 СЧИ М1            N СИМВОЛА
 СДА 64-18
 ЗП Р7920
 СЛИА -1(М2)
 СЧИ М2            КОЛЛИЧЕСТВО СЛОВ
 ИЛИ Р7920
 СДА 64-20
 ЗП Р7920
 СЧ БАСЕС
 СДА 64-3
 СДА 64+31
 ИЛИ АБВТЕС(М4)
 ИЛИ Р7920
 СПО ПВВЕС
 СЧ ЕВТЕС
 ПВ ЗАНПРТ(М15)
 УИИ М15(М14)
 ПВ КОНВМ(М16)    КОНВЫД В МОНТЕР-Е
*
* НАЧАЛО  ВЫДАЧИ НА ЕС-7920
*
НВ7920 НОП
 СЧ ШКВТ
 НТЖ ВСЕЕД
 И ШКУСТР
 И ШКВЫД
 ПО ВЫХВЕС
 НЕД П77777
 УИ М15           М15:= ЛОГ. НОМЕР ЕС-7920
 УИА 3
*
 УИА 1(М13)        М13 :=  1  - ЕС-7920
 УИА 0(М2)         М2  :=  0  - N СЛОВА В БУФЕРЕ
 СЧ ТСЛ(М15)
 И Е37
 ПЕ ТЕСТВЫ
 УИА ВБСИМР(М12)   М12 := ВБСИМР (ВЫХОД ИЗ КОНСТР)
 УИА -79(М3)       М3  := -79 - ЧИСЛО СИМВОЛОВ В СТРОКЕ
 УИА -5(М1)        М1  := -5  - N СИМ.В БУФЕРЕ
 СЧ ШКППМ
 И Е48(М15)
 НЕД
 УИ М4            М4 = 0 - ЕС7920
 СЧ СУЗ
 ПИО НЕ7934(М4)
 УИА 1(М4)         М4 = 1  - ЕС7934
НЕ7934 НОП ,
  ЗП Р7920
 ПВ ЗПСИМБ(М14)
* ВЫБОР СИМВОЛА ИЗ РЕКОРДА
ВБСИМР НОП ,
 МОД ТБЛ(М15)
 УИА (М16)         М16 := АДРЕС РЕКОРДА
 МОД (М16)
 МОДА (М16)
 УИА (М17)         М17 := АБС.АДРЕС ТЕКСТА
 СЧ Н(М17)
 СДА 64-8
 ЗП Н(М17)
 СЧМР
 ЗП Р7920        Р7920 := СИМВОЛ
 ПО ВЫДАЛИ        ВЕСЬ ТЕКСТ
 СЧ Н(М17)
 ПЕ ЕСТЬ          ЕСТЬ ТЕКСТ В СЛОВЕ
 СЧ (М16)
 СЛЦ Е1
 ЗП (М16)
 СЛЦ П77777
 НТЖ ДЛ(М16)
 И П7777
 ПЕ ЕСТЬ          ЕСТЬ ТЕКСТ В РЕКОРДЕ
 СЧ ВН(М16)
 ПЕ ЕСТЬР         ЕСТЬ ЕЩЕ РЕКОРД
 СЧ (М16)
 СЛЦ П77777        СЧЕТ.ВЫДАЧИ СЛОВ -1
 ЗП (М16)
 ПБ ЕСТЬ
ЕСТЬР НОП
 ЗП ТБЛ(М15)
ЕСТЬ НОП
 СЧ Р7920
 НТЖ П377
 ПО КОНСТР        СИМВОЛ КОНЕЦ СТРОКИ
 ПВ ЗПСИМБ(М14)
 ПИНО ВБСИМР(М4)    ЕС-7934
 ЦИКЛ ВБСИМР(М3)
 ПБ КОНСТ2
* ДОПОЛНЯЕМ СТРОКУ ПРОБЕЛАМИ
КОНСТР НОП
 ПИО КОНСТ0(М4)    НЕ ЕС-7934
 УИА ПС7934(М16)
 СЧИ М16
 ЗП Р7920
 УИА ВБСИМР(М14)
 ПБ ЗПСИМБ
КОНСТ0 НОП ,
 СЧ 0             СИМВОЛ "ПУСТО"
 ЗП Р7920
КОНСТ1 НОП
 ПВ ЗПСИМБ(М14)
 ЦИКЛ КОНСТ1(М3)
КОНСТ2 СЧ NСТР(М15)
 НТЖ ДЛЭКР
 ПО ВВЕРХ         ЭКРАН ЗАПОЛНЕН
 СЧ NСТР(М15)
 СЛЦ П120
ВВЕРХ ЗП NСТР(М15)
 УИА -79(М3)
 ПБ (М12)
*
ВЫДАЛИ НОП
*
 ПИНО ВЫ7934(М4)    ЕС-7934
 СЧ ТСЛ(М15)
 И ЕПРИГ
 ПЕ НЕНАДО
 ПВ КОНСТР(М12)
НЕНАДО НОП
 СЛИА 79(М3)
 СЧИ М3            N СИМВОЛА В СТРОКЕ
 ЗП КNСТР(М15)
 СЛЦ NСТР(М15)
 СДА 64-36
 РЗБ КРЗБВ
 ИЛИ КПЕРЕВ
 ЗП ТЕКПОЗ
КУРСОР НОП
 СЧ ТЕКПОЗ
 ПО ДВИГАЙ
 СДА 64-8
 ЗП ТЕКПОЗ
 СЧМР
 ЗП Р7920
 УИА КУРСОР(М14)    М14 := КУРСОР
 ПБ ЗПСИМБ
ВЫ7934 НОП ,
 УИА КС7934(М16)
 СЧИ М16
 ЗП Р7920
 ПВ ЗПСИМБ(М14)
ДВИГАЙ НОП ,
 УИА ППСПО(М14)     М14 := ППСПО
 УИИ М16(М1)
ДОДВИ1 НОП
 СЧ СЛОВОБ
 СДА 64-8
 ЗП СЛОВОБ
 ЦИКЛ ДОДВИ1(М16)
 СЛИА 5(М1)          М1 := М1+5
 ПБ ВЫТЗП          ВЫТОЛКНИМ ПОСЛ.СЛОВО В БУФЕР
*
* ЗАПИСЬ СИМВОЛА В БУФЕР ЕС-7920 (ЛИСТБМ)
ЗПСИМБ НОП
 СЧ СЛОВОБ
 СДА 64-8
 ИЛИ Р7920
 ЗП СЛОВОБ
 МОДА (М14)
 ЦИКЛ (М1)
 УИА -5(М1)
ВЫТЗП НОП
 СЧ БАСЕС
 РЗБ ПРИП
 УИА 1027
 РЕГ ’27’          ПРИПИСКА 34-ОМУ ЛИСТУ МАТЕМАТИКА
 УИ 0
 СЧ СЛОВОБ
 УИА 1026
 КК ’100’,БВТЕС(М2)
 СЛИА 1(М2)
 ДК ’36’,ВЫТЗП2
ВЫТЗП2 НОП
 УИА 3
 ПБ (М14)
*
*
*
*
ВЫХВЕС НОП ,
*
 ЗП ВЫДТЕС       ВЫДТЕС:= 0 - ПРОЦЕСС ЕВТЕС СБОБОДЕН
 УИА НВ7920(М16)
 СЧИ М16
 ЗП ИВ7920+3
 СЧ ИВ25
 ЗП ИВ7920+5
 ПБ ВИСП16
*
*
* В Н Е Ш Н И Е
МОТТ ВНЕШ ДЛЭКР,ШКППМ,П120,(КОНВМ,КОНВЫД)
МОТТ ВНЕШ ЗАНПАД,ЗАНПРТ
СОСТАВ ВНЕШ ЕВТЕС
МОТТ ВНЕШ ШКУСТР,ШСВЯТ
ДИСП70 ВНЕШ ВИГ11,ВИСП15,ВИСП16,ШКИВ,БВЗ,ТВ16,ШКСВН
ДИСП70 ВНЕШ ЗАНЯТА,(Е48П46,Е46П48)
ПВВ ВНЕШ ТИПАЦД,ПВВЕС,ДАЙКОТ
МОТТ ВНЕШ ВН,ДЛ,Н,ЕПРИГ,ШКВЫД
ТЕРМ ВНЕШ КОТПВВ,ШКВТ
ТЕРМ ВНЕШ ТЕСТВЫ
 ВХОД ППСПО,ВЫТЗП,СЛОВОБ
ДИСП70 ВНЕШ ВСЕЕД
ДИСП70 ВНЕШ БАСЕС,ТСЛ,П7777,П77777,П377,ТБЛ,ПРИП,ИВ25
*
* В Х О Д Н Ы Е
 ЭКВИВ (БВТЕС,0)
 ЭКВИВ (ПС7934,’45’),(КС7934,’31’)
 ВХОД ИВ7920,ВЫДТЕС,NСТР,КNСТР
 ЛИТЕР
 СТРН
*
*
*
*//////////////////////////*
*///                    ///*
*/// ЗАДАЧА  ВЫДЕЛЕНИЯ  ///*
*///   ТЕРМИНАЛЬНЫХ     ///*
*///  Р Е К О Р Д О В   ///*
*///     Е46            ///*
*///     NЗАД =    3    ///*
*///     ТУС  = 2130    ///*
*///                    ///*
*//////////////////////////*
*
ИПЗДИН НОП .             ИПЗ ВЫДЕЛЕНИЯ РЕКОРДОВ
 КОНД В’0’          СМ
 КОНД М41В’7’       РК
 КОНД В’0’          РМР
 КОНД А(ДИНАМО)     М33
 КОНД В’0’          М32
 КОНД В’200700007’  М27,М21
 КОНД (16)В’0’      М20 - М1
*
* НАЧАЛО ПРОГРАММЫ
*
ДИНАМО НОП .
 УИА ’2003’
 СЧ ДАЙРЕК        ШКАЛА ЗАПРОСЧИКОВ
 ПО ЗАКР          ВСЕ ОБСЛУЖЕНЫ
 УИА 3
 НЕД П77777
 УИ М2
 ПВ ПОИСК(М6)     ПРИСОЕДИНЕНИЕ РЕКОРДА К ЦЕПОЧКЕ
 МОД ТСЛ(М2)
 УИА (М6)          М6 := АДРЕС ПЕРВОГО РЕКОРДА
 СЧИ М14           ДЛИНА ПРИСОЕДИНЕННОЙ ЧАСТИ
 ВЧОБ ДЛИНА(М6)
 ПЕ ХВАТИТ        УДОВЛЕТВОРИЛИ
 НТЖ 0
 ПО ХВАТИТ
 ЗП ДЛИНА(М6)     СТОЛЬКО ЕЩЕ МОЖНО ЗАПРОСИТЬ
 УИА ’2003’
 ПБ ГОТОВ
 ПАМ 2
 КОНД М47В’1’       ВРЕМЯ
ХВАТИТ НОП ,
 СЛЦ ДЛ(М15)
 И П77777
 ЗП ДЛ(М15)       КОРР.ДЛИНЫ(ЧТОБЫ НЕ ПРИНЯТЬ ЛИШНЕГО)
 СЧ 0
 ЗП ДЛИНА(М6)     БОЛЬШЕ ЗАПРОСОВ НЕ БУДЕТ
 СЧ НУЖРЕК
 НТЖ Е48(М2)
 ЗП НУЖРЕК
ГОТОВ СЧ Е48(М2)
 НТЖ ДАЙРЕК
 ЗП ДАЙРЕК        ЗАКАЗ ВЫПОЛНЕН
 ПБ ДИНАМО
ЗАКР СЧ ЖДУРЕК
 ПВ ВШГ(М15)      ТЕ КТО ЖДАЛИ
 СЧ 0
 ЗП ЖДУРЕК
 ЗП РАБДИН
 УИА ДИНАМО(М16)
 ПБ ТБ13
*
*
* В Н Е Ш Н И Е
МОТТ ВНЕШ ДАЙРЕК,ДЛИНА,РАБДИН,НУЖРЕК,ЖДУРЕК,ПОИСК
ДИСП70 ВНЕШ ТБ13,Е48П16
ВИСП ВНЕШ ВШГ
*
* В Х О Д Н Ы Е
 ВХОД ИПЗДИН
*
*
 ФИНИШ ,
*КОНЕЦ ЧАСТИ

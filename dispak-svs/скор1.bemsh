СКОР1 СТАРТ ’10000’
 Б
 УПОТР СКОР1(М5)
 РЖА 3
 СЧ ПРСК2-П(М7)     ’0’-->ЗАДА. ’1’-->ШКПУСК.
 ПЕ НАШКПУ
 ПВ ТБУФ(М15)
 СДА 64-8
 УИ М1
 СДА 64-2
 ИЛИ ИНФКАТ+1-П(М7)
 СЛЦ НОММЛ1
 ЗП ИНФКАТ-П(М7)     К У С   ДЛЯ КАТАЛОГА ШИФРОВ.
 ЗП К-П(М7)
*
*********************************
* БЛОК ТЕРМИНАЛЬНОГО РАСПИСАНИЯ *
*********************************
*
 СЛЦ ДЕЛ+1-П(М7)
 ЗП ДЕЛ-П(М7)
 СЛЦ ВТТ+1-П(М7)
 ЗП ВТТ-П(М7)
 СЧ КВАНТ-П(М7)
 СДА 64+1
 ЗП КВАС
 СЧ ГОД
 И П3
 ПЕ НЕМ4
 СЧ Е3
НЕМ4 СДА 64-7
 УИ М16
 СДА 64-1
 УИ М14
 СЛИ М16(М1)
 СЛИ М14(М1)
 СЧИ М16
 ЗП АРК-П(М7)    АДРЕС РАСП-Я КЛЮЧЕЙ
ВИРА СЧ ФЛАТЕР
 И Е46П48
 ПО НЭП    --> НА ЭК-Д И ПУСК ТТ
 НЕД П77777
 УИ М13
 УИА 1027
 СЧ Е48(М13)
 НТЖ ФЛАТЕР
 СЛЦ МИН-П(М7)
 ЗП ФЛАТЕР
 УИА 3
 МОДА ЗВОНОК    --> ЗВОНОК ОТ НОМБОБ
 ПИНО (М13)
ВГЕН СЧ ВРЕМЯ
 ПБ ГЕНС     --> НА ГЕНЕРАЦИЮ
*
* БЛОК <ЗВОНОК ОТ НОМБОБА> *
*
ЗВОНОК УИА (М13)
 И ЕР
 ПО ВГЕН     --> ПРОВЕРИТЬ НА ВВОД РАСП-Я
 СЧ ШКНТ
 НТЖ ВСЕЕД
 ЗП ШКТК
 ЗП РЯТ     ТЕЛЕКАНАЛЫ
 ПО БУД     --> БУДИЛЬНИК
 СЧ ДЕЛ-П(М7)
 ПВ ФИЗОБМ(М16)     --> ПРОЧТИ КЛЮЧИ
ЗВОН УИА 3
 СЧ РЯТ
 ПО БУД     --> ФОРМИРОВАТЬ ШКАЛУ СООБЩЕНИЙ
 НЕД
 УИ М13     =>  N КАНАЛА
 СЧ Е48-1(М13)
 НТЖ РЯТ
 ЗП РЯТ
 МОД ТУСП-1(М13)
 УИА (М15)
 СЧ ШИФРМ(М15)
 ЗП ШИФТЗ
 СЧ ЦПМЛАР(М15)
 СДА 64+32
 ЗП ВРЕЗА
 СЧ ЦПСТАР(М15)
 СДА 64+39
 СДА 64-16
 ИЛИ ВРЕЗА
 ЗП ВРЕЗА
 СЧ ТЕРЕЗА(М15)
 ЗП ТЕРМЫ
 НЕД
 УИ М16
 СЧ ТСЛ-1(М16)
 СДА 64+АТ
 ПЕ ТТАС
 СЧИ М16
ТТАС МОДА I-П(М7)
 ПВ (М15)
* ПРОВЕРЯЕМ ПРИОРИТЕТНЫЕ ТЕРМИНАЛЫ
 МОДА ШКОТ-П(М7)
 И (М16)
 ПЕ ШИП     --> ВЫЧЕРКНИ ЗАДАЧУ
 УИА -3+1(М16)
ПШ СЧ ШИФТЗ
 МОДА ТПШ-П(М7)
 НТЖ 3-1(М16)
 ПЕ НПШ
ШИП СЧ ШКТК
 НТЖ Е48-1(М13)
 ЗП ШКТК
 ПБ ЗВОН
НПШ МОДА ПШ
 ЦИКЛ (М16)
* БЛОК МНОГОСТАНОЧНЫХ СИСТЕМ
*
 УИИ М12(М1)
 УИА -127+1(М11)
БМС СЧ ’1402’(М12)
 ПЕ ЕШИ     --> МС С ШИФРОМ
 СЛИА 127(М11)
 СЧИ М11
 СЛИА -127(М11)
 ПВ ПЕРЕВ(М15)
 СЛЦ Ш1-П(М7)
ЕШИ НТЖ ШИФТЗ
 ПО БРОМ     --> В БЛОК РАСПИСАНИЯ МС
 СЛИА 2(М12)
 МОДА БМС
 ЦИКЛ (М11)
 ПБ БОС     --> В БЛОК ОДНОСТАНОЧНЫХ
*
* БЛОК РАСПИСАНИЯ МНОГОСТАНОЧНЫХ СИСТЕМ
*
БРОМ МОД АРК-П(М7)
 СЧ 128
 НТЖ КЛЮСЭИ-П(М7)
 ПО РОК     --> РАСП-Е КЛЮЧЕЙ ЕСТЬ
*
* БЛОК ОПРЕДЕЛЕНИЯ ЗАКРЫВАЕМЫХ ТЕРМИНАЛОВ МULТSISТ
*
РМТ СЧ ТЕРМЫ
 ПО ШИП
 НЕД
 УИ М12
 СЧ ТСЛ-1(М12)
 СДА 64+АТ
 ПЕ ТАСМ
 СЧИ М12
ТАСМ СЛЦ Е7
 МОДА I-П(М7)
 ПВ (М15)
 МОДА ШКН-П(М7)
 И (М16)
 ПЕ МТРАБ
 СЧ ЯС-П(М7)
 ИЛИ Е48-1(М12)
 ЗП ЯС-П(М7)
МТРАБ СЧ Е48-1(М12)
 НТЖ ТЕРМЫ
 ЗП ТЕРМЫ
 ПБ РМТ
*
* РАСПИСАНИЕ НА КЛЮЧ МС
*
РОК МОД АРК-П(М7)
 СЧ 128+127(М11)
 И КВАС
 ПЕ РМТ     --> ВНЕСТИ В ЯПТ
ВЫБТЗ СЧ ТЕРМЫ
 ИЛИ ЯС-П(М7)
 ЗП ЯС-П(М7)     => КОМУ СООБЩИТЬ
 ПБ ЗВОН
*
* БЛОК ОДНОСТАНОЧНЫХ СИСТЕМ
*
БОС СЧ ШИФТЗ
 СДА 64+40
 ПВ ИЗ10В8(М15)
 ЗП NТ-П(М7)
 СЛЦ Е8
 МОДА IО-П(М7)
 ПВ (М15)
 МОДА ШКН-П(М7)
 И (М16)
 ПО ВЫБТЗ     --> ВЫБРОСИТЬ ТЕЛЕЗАДАЧУ
 СЧ ТЕРМЫ
 ПЕ ЕСТЕР
 СЧ ШКОТ+1-П(М7)
 И Е24П1
 ВЧ ВРЕЗА
 ПО ШИП
 ПБ ЗВОН
ЕСТЕР НЕД
 УИ М12
 СЧ ТСЛ-1(М12)
 СДА 64+АТ
 ПЕ ТАСО
 СЧИ М12
ТАСО ЗП Р-П(М7)
 СДА 64-7
 ИЛИ NТ-П(М7)
 ИЛИ Е16
 ЗП NТ-П(М7)     => ЗАП. РАБ.МЕСТО - РМ
 СЧ Р-П(М7)
 МОДА I-П(М7)
 ПВ (М15)
 МОДА ШКН-П(М7)
 И (М16)
 ПО ВЫБТЗ     --> ВЫБРОСИТЬ ТЕЛЕЗАДАЧУ
*
* ВСЕ ЛИ РАБОЧИЕ МЕСТА ОСТАЮТСЯ
*
 УИА -5+1(М16)
ПРОМ МОДА ТРМ-П(М7)
 СЧ 5-1(М16)
РМ СДА 64-16
 ЗП Р-П(М7)
 СЧМР
 НТЖ NТ-П(М7)
 ПО ВЫБТЗ     --> МЕСТО ЗАКР.
 СЧ Р-П(М7)
 ПЕ РМ
 МОДА ПРОМ
 ЦИКЛ (М16)
 ПБ ШИП
*
* БЛОК БУДИЛЬНИКА ТЕРМИНАЛЬНОГО РАСПИСАНИЯ
*
БУД СЧ ФЛАТЕР
 И Е26Е25
 НТЖ Е26Е25
 СДА 64+24
 УИ М12
 ИЛИ ТОСТ-П(М7)
 ЗП ТОСТ-П(М7)
 УИА ТОСТ-П(М16)
 МОДА ФОРС
 УИА (М13)
ФОРС СЧ ЯС-П(М7)
 ПЕ СКАЖИ
 СЧ ФЛАТЕР
 СЛЦ Е25
 ЗП ФЛАТЕР
 МОДА ВИРА
 ПИНО (М12)
 ПБ СНЯТЬ
*
* СООБЩИ ОБ ОКОНЧАНИИ СЕАНСА
*
СКАЖИ НЕД
 УИ М2
 СЧ ЯС-П(М7)
 НТЖ Е48-1(М2)
 ЗП ЯС-П(М7)
 ПБ ВЫДАЧА-П(М7)
*
* БЛОК СНЯТИЯ ТЕЛЕЗАДАЧ ПО РАСПИСАНИЮ
*
СНЯТЬ СЧ ШКТК
 ПО НОВКВА
 НЕД
 УИ М16
 СЧ ШКТК
 НТЖ Е48-1(М16)
 ЗП ШКТК
 УИА ’106’(М17)     => УСТ. ПРИЧИНУ:НЕ ЗАКАЗАН ТЕРМ.
 МОД ТУСП-1(М16)
 УИА (М15)
СНИМЕМ УИА 1027
 ПВ АВОСТА(М16)
 УИА 3
 ПБ СНЯТЬ
*
* БЛОК ФОРМИРОВАНИЯ РАСПИСАНИЯ НА СЛЕДУЮЩИЕ ПОЛЧАСА
*
СФЛА КОНД В’-1’М24В’7’
НОВКВА СЧ ВТТ-П(М7)
 ПВ ФИЗОБМ(М16)
 УИА 3
 СЧ ПОЛ
ВНЕСИ УИА 1027
 СЛ ФЛАТЕР
 ВЧ ТМИН4-П(М7)
 И СФЛА
 ЗП ФЛАТЕР
 УИА 3
 МОДА ЗАПРИО
 УИА (М11)
 СЧ КВАС
 ЗП КВАНТ-П(М7)
 СДА 64+1
 ПЕ НЕКОД     --> НЕ КОНЕЦ ДНЯ
 СЧ ФЛАТЕР
 ИЛИ БЛР
 ЗП ФЛАТЕР     ==> ЗАБЛОКИРОВАЛ ВЫЗОВ ДО СМЕНЫ СУТОК
 СЧ Е48
НЕКОД ЗП КВАС
*
* ПЕРЕНОС ШКН ---> ШКО; 0 ---> ТРМ. *
*
 УИА -5+1(М16)
ПЕРС МОДА ШКН-П(М7)
 СЧ 5-1(М16)
 МОДА ШКО-П(М7)
 ЗП 5-1(М16)
 СЧ
 МОДА ТРМ-П(М7)
 ЗП 5-1(М16)
 МОДА ПЕРС
 ЦИКЛ (М16)
* ФОРМИРОВАНИЕ ОБОБЩЕННОЙ ШКАЛЫ СЛЕДУЮЩЕГО КВАНТА
 УИА 228(М12)     ==> В ОБОБЩЕННОЙ ШКАЛЕ 228 БИТ
УШК СЧИ М12
 МОДА I-П(М7)
 ПВ (М15)
 МОДА ШКН-П(М7)
 ИЛИ (М16)
 НТЖ Е48(М17)
 ЗП Р-П(М7)
 МОДА -256-1(М14)
 СЧ (М12)
 И КВАС
 СБР КВАС
 СДА 64(М17)
 ИЛИ Р-П(М7)
 МОДА ШКН-П(М7)
 ЗП (М16)
 СЛИА -1(М12)
 МОДА УШК
 ПИНО (М12)
 ПБ (М11)
*
* ЗАПИСЬ ПРИОРИТЕНЫХ ШИФРОВ И ТЕРМИНАЛОВ
*
ЗАПРИО СЧ ШКОД-256(М14)
 ЗП ШКОТ-П(М7)
 СЧ ШКОД-256+1(М14)
 ЗП ШКОТ+1-П(М7)
 СЧ ПШД-256(М14)
 ЗП ТПШ+1-П(М7)
 СЧ ПШД1-256(М14)
 ЗП ТПШ+2-П(М7)
*
* РАБОТА С ТАБЛИЦЕЙ РАБОЧИХ МЕСТ -->  Т Р М
*
 УИА -21+1(М13)
 УИА -48(М11)
* <М12>=00000 ПОСЛЕ П/П  У Ш К  -> ДЛЯ БЛОКА  Е Р М
ОПРМ МОДА ТРД-256(М14)
 СЧ 21-1(М13)
ПРМ ЗП Р-П(М7)
 И Е15П1
 ПО ОЧМ
 ЗП Р1-П(М7)
 И П177
 СЛЦ Е8
 МОДА I-П(М7)
 ПВ (М15)
 МОДА ШКО-П(М7)
 И (М16)
 ПО ОЧМ     --> ОТДЕЛ НЕ РАБ
 СЧ Р1-П(М7)
 СДА 64+13
 ПО ЕРМ     --> ЕСТЬ РАБОЧЕЕ МЕСТО
 УИ М16
 МОДА МАТР-256-1(М14)
 СЧ (М16)
 И КВАНТ-П(М7)
 ПО ОЧМ     --> РМ НЕ РАБ. В ЭТОМ КВАНТЕ
 СДА 64+1
 МОДА МАТР-256-1(М14)
 И (М16)
 ПЕ ОР1415
 СЧ Е16
 ИЛИ Р1-П(М7)
 ЗП Р1-П(М7)
ОР1415 СЧ Р1-П(М7)
 И СРМ
 ЗП Р1-П(М7)
 ПБ ЕРМ
СРМ КОНД М15В’1’В’17777’
ЗАППУС СЧИ М1
 СДА 64-2
 ИЛИ КУСПУС
 ИЛИ НОММБ1
 ЗП КУСПУС
 ИЛИ Е18
 ПВ ФИЗОБМ(М16)
 УИА 3
 УИА -21+1(М16)
ЗАПСЕК МОДА ШКО-П(М7)
 СЧ 21-1(М16)
 МОДА ШКО-П(М1)
 ЗП 21-1(М16)
 МОДА ЗАПСЕК
 ЦИКЛ (М16)
 СЧ КУСПУС
 ПВ ФИЗОБМ(М16)     ----> ЗАПОМНИЛИ НА МБ
 УИА 3
 ПБ ВИРА
*
* ВНЕСИ РАБОЧЕЕ МЕСТО
*
ЕРМ СЧ Р1-П(М7)
 СДА 64+16(М11)
 МОДА ТРМ-П(М7)
 ИЛИ (М12)
 МОДА ТРМ-П(М7)
 ЗП (М12)
 СЛИА 16(М11)
 МОДА ОЧМ
 ПИНО (М11)
 УИА -48(М11)
 СЛИА 1(М12)
 СЧИ М12
 НТЖ П5
 ПО ЗАППУС    ЗП ПУСКАЧ
ОЧМ СЧ Р-П(М7)
 СДА 64+16
 ПЕ ПРМ
 МОДА ОПРМ
 ЦИКЛ (М13)
 ПБ ЗАППУС
* БЛОК ГЕНЕРАЦИИ ТЕРМИНАЛЬНОГО РАСПИСАНИЯ *
*******************************************
*
ГЕНС ВЧ ПОЛ
 ПЕ УСТК
 СЛИА 1(М13)
 ПБ ГЕНС
УСТК ВЧОБ ВРЕМЯ
 ЗП ФЛАТЕР
 СЧ ВТТ-П(М7)
 ПВ ФИЗОБМ(М16)
 УИА 3
 СЧ КЛМД-256(М14)
 НТЖ КЛЮСЭИ-П(М7)
 ПЕ ВИРА    НЕТ РАСПИСАНИЯ --> НА ВИД РАБОТ
 СЧ ФЛАД-256(М14)
 ИЛИ ФЛАТЕР
 ЗП ФЛАТЕР
 СЧ Е48(М13)
 МОДА НЕКОД
 ПВ (М11)     ---> ВНЕСТИ ИНФ. В ШКН <---
 СЧ
 ПБ ВНЕСИ     ---> ВНЕСТИ ИНФ. ВО ВСЕ ТАБЛИЦЫ --->
*
*****************************************
* ВХОД ПО ЭКСТРАКОДУ И ШКПУСК ОТ БОНБОТ *
*****************************************
*
НЭП СЧ ЗАДА-П(М7)
 ПЕ ВЭКСТР-П(М7)
НАШКПУ СЧ ШКПУСК
 ПО ВЫХ1-П(М7)
 НЕД 0
 УИ М2
 ЗП К-П(М7)
 СЧ ШКПУСК
 НТЖ Е48-1(М2)
 ЗП ШКПУСК
 МОД ТСЛ-1(М2)
 УИА (М15)
 СЧ Н(М15)
 НТЖ =М8П’ТАЙНА’В’377’
 ПЕ НЕТАЙ
 СЧ Е48-1(М2)
 НТЖ ТАЙНА
 ЗП ТАЙНА
НЕТАЙ НОП ,
 СЧ Н(М15)
 НТЖ =М8П’ИТМВТ’В’377’
 ПЕ НЕИТМ
 СЧ Е48-1(М2)
 НТЖ ШКОПТТ
 ЗП ШКОПТТ
НЕИТМ НОП ,
 СЧ ТСЛ-1(М2)
 СДА 64+АТ
 ПЕ ЭТОТАС
 СЧИ М2
ЭТОТАС ЗП NТ-П(М7)
 СЧ
 ЗП RR-П(М7)
 СЧ ШКУСТР
 ИЛИ ШКАС
 И Е48-1(М2)
 ПЕ ПРОВ
 СЧ ШЗПРИМ
 ИЛИ Е48-1(М2)
 НТЖ Е48-1(М2)
 ЗП ШЗПРИМ
 ПБ НАШКПУ
*
* УСТАНОВКА "МЯГКОГО" ВХОДА
*
ОТКРОЙ СЧ ШКОТ+1-П(М7)
 СДА 64-12
 И Е24П1
 СЛ ФЛАТЕР
 СЛ ТМИН4-П(М7)
 ВЧ ПОЛ
 ВЧ ВРЕМЯ
 ПО РАНО
 СЧ ВСЕЕД
 ЗП ПУСТИ-П(М7)
РАНО СЧ NТ-П(М7)
 ПБ ОПРТ
*
* ПРОВЕРКИ ПО ТЕРМИНАЛЬНОМУ РАСПИСАНИЮ
*
ПРОВ СЧ ФЛАТЕР
 И Е35П31
 ПО ПОП
 И ЕМ
 ПЕ ОТКРОЙ
 СЧ NТ-П(М7)
ОПРТ МОДА I-П(М7)
 ПВ (М15)
 МОДА ШКО-П(М7)
 И (М16)
 ПО ВНОС
 СЧ Е8
ВНОС ЗП ВР-П(М7)
 МОДА ШКОТ-П(М7)
 СЧ (М16)
 И Е48(М17)
 ПО ВНМС
 СЧ ВСЕЕД
 ЗП ВР-П(М7)
 ПБ ПОП
ВНМС СЧ NТ-П(М7)
 СЛЦ Е7
 МОДА I-П(М7)
 ПВ (М15)
 МОДА ШКО-П(М7)
 И (М16)
 ПО ПОП
 СЧ Е48
 ИЛИ ВР-П(М7)
 ЗП ВР-П(М7)
ПОП СЧ NТ-П(М7)
 СДА 64-7
 ЗП NТ-П(М7)
 СЧ
 ЗП Ш-П(М7)
 ЗП Ф-П(М7)
 ЗП ИНФ-П(М7)
 УИА -11(М14)
 МОД ТСЛ-1(М2)
 УИА (М11)
СЛС МОДА 11(М14)
 СЧ Н(М11)
 МОДА БУФ-П(М7)
 ЗП 11(М14)
 МОДА СЛС
 ЦИКЛ (М14)
 СЧ ЗАНЯТА
 И Е43
 УИА ДЗА-СКОР1(М16)
 ПЕ ОБЩ-П(М7)
 СЧ МГРП
 И ЕСТОП
 ПО С2-П(М7)
 ПБ СТОП-П(М7)
КУСПУС КОНД М17В’0’М2В’12’
ДЗА КОНД П’0ДИАЛО’
 КОНД П’Г ЗАК↑’
ПОЛ КОНД В’257620’
ПУСКАЧ ВНЕШ ТРМ,ТПШ,ШКО,ШКН,ШКОТ,NТ,Р,Р1,КВАНТ,АРК,Ш
МОТТ ВНЕШ ФЛАТЕР,ТАЙНА,ШКОПТТ
ДИСП70 ВНЕШ П5,П177,(Е15П1,П77777),П77777,НОММБ1,Е35П31
ДИСП70 ВНЕШ Е46П48,ТЕРЕЗА,ШКНТ,ПЕРЕВ,Е26Е25,АВОСТА
ДИСП70 ВНЕШ МГРП,ЕСТОП
ПУСКАЧ ВНЕШ I,IО,(ЯС,Ш),ТОСТ,ВЫДАЧА,ДЕЛ,КЛЮСЭИ,ПУСТИ
ПУСКАЧ ВНЕШ (Ш1,ШС),МИН,ТМИН4,ВЭКСТР
 ЭКВИВ (ШИФТЗ,СКОР1+2),(ТЕРМЫ,СКОР1+3),(КВАС,СКОР1+4)
 ЭКВИВ (ШКТК,СКОР1+5),(РЯТ,СКОР1+6),(ВРЕЗА,СКОР1+7)
 ЭКВИВ (ПШД,63),(ПШД1,127),(ШКОД,249),(МАТР,251)
 ВХОД НАШКПУ
ДИСП70 ВНЕШ ТБУФ,ШКПУСК,ИЗ10В8,НОММЛ1,ПРЕДЕЛ,ВСЕЕД
ДИСП70 ВНЕШ ГОД,Е24П1,П7,ВРЕМЯ,ТАС,ТСЛ
ДИСП70 ВНЕШ ТУСП,ШИФРМ,ЗАНЯТА,ТВНПР1
ДИСП70 ВНЕШ Э75,П3,П77,П377,Е48П25
ПУСКАЧ ВНЕШ СТОП
ПУСКАЧ ВНЕШ RR,(П,ПУСКАЧ),ИНФКАТ,К,ВР,ЗАДА,ШИФ,БУФ
ПУСКАЧ ВНЕШ ВЫХ1,Ф,ПРСК2,ОТВЕТ,ОБЩ,С2,ИНФ,НДС1,ВТТ,СИН
МОТТ ВНЕШ ШЗПРИМ,ШКАС,Н,ШКУСТР
МОТТ ВНЕШ НОМУСД,НОМУС
ВЗУ ВНЕШ ФИЗОБМ
БОЭК1 ВНЕШ ЗАПВЫХ
СОСТАВ ВНЕШ АТ
 Е
 М
 ЭКВИВ (ЕР,Е31),(ЕАВ,Е32),(ЕМ,Е33),(ЕФ,Е34),(БЛР,Е35)
 ЭКВИВ (ТРД,228),(КЛМД,255),(ДЕЗА,254),(ФЛАД,253)
 ВХОД ЕР,ЕАВ,ЕМ,ЕФ,БЛР,ТРД,КЛМД,ДЕЗА,ФЛАД
 ЭКВИВ (ЦПМЛАР,’43’),(ЦПСТАР,’32’)
 ФИНИШ
*КОНЕЦ ЧАСТИ

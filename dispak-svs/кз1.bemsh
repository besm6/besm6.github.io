 Б
 GВLА ∧Щ
∧Щ SЕТА 0
КЗ1 СТАРТ ’10000’
 УПОТР КЗ1(М6)
КЗ2 СЧ Е2
 ПВ ЗАХЗАН(М15)   РАЗРЕШ.РАБОТЫ С СП
КЗ23 УИА (М11)         ПРИЗ:БУФЕР НЕ БРАЛИ
 УИА (М12)         НЕТ СП НА МБ
 УИА -3(М4)
 УИА -СП+1(М10)
 СЧ ЧВЫЗСП
 ВЧОБ КСП
 ПО НЕТНАБ        ВСЕ В ОЗУ
 СЧ ЧВЫЗСП
 ВЧ КСП
 УИ М12           ЧИСЛО СП НА МБ В ТАБЛИЦЕ УС
 СЧ Е1
 ПВ ТБУФ(М15)     ДАЙ АБЗАЦ
 УИА 3
 СДА 64-8
 УИ М11           А АБЗАЦА
 СДА 64-2
 ИЛИ НОМСЕК
 ИЛИ НОММБ
 ЗП РЯ
 ПВ ФИЗОБМ(М16)   ЧТ.ТАБЛ.УС-ОВ СП
НЕТНАБ УИА 3
 СЧ ТУСП-1(М1)
 СДА 64-П
 НТЖ СПНОМТ+СП-1(М10)
 И Е48П43
 ПЕ НЕТОЙЗ        СП ДРУГОЙ ЗАДАЧИ
 ЗП СТРЕЛ
 ЗП СПНОМТ+СП-1(М10)  ВЫБРОС СП
 СЧ ЧВЫЗСП
 ВЧ Е1
 ЗП ЧВЫЗСП        ЧИСЛО СП-1
 МОДА НЕТОЙЗ
 ПИО (М12)         СП НА МБ НЕТ
 СЛИА -1(М12)
 МОДА (М12)
 СЧ (М11)
 ЗП СПНОМТ+СП-1(М10)   СП С МБ В ОЗУ
 СЧ
 МОДА (М12)
 ЗП (М11)         ОСВ.МЕСТА НА МБ
 ПБ НЕТНАБ        НА ПРОБУ:СП ЭТОЙ ЗАДАЧИ
НЕТОЙЗ МОДА НЕТНАБ
 ЦИКЛ (М10)         НА 32 УС
 МОДА ПУСТМБ
 ПИО (М11)         НА МБ СП НЕТ
 СЛИ М12(М11)
ОСВМБ СЧ (М11)
 ПО КОНСП         ВСЕ СП
 СЧ ТУСП-1(М1)
 СДА 64-П
 НТЖ (М11)
 И Е48П43
 ПЕ ОСВМБ1        ДРУГОЙ ЗАДАЧИ
 СЧ ЧВЫЗСП
 ВЧ Е1
 ЗП ЧВЫЗСП        СП-1
 СЧ -1(М12)
 ЗП (М11)         ПОСЛ.СП НА СВ.МЕСТО
 СЧ
 ЗП -1(М12)
 СЛИА -1(М12)
 ПБ ОСВМБ
ОСВМБ1 СЛИА 1(М11)
 ПБ ОСВМБ
ДСТ НОП ,
 СЧИ М1
 СДА 64-3
 ЗП РЯР-КЗ(М7)
 СЧ ГОД
 И П7
 ИЛИ РЯР-КЗ(М7)
 ЗП РЯР-КЗ(М7)
 МОДА РЗПУЛ
 НТЖ 0
 ПЕ ПУЛНЗ         ОБЩПУЛ НЕ ЗАХВАЧЕН ЭТОЙ ЗАДАЧЕЙ
 СЧ РЯР-КЗ(М7)
 ИЛИ Е48
 ПВ ЗОПУЛ(М15)    ОСВОБОЖДЕНИЕ ОБЩПУЛ
ПУЛНЗ НОП ,
 СЧ 0
 ПВ ТБУФ(М15)     ДАЙ ЛИСТ
 СДА 64-8
 УИ М12           А ЛИСТА
 СДА 64-2
 ИЛИ НОММЛ
 ЗП КЗ112-КЗ(М7)
 ПВ ЕNQ(М15)      ЗАХВАТ ДИСКА
СС УИА 3
 УИА -23(М15)
СЛЕД СЧ ЗОНЫСТ
 И Е33П25
 СЛЦ КЗ112-КЗ(М7)
 ИЛИ Е18
 ЗП РЯР-КЗ(М7)
 ПВ ФИЗОБМ(М16)   ЧТ.ЗОНЫ СТАТИСТИКИ
 СЧИ М15
 НТЖ Е2
 ПЕ СЛЕДУ    НЕ ИНИЦИАЛИЗИРУЮ СЛЕД. ЗОНУ
 СЧ Е3
 ЗП (М12)      СЧЕТЧИК
 СЧ ВОЛОДЯ
 ЗП 1(М12)      КЛЮЧ
 ПБ ЕЗ
СЛЕДУ НОП
 УИА -11(М11)  ДЛЯ ЦИКЛА В ПЕРЕНС
 СЧ 1(М12)
 НТЖ ВОЛОДЯ
 ПО КЛЮЧС         ЗОНА ХОРОШАЯ
 СЧ КЗ18А1-КЗ(М7)
 ПО РАЗМЗ
ОКОНЧ СЧ ЗОНЫСТ
 СЛЦ Е25
 ЗП ЗОНЫСТ
 НТЖ Е29П28-КЗ(М7)
 И Е29П28-КЗ(М7)
 ПЕ НЕПО
 СЧ ЗОНЫСТ
 НТЖ Е29П28-КЗ(М7)
 ЗП ЗОНЫСТ
НЕПО МОДА СЛЕД
 ЦИКЛ (М15)
РАЗМЗ СЧ Е3
 ЗП (М12)   СВ.АДР
 СЧ ВОЛОДЯ
 ЗП 1(М12)        КЛЮЧ
КЛЮЧС ЗП КЗ18А1-КЗ(М7)
 СЧ (М12)
 УИ М13
 И П77777
 НТЖ Е11
 ПО ОКОНЧ
 СЧИ М13
 ВЧОБ Е11
 ВЧ ПР14
 ПЕ РАЗМЗ
 СЛИ М13(М12)      А СВОБ.МЕСТА
ПЕРЕНС МОДА СТАТЗ-КЗ(М7)
 СЧ 11(М11)
 УИА 3
 МОДА (М11)
 ЗП 11(М13)
 МОДА ПЕРЕНС
 ЦИКЛ (М11)
*
*   Н А Ч А Л О  А П П Е Н Д И К С А
*
* АППЕНДИКС УДАЛИТЬ С РАЗРЕШЕНИЯ
*     Б.Ф. СИНЕНКИНА
 СЧ СТАТЗ+1-КЗ(М7)
 И Е48П40
 ЗП 1(М13)
 СЧ СТАТЗ+1-КЗ(М7)
 СДА 64-14
 СДА 64+9
 ИЛИ 1(М13)
 ЗП 1(М13)
 СЧ СТПАСП-КЗ(М7)
 И Е5П1
 ИЛИ 1(М13)
 ЗП 1(М13)
 ПБ КОНАП
Е48П40 КОНД М39В’-1’
Е5П1 КОНД В’37’
КЗ ВНЕШ СТПАСП
КОНАП НОП
*
*   К О Н Е Ц  А П П Е Н Д И К С А
*
 СЧ (М12)
 СЛЦ ПР14
 ЗП (М12)
ЕЗ СЧ РЯР-КЗ(М7)
 НТЖ Е18
 ЗП РЯР-КЗ(М7)
 ПВ ФИЗОБМ(М16)   ЗП СТ-КИ
 СЧ (М12)
 НТЖ Е11
 ПЕ ВКЗ1      ЗОНА НЕ ПОЛНАЯ
 УИА 1(М15)
 ПБ ОКОНЧ     НА ПОДПРАВКУ  СЛЕД. ЗОНЫ
ВКЗ1 СЧ РК-КЗ(М7)
 ПВ ТБУФ(М15)
 СЧ РК-КЗ(М7)
 ПВ DЕQ(М15)
 ПБ КЗ2
КОНСП СЧ РЯ
 НТЖ Е18
 ЗП РЯ
 ПВ ФИЗОБМ(М16)   ЗАПИСЬ НА МБ
 СЧ РЯ
 ПВ ТБУФ(М15)     ВОЗЬМИ АБЗАЦ
ПУСТМБ СЧ Е2
 ПВ ГАШЗАН(М15)   ОСВ.СП ДЛЯ ДР.РАБОТЫ
 ПВ УПРИ(М10)
 ПБ ВСЕЛИС
ОСВЛИ УИА ТЗФСТР(М10)
ОСВЛИС СЧИ М11
 УИА 1027
 СДА 64+5
 УИ М15           НОМЕР КУБА ОЗУ
 СЛИ М10(М15)
 СЧИ М11
 И П37
 УИ М14
 СЧ Е48(М14)
 НТЖ (М10)
 ЗП (М10)
 ПБ (М17)
ВСЕЛИС ПАМ 0
 СЧ ШЗБУДП
       ИЛИ   Е48-1(М1)
       НТЖ   Е48-1(М1)
 ЗП ШЗБУДП
 СЧ ЗАХВМЛ
 И Е48-1(М1)
 ПО НЕТСОБ
 НТЖ ЗАХВМЛ
 ПВ ВШГП(М15)
 СЧ 0
 ЗП ЗАХВМЛ
НЕТСОБ НОП ,
 СЧ ШКРЗ
 ИЛИ Е48-1(М1)
 НТЖ Е48-1(М1)
 ЗП ШКРЗ          ИЗ ШКАЛЫ ЗАДАЧ В РЕШЕНИИ
НЕМОНР НОП
 СЧ 0
 ЗП ПРМЗОН(М2)
 УИА 3
 АIF (∧Щ ЕQ 0).ЧЕР
 СЧ ПРЕДЕЛ
 И Е28
 ПЕ ВИЗ
ВЫХОД НОП 0
 ВХОД ВЫХОД
ЛИСТСВ ВНЕШ ВИЗ
.ЧЕР АNОР
ВОЗЛТ НОП
* ВОЗВРАТ ВИРТ. ЛИСТОВ
* НАХОДЯЩИХСЯ В ОЗУ
 СЧ Е5
 ПВ ЗАХЗАН(М15)
 СЧ ШЗНМБ+ЯЧТОП(М2)
 СДА 64+16
 И П17777
ВВЛО ПО ОТТР
 ВЧ Е1
 УИ М11
 ПРЕ 2
 СЧ ТОП(М11)
 УИ М16
 УИА 3
 ЗП РЯШ2
 СЧ СЧК+1
 УИА 2
 И ТОП(М11)
 ЗП ТОП(М11)
 ПРЕ 3
 СЧИ М11
 ВЧ АКТОП
 УИА ТЗФСТР(М10)
 ПЕ ЛВОП1
 УИ М11
 УИА ШЗНМБО(М10)
 УИА 1027
 СЧ ТМБ
 СЛЦ Е1
 ЗП ТМБ
ЛВОП МОДА ОСВЛИС
 ПВ (М17)
ЛВОП2 СЧИ М16
 ПБ ВВЛО
ЛВОП1 СЧ РЯШ2
 СДА 64+ТБДТР
 И П77
 УИ М17
 И Е6
 ПЕ ЛВОП
 УИА 1027
 СЧ 32(М2)
 ИЛИ Е48(М17)
 ЗП 32(М2)
 УИА 3
 ПБ ЛВОП
ОТТР УИА 3
 СЧ ШЗНМБ(М2)
 СДА 64+ССТБД
ВЗВ И П77777
 ПО ВЗК
 ВЧ Е1
 УИ М11
 ПРЕ 2
 СЧ ТБД(М11)
 УИ М16
 ПРЕ 3
 СЧ ВСЕЕД
 НТЖ КДД
 ПРЕ 1026
 И ТБД(М11)
 ЗП ТБД(М11)
 ПРЕ 3
 СЧИ М11
 ВЧ АКТБД
 ПО ТВОП  В ОП
 УИА ШЗНМБО(М10)
 МОДА ОСВЛИС
 ПВ (М17)
 УИА (М14)
 СЧИ М15
 АIF (∧ВS ЕQ 1).ВЗВ
 СДА 64+4
 ПО ВОЗВ
 АGО .ВЗВ1
.ВЗВ АNОР
 ВЧ ГРАНЬБ
 ПЕ ВОЗВ
.ВЗВ1 АNОР
 УИА 1(М14)
ВОЗВ СЧ Е1
 СЛЦ ТМБ(М14)
 ЗП ТМБ(М14)
ВЗ СЧ ТОБ
 СЛЦ Е1
 ЗП ТОБ
 УИА 3
 СЧИ М16
 ПБ ВЗВ
ТВОП СЛИА ТБД-ТОП(М11)
 МОДА ОСВЛИ
 ПВ (М17)
 СЧ Е1
 СЛЦ ОЗУ
 ЗП ОЗУ
 ПБ ВЗ
РЯШ2 КОНД В’0’
ВЗК СЧ ЯЧЛИС(М2)
 И П7777
 СЛЦ ЕДОБ
 ЗП РЯ
 УИА 1027
 СЛЦ ОЗУ
 ЗП ОЗУ
 СЧ Е5
 ПВ ГАШЗАН(М15)
 СЧ РЯ
 СЛЦ ТОБ
 ЗП ТОБ
 СЧ ЯЧЧТР(М2)
 СДА 64+СДТ
 НТЖ П777
 СЛЦ Е1
 И П777
 СЛЦ ТОБ
 ЗП ТОБ
КЗ10А УИА 3
 СЧ ЯЧОТБР(М2)
 И ЕОТКЗ
 ПО НЕОТБР        НЕТ ОТБРАК.ТРАКТОВ
 СЧ ОТБР(М2)
 И Е35
 ПО СТ412
 СЧ ОТБР(М2)
 АIF (∧ВS ЕQ 1).КЗ10
 И Е48
 СДА 64+44
 АGО .КЗ10А
.КЗ10 АNОР
 СБР КНАП
 СДА 64+42
 ВЧ Е4
.КЗ10А АNОР
 УИ М3            НАПРАВЛЕНИЕ
 СДА 64+3
 УИ М14
 СЧ ОТБР(М2)
 СДА 64+37
 И П37
 УИ М4            ТРАКТ
 СЧ ОТБР(М2)
 СДА 64+42
 И П7
 УИ М5            МБ
 СЛИ М3(М5)
 СЧ ОТБР(М2)
 СДА 64+30
 И П7
 УИ М12           ПРИЧИНА ОТБРАКОВКИ
 АIF (∧ВS ЕQ 1).КЗ10Б
 НТЖ Е3
 ПО НЕОТБР
.КЗ10Б АNОР
 УИА ’2003’
 СЧ ТОБ
 ВЧ Е1
 ЗП ТОБ
 СЧ ТМБ
 ПО Ш7
 ВЧ Е1
 ЗП ТМБ
Ш7 ПАМ 0
 СЧ ОЗУ
 ПО Ш8
 ВЧ Е1
 ЗП ОЗУ
Ш8 ПАМ 0
 СЧ ШЗНМБО(М3)
 И Е48(М4)
 НТЖ ШЗНМБО(М3)
 ЗП ШЗНМБО(М3)    ЗАКР.ТРАКТА
 АIF (∧ВS ЕQ 0).Ш8
 СЧИ М3
 ВЧ ГРАНЬБ
 ПО НЕОТБР ТР-Т НА РМД,РАСПЕЧАТКА НЕ НУЖНА
.Ш8 АNОР
 СЧ Е1
 ПВ ТБУФ(М15)
 УИА 3
 СДА 64-8
 УИ М14
 СДА 64-2
 ИЛИ МАЛКАТ
 ЗП РЯ1
 ПВ ФИЗОБМ(М16)
 УИА 3
 АIF (∧ВS ЕQ 0).Б6
 СЧИ М3
 СДА 64+4
 ПЕ КЗ10А1
.Б6 АNОР
 СЛИ М14(М3)
 СЧ БРАКТР(М14)
 НТЖ Е48(М4)
 ЗП БРАКТР(М14)
КЗ10А1 СЧ РЯ1
 НТЖ Е18
 ПВ ФИЗОБМ(М16)
 СЧ РЯ1
 ПВ ТБУФ(М15)
 УИА 3
 ПБ НЕОТБР
 СЛИА 8(М3)
 СЧИ М3
 СДА 64-42
 РЗБ КРЗБ
 СДА 64-8
 ИЛИ ТОШ
 ЗП КЗ23          2МБХХТ
 СЧ ОТБР(М2)
 СДА 64+45
 И Е1
 УИ М16
 СЧИ М4
 СДА 64-42
 РЗБ КРЗБ
 СДА 64-32
 МОДА (М16)
 ИЛИ ЗП
 ЗП КЗ23+1        НОМ.ТРАКТА  ЗП ИЛИ СЧ
 СЧ ОТБР(М2)
 И Е47
 ПЕ ЛИСТО         ОБМЕН ЛИСТОМ
 СЧ ОТБР(М2)
 СДА 64+35
 И П3
 ИЛИ С0
 СДА 64-16
ЛИСТ ИЛИ КЗ23+1
 ЗП КЗ23+1        НОМЕР СЕКТОРА ИЛИ 2 ПРОБЕЛА
 МОДА (М12)
 СЧ ПРИЧИН
 ЗП КЗ23+2        ПРИЧИНА ОТБРАКОВКИ
 СЧ ОТБР(М2)
 СДА 64-24
 РЗБ КМБТР
 ИЛИ МБТР
 ЗП КЗ23+5        МБ.ТР ИЗ СЛУЖЕБНЫХ СЛОВ
 СЧ ОТБР(М2)
 СДА 64-32
 РЗБ КНС
 ИЛИ ЗП+1
 ЗП КЗ23+3        НОМЕРА СЕКТОРОВ
 СЧ ОТБР(М2)
 СДА 64-40
 РЗБ КСЧ
 ИЛИ КЗП
 ЗП КЗ23+4        СЧЕТЧИКИ ЗАПИСЕЙ
 СЧ ОТБР(М2)
 СДА 64-44
 РЗБ КСЧ
 СДА 64-8
 ИЛИ ЭТ
 ЗП КЗ23+6        ЭТАЛОНЫ СЧЕТЧИКОВ ЗАПИСЕЙ
 МОДА НЕОТБР
 УИА (М15)
 УИИ ’32’(М15)
 МОДА КЗ23
 УИА (М16)
 ПБ ПЕЧ
ЛИСТО СЧ ПРОБ2
 ПБ ЛИСТ
 АIF (∧ВS ЕQ 0).НЕСВС
ГРАНЬБ ВНЕШН ДИСП70.ГРАНЬБ
КНАП КОНД М47В’1’М28В’3’
.НЕСВС АNОР
ЗП КОНД П’ЗП’
 КОНД П’СЧ’
ПРОБ2 КОНД П’  00’
С0 КОНД П’С0’
ПРИЧИН КОНД П’ ОШ НС’
 КОНД П’ КЧ НС’
 КОНД П’ КС НС’
 КОНД П’ СС НС’
 КОНД П’ МУ НС’
КНС КОНД П’333300’
КЗП КОНД М32П’ЗП’
КСЧ КОНД П’1111’
ЭТ КОНД П’Э0000↑’
КМБТР КОНД П’7037’
МБТР КОНД П’МБ0Т00’
КРЗБ КОНД П’77’
ТОШ КОНД П’2МБ00Т’
НЕОТБР ПАМ 0
 СЧ ГРАН
 ВЧ П77
 ПО НЕОТ
 МОДА ОСВБВ
 УИА (М16)
 МОДА НЕОТ
 УИА (М15)
 УИИ ’32’(М15)
 СЧ Е23
 ПБ ТВ40
ОСВБВ ТЕКСТ П’2ОСВОБ БУФ.ВЫВ↑’
ЗОНЫ КОНД М24В’600’М17В’1’
 КОНД М24В’601’М17В’1’
 КОНД М24В’7770’М17В’1’
ТЕКЗОН КОНД В’0’
ТЕККУС КОНД В’0’
ДАЙЗОН СЧ ТЕКЗОН
 ПО ДАЙЗО1
 МОДА (М4)
 НТЖ ЗОНЫ
 ПО  (М13)         ЗОНА В ОЗУ,БАЗА НА М11
 СЧ ТЕККУС
 ПВ ФИЗЗП(М16)
 ПБ ЧТЗОНЫ
ДАЙЗО1 ПВ ТБУФ(М15)
 УИА 3
 СДА 64-8
 УИ М11            М1 - БАЗА БУФЕРА
 СЧ НОММЛ1
 ПВ ЕNQ(М15)
ЧТЗОНЫ УИА 3
 МОДА (М4)
 СЧ ЗОНЫ
 ЗП ТЕКЗОН
 СЧИ М11
 СДА 64-2
 ИЛИ НОММЛ1
 СЛЦ ТЕКЗОН
 ЗП ТЕККУС
 ПВ ФИЗОБМ(М16)
 УИА 3
 ПБ (М13)
НЕОТ УИА 1027
 СЧ ЯЧЭК(М2)
 И ЕЭ52
 МОД ТУС+24
 ПЕ КОЗА-ОСА
ВТР УИА 3
 УИА -15(М10)
 УИА (М15)
 УИА (М5)
КЗ11 МОДА (М2)
 СЧ ШЗНМБ+15(М10)
 СДА 64+8(М15)
 И П17
 ПО КЗ14
 ЗП РЯ1
 И П3
 УИ М14
 СЧ РЯ1
 СДА 64+2
 УИ М13
 МОДА (М2)
 СЧ ОБЩТОМ(М13)
 СД КТОМ-1(М14)
 ЗП РЯ
 И П77
 УИ М12
 СЧ МДР(М2)
 И Е32(М5)
 ПЕ МЛМД
 УИИ М15(М12)
 СЛИА 32(М12)
 СЧ УКАЗД(М2)
 И Е32(М5)
 ПЕ ЭТОМД
* ОСВОБОЖБЕНИЕ МЛ
 СЛИА -56(М12)
 СЧИ М15
 ПВ ОСВМЛ(М15)    В АДАП
ЭТОМД НОП ,
 СЧ ТЗНМЛ(М12)
 ЗП БУЗА    НАША ЭВМ
 И Е41
 ПЕ ОБЩ
 СЧ ТЗНМЛ(М12)
 И Е13
 ПЕ КЗ12А        ЛЕНТА ЕС
 СЧ ТЗНМЛ(М12)
 СДА 64+38
 И П3
 ПО КЗ12А         НЕ ДР.БОБИНП
 СЧ
 ЗП ТЗНМЛ(М12)
  ПБ КЗ14
КЗ12А СЧ ТЗНМЛ(М12)
 И АВС
 ЗП ТЗНМЛ(М12)
 СЧ Е47
 ИЛИ ЕАР
 И ТЗНМЛ(М12)
 ПЕ КЗ14          СИС.МД ИЛИ АРХБОБ
 СЧ ТЗНМЛ(М12)
 ИЛИ ПРУПР-КЗ(М7)
 ЗП ТЗНМЛ(М12)
ОБЩЛУ НОП
 СЧ ПРУПР-КЗ(М7)
 ПО КЗ14
*ОТМЕТИМ ИНДИВИДУАЛЬНЫЙ НОСИТЕЛЬ УП.ЗАДАЧИ
 СЧ БУЗА
 СДА 64+11
 И Е1
 УИ М4
 МОДА ДАЙЗОН
 ПВ (М13)
 СЧ БУЗА
 И Е1
 УИ М14
 СЧ БУЗА
 СДА 64+1
 И П1777
 УИ М13
 СЛИ М13(М11)
 СЧ Е24
 СД КСН(М14)
 ИЛИ (М13)
 ЗП (М13)         24Р. КВАНТА ЗОН ПАРОЛЕЙ-БОБ УПР.ЗАДАЧИ
 ПБ КЗ14
АВС КОНДС М43В’13’В’7777777’    БЕЗДУБ И УПНОВ=Е21
АВС КОНДБ М43В’13’В’1777777’
КРОБЩ ПАМ 1
БУЗА ПАМ 1
МЛМД СЧ РЯ
 СДА 64+12
 И П17
 УИ М3 Ч.КУСКОВ
 СЧ ВСЕЕД
 СДА 64-48(М3)
 СДА 64(М12)
 УИА ’2003’
 НТЖ КУСКИ
 ЗП КУСКИ ОТДАТЬ СЕГМЕНТЫ НА МД
 УИА 3
 ПБ КЗ14
ОБЩ СЧ ТЗНМЛ(М12)
 СДА 64+41
 ПО КЗ14          ОБЩАЯ БОБИНА СВОБ.
 УИА 1027
 СЧ ТЗНМЛ(М12)
 ИЛИ ЕОТМД
 ЗП ТЗНМЛ(М12)
 СЧ Е48(М12)
 НТЖ ВСЕЕД
 И ПЕРЕ2
 ЗП ПЕРЕ2
 СЧ ПРЕДЕЛ
 ИЛИ ЕОТКД
 ЗП ПРЕДЕЛ
КЗ14 СЛИА 1(М5)
 УИА 3
 СЧИ М5
 И Е1
 СДА 64-2
 УИ М15           ДЛЯ ОПРЕД.НОМ.КУСКА В ШЗНМБ
 ПЕ КЗ11
 МОДА КЗ11
 ЦИКЛ (М10)
 СЧ ТЕККУС
 ПО ОС
 ПВ ФИЗЗП(М16)
 СЧ ТЕККУС
 ПВ DЕQ(М15)
 СЧ ТЕККУС
  ПВ ТБУФ(М15)
 УИА 3
 СЧ КРОБЩ
 ПО ОС
 ИЛИ РАОБЩ
 ЗП РАОБЩ
 СЧ НОММЛ1
 УИА 3(М16)
 И Е42
 ПО КЗМЛ1
 УИА 4(М16)
КЗМЛ1 УИИ М17(М6)
 ПВ ОСВУ(М6)
 УИИ М6(М17)
 УИА 3
ОС СЧ 0
 ЗП ОБЩМЛ(М2)
РОМБ5 УИА ’2003’
 СЧ ТЕРЕЗА(М2)
 ИЛИ ШАКТИ
 ЗП ШАКТИ
 СЧ ШКРАБН
 ИЛИ ЕАКТИВ
 ЗП ШКРАБН
 СЧ ТЕРЕЗА(М2)
 ИЛИ ШЗНКТ
 НТЖ ТЕРЕЗА(М2)
 ЗП ШЗНКТ
 УИА -ДТУС+1(М17)
ОТКАВН УИИ М16(М17)
 СЛИА ДТУС-1(М16)
 СЧИ М1
 УИА 3
 ПВ ОТВНИМ(М15)
 МОДА ОТКАВН
 ЦИКЛ (М17)
 СЧИ М1
 НТЖ МУЛЬЗ         РАБОТАЛА С КАНАЛАМИ
 ПЕ НЕМУЛЬ
 ЗП МУЛЬЗ
 РЕГ ’110’
 РЕГ ’111’
 РЕГ ’114’
 РЕГ ’115’
НЕМУЛЬ НОП ,
 СЧ Е48-1(М1)
 СДА 64+17
 ПЕ РОМБ6
 СЧ ОПТИМ
 ИЛИ Е48-32(М1)
 НТЖ Е48-32(М1)
 ЗП ОПТИМ
 СДА 64+30
 ПЕ РОМБ6
 СЧ ОПТИМ
 СДА 64+15
 ПО РОМБ6
 ЗП СМ
 СЧ ОПТИМ
 И П77777
 ЗП ОПТИМ
 СЧ СМ
 СДА 64-2
 ПВ ТБУФ(М15)
РОМБ6 СЧ ТРТЕСТ
 СДА 64+33
 НТЖ ТУСП-1(М1)
 И П77777
 ПЕ Ш
 СЧ ТРТЕСТ
 И П77777
 ПО ОЧТРТ         НЕТ ЗП В ТЕСТ.РЕЖИМЕ МБ
 УИ М15
 СЧ ТРТЕСТ
 СДА 64+15
 УИ М16
 СЧ ШЗНМБО-8(М15)
 НТЖ Е48(М16)
 ЗП ШЗНМБО-8(М15) ВОЗВРАТ ТРАКТА СИСТЕМЕ
ОЧТРТ СЧ
 ЗП ТРТЕСТ
Ш СЧ ШИФРМ(М2)
 РЗБ ПРОБ6
 ЗП ВЫДТТ+2-КЗ(М7)
 СЧ ШИФРМ(М2)
 СДА 64-24
 РЗБ ПРОБ6
 ЗП ВЫДТТ+3-КЗ(М7)
 СЧ СТАТЗ-КЗ+14(М7)
 ЗП ВЫДТТ-КЗ+4(М7) ЧАСЫ И МИН.КОНЦА РЕШ.
 УИА УЗ(М15)
 СЧ ПРУПР-КЗ(М7)
 ПЕ ЗАДУПР
 СЧ СТАТЗ+7-КЗ(М7)
 СДА 64+17
 И П177
 УИА КОНЕЦЗ(М15)
 ПО КЗ26          КЗ
 НТЖ П60
 УИА ЧБ(М15)
 ПО КЗ25          ОШИБКА МБ БЭСМ-6
 НТЖ КСБОЙ
 УИА ЧП(М15)
 ПО КЗ25          СБОЙ МАШИНЫ
 НТЖ П153
 УИА ЧД(М15)
 ПО КЗ25          ОШИБКА МД БЭСМ-6
 НТЖ П17
 УИА ЧЛ(М15)
 ПО КЗ25          ОШ.МЛ
 УИА ОЗ(М15)
КЗ26 СЧ СТАТЗ-КЗ+5(М7)
 И Е16П1
 СДА 64-5
 СЛЦ СТАТЗ-КЗ+10(М7)
КЗ25 И Е1П25
 СЛЦ КОМВРЕ
 ЗП КОМВРЕ
 СЛЦ СТАТЗ-КЗ+15(М7)
КЗ28 ИЛИ Л64
 ЗП СТАТЗ-КЗ+15(М7)
 СЧИ М15
 ИЛИ ПРИЧСН
 ЗП ВЫДТТ-КЗ(М7)
 СЧ Т
 СЛЦ ЖИЗОС
 ИЛИ Л64
 РЖА 0
 ЗНАК 0
 ЗП РЯО
 СЧ СТАТЗ-КЗ+15(М7)
 РЖА 3
 ВЧ ТКОМУЗ
 РЖА 0
 ДЕЛ РЯО
 УМН =Е’100’
 РЖА 3
 СЛ Л64
 НТЖ Л64
 И П777
 ПВ ПЕРЕВ(М15)
 ИЛИ =П’ М=000’
 АIF (∧ВS ЕQ 0).КЗ27
 ЗП ВЫДТТ-КЗ+6(М7)
 АGО .КЗ24
.КЗ27 АNОР
 ЗП ВЫДТТ-КЗ+5(М7)
.КЗ24 АNОР
 АIF (∧ВS ЕQ 0).ДИД
 УИА -2(М15)
ПЕРЕП МОДА (М15)
 СЧ ШПЭГ+2(М2)
 МОДА (М15)
 ЗП ВЫДТТ-КЗ+9(М7)
 МОДА ПЕРЕП
 ЦИКЛ (М15)    ПЕРЕНОС ПРИЧИНЫ ИЗ ИПЗ
 СЧ ИНФ(М2)
 ЗП ВЫДТТ-КЗ+5(М7)
.ДИД АNОР
КЗ24 НОП
 СЧ КЗ6-КЗ(М7)
 И Е48П41
 РЗБ =П’037700’
 ИЛИ ВЫДТТ+1-КЗ(М7)
 ЗП ВЫДТТ+1-КЗ(М7)
 СЧ ПОДЧ(М2)
ДИД НОП 0
 УИА 1027
 ПО НЕТПОД
 ЗП РЯ
 НЕД 0
 УИ М15
 СЧ Е48-1(М15)
 ИЛИ МШГП
 ЗП МШГП
 СЧ Е48-1(М15)
 ИЛИ ОСПОД
 НТЖ Е48-1(М15)
 ЗП ОСПОД
 СЧ ШКОК
 ИЛИ Е48-1(М15)
 ЗП ШКОК
 УИА 3
 УИА ЭК74(М14)
 МОД ТУСП-1(М15)
 УИА (М13)
 СЧ 0
 ЗП ПОДЧ(М2)
 ЗП МАСГЛ(М13)
 СЧ ШПЭГ(М13)
 И Е48П27
 ЗП ШПЭГ(М13)
 СЧ 36(М13)
 И Е41П1
 ИЛИ КС
 ЗП 36(М13)
 СЧИ М14
 ЗП 3(М13)
 СЧ 5(М13)
 И Е16П32
 ИЛИ П2007
 ЗП 5(М13)
 СЧ РЯ
 НТЖ Е48-1(М15)
 ПБ ДИД
НЕТПОД  СЧ ЗУК
 ПВ ВШГП(М15)
 СЧ
 ЗП ЗУК
 СЧ ШКНТ
 ИЛИ Е48-1(М1)
 ЗП ШКНТ
 УИА -МЧПЗ(М17)
 СЧ ВСЕЕД
 НТЖ Е48-1(М1)
 ЗП СМ
НАСЛЗ СЧ ШКРЗ
 И Е48+МЧПЗ(М17)
 ПО НАЗ
 МОД ТУСП+МЧПЗ(М17)
 СЧ ПОДЧ
 И СМ
 МОД ТУСП+МЧПЗ+НПЗ(М17)
 ЗП ПОДЧ
НАЗ МОДА НАСЛЗ
 ЦИКЛ (М17)
 СЧ ШК31
*   ДЛЯ ХАРИНОЙ
 И Е48-1(М1)
 ПО ДАЛЬШЕ
 НТЖ ШК31
 ЗП ШК31
 СЧ Е31
 ПВ ТВ67А(М15)
ДАЛЬШЕ СЧ ОСПОД
 И СМ
 ЗП ОСПОД
 УИА 3
 СЧ АВОС(М2)
 И Е32
 ПЕ ИСКЛЮЧ        ИНКОГНИТО
 МОДА ИСКЛЮЧ
 УИА (М17)
 УИИ ’32’(М17)
 МОДА ВЫДТТ-КЗ(М7)
 УИА (М16)
 СЧ Е23
 ПБ ТВ40
ИСКЛЮЧ СЧ ШКГР
 И Е48-1(М1)
 ПО НЕГЛАВ
 УИА 1027
 НТЖ ШКГР
 ЗП ШКГР
 УИА -15(М15)
СЛЕЗА СЧ ГЛГРУП+15(М15)
 НТЖ Е48-1(М1)
 ПЕ НЕЭТА
 ЗП ГЛГРУП+15(М15)
 ЗП ГЛСОБГ+15(М15)
НЕЭТА МОДА СЛЕЗА
 ЦИКЛ (М15)
НЕГЛАВ УИА 3
 УИ 0
 УИА -15(М15)
СЛЕЗАД СЧ ЗАГРУП+15(М15)
 И Е48-1(М1)
 ПО НЕТА
 УИА 1027
 НТЖ ЗАГРУП+15(М15)
 ЗП ЗАГРУП+15(М15)
 СЧ Е48-1(М1)
 ИЛИ ШКЗАПР+15(М15)
 ЗП ШКЗАПР+15(М15)
 СЧ ГЛГРУП+15(М15)
 ПО НЕТА
 НЕД 0
 УИ М16
 ИЛИ ГЛСОБГ+15(М15)
 ЗП СМ
 СЧ ШКРЗ
 И Е48-1(М16)
 ПО НЕТА
 СЧ СМ
 ПВ ЗАПВЕТ(М16)
НЕТА УИА 3
 МОДА СЛЕЗАД
 ЦИКЛ (М15)
*
 УИА -ДЛИН+1(М16)
 СЧ 0
ОЧИПЗ МОДА (М2)
 ЗП ДЛИН-1(М16)
 УИА 3
 МОДА ОЧИПЗ
 ЦИКЛ (М16)
 УИА 1027
 СЧ Е48-1(М1)
 И ШККТ
 НТЖ ШККТ
 ЗП ШККТ
 УИА 3
 СЧИ М1
 НТЖ НЗТСВТ
 ПЕ НЕТЗ          НЕ ТРАНС.ЗАДАЧА В.Т.
 ЗП НЗТСВТ
НЕТЗ НОП ,
 УИА 1-КГРУП(М16)
 СЧ Е48-1(М1)
 НТЖ ВСЕЕД
 ЗП РЯ
ЧИСГРУ СЧ РЯ
 И ГРУП+КГРУП-1(М16)
 ЗП ГРУП+КГРУП-1(М16)
 МОДА ЧИСГРУ
 ЦИКЛ (М16)
 УИА 1-КЛЕСТ(М16)
ЧИСЛЕС СЧ РЯ
 И ЛЕСТ+КЛЕСТ-1(М16)
 ЗП ЛЕСТ+КЛЕСТ-1(М16)
 МОДА ЧИСЛЕС
 ЦИКЛ (М16)
 СЧ НОМАРС
 НТЖ Е48-1(М1)
 ПЕ НЕМАРС
 ЗП НОМАРС
 ЗП НОМАРС+1
НЕМАРС СЧ ШККЗ
 НТЖ Е48-1(М1)
 ЗП ШККЗ
 ПО НЕТКЗ
 СЧ ШКИВ
 ИЛИ Е6
 ЗП ШКИВ
НЕТКЗ  УИА 3
 СЧ ШКРЗ
СОБЗАД ЗП РЯ
 МОДА ВЫКИД3-КЗ(М7)
 УИА (М16)
 ПО ЗАНОСМ
 НЕД 0
 УИ М2
 ИЛИ Е31
 ПВ ЗАПВЕТ(М16)
 УИА 3
 СЧ РЯ
 НТЖ Е48-1(М2)
 ПБ СОБЗАД
ЗАДУПР СЧИ М15
 ИЛИ ПРИЧСН
 ЗП ВЫДТТ-КЗ(М7)
 СЧ СОБЫТ(М2)    ГОТ УПР
 ЗП ВЫДТТ+4-КЗ(М7)
 СЧ ИНФСЛП(М2)
 ЗП ВЫДТТ+5-КЗ(М7)
 ПБ КЗ24
П153 КОНД В’153’
КСБОЙ КОНД В’161’        КОНСТ.СБОЯ МАШИНЫ
ПРИЧСН КОНД М40П’2’М32В’143’М24В’143’М16В’143’
НОМСЕК КОНД М17В’1’В’170’ 36 ТР.0-СЕКТОР С УС СП
КС КОНД М41В’74’
БЮД РЖА 3
 СЧ СТАТЗ-КЗ+1(М7)
 СДА 64-5
 И Е25П1
 ЗП РЯО        ЗАК.ВР.
 СЧ СТАТЗ-КЗ+8(М7)
 СДА 64+16
 И П377
 УМН К144
 СЧМР 64
 ЗП РЯО1       ЗАК.СМ
 СЧ СТАТЗ-КЗ+7(М7)
 СДА 64+17
 И П177
 ПО БЮД1КЗ     НОРМАЛЬНЫЙ КОНЕЦ
 НТЖ П60
 ПО БЮД1ЧП     ОШ.МБ
 НТЖ КСБОЙ
 ПО БЮД1ЧП     СБОЙ ВС
 НТЖ П153
 ПО БЮД1ЧП     ОШ.МД
 НТЖ П17
 ПО БЮД1ЧП     ОШ.МЛ
БЮД1КЗ СЧ СТАТЗ-КЗ+10(М7)
 И Е25П1
 ВЧОБ РЯО
 ПЕ ОШВР       ВОЗВРАТ=0
 ЗП ВР1        ВР.ЗАК-ВР.ИСП.
ОШВР СЧ АВОС(М2)
 И =М29В’5’
 ПЕ БД    ИНКО,ПРО
 СЧ СТАТЗ-КЗ+8(М7)
 И Е16П1
 ВЧОБ РЯО1
 ПЕ БД
 ЗП МЕТР1      СМ ЗАК.-СМ ИСП.
 ПБ БЮДЖ
БЮД1ЧП СЧ РЯО
 ЗП ВР1        ВОЗВРАТ=ЗАКАЗ
БД СЧ РЯО1
 ЗП МЕТР1      ВОЗВРАТ=ЗАКАЗ
БЮДЖ СЧ 0   ПРИХ. МОЖЕМ ИЗ ЗАД.ВВОДА
 ПВ ТБУФ(М15)
 УИА 3
 СДА 64-8
  УИ М4
 СДА 64-2
 ИЛИ КУСЛИМ
 СЛЦ НОММЛ1
 ЗП КУСЛМ      КУСЧТ.710 З.
 СЧ КУСБ
 ПЕ БЮДЗ
 ПВ ТБУФ(М15)
 УИА 3
 СДА 64-8
 УИ М13
 СДА 64-2
 ИЛИ КУСБОЛ
 СЛЦ НОММЛ1
БЮДЗ ЗП КУСБ
 ПВ ЕNQ(М15)
КОРР СЧ КУСЛМ
 ПВ ФИЗОБМ(М16)
 УИА 3
 НТЖ 0
 ПО КОРР
 МОДА БЮДА
 ПИО (М1)       ПРИШЛИ ИЗ З-ЧИ ВВОДА
 СЧ СТАТЗ-КЗ+6(М7)
 СДА 64-29
 И Е48П46
 ПО БЮДА
 НЕД П77777
 УИ М10        КАТЕГ.РЕШ.
 СЧ СТАТЗ-КЗ(М7)
 СБР МАСКО(М4)
 ПО БЮДА
 АIF (NОТ ∧АС).АС17
 ЗП R2
 НТЖ =М40Х’41’
 ПО ОТД2
 СЧ R2
 И =М44В’17’
 ПО ОТД2
 НТЖ =М44Х’9’
 ПО ОТД2
 НТЖ =М44Х’9’
 ПБ ОТД1
ОТД2 СЧ R2
ОТД1 СДА 64+40
 АGО .АС16
.АС17 АNОР
  СДА 64+40
.АС16 АNОР
  ПВ ИЗ10В8(М15)
 ЗП НОМИНД  ЗАПИС. НОМЕР ОТД. НА ПОВТ.
 УИ М11  ЗАПОМИНАЕМ НОМЕР ОТДЕЛА
 СЛИ М11(М4)
  СДА 64-2    УМН.НА 4
  УИ М5   АДРЕС ЯЧЕЕК ОТДЕЛА
ПОД СЛИ М5(М4)
 СЧ СМНАОТ(М5)
 СЛЦ МЕТР1
 ЗП СМНАОТ(М5) ВОЗВРАТ БУМАГИ
 МОДА (М10)
 СЧ ДЕЖ(М5)
 СЛЦ ВР1
  МОДА (М10)  ОТДЕЛУ
  ЗП ДЕЖ(М5)
 СЧ СТАТЗ+13-КЗ(М7)
 СЛЦ СЧЕТОТ(М11)
 ЗП СЧЕТОТ(М11) СЧЕТ ОТД. И ИНД. ПОЛЬЗ.
БЮДА СЧ ГОД1
 СДА 64-45
 ЗП НОММ
 СЧ 0
 ЗП РПКВ
 УИА 2(М10)
***ПРОВЕРКА ВРЕМЕНИ ВКЛ. ЛИМ.ДЕЖ.ЭКС. ЗАДАЧ
ПРЛДЭ МОДА (М10)
 СЧ ВРЕД(М4) НАЧАЛО ИНТЕРВАЛА
 И Е1П24
 ЗП РЯО  1 ИНТ.
 УИА 0(М16)
 ВЧ ВРЕМЯ
 ПЕ ПРО  НА ПРОВЕРКУ 2 ИНТЕРВАЛА
 СЛИА 1(М16)
ПРО МОДА (М10)
 СЧ ВРЕД(М4)
 СДА 64+24
 ЗП РЯО1  ВТОРОЙ ИНТ.
 ВЧ ВРЕМЯ
 ПЕ ПРОК
 СЛИА 2(М16)
ПРОК СЧ РЯО   1 ИНТЕРВАЛ
 ВЧ РЯО1   2 ИНТЕРВАЛ
 ПЕ ХОДЫ
 СЧИ М16
 НТЖ Е1
 АIF (∧АС).АС105
 ПО ККОН
РЗРКАТ СЧ РПКВ
 ИЛИ Е27(М10)
 ЗП РПКВ
ККОН МОДА ПРОДЛЭ
 ПИО (М10)
 СЛИА -1(М10)
 ПБ ПРЛДЭ
 АGО .АС106
.АС105 АNОР
 ПО КОН    КАТ.ВНЕ ИНТ.
.АС106 АNОР
ПРОДЛЭ СЧ КУСБК
 ПЕ ПЕРЕСЛ НА ПЕРБС. ИНД.
ВК1 СЧ КУСБ
 ЗП КУСБК ЗАПИСЫВ. КУС.
 УИА 3
 ПВ ФИЗОБМ(М16) ЧТ.БК1
 НТЖ 0
 ПО ВК1
ПЕРЕСЛ УИИ М3(М13)  М13->М3
 РЖА 3
ПЛ2 УИИ М5(М3)  М3->М5
 УИА 3
  СЧ М+7(М5)
  И П1777
 УИ М3
 ЗП НВК ЗАПИСЫВ.  ССЫЛКУ ДЛЯ ВОЗМ. ПЕРЕС.
 ПО КОНЦ    ПРОСМ.ВЕСЬ КАТ
 СЛИ М3(М13)
 СЧ М+6(М3)
 И Е48Е47
 НТЖ Е48Е47
 ПО ЗПОВТ ЗАДАЧА С ПОВТОРОМ
ИЗПОВТ СЧ М+6(М3)
  И Е32  ЗА ДАЧА СО ЖДЕМ
 ПЕ ПЛ2  НЕ РАСС. ЗАД. СО ЖД.
 УИА 3(М16)
 АIF (NОТ ∧АС).АС12
 МОДА ПЛ2  ДЛЯ ВОЗВ. ПОСЛЕ ПОДСЧ.
 УИА (М12)
 СЧ М+6(М3)
  И Е27  РАЗРЕШЕНО ЛИ ВКЛЮЧАТЬ В ПЛАНИРОВЩИК
  ПО ПОДСЧ
  СЧ М+6(М3) ОТН ЛИ К ДАННОЙ КАТ.
  И Е44(М10)
  ПО ПЛ2  НЕ ОТНОСИТСЯ
ПРОПУС ПБ ПРОХОР
ПРОХОР СЧ М+7(М3)
 И НОМО
 АGО .АС11
.АС12 АNОР
 СЧ М+6(М3)
 И Е27
 ПО ПЛ2    ЗАД.НЕ ДЛЯ БС
 СЧ М+6(М3)
 И Е47  ЗАПР.
 ПЕ НЕВКЛ
 СЧ НЕИЩ(М4)
 И Е25
 ПЕ ПРОВРЖ   НА ПРОВЕРКУ РЕЖИМА  ВКЛ. ТОЛЬКО ПЕРЕЧ.
ПРОПУС СЧ 6(М3)
  И Е48
  ПЕ ДАЗМ  ДА ЗАМОРОЖЕНА
ИЗНЕ СЧ НЕИЩ(М4)
  И Е48
  ПЕ ПЛ2 ИДЕТ ТОЛЬКО ПЛАН
ПРОХОР СЧ М+7(М3)
  И НОМО
.АС11 АNОР
 СДА 64+15-2
 ПО ПЛ2        0-Й ОТДЕЛ НЕ ПРОСМ.
 УИ М15        N ОТД.*4
 СЛИ М15(М4)
 АIF (∧АС).АС26
 СЧ СМНАОТ(М15)
 И Е25
 ПО ВНЕБС      ОТДЕЛ ВНЕ Б.С.
 СЧ СМНАОТ(М15)
 ИЛИ РПКВ
 И Е27П25
 ПО ПЛ2    ОТД.НЕ ПРОСМ
 ЗП РПОВ
 УИА 3(М10)
ВЫЧКАТ СЛИА -1(М10)
 СЧ Е44(М10)
 И М+6(М3)
 ПЕ ЕСКАТ   НАШЛИ КАТ.ВВОДА
 МОДА ВЫЧКАТ
 ПИНО (М10)
ЕСКАТ СЧ Е27(М10)
 И РПОВ
 ПО ПЛ2   КАТ.НЕ ПРОСМ
.АС26 АNОР
 СЧ (М3)
 СДА 64-5
 И Е25П1
 ЗП ВР1        ЗАК.ВР.
  СЧ М+5(М3)
  СДА 64+16
 И П377
 УМН К144
 СЧМР 64
 ЗП МЕТР1      ЗАК.СМ
ПРОСК НОП 0
 УИА -1(М16)
  МОДА (М10)
  СЧ ДЕЖ(М15)
  И ШТУБ
 ВЧ ВР1
  ПЕ ПЕРВД  ПЕРЕВОД В ДР. КАТ.
 ЗП ВР1        ОСТ.ВР.ПО КАТЕГ.
 УИА 2(М16)
  СЧ СМНАОТ(М15)    ВЫДЕЛЯЕМ СМ НА ОТДЕЛ
  И Е1П24
 ВЧ МЕТР1
  ПЕ КОНВД КОНЧИЛАСЬ БУМАГА
 ЗП МЕТР1
 СЧ СМНАОТ(М15)
 СДА 64+30
 ПО ПЛ2
 ВЧ Е1
 СДА 64-30  ЗАХОДЫ
 ЗП RОТ
 МОДА ПОДСЧ ПОДСЧЕТ ТОЛЬКО ПОСЛЕ П-КИ РЕСУРСОВ
 ПВ (М12)
 СЧ СМНАОТ(М15)
 И Е30П25   СОХ.ПРИЗ.
 ИЛИ МЕТР1      БУМАГА
 ИЛИ RОТ        ЗАХОДЫ
 ЗП СМНАОТ(М15)
 МОДА (М10)
  СЧ ДЕЖ(М15)
 И АЦПУ
 ИЛИ ВР1
  МОДА (М10)
  ЗП ДЕЖ(М15)
* ПРИСВОЕНИЕ ПРИОРИТЕТА ПО КАТЕГОРИЯМ
 АIF (NОТ ∧АС).АС32
 СЧ 6(М3)
 И Е47
 ПО ЭК
 СЧ П57    СУПЕР
 ИЛИ 7(М3)
 ЗП 7(М3)
 ПБ ПРИКАТ
ЭК СЧИ М10
 И Е2
 ПО ПРИКАТ    ЛИМ,ДЕЖ
 СЧ 7(М3)
 И Е44
 ПЕ ПРИКАТ     ЭКС-ТЕЛЕ
 СЧ П30      ПРИОЭКС
 ИЛИ 7(М3)
 ЗП 7(М3)
 ПБ ПРИКАТ
.АС32 АNОР
 СЧ 7(М3)
 СДА 64+41
 И П7
 ВЧ П5
 ПО ПРИКАТ     ВЫШЕ 5 КЛАССА НЕ ПОДНИМАТЬ
 СЧИ М10
 И Е2
 СДА 64-40
 СЛЦ 7(М3)
 ЗП 7(М3)      ЭКС.З-ЧИ НА КЛАСС ВЫШЕ
ПРИКАТ СЧИ М10
 СЛЦ Е1
 СДА 64-39
 ИЛИ 7(М3)
 ИЛИ Е45
 ЗП 7(М3)      ПРИОРИТЕТ КАТЕГОРИИ
 УИИ М15(М10)
УНИЧТ СЧ М+6(М3)
 МОДА РАЗБЛ
 ПИО (М15)      НИЖЕ ДЕЖ.КАТЕГ.НЕТ
 СЛИА -1(М15)
 И Е44(М15)
 НТЖ 6(М3)
 ЗП 6(М3)      СНЯЛИ ПРИЗН.КАТ-И
 ПБ УНИЧТ
ВНЕБС СЧ 6(М3)
 ИЛИ Е45П42
 НТЖ Е45П42     ГАСИМ КАТ.Б.С.
РАЗБЛ НТЖ Е27
 ЗП 6(М3)      СНЯЛИ БЛОК
 ЗП ПРОИЗВ
* ПЕРЕСТРОЙКА ОЧЕРЕДИ
 СЧ 7(М3)
 СДА 64-3
 СДА 64+26
 НТЖ П77777
 ЗП R1         ПРИОР.РАССМ.З-ЧИ
 СЧ 7(М3)
 НТЖ 7(М5)
 И П1777
 НТЖ 7(М5)
 ЗП 7(М5)      ВРЕМЕННО ИСКЛ.З-ЧУ ИЗ ОЧЕРЕДИ
 УИИ М16(М13)
ПРИТ2 УИИ М15(М16)
 СЧ 7(М15)
 И П1777
 УИ М16
 ПО ПРИТ3      КОНЕЦ ОЧЕРЕДИ
 СЛИ М16(М13)
 СЧ 7(М16)
 СДА 64-3
 СДА 64+26
 НТЖ П77777
 ВЧ R1
 ПО ПРИТ2      У РАССМ.З-ЧИ ПРИОР.НЕ ВЫШЕ ТЕК.
ПРИТ3 СЧ 7(М15)
 И Е48П11
 ИЛИ НВК
 ЗП 7(М15)     ССЫЛКА НА РАССМ.З-ЧУ
 СЧИ М16
 НТЖ 7(М3)
 И П1777
 НТЖ 7(М3)      ССЫЛКА ИЗ РАССМ.З-ЧИ
  ИЛИ НОМО  ОСВОБОЖД. МЕСТО
  НТЖ НОМО
  ЗП М+7(М3) ПОД КАНАЛЫ СЧЕТА
 СЧ ГОД1
 И П7
 СДА 64-10  С 11-13 РР  N БЭСМ
 ИЛИ М+5(М3)
 ЗП М+5(М3)
  ПБ ПЛ2 НА ВЫБОР СЛЕД
****КОНЕЦ ОСНОВНОГО БЛОКА ВЫБОРКИ ПО ЗАДАЧАМ
 АIF (∧АС).АС7
ДАЗМ СЧ 5(М3)
  СДА 64+10
 И П37
  СДА 64-1  ПОЛУЧ НОМЕР ЗАМОР.
  УИ М17
  УИА 0(М16)
 ПО ИЗНЕ ЗАДАЧА БЫЛА ЗАМОР.ПО НЕХВАТКЕ РЕС.
  МОДА (М4)
  СЧ ЗАМОР+1(М17)
 И Е1П24
  ВЧ ВРЕМЯ
  ПО ПЛ2  НЕ ВРЕМЯ ПОДКЛ. ЗАДАЧИ
  МОДА (М4)
  СЧ ЗАМОР+1(М17)
 И Е25П48
 ПО ДЗАМ
 УИА 1(М16)
  СЧ НЕИЩ(М4)
  И Е48
 ПЕ СЧПЛ
 СЧ НЕИЩ(М4)
 И Е39
 ПО ПЛ2        ТОЛЬКО СЧЕТ
СЧПЛ СЧ ЗАМОР+1(М4)
 И Е25П48
  СЛЦ Е25
  ЗП РЯО
  МОДА (М4)
  СЧ ЗАМОР+1(М17)
 И Е25П48
  НТЖ РЯО
  ПЕ ПЛ2
  СЧ ЗАМОР(М4)
  НТЖ РЯО
 ПЕ ПОДИТ
 СЧ НЕИЩ(М4)
 ИЛИ Е48
 НТЖ Е48
 ЗП НЕИЩ(М4)
ПОДИТ СЧ ЗАМОР+1(М4)
 И Е1П24
  ИЛИ РЯО  ЗАП СЛЕД ПО ПЛАНУ
  ЗП ЗАМОР+1(М4)
ДЗАМ СЧ 6(М3)
 НТЖ Е48
 ЗП 6(М3)
 СЧ 5(М3)
 И О15П11
 ЗП 5(М3)
 ЗП ПРОИЗВ
 СЧ 0
 МОДА (М4)
 ЗП ЗАМОР(М17)
 МОДА ИЗНЕ
 ПИО (М16)
 ПБ ПРОХОР
.АС7 АNОР
ПЕРВД СЧИ М10
  ПО КОНВД
 УИИ М16(М10)
 СЛИА -1(М16)
 СЧ НЕИЩ(М4)
 И Е47(М16)
 ПЕ КОНВД    ПЕР.В ДР.КАТ.ЗАПР
 АIF (∧АС).АС104
 СЧ РПОВ
 И Е27(М16)
 ПО КОНВД   СЛЕД.КАТ.НЕ ПРОСМ
.АС104 АNОР
  СЧ М+6(М3)
  ИЛИ Е44(М16)
  ЗП М+6(М3)
 ЗП ПРОИЗВ
 АIF (∧АС).АС102
 УИИ М10(М16)
 ПБ ПРОСК
 АGО .АС103
.АС102 АNОР
 ПБ ПЛ2
.АС103 АNОР
КОНВД СЧ НЕИЩ(М4)
 И Е28
 ПЕ НЕВКЛ1
 СЧ М+6(М3)
 И Е48
 ПЕ ПЛ2 УЖЕ ЗАМОРОЗИЛИ РАСПЕЧ. НЕ НУЖНО
  СЧ М+6(М3)
  ИЛИ Е48
  ЗП М+6(М3)
 ЗП ПРОИЗВ  ФИКСИРУЕМ ЧТО ЗАД. ЗАМОРОЗИЛИ
РСП МОДА 1(М16)
  СЧ КВД
  ЗП РАСП
  СЧ М(М3)
  И Е48П41
  РЗБ ШУРА
 ИЛИ КВД-1
  ЗП РАСП+1
  МОДА РАСП
  УИА (М16)
 МОДА ПЛ2
 УИА (М5)
 УИА 3
 ПБ ПЛ11
НЕВКЛ СЧ НЕИЩ(М4)
 АIF (∧АС).АС6
 И Е25
 ПЕ ПРОПУС ПРОПУСКАТЬ ПЕРЕЧИСЛЕННЫЕ
 СЧ НЕИЩ(М4)
.АС6 АNОР
 И Е26
 ПЕ ПРОПУС ПРОПУСКАЙ ВСЕХ
НЕВКЛ1 УИА ’2003’
 СЧ ШКИВ
 ИЛИ Е25
 ЗП ШКИВ
 СЧ М(М3)
 СДА 64+40
  ЗП ТР5
  СЧ =В’21’
 ЗП ТУМБ6
 ПБ РСП  НА РАСПЕЧАТКУ
ХОДЫ СЧИ М16
 НТЖ Е2
 АIF (∧АС).АС100
 ПО РЗРКАТ
 ПБ ККОН
 АGО .АС101
.АС100 АNОР
 ПО ПРОДЛЭ
КОН МОДА КОНЦ
 ПИО (М10)
 СЛИА -1(М10)
 ПБ ПРЛДЭ
.АС101 АNОР
КОНЦ СЧ КУСЛМ
  НТЖ Е18
  ПВ ФИЗОБМ(М16)
 УИА 3
 ПБ КОНЦЫ
ПЛ11 УИИ ’32’(М5)
 УИА (М5)
ПЕЧ СЧ Е23
 ПБ ТВ40
КОНЦЫ СЧ ПРОИЗВ ПРОИЗ. ЛИ ИЗМ.В КАТАЛОГЕ
  ПО НЕСТ
  СЧ КУСБК
НЕСТ2 НТЖ Е18
  ПВ ФИЗОБМ(М16)
НЕСТ СЧ КУСЛМ
  ПВ DЕQ(М15)
  СЧ КУСЛМ
  ПВ ТБУФ(М15)
 СЧ КУСБ
  ПВ ТБУФ(М15)
  УИА 3
  ПБ КЗ18А1-КЗ(М7)
СКОН СЧ ПРОИЗВ
  ПО НЕПИШ
  СЧ КУСБК
  НТЖ Е18
  ПВ ФИЗОБМ(М16)
НЕПИШ СЧ КУСЛМ
 ПБ НЕСТ2
ПРОВРЖ СЧ НЕИЩ(М4)
 И Е24
 ПЕ НЕВКЛ1  НЕ ВКЛ. НЕ ПЕРЕЧ.
 ПБ ПЛ2  ПОДОЖДАТЬ
ЗПОВТ СЧ НОМИНД   НОМЕР ОТД. ИНД. ПОЛЬЗ.
 СДА 64-15
 НТЖ 7(М3)      БЫЛ N КАН.СЧЕТА
 И НОМО
 НТЖ 7(М3)      ЗАНЕС N ОТДЕЛА
 ЗП М+7(М3)
 СЧ М+6(М3)
 ИЛИ Е48Е47
 НТЖ Е48Е47
 ЗП М+6(М3)
 СЧ М+5(М3)
 И О13П11
 ЗП М+5(М3)
 ЗП ПРОИЗВ ПРОИЗВОДИЛИ ИЗМ. В Б,К.
 ПБ ИЗПОВТ
ПОДСЧ СЧ М+6(М3)
 АIF (∧АС).АС30
 И Е46
 ПЕ ПЛ2  НЕТ БОБИН У ЗАДАЧИ
.АС30 АNОР
 СЧ М+5(М3)
 СДА 64-35   48-46 РР. N БЭСМ
 И Е46П48  ПРОПУЩЕНА ЗАД.Б.С.
 ПО НС НЕТ ТОЛЬКО ПРОПУСК.
 НТЖ НОММ
 И Е46П48
 ПЕ ПЛ2 ПРОПУЩЕНА НА ДР.БЭСМ
НС СЧ М+7(М3)
 И Е46П48
 ПО НС1 НОМЕР БЭСМ НЕ ВАЖЕН
 НТЖ НОММ
 И Е46П48
 ПО НС1          ЭТОТ СВС
*
 СЧ ’7777’
 РЕГ ’254’
 МОДА ВСЕСВС
 ИЛИ 0
 СДА 64+38
 И П17
 ЧЕД
 НТЖ Е1
 ПЕ ПЛ2           СВС > 2-УХ
НС1 СЧ М+7(М3)
 СДА 64+41
 И П7
 УИ М16
 ВЧ П5
 ПО БОЛ4
 СЧИ М10
 И Е2
 СДА 64-40   ЭКС.ЗАД. НА КЛАСС ВЫШЕ
 СЛЦ М+7(М3)
 СДА 64+41
 И П7
 УИ М16
БОЛ4 НОП ,
 СЧ МГРП
 И Е16
 СДА 64+12
 УИ М14
 СЛИ М14(М16)
 СЧ КАНСЧТ(М14)
 НТЖ ШКРЗ
 И КАНСЧТ(М14)
 И ШКАП
 ПО ПЛ2
 ПБ (М12)
НОММ КОНД В’0’
Е17П2 КОНД В’377776’
 АIF (NОТ ∧АС).АС0
П57 КОНД М38В’57’
П30 КОНД М38В’30’
.АС0 АNОР
ПРИНДП КОНД В’1’   ЕСЛИ 0 ТО ИНД. ПОЛЬЗ.
О15П11 КОНД В’-1’М10В’37’
НОМИНД КОНД В’0’
КУСБ КОНД В’0’
КУСБОЛ КОНД М24В’446’М17В’1’
ПРОСМ КОНД В’0’
ПРОИЗВ КОНД В’0’
К144 КОНД М47В’1’В’144’
КУСЛИМ КОНД М24В’610’М17В’1’  КУС КАТ. ЛИМИТОВ
КУСЛМ КОНД В’0’
КУСЛМВ КОНД В’0’
ШТУБ КОНД В’7777777777’
КУСБК КОНД В’0’
Е17П25 КОНД М16В’777’
НОМО КОНД М15В’377’
АЦПУ КОНД М30В’777777’
ШУРА КОНД П’003770’ КОНСТ. ДЛЯ ФОРМИР.РАСП.
* НЕ РАЗДЕЛЯТЬ КВД,КНД,НЕТМА,КОНЧМА
КОНСТА КОНД П’  0000’В’21’
 АIF (∧АС).АС22
КВД КОНД П’2НВД  ’
 КОНД П’2НВЛ  ’
 КОНД П’2НВЭ  ’
 КОНД П’2НМА  ’
 КОНД П’2ЗАП  ’
НЕТВР КОНД П’2НОБВ.’
 КОНД П’2НОБМ.’
 АGО .АС21
.АС22 АNОР
КВД КОНД П’0НВД  ’
 КОНД П’0НВЛ  ’
НВЭ КОНД П’0НВЭ  ’
НЕТМА КОНД П’0НМА  ’
ЗАПРЕЩ КОНД П’0ЗАП  ’
НЕТВР КОНД П’0НОБВ.’
 КОНД П’0НОБМ0’В’21’
.АС21 АNОР
РАСП ПАМ 2
Е48П11 КОНД М10В’-1’
Е32П1 КОНД В’37777777777’
ВР1 КОНД В’0’
МЕТР1 КОНД В’0’ ВР1 И МЕТР1 ОБЯЗАТЕЛЬНО=0
R1 ПАМ 1
R2 ПАМ 1
РЯО1 ПАМ 1
НВК ПАМ 1
RО ПАМ 1
RОТ ПАМ 1
РЯО ПАМ 1
РПОВ ПАМ 1
РПКВ ПАМ 1
О13П11 КОНД В’-1’М10В’7’
Е45П42 КОНД М41В’17’
Е27П25 КОНД М24В’7’
Е30П25 КОНД М24В’77’
Е48П27 КОНД М26В’-1’
 Е
 М
ГРУППА ВНЕШ ШКГР,ГЛГРУП,ЗАГРУП,ШКЗАПР,ГЛСОБГ
ДИСП70 ВНЕШ П37,П777,П377,ОПТИМ,СМ,П1777,П7777,НАПРМГ
ДИСП70 ВНЕШ (Е1П24,Е24П1),(Л64,ЭК9),ВРЕМЯ,ШИФРМ,П17,П7,ДЛЯЗП
ДИСП70 ВНЕШ (ВОЛОДЯ,О25О11),ТБУФ,НОММБ,ЗАХЗАН,ГАШЗАН,ТУС,НОММЛ
ДИСП70 ВНЕШ (ЧВЫЗСП,СПR),СП,КСП,(СПНОМТ,СПНОМ),П177,Е48П43,ТВ40
ДИСП70 ВНЕШ ТЗФСТР,(ЗОНЫСТ,СТЗОН),(ШЗНМБД,ШЗНМБО),СТРЕЛ,П37777
ДИСП70 ВНЕШ ОБЩТОМ,ТЗНМЛ,РЯВВ1,Е33П25,ПР14,ШЗБУДП
ДИСП70 ВНЕШ Е15П11,ЗАХВМЛ,ПРМЗОН,СЧК
ДИСП70 ВНЕШ П2007,Е16П32,Е48Е47,ИЗ10В8,ОБЩМЛ,НОММЛ1,ЗАНЯТА
ДИСП70 ВНЕШ ШККЗ,ШКИВ,П60,ПОДЧ,ОСПОД,МШГ,ШКОК,ЭК74,(ЛИС,ЛИСТА)
ДИСП70 ВНЕШ ТЕРЕЗА,ШГ,ЗУК,МДР,ВСЕЕД,Е41П1,Е24П1
ДИСП70 ВНЕШ ПРИП,П77777,П77,(Е48П46,Е46П48)
ДИСП70 ВНЕШ ШЗНМБО,НОММБ1,Е48П16,ШКНТ,АВОС
ДИСП70 ВНЕШ Е1П23,Е46П48,(ГОД1,ГОД),ПЕРЕВ,П3777,ТМБ
ДИСП70 ВНЕШ ТУМБ6,ТР5,П5,КДД,ОЗУ,ТОБ,П3,РМД,РМД1
ДИСП70 ВНЕШ Т,ЖИЗОС,КСН,ЕАР,СОБЫТ,ИНФСЛП
АВОСТ ВНЕШ СТ412
СОСТАВ ВНЕШ ЯЧЛИС,ЯЧЧТР,СДТ,ЯЧОТБР,ЕОТКЗ,ШЗНМБ
ДИСП70 ВНЕШ ГРАН,(Е25П48,Е48П25),Е48П25
ДИСП70 ВНЕШ ЗАПВЕТ,ДЛИН,ЗАНОСМ,Е48П7
ДИСП70 ВНЕШ ЛЕСТ,КЛЕСТ,ГРУП,КГРУП,КАНСЧТ,МГРП
ДИСП70 ВНЕШ МЧПЗ,ШП,ШК31,ШПЭГ,МАСГЛ,ТУСП,НПЗ,ЕДОБ
ДИСП70 ВНЕШ МШГП,ЯЧЭК,ЕЭ52,ШКАП,НЗАРХ,П17777,ВР
ВЗУ ВНЕШ П
ВИСП ВНЕШ ВШГП
ТЕРМ ВНЕШ КОМВРЕ,ТКОМУЗ,НЗТСВТ
ДИСП70 ВНЕШ ШККТ
КИТ ВНЕШ Е1П25,ТРТЕСТ,Е48П41,(Е25П1,Е1П25)
ОСА ВНЕШ КОЗА,ОСА
КЗ ВНЕШ СТАТЗ,КЗ18А,КЗ6,(РК,РЯ),ВЫДТТ,ВЫКИД3
КЗ ВНЕШ (РЯР,РЯ),КЗ,КЗ112,Е29П28,КЗ18А1
ДИСКИ ВНЕШ (Е1П16,Е16П1),КТОМ,РАОБЩ,КУСКИ,Е16П1,ЕNQ,DЕQ
МОТТ ВНЕШ ШЗНКТ,ПРОБ6,ШАКТИ,ЕАКТИВ,ШКРАБН
КЗ ВНЕШ ПРУПР
КАЧКА ВНЕШ ШКРЗ,Е48П17
ВЗУ ВНЕШ ФИЗОБМ
ДИСП70 ВНЕШ ИНФ,ТВ67А,ЛТБД,МУЛЬЗ,НОМАРС
ХЛАМ ВНЕШ ОСВУ,УПРИ
ДИСП70 ВНЕШ ПРЕДЕЛ,УКАЗД,ГОД,МАЛКАТ
КИТ ВНЕШ Е48П42,БРАКТР,ШКОР,ПЕРЕ2
КИТ ВНЕШ АКТБД
СОСТАВ ВНЕШ ЕОТМД,ЕОТКД,ССТБД
КИТ ВНЕШ АКТОП
СОСТАВ ВНЕШ ТБД,ТОП,ТОПНЗ,ЯЧТОП,ТБДТР
ВЗУ ВНЕШ ФИЗЗП
ПВВ ВНЕШ РЗПУЛ,ЗОПУЛ,ДТУС
ПВВ ВНЕШ ОСВМЛ,ВСЕСВС
ТЕСТЭК ВНЕШ ОТВНИМ
 ЭКВИВ (ДЛИНВ,20),(РЯ1,КЗ23),(РЯ,КЗ23+6)
 ЭКВИВ (ОТБР,34),(Р,2),(М,0),(ОБД,1),(МЯЧРЖП,8),(НЕИЩ,12)
 ЭКВИВ (ВРЕД,13),(СМНАОТ,12),(ДЕЖ,13),(МАСКО,’651’)
 ЭКВИВ (СЧЕТОТ,’731’),(ЗАМОР,’1500’)
 ВХОД ВТР,БЮД,БЮДЖ,ДСТ,КЗ2
 ВХОД НЕОТБР
 ЭКВИВ (КОНЕЦЗ,’25047’),(ОЗ,’27047’),(ЧЛ,’33453’)
 ЭКВИВ (ЧБ,’33441’),(ЧП,’33457’),(ЧД,’33444’),(УЗ,’31447’)
*КОНЕЦ ЧАСТИ

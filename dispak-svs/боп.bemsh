БОП СТАРТ 4096
 Б
 Е
 М
 УПОТР БОП(М7)
 КОНД В’0’          ИПЗ ОБРАБОТКИ ПАСПОРТА СМ
 КОНД М41В’3’       РК=БЛН.И БЛОКР.
 КОНД (21)В’0’
 КОНД М47В’1’       КОМ.СВЕРТКА
 КОНД В’0’
NЗАД КОНД В’0’          НОМЕР ЗАДАЧИ ДЛЯ Э64
КУСДП КОНД М17В’3’М2В’25’ КУС ДНЕПРА
СЧОНМЛ КОНД В’0’
БУФ2 КОНД М17В’1’В’64’
ЗОН1 КОНД М24В’7770’М17В’1’
 КОНД В’714244’     ПРИПИСКА ДЛЯ Э64
ПРИП2Л КОНД В’0’          ДЛЯ Э64
 КОНД М15В’1’       ДЛЯ Э64
 КОНД М47В’1’       ВРЕМЯ СЧЕТА
РЯ КОНД В’0’
РЯ1 КОНД В’0’
РЯ2 КОНД В’0’
ПРМД КОНД В’0’
РЯ4 КОНД В’0’
РР КОНД В’0’
БОП1 СЧ ШКОР
 ПО БОП18Б
 НЕД
 УИ М1            НОМЕР КАНАЛА
 ЗП NЗАД
 СДА 64+4
 ВЧОБ Е2
 ЗП РЯ2  СЕКТ
 СЧ Е1
 ПВ ТБУФ(М15)
 УИА 3
 СДА 64-8
 УИ М2
 СДА 64-2
 ИЛИ ИНФСЛ
 ИЛИ РЯ2
 ЗП РЯ2
 ПВ ФИЗОБМ(М16)  ЧТ МАЛ КАТ
 СЧИ М1
 И П17
 УИ М12
 СЛИ М12(М2)
 СЧ ИН(М12)
 ЗП РБ       НОМЕР З.
 И П1777
 УИ М13           А СЛОВА С НОМЕРАМИ ЗОН МЛ ВВОДА
 СЧ ТУСП-1(М1)
 УИ М3            А ИПЗ
 СЧ ВРЕМЯ
 СДА 64+10
 СДА 64-25
 ИЛИ ИН(М12)
 ЗП ИН(М12)
 СЧ ШКОР
 НТЖ Е48-1(М1)
 ЗП ШКОР
 СЧ ТЕР(М12)
 ЗП РР
 СДА 64+34
 И П77   ВЫД. НОМ ТЕРМ.
 УИ М15           НОМЕР ТЕРМИНАЛА
 ПО ДЛ            НЕТ ТЕРМИНАЛОВ
 СЧ ЧПТ(М3)
 ИЛИ Е1
 ЗП ЧПТ(М3)
 СЧ Е48-1(М15)
ДЛ  ЗП  ТЕРЕЗА(М3)
 СЧ ШКВМК
 И Е48-1(М1)
 ЗП РЯ4
 НТЖ ШКВМК
 ЗП ШКВМК
   СЧ  0
 ПВ ТБУФ(М15)     ДАЙ ЛИСТ
 УИА 3
 СДА 64-8
 УИ М2            А БУФЕРА
 ЗП ПРИП2Л        ДЛЯ Э64 БОП2
 МОДА ЧТЗОН2
 ПВ (М14)         ЧТ.ЗОНЫ С ИНФОРМ.
 УИА 3             ФИО,ШИФР,ГРИФ В МАЛЫЙ КАТАЛОГ
 СЧ 15(М2)
 ЗП ФИО(М12)
 СЧ 16(М2)
 ЗП ФИО1(М12)
 СЧ 1(М2)
 ЗП ШИФР(М12)
 ЗП ШИФРМ(М3)
 СЧ 20(М2)
 ЗП ГРИФ(М12)
 СЧ СТПМ0(М2)
 ЗП СТПМК(М12)
 СЧ КЛМ0(М2)
 ЗП КЛМК(М12)
 СЧ РЯ2
 ПВ ФИЗЗП(М16)   ЗАПИСЬ М.К.
 СЧ РЯ2
 ПВ ТБУФ(М15)     ВОЗЬМИ АБЗАЦ
 МОДА БУФТРВ
 УИА (М15)
 ПВ СЧЕТРВ(М16)    РАСЧЕТ С КОНК.
 УИА 1027
 СЧ ШКРЗ
 ИЛИ Е48-1(М1)
 ЗП ШКРЗ
 СЧ УЧЕТРВ
 СДА 64-16
 И Е47П1
 НТЖ ШЗНМБ+ТРВ+1(М3)
 И Е48П17
 НТЖ ШЗНМБ+ТРВ+1(М3)    НАЧ РЕШ. ПО Ж.ОС
 ЗП ШЗНМБ+ТРВ+1(М3)  НАЧ РЕШ ПО Ж ОС
 УИА 3
 СЧ ШКАНУ(М2)
 И Е13
 СДА 64+ЕСЛУЖ-Е13
 ИЛИ ЯЧСЛУЖ(М3)
 ЗП ЯЧСЛУЖ(М3)
 СЧ 2(М2)
 СДА 64+20
 И П777
 ЗП РЯ2
 СДА 64-СДТ
 УИА 1027
 ИЛИ ЯЧТР(М3)
 ЗП ЯЧТР(М3)     ЧИСЛО ТРАКТОВ В ПАС.
 СЧ РЯ2
 ВЧОБ Е10
 СДА 64-СДТ
 ИЛИ ЯЧЧТР(М3)
 ЗП ЯЧЧТР(М3)     ЧИСЛО ТРКТОВ В ДОП.КОДЕ
 СЧ 8(М2)
 И П7777
 СЛЦ Е1
 ЗП РЯ
 ИЛИ ЯЧЛИС(М3)
 ЗП ЯЧЛИС(М3)     ЧИСЛО ЛИСТОВ В ПАС.
 СЧ РЯ
 ВЧОБ Е13
 ИЛИ ЛИСОСТ(М3)
 ЗП ЛИСОСТ(М3)
 СЧ 13(М2)
 И Е46П48
 НЕД
 СДА 64-28
 ЗП РЯ
 СЧ ШЗНМБ+Л(М3)
 И Е16П1
 ИЛИ РЯ
 ЗП ШЗНМБ+Л(М3)   ЧИСЛО ЛИСТОВ В ДОП.КОДЕ+КОД РОСПИСИ
 СЛИА -1(М1)
* РАБОТА С МЛ,МД,СЕГМЕНТАМИ
 МОДА КОЛПОД
 СЧ (М1)
 МОДА СУМПОД
 СЛЦ
 МОДА СУМПОД
 ЗП
 СЧ 0
 МОДА КОЛПОД
 ЗП (М1)
 ЗП СЧОНМЛ
 УИА (М12)         ДЛЯ ВЫБОРКИ НОМЕРОВ БОБИН
БОП10А УИА -1(М10)       ДЛЯ ЦИКЛА НА 12 МЛ
 УИА (М11)
БОП11 МОДА (М10)
 СЧ 11(М2)        НОМЕРА МАТЕМ.МЛ
 УИА 3
 ПО БОП14         МЛ ВСЕ ОБРАБ.
 СДА 64-8
 МОДА (М10)
 ЗП 11(М2)        ОСТАТОК СЛОВА
 СЧМР
 ЗП РЯ
 И Е7
 УИ М5            М5 - ПРИЗНАК СЕГМЕНТА НА МБ 7-ОГО НАПР.
 СЧ РЯ
 И П77
 УИ М4
 ПО БОП11         ОБХОДИМ НУ=0
 СЛИА 1(М11)
 МОДА НЕСЕГБ
 ПИО (М5)
 ИЛИ СЕГМБ(М3)
 ЗП СЕГМБ(М3)
 ПБ ММ
НЕСЕГБ НОП
 СДА 64+1
 УИ М5           НОМ. СЛОВА ШЗНМБ+12
 СЧМР
 СДА 64+45
 НТЖ Е3
 УИ М6            НОМЕР КУСКА В ШЗНМБ∧4
 СЛИ М5(М3)
 СЧ РЯ
 И Е8
 ПЕ СЕГМЕН
 УИА ТЗНМЛ+96(М16)
 СЧИ М16
 ЗП ЧИ
 УИА ТЗНМЛ(М16)
 МОДА (М12)
 СЧ 3(М2)
 СД КТОМ-1(М11)
 ЗП РЯ БОБИНА,ПРИЗН-ЗАПИСИ
 И Е12
 ПО ПРЛ  МЛ
 СЧ РЯ
 И П7777
 ВЧ NБЛ
 ПЕ ПРЛ
 СЧ 0
ПРЛ ЗП ПРМД
БОП12 СЧ РЯ
 НТЖ (М16)
 И П7777
 ПЕ БОП12Б
 УИИ М14(М16)
 СЧ (М16)
 И ЕН
 ПО ДИСНОР
 СЧ Е1
 ЗП СЧОНМЛ
ДИСНОР НОП
 СЛИА -ТЗНМЛ(М14)
 СЧ (М16)
 И Е41
 ПЕ ОБЩБОБ
 СЧ (М16)
 И Е46П48
 ПО БОБСВО        БОБИНА СВОБОДНА
 И Е47
 ПО БОП12Б        НЕ 2053
 СЧ ШКАНУ(М2)
 И Е48-’30’(М4)
 ПО БОП12Б        НЕ ОБЛАСТЬ
БОБСВО НОП ,
 СЧ (М16)
 ИЛИ Е48
 ЗП (М16)
ОБЩБОБ СЧИ М6
 СДА 64+2
 УИ М17
 СЧ РЯ
 И Е16
 СДА 64+8(М17)
 УИА 1027
 ИЛИ ШЗНМБ-12(М5)
 ЗП ШЗНМБ-12(М5)  8,7 Р.-ПР.ЗАПИСИ
 СЧИ М12
 СДА 64-2
 УИ М15
 СЛИ М15(М11)      М15:= ССЫЛКА НА ОБЩТОМ
 СЧ ПРМД
 ПЕ БН1
БН СЛИА 24(М14)
 СЧИ М14
 СД КТОМЛ-1(М11)
 МОДА (М12)
 ИЛИ  ОБЩТОМ(М3)
 МОДА (М12)
 ЗП ОБЩТОМ(М3)    ПРИЗ.ЗП,НУ ФИЗ.
 СЧ РЯ4
 ПЕ ЗАНСОТ
 СЧ Е48(М1)
 НТЖ ЕЗАР
 ПО АРХИВ
 СЧ ШКАНУ(М2)
 И Е48-’30’(М4)
 ПЕ АРХИВ
 СЧ (М16)
 И ЕАР
 ПО НЕАРХБ
 СЧ Е1
 ЗП СЧОНМЛ
АРХИВ УИА ВСЕЕД(М16)
НЕАРХБ СЧИ М12
 СДА 64-1
 НТЖ ВСЕЕД
 УИ М4
 СЧ ЕЗ
 ИЛИ ЕЧ
 И (М16)
 СДА 64-СДВПР+1(М4)
 МОДА (М3)
 ИЛИ 27(М11)
 МОДА (М3)
 ЗП 27(М11)
ЗАНСОТ СЧИ М15
 СДА 64-12(М6)
 ИЛИ ШЗНМБ-12(М5)
 ЗП ШЗНМБ-12(М5)  ССЫЛКА НА ОБЩТОМ
ММ СЧИ М11
 НТЖ П3
 ПЕ БОП11         ЦИКЛ НА 3 БОБИНЫ
 УИА (М11)
 СЛИА 1(М12)
 ПБ БОП11         НА СЛ.СЛОВО
БОП12Б СЛИА 1(М16)
 СЧИ М16
 НТЖ ЧИ
 ПЕ БОП12
 ПБ ММ
БН1 СЛИА -56(М14)
 ПБ БН
ЭТА СЧ ТЗНМЛ(М14)
 ИЛИ Е48
 ЗП ТЗНМЛ(М14)
 ПБ БН
СЕГМЕН СЧ Е32-’30’(М4)
 ИЛИ МДР(М3)
 ЗП МДР(М3)
 СЧИ М12
 СДА 64-2
 УИ М15
 СЛИ М15(М11)
 ПБ ЗАНСОТ
БОП14 МОДА БОП11
 ЦИКЛ (М10)
 СЧ 17(М2)
 ИЛИ УКАЗД(М3)
 ЗП УКАЗД(М3)
 СЧ РЯ4
   ПЕ БОПВОС
 СЧ Е1
 ПВ ТБУФ(М15)
 СДА 64-8
 УИ М5
 СДА 64-2
 ИЛИ НОММБ1
 ИЛИ БУФ2
 УИИ М16(М5)
 СЛИА ВБОП1-БП(М16)
 ПБ ФИЗОБМ
БОПВОС НОП
 СЧ УПРМ0(М2)
 ЗП РЯ
 СЧИ М2
 СДА 64-2
 ИЛИ Е19
 ПВ ТБУФ(М15)
 СЛИА 1(М1)
 СЧИ М1
 СДА 64-6
 ИЛИ ЛИСТУП
 ПВ ТБУФ(М15)
 СДА 64-8
 УИ М2
 СДА 64-2
 ИЛИ КУСДП
 СЛЦ НОММБ1
 ЗП R
 ПВ ФИЗОБМ(М16)
 СЧ РБ
 ЗП NСКВ-ДНЕПР(М2)
 СЧ РЯ
 ЗП NЭВМУП-ДНЕПР(М2)
 СЧИ М1
 СДА 64-42
 ИЛИ R
 ПВ ТБУФ(М15)
 СЧ ШКВОС
 ИЛИ РЯ4
 ЗП ШКВОС
 СЧ РАБЕ16
 ПЕ БОП22
 СЧ Е3
 ЗП ЭТУПР
 СЧ ШКИВ
 ИЛИ Е16
 ЗП ШКИВ
 ЗП РАБЕ16
 УИА 3
БОП22 СЧ ШКОР
 ПЕ БОП1          ЕСТЬ ЗАДАЧИ ДЛЯ ВКЛЮЧ.В СЧЕТ
БОП18Б СЧ Е36
 ПБ БЛИСКН        НА ИСКЛЮЧ.БОП ИЗ РАБОТЫ
ЧТЗОН2 СЧИ М13
 СДА 64-24
 СЛЦ НОММЛ2
 ИЛИ Е18
 ЗП РЯ
 СЧИ М2
 СДА 64-2
 ИЛИ РЯ
 ЗП РЯ1           КУС ЧТ.ЗОНЫ С МЛ
 ПВ ФИЗОБМ(М16)
 УИИ М15(М2)
 ПБ (М14)         *С ВЫХОД ПОСЛЕ ЧТ.ИНФОРМ.С МД
БУФТРВ ПАМ 6
РБ КОНД В’0’
ЧИ КОНД В’0’
R ПАМ 1
СОСТАВ ВНЕШ ТОП,ТОПНЗ,ТБДТР,СЕГМБ,УКАЗД
СОСТАВ ВНЕШ ЯЧТР,ЯЧЧТР,СДТ,ЯЧЛИС,СДЛИ,ШЗНМБ
ДИСП70 ВНЕШ ШКВОС,ТРВ,РАБЕ16,ЭТУПР,ЛИСТУП
ДНЕПР ВНЕШ NСКВ,ДНЕПР,NЭВМУП
ВЗУ ВНЕШ ФИЗОБМ,ФИЗЗП
ДИСП70 ВНЕШ ТЗНМЛ,ВРЕМЯ,БЛИСКН,НОММЛ1,МИ
ДИСП70 ВНЕШ П777,П7777,П1777,ОБЩТОМ,П37,ШИФРМ,ТУСП
ДИСП70 ВНЕШ П3,ТБУФ,ТЕРЕЗА,МЧПЗ,НПЗ
ДИСП70 ВНЕШ ШКИВ,МДР,ВСЕЕД,УЧЕТРВ
ДИСП70 ВНЕШ ОБЩМЛ,Е15П11,ГОД,П7,Е46П48
ДИСП70 ВНЕШ П77,НОММБ1,НОММЛ2,Е47П1
ДИСП70 ВНЕШ П17,NБЛ,ЗАХЗАН,ГАШЗАН
ДИСКИ ВНЕШ Е16П1,КТОМ,КТОМЛ,ЕNQ,DЕQ
КАЧКА ВНЕШ ШКРЗ
БОЭК1 ВНЕШ Л,СЧЕТРВ
В3В4 ВНЕШ ШКАНУ,КЛМ0,СТПМ0,УПРМ0
БОП1 ВНЕШ ВБОП1,(БП,БОП1)
ДИСП70 ВНЕШ (ИНФСЛ,МАЛКАТ),ЕАР,ЕЧ,ЕЗ,ЕН,АВОСТА,СДВПР
КАЧКА ВНЕШ Е48П17
КИТ ВНЕШ ШКОР,ШКВМК,ГРИФ,СТПМК,КЛМК,ШИФР
ПЛАН2 ВНЕШ ФИО,ФИО1,ТЕР,ИН
СОСТАВ ВНЕШ ЧПТ,ЛИСОСТ
СОСТАВ ВНЕШ ЕЗАР,ЕСЛУЖ,ЯЧСЛУЖ
ХЛАМ ВНЕШ УПРИ
ВИСП ВНЕШ КОЛПОД,СУМПОД
 ВХОД ЧТЗОН2,РЯ,РБ,БОП22,РР,РЯ4,РЯ1,СЧОНМЛ

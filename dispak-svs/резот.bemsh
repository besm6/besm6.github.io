РЕЗОТ СТАРТ ’57226’
*
*/////////////////////////////////////////////*
*//////                                 //////*
*//////        Р  Е  З  О  Т            //////*
*//////                                 //////*
*//////            ЕРЕЗОТ = Е30         //////*
*//////            NЗАД   =   23(114)   //////*
*//////            ТУС    = 2222        //////*
*//////            ЗОНГП  = 0606        //////*
*//////            ЗОНМОД = 1702        //////*
*//////                                 //////*
*/////////////////////////////////////////////*
*
*
ИРЕЗОТ НОП ,             ИПЗ  РЕЗОТ-А
 КОНД В’0’
 КОНД М41В’3’       БЛ. РЕЖ. АУ   1
 КОНД В’0’          РМР
 КОНД А(НАЧАЛО)     М33 := НАЧАЛО
 КОНД В’0’          М32
 КОНД В’200300007’  М27,М21
 КОНД В’0’          М20
 КОНД (15)В’0’      М17 - М1
 КОНД В’0’          НЕ ЗАНИМАТЬ :26 ЯЧ.ИПЗ(26=40)
 КОНД В’0’          НЕ ЗАНИМАТЬ :27 ЯЧ.ИПЗ(ЗАПВЫХ)
РПМВЫД КОНД (6)В’0’       РАБОЧИИ ЯЧ.ДЛЯ ПМВЫД
ПНО КОНД М32П’ -’М24В’172’
 КОНД В’0’          НЕ ЗАНИМАТЬ  :37 ЯЧ.ИПЗ(ЛТВЫД)
 КОНД В’0’          НЕ ЗАНИМАТЬ  :40 ЯЧ.ИПЗ(26=40)
 КОНД М47В’1’       НЕ ЗАНИМАТЬ  :41 ЯЧ.ИПЗ(ВРЕМЯ)
NСВС КОНД М40В’143’П’СВС-0’
 КОНД М40П’↑’
*
*
*
*
*
*
*
*
НАЧАЛО НОП ,
 УИА 1027
 СЧ ШКОРАБ
 ЗП ШРЕЗОТ        ШРЕЗОТ := <ШКОРАБ>
 СЧ
 ЗП ШКОРАБ        ШКОРАБ := 0
 УИА 3
СЛЕДР НОП ,             СЛЕДУЮЩАЯ РАБОТА
 СЧ ШРЕЗОТ
 ПО ВЫХОД         ВСЕ РАБОТЫ ВЫПОЛНЕНЫ
 НЕД П77777        М1:= N РАБОТЫ
 УИ М1
 СЧ ШРЕЗОТ
 НТЖ Е48(М1)
 ЗП ШРЕЗОТ
 ПБ ПЕРЕКЛ-1(М1)  НА ПЕРЕКЛЮЧАТЕЛЬ РАБОТ
*
ПЕРЕКЛ НОП ,
 УИА ЗАПРОС(М4)
 ПБ ОЧТЕР         Е47 - КОНЕЦ (ОТМЕНА) ПРИЕМА
 УИА ЗВОНОК(М4)
 ПБ ОЧТЕР         Е46 - БУДИЛЬНИК
 УИА 1027
 ПБ ВЫДАЧА        Е45 - ВЫДАЧА
 УИА 3
 ПБ СЛЕДР         Е44 - ХОЛОСТАЯ РАБОТА
*
*
*
ОТПРИМ НОП ,             ОТКРЫТИЕ ПРИЕМА
 СЧ ВСЕЕД
 НТЖ Е48(М2)
 И ШЗПРИМ        ’0’ -> ШЗПРИМ - Б Л П Р НЕ ТРЕБУЕТСЯ
 ЗП ШЗПРИМ
*
ОЧТЕР УИА ’2003’        ПЕРЕХОД К ОЧЕРЕДНОМУ ТЕРМИНАЛУ
 СЧ ВТОРШК(М1)
 ПО НЕТТЕР        НЕТ ТЕРМИНАЛОВ ПО РАБОТЕ N<М1>
 НЕД П77777        М2:= N
 УИ М2
 СЧ ВТОРШК(М1)
 НТЖ Е48(М2)
 ЗП ВТОРШК(М1)
 УИА 3             СНТ.БЛПР
 ПБ (М4)
*
НЕТТЕР НОП ,
 СЧ ОТЛТЕР
 ЗП ВТОРШК(М1)
 УИА 3
 ПО СЛЕДР         НЕТ ОТЛОЖ.ТЕРМИН.ПО ЭТОЙ РАБОТЕ
 СЧ Е48(М1)
 ПАМ 0             Б Л П Р  НЕ ТРЕБУЕТСЯ
 ИЛИ ШКОРАБ
 ЗП ШКОРАБ        ОБРАБОТАЕМ В СЛЕД.ВЫЗОВ РЕЗОТ-А !
 СЧ 0
 ЗП ОТЛТЕР        ОТЛТЕР := 0
  ПБ СЛЕДР
*
ОТЛРАБ НОП ,
 СЧ ОТЛТЕР        ОТКЛАДЫВАЕМ ОБРАБОТКУ ЗАПРОСА ОТ
 ИЛИ Е48(М2)       ДАННОГО ТЕРМИНАЛА ДО ЛУЧШИХ ВРЕМЕН
 ЗП ОТЛТЕР
 ПБ ОЧТЕР
*
*
*      В  Ы  Х  О  Д
*
*
ВЫХОД НОП ,
 УИА 1027
 СЧ ЗАНЯТА
 И Е44
 ПО ВЫХ1          НЕ БЫЛО ДИРЕК.’НЕТ ДИА’
 НТЖ ЗАНЯТА
 ЗП ЗАНЯТА
 СЧ ЕДИА
 ИЛИ ШКРАБН
 ЗП ШКРАБН
 СЧ ШЗНКТ
 ЗП ШКДИА
ВЫХ1 НОП ,
 СЧ ШКРАБН
 ПО ВЫХ2          НЕТ РАБОТЫ НЕРОТ-У
 СЧ ШКИВ
 ИЛИ ЕНЕРОТ        ВЫЗОВ НЕРОТ-А
 ЗП ШКИВ
ВЫХ2 НОП ,
 СЧ 0
 ЗП РРЕЗОТ        РРЕЗОТ := 0 - РЕЗОТ СВОБОДЕН
 УИА НАЧАЛО(М16)
 СЧИ М16
 МОДА ИРЕЗОТ+3
 ЗП 0             М33 := НАЧАЛО
 УИА ИРЕЗОТ(М7)    М7 := АДР ИПЗ
 СЧ ИВ25
 ЗП 5(М7)         М21,М27
 УИА БИНОТ2(М16)   М16 := БИНОТ2
 ПБ ЗАНОСМ
*
*
*
*
*/////////////////////////////////////////////*
*//////                                 //////*
*//////            4 7                  //////*
*//////                                 //////*
*/////////////////////////////////////////////*
*
* РЕАКЦИЯ НА СИМВОЛ ЕТХ ( Е N D   О F   Т Е Х Т )
*
ЗАПРОС СЧ ТСЛ(М2)       ПРЕДВАРИТЕЛЬНЫЙ АНАЛИЗ
 И ЕПРИМ
 ПЕ КОМЛИН        КОММУТУЦИЯ ЛИНИИ (47.1)
 СЧ ТСЛ(М2)
 И ЕСВД
 ПЕ ОТЛРАБ        ИДЕТ <LF> ДЛЯ ОТЛОЖЕННОГО ЗАПРОСА !
 СЧ ДАЙРЕК
 И Е48(М2)
 ПЕ ОТЛРАБ        ИДЕТ ДОБАВЛЕНИЕ РЕКОРДА
 УИА ЗАПР1(М16)    М16 := ЗАПР1
ВОКАР НОП
 СЧ ТСЛ(М2)
 ЗП ТБЛ(М2)       ВОССТАНОВЛЕНИЕ ТБЛ
 УИ М15           М15:= А НАЧАЛЬНОГО РЕКОРДА
 И ЕТАВ
 ПЕ ЗАПР4
 СЧ ШСВЯТ
 ИЛИ ШКВТ
 И Е48(М2)
 ПЕ ТЕРСВЯ
 МОД ТСЛ(М2)
 СЧ Н
 СДА 64+40
 НТЖ ПЕОТ
 УИА 1027
 ПО СЛУЖВК        ВОЗВРАТ КАРЕТКИ (47.2)
 СЧ ШКЭ71
 И Е48(М2)       ’Е’ ИЗ ШКЭ71 -> ШКВЭ71
 ИЛИ ШКВЭ71
 ЗП ШКВЭ71
 ПБ СЛУЖВК        ВОЗВРАТ КАРЕТКИ (47.2)
ЗАПР4 НТЖ ТСЛ(М2)       ГАШЕНИЕ ЕТАВ
 ЗП ТСЛ(М2)
ТЕРСВЯ НОП 0
 СЧ Е46Е45        ВОССТАНОВЛЕНИЕ СЧЕТЧИКА
 ЗП (М15)
ЗАПР1 НОП
 МОД ТСЛ(М2)
 СЧ Н
 СДА 64+40
 НТЖ ПЕОТ
 ПО СТRLО        (47.3)
 СЧ ВСЕЕД
 НТЖ Е48(М2)
 ПАМ 0             Б Л П Р  НЕ ТРЕБУЕТСЯ
 И ШСТRLО
 ЗП ШСТRLО        УБРАЛИ ТЕРМ.ИЗ ШСТRLО
 СЧ ШЗНКТ
 И Е48(М2)
 ПО ВШКДОП        ТЕРМИНАЛ НЕ МАТЕМАТИЧЕСКИЙ
 И ШКЭ71
 ПО КУД          (47.4)
 И ШКВЭ71
 ПО КОНЭ71
 ПБ ОЧТЕР         ЖДЕМ ВОЗВРАТА КАРЕТКИ
*
*
*                 47.1
*************************** К О М М У Т А Ц И Я   Л И Н И Й
*
КОМЛИН НОП ,
 СЧ ШКСТАР
 ИЛИ Е48(М2)
 ЗП ШКСТАР
*
 СЧ ШКРАБН
 ИЛИ ЕСТАР
 ЗП ШКРАБН        РАБОТА НЕРОТ-У
 ПБ ОЧТЕР
*
*                 47.2
*   LINЕ FЕЕD ПОСЛЕ ЕТХ
*
СЛУЖВК НОП
 УИИ М17(М2)       М17 := ЛОГ.N ТЕРМИНАЛА
 СЧ АДРТСВ
 ЗП (М15)
 СЧ ТСЛ(М2)
 ИЛИ ЕСВД          НЕ ГАСИТЬ ШЗПРИМ В КОНВЫД
 ЗП ТСЛ(М2)
 СЧ LINFЕЕ
 ЗП ТСВ(М15)
 СЧ ШVD130
 И Е48(М2)
 ПО УСТВД1
 СЧ LF130
 ЗП ТСВ(М15)
 ПБ УСТВД1
*
*                 47.3
СТRLО НОП ,
 УИА 1027
 СЧ Е48(М2)
 ИЛИ ШСТRLО
 ЗП ШСТRLО
*
ВШКДОП НОП ,
 УИА 1027
 СЧ Е48(М2)
 ИЛИ ШКДОП         ОБРАБОТКУ ЗАПРОСА С ЭТОГО ТЕРМИНАЛА
 ЗП  ШКДОП         ВОЗЛОЖИЛИ НА  Н Е Р О Т -А
 СЧ ШКРАБН
 ИЛИ ЕДОП
 ЗП ШКРАБН
 УИА 3
 ПБ ОЧТЕР
*
*                 47.4
*
КУД НОП ,
 УИА ОТПРИМ(М16)
 СЧ ШКОТЛ
 И Е48(М2)
 ПО КУДА1         ТЕРМИНАЛ НЕ  О Т Л А Д Ч И К А
 УИА ТЕРОТЛ(М16)
КУДА1 НОП ,
 СЧ ТСЛ(М2)
 СДА 64+20
 И П77
 ПО (М16)         В ТСЛ НЕТ N ЗАДАЧИ
 ИЛИ Е34
 ПБ ЗАПВЕТ
*
*
ТЕРОТЛ УИА ’2003’        УСТ.БЛПР
 СЧ Е48(М2)
 ИЛИ ШТО           ’1’ -> ШТО. Е ОТЛАДЧИКА -> ШКИВ
 ЗП ШТО
 СЧ ШКИВ
 ИЛИ ЕНРОТ
 ЗП ШКИВ
 ПБ ОЧТЕР
*
*
*
*/////////////////////////////////////////////*
*//////                                 //////*
*//////            4 6                  //////*
*//////                                 //////*
*/////////////////////////////////////////////*
*
*
* РЕАКЦИЯ НА ’БУДИЛЬНИК’ * ’1’ ШЗПРИМ УСТАНОВЛЕНА НОМБОБ-ОМ
*
ЗВОНОК УИА ’2003’        УСТ.БЛПР. ’ВЫКЛЮЧЕНИЕ БУДИЛЬНИКА’
 СЧ ТСЛ(М2)       ОСВОБОЖДЕНИЕ ТЕРМИНАЛА ОТ ПРИЕМА
 ИЛИ ЕПРИМ
 НТЖ ЕПРИМ
 ЗП ТСЛ(М2)
 СЧ ВППС(М2)
 НТЖ ВППС(М2)
 ЗП ВППС(М2)
 СЧМР
 ЗП РЯРОТ         РЯРОТ := <ВППС>
 И Е41
 ПО БУДИК
*
*************************** О Б Р Ы В   С В Я З И
*
 УИИ М15(М2)
 СЧ ВСЕЕД
 НТЖ Е48(М2)
 ЗП СМ
 ПВ NКАН(М17)     М5 := NЗАД, М3 := АДР.ИПЗ
 УИА ОТПРИМ(М17)   М17 := ОТПРИМ
 СЧ ШКОТЛ
 И Е48(М2)
 ПО Ц
 УИА ТЕРОТЛ(М17)   М17 := ТЕРОТЛ
Ф СЧ ТСЛ(М2)
 ИЛИ КОТ
 ЗП ТСЛ(М2)
 ПБ У
Ц СЧ ШКЭ71
 И Е48(М2)
 ПО У
 НТЖ ШКЭ71
 ЗП ШКЭ71
 СЧ ШЗНКТ1
 ИЛИ Е48(М2)
 ЗП ШЗНКТ1
 УИА ОТРЕК(М17)    М17 := ОТРЕК
 ПБ Ф
У СЧ ШЗНКТ
 И СМ
 ЗП ШЗНКТ
 МОДА У2
 ПИО (М5)
 СЧ ТЕРЕЗА(М3)
 И СМ
 ЗП ТЕРЕЗА(М3)
У2 СЧ ШКВЫД
 И СМ
 ЗП ШКВЫД
 СЧ ШКОТЛ
 И Е48(М2)
 ПЕ ТЕРОТЛ
 СЧ ТСЛ(М2)
 И Н3724
 ЗП ТСЛ(М2)
 ПБ (М17)
*************************** НЕ БЫЛО СИМВОЛА 5N СЕКУНД
*
БУДИК УИА 3             СНТ.БЛПР
 УИА ОТПРИМ(М16)   М16 := ОТПРИМ
 СЧ ШКЭ71
 ИЛИ ШКОТЛ
 И Е48(М2)
 ПО ВОКАР         ТЕРМИНАЛ НЕ ОТЛАДЧИКА И НЕ Э71
 И ШКЭ71
 ПО ОТПРИМ        ТЕРМИНАЛ ОТЛАДЧИКА
 СЧ РЯРОТ
 СЛЦ Е48
 ИЛИ Е45
 ЗП РЯРОТ
 И П17
 НТЖ П17
 ПЕ БУДИК5        НЕ ЗВЕНИТ
 УИА ’2003’        НЕ БЫЛО СИМВОЛА 5 МИНУТ
 СЧ ТСЛ(М2)
 ИЛИ ЕЗВОН         КОТ:= ’01’
 ЗП ТСЛ(М2)
 ПБ ВОКАР
БУДИК5 НОП ,             ВОССТАНОВЛЕНИЕ ВППС
 СЧ РЯРОТ
 ЗП ВППС(М2)
 ПБ ОТПРИМ
*
*
*/////////////////////////////////////////////*
*//////                                 //////*
*//////            4 5                  //////*
*//////                                 //////*
*/////////////////////////////////////////////*
*
*
* ПРИШЛО ТРЕБОВАНИЕ ВЫДАЧИ ИЛИ СООБЩЕНИЕ О ЕЕ ЗАВЕРШЕНИИ
*
ВЫДАЧА НОП ,
 СЧ ШКОКВД
 И СЛОN
 НТЖ ШКОКВД
 ЗП РЯРОТ         РЯРОТ := <ШКОКВД> - ПЕРЕКЛЮЧ.ТЕРМИНАЛЫ
 СЧ МЫЖДЕМ
 ИЛИ ШЗН
 ЗП ШЗН
 СЧ 0
 ЗП ШКОКВД        ШКОКВД := 0
 ЗП МЫЖДЕМ        МЫЖДЕМ := 0
 СЧ РЯРОТ
 И ШКВЭ71        ФОРМИРОВАНИЕ РАБОЧЕЙ ШКАЛЫ:
 ИЛИ ШКЗАК         ( ШКОКВД ∧ ШКВЭ71 )
 ЗП ШКЗАКВ
 И ШКОЛА
 НТЖ ШКЗАКВ        - ОПРАШИВАЕМЫЕ АС-ТЕРМИНАЛЫ
 ЗП ШКЗАКВ
 ПВ ОЧТЕР(М4)     ЗА НОМЕРОМ ТЕРМИНАЛА
 СЧ ШКВЭ71
 И Е48(М2)
 ПО ВТЕ23         ВЫДАЧА ТЕКСТОВ ОТ Е23
 И РЯРОТ
 ПО ОЧТЕР         ВЫДАЧА НЕ ОКОНЧИЛАСЬ, НО ЕСТЬ ’ШКЗАК’
 НТЖ ВСЕЕД
 ПАМ 0
 И ШСТRLО
 ЗП ШСТRLО
*
*
* ВЫПОЛНЕНА РАБОТА ПО ЭКСТРАКОДУ 071 *
*
КОНЭ71 НОП ,
 ПВ NКАН(М17)     М5 := NЗАД, М3 := АДР.ИПЗ
 СЧ ШЗНКТ1
 И Е48(М2)
 ПЕ ОТРЕК         ЗАДАЧА УЖЕ СНЯТА! ОСВОБОДИМ РЕКОРДЫ
 СЧ ШСТRLО
 И Е48(М2)
 ПЕ ОЧТЕР
 УИИ М17(М5)
 МОДА ОТРЕК
 ПИО (М5)
 ПВ ЗАНПРП(М15)
 УИА 3             СНТ.БЛПР
 СЧ ТСЛ(М2)
 И Е40
*
 УИА 0(М14)
* М14 = 0 :
*          ВЫДАЧА - СОБЫТИЕ ОБ ОКОН. ВЫДАЧИ
*          ПРИЕМ  - ОТНОСИТЕЛЬНЫЙ АДРЕС СЛОВА В РЕКОРДЕ
 ПО СОБМАТ       ВЫДАЧА
 СЧ ШСБРОС
 И Е48(М2)
 ПО СОБПР        БЫЛ СБРОС С ПРИЕМА: Э62 ’0102’
 МОД ТСЛ(М2)
 УИА (М16)
 СЧ ДЛ(М16)
 СДА 64+15
 УИ М17
 УИА 6(М12)
ЛИОБ МОДА СОБПР
 УИА (М15)
 СЧИ М15
 ПВ ЗАПВЫХ(М15)
 СЧИ М17
 СДА 64+10
 НТЖ П37
 УИ М15
 СЧ ШКРЗ
 И Е48-1(М5)
 ПО ОТРЕК
 СЧ 22(М3)
 НТЖ 32(М3)
 И Е48(М15)
 ПЕ ОТЛРАБ
ПРП3 СЧ КОТ           ’БУДИЛЬНИК’ ИЛИ ’ОБРЫВ СВЯЗИ’
 ИЛИ Е37           ИЛИ ’ТЕСТОВЫЙ РЕЖИМ’
 И ТСЛ(М2)
 ПО ПРП6          НЕ ТО, НЕ ДРУГОЕ, НЕ ТРЕТЬЕ
 УИА (М12)         ИСКАТЬ ЕТХ НЕЛЬЗЯ - ЕГО НЕТ
 ПБ ПЕРЕСЛ
ПРП6 МОДА (М16)         ***** П О И С К   Е Т Х *****
 СЧ Н(М14)
 СЛИА -1(М12)
 И БАЙТ(М12)
 НТЖ БАЙТ(М12)
 ПО ПРП4          ’377’
 МОДА Т
 НТЖ (М12)
 ПЕ ПРП5          НЕ ’145’
ПРП4 УИА -1(М12)       М12:
 ПБ ПЕРЕСЛ
ПРП5 МОДА ПРП6          НЕ ВСЕ СЛОВО
 ПИНО (М12)
ПЕРЕСЛ НОП ,             ПЕРЕПИСЬ СЛОВА К МАТЕМ.
 МОДА (М16)
 СЧ Н(М14)
 УИА ’2000’
 ЗП (М17)         МАГАЗИН !
 УИА 3
 СЧИ М14
 НТЖ ДЛ(М16)
 И П77777
 ПЕ ПРП2          РЕКОРД НЕ ИСЧЕРПАН
 МОД ВН(М16)
 УИА (М16)
 МОДА СОБПР
 ПИО (М16)         ЗАКОНЧИЛИ ПЕРЕСЫЛКУ
 УИА -1(М14)       НОВЫЙ РЕКОРД
ПРП2 МОДА СОБПР
 ПИНО (М12)         НАШЛИ ЕТХ - ПЕРЕСЫЛКУ ЗАКОНЧИЛИ
 СЛИА 1(М14)        ОЧЕРЕДНОЕ СЛОВО
 УИА 6(М12)
 СЧИ М17
 И П1777
 ПЕ ПРП3          В ТОМ ЖЕ ЛИСТЕ
 ПБ ЛИОБ
СОБПР НОП ,             СОБЫТИЕ ПО ПРИЕМУ С ТЕРМИНАЛА
 УИА 1(М14)
СОБМАТ НОП
 УИА 1027
 СЧ ШСБРОС
 ИЛИ Е48(М2)
 ЗП ШСБРОС        ’1’- НОРМАЛЬНОЕ СОСТОЯНИЕ
 СЧИ М5
 ИЛИ Е28(М14)
 ПВ ЗАПВЕТ(М16)
ОТРЕК МОД ТСЛ(М2)       ОТКАЗ ОТ БУФЕРА -
 УИА (М14)         - В О З В Р А Т   Р Е К О Р Д О В
ОРР1 МОД ВН(М14)       ( 1-Й РЕКОРД НЕ ОТДАЕМ)
 УИА (М14)         М14:= НАЧАЛО РЕКОРДА
 УИА 3
 ПИО ВСЕРЕК(М14)   ОТДАЛИ ВСЕ РЕКОРДЫ
 СЧИ М14
 СДА 64+5          М11:= N РЕКОРДА. М12:= N СТРОКИ ТРР
 И П37
 УИ М11
 УИА -КЛР+1(М12)
ОРР4 СЧИ М14           ЦИКЛ ПОИСКА СТРОКИ ТРР
 НТЖ ТРР1+КЛР-1(М12)
 И Е15П11
 ПО ОРР5          НАШЛИ СТРОКУ
 ЦИКЛ ОРР4(М12)
 ПБ СТ426         ИСПОРЧЕНА ССЫЛКА ВНИЗ
ОРР5 СЧ ДЛ(М14)       М13:= ЧИСЛО РЕКОРДОВ В МАКРОРЕКОРДЕ
 СЛЦ НАЧТЕК
 И П77777
 СДА 64+5
 НТЖ П77777
 СЛЦ Е1
 УИ М13
 УИА ’2003’
ОРР2 СЧ  ТРР1+КЛР-1(М12) 1 -> ТРР1
 ИЛИ Е48(М11)
 ЗП  ТРР1+КЛР-1(М12)
 И Е48П17
 НТЖ Е48П17
 ПЕ ОРР3          ЛИСТ НЕ ОСВОБОДИЛСЯ
 СЧ ТРР1+КЛР-1(М12)
 И Е16
 ПЕ ОРР3          ЛИСТ РЕНЕЛЬЗЯ ОТДАВАТЬ
 СЧ  ТРР1+КЛР-1(М12) ОТДАЕМ ЛИСТ
 И Е18П1
 СДА 64-2
 ПВ ТБУФ(М15)
 СЧ
 ЗП  ТРР1+КЛР-1(М12) 0 -> ТРР1
ОРР3 УИИ М15(М11)      ВНИМАНИЕ !
 СЛИА -’37’(М15)
 ПИНО ОРР3А(М15)    ВЕРНУЛИ НЕ ’37’-Й РЕКОРД
 ПИО ОРР1(М13)     ЭТО ПОСЛЕДНИЙ Р.МАКРОРЕКОРДА
 УИА -КЛР+1(М12)
 УИА -1(М11)       ЭТО ДЛЯ МАКРОРЕК-А В СМЕЖНЫХ ЛИСТАХ
ОРР3В СЧИ М14           ЦИКЛ ПОИСКА СТРОКИ ТРР
 СЛЦ Е11
 НТЖ ТРР1+КЛР-1(М12)
 И Е15П11
 ПО ОРР3А         НАШЛИ СТРОКУ СО СМЕЖНЫМ ЛИСТОМ
 МОДА ОРР3В
 ЦИКЛ (М12)
 ПБ СТ426         КАКАЯ-ТО ПУТАНИЦА!
ОРР3А СЛИА 1(М11)        НЕ ПОСЛЕДНИЙ Р.МАКРОРЕКОРДА *
 ЦИКЛ ОРР2(М13)     НЕТ
 ПБ ОРР1
ВСЕРЕК НОП ,
 МОД ТСЛ(М2)
 СЧ ТСВ           СОХРАНЕНИЕ КОДА ФУНКЦИОНАЛЬНОЙ КЛАВИШИ В М13
 УИ М13           М13 := КОД ФУНКЦ. КЛАВИШИ
 ПВ УСТНСР(М14)   УСТ.НАЧ.СОСТ.1-ГО РЕКОРДА
 УИА ’2003’        УСТ. БЛПР
 СЧ ВСЕЕД
 НТЖ Е48(М2)
 ЗП СМ
 И НУЖРЕК
 ЗП НУЖРЕК        ’0’ -> НУЖРЕК
 СЧ ШКЭ71         ’0’ -> ШКЭ71
 И СМ
 ЗП ШКЭ71
 СЧ ШКВЭ71
 И СМ
 ЗП ШКВЭ71
 СЧ ШЗНКТ1
 И Е48(М2)
 ПО ЕЕ
 НТЖ ШЗНКТ1
 ЗП ШЗНКТ1
 СЧ ШЗНКТ
 И СМ
 ЗП ШЗНКТ
ЕЕ СЧ ТСЛ(М2)
 И КОТ
 ПЕ ЕСТКОТ        КОД ОТВЕТА УЖЕ ЕСТЬ: <ОБРЫВ>,<БУДИЛ>
 СЧ ЕЗВОН-1       КОТ:=’10’
ЕСТКОТ ИЛИ ТСЛ(М2)       ГАШЕНИЕ ВСЕГО ЛИШНЕГО В ТСЛ
 И КГШТСЛ
 ЗП ТСЛ(М2)
 СЧ ЯЧОК(М3)
 И ЕОК
 ПО СОХРНФ        ЗАДАЧА НЕ ЗАКРЫТА ПО ОПРОСУ
 СЧ (М3)
 И Е48(М2)
 ПО СОХРНФ        ЗАКОНЧИЛ РАБОТУ НЕ ТОТ, КОГО ЖДЕМ
 СЧ ТСЛ(М2)       ЗАНЕСЕНИЕ КОДА ОТВЕТА
 И КОТ
 НТЖ ТСЛ(М2)       ГАШЕНИЕ КОТ-А В ТСЛ
 ЗП ТСЛ(М2)
 СЧМР
 СДА 64-6
 ЗП (М3)
 СЧ ЯЧОК(М3)
 НТЖ ЕОК
 ЗП ЯЧОК(М3)
 СЧ ЯЧОПР(М3)
 И ЕОПР
 ПО ПОСТАР        РАБОТА С ТЕРМИНАЛАМИ ПО СТАРОМУ
****** ЕСЛИ ЗАДАЧА ЗАКРЫТА, ТО ПИШЕМ КОД
****** ФУНКЦИОНАЛЬНОЙ КЛАВИШИ В М16 ЗАДАЧИ
 СЧИ М13
 ЗП ’10’(М3)      М16 := КОД ФУНКЦ. КЛАВИШИ
 СЧИ М2
 СЛЦ Е1
 ИЛИ (М3)          ОТВЕТ ПО НОВОМУ
 ЗП (М3)
 СЧ ТСЛ(М2)
 СДА 64-48+АТ
 СЧМР
 СДА 64-15
 ИЛИ (М3)
 ЗП (М3)
 СЧ ТСЛ(М2)
 И Q
 СДА 64-30
 ИЛИ (М3)
 ПБ ОТКРШГ
*
ПОСТАР СЧ (М3)
 СДА 64-22
 РЗБ Е48Е24
 СДА 64(М2)
ОТКРШГ ЗП (М3)
 СЧ Е48-1(М5)
 УИА ОТПРИМ(М15)   М15 := ОТПРИМ
 ПБ ВШГП
****** СОХРАНЕНИЕ КОДА ФУНКЦИОНАЛЬНОЙ КЛАВИШИ В БУФЕРЕ
СОХРНФ НОП
 СЧИ М2            НОМЕР ТЕРМИНАЛА
 СДА 64+3
 УИ М14           М14 := М2 DIV 8
 СЧМР
 СДА 64+45
 УМН ДЕЛЮ1
 СЧМР 64
 УИ М15           М15 := М2 МОD 8
 СЧ Е48П43
 СДА 64(М15)
 И НОМФУН(М14)
 НТЖ НОМФУН(М14)
 ЗП НОМФУН(М14)   ГАШЕНИЕ ПРЕДЫДУЩЕГО КОДА ФУНКЦ. КЛАВИШИ.
 СЧИ М13
 СДА 64-42(М15)
 ИЛИ НОМФУН(М14)
 ЗП НОМФУН(М14)   СОХРАНЕНИЕ КОДА ФУНКЦ КЛАВИШИ
 ПБ ОТПРИМ
*
*
*/////////////////////////////////////////////*
*//////                                 //////*
*//////   ВЫДАЧА ТЕКСТОВ ОТ Е23         //////*
*//////                                 //////*
*/////////////////////////////////////////////*
*
*
ВТЕ23 СЧ ШКУСТР
 ИЛИ ШКАС
 И Е48(М2)
 ПО ОЧТЕР   ТЕРМ.ОТБР
 СЧ ТСЛ(М2)
 И ЕПРИМ
 ПЕ ОЧТЕР   ТЕРМ.ЗАН.НА ПРИЕМ
 СЧ ШЗПРИМ
 ИЛИ ШКНОВ
 ИЛИ ШКВЫД
 И Е48(М2)
 ПЕ ОЧТЕР
 СЧ ШСТRLО
 И Е48(М2)
 ПЕ ВДОП1
 СЧ ШКЭ71
 И Е48(М2)
 ПЕ ОТЛРАБ        БЫЛО : ’ ПЕ ОЧТЕР ’
ВДОП1 СЧ Е17
 И ЗАНЯТА
 ПЕ ОТЛРАБ  ЗАНЯТ БУФ.ВЫВОДА
 ПВ ПРЛТВ(М14)    ЗАХВАТ Е17,ПРИП.ЛТВЫД К ЛИСТ.М.
 СЧ ШЗПРИМ
 ИЛИ Е48(М2)
 ЗП ШЗПРИМ        ЗАПРЕТ ПРИЕМА
 УИА 0
 СЧ ЦЕПТЕР(М2)
 СДА 64+15
 УИ М13           М13 := АДРЕС НАЧАЛЬНОГО КВАНТА
 СЧ 13(М13)       АДРЕС СЛЕДУЮЩЕГО КВАНТА
 ПО ПОСЛЕД        ПОСЛЕДНЯЯ ВЫДАЧА НА ЭТОТ ТЕРМИНАЛ
  СДА 64-15
 ЗП 13(М13)       ИСПОЛЬЗУЕМ КАК РАБОЧАЮ ЯЧ.
 СЧ ЦЕПТЕР(М2)
 СДА 64-33
 СДА 64+33
 ИЛИ 13(М13)       НОВЫЙ НАЧАЛЬНЫЙ АДРЕС
ПОСЛЕД ЗП ЦЕПТЕР(М2)
 УИА 3
 ПЕ ЕЩЕЕСТ        ЕЩЕ ЕСТЬ ВЫДАЧИ НА ЭТОТ ТЕРМИНАЛ
 СЧ Е48(М2)
ЛЕВАЯ НОП ,
 НТЖ ШКЗАК
 ЗП ШКЗАК
ЕЩЕЕСТ НОП ,
 УИА 0
 СЧ (М13)
 УИА 3
 МОДА ПНО
 НТЖ 0
 ПЕ ПОДГВ1        НЕ ’-’ ОТЛАДЧИКА
 СЧ ТСЛ(М2)
 ИЛИ ЕСЛВ          ЕСЛВ -> ТСЛ
 ИЛИ ЕПРИГ
 ЗП ТСЛ(М2)
ПОДГВ1 НОП ,
 УИА РПМВЫД-41(М7) М7 := РПМВЫД-41
 СЧ ТСЛ(М2)
 УИ М16           М16 := АДРЕС ПЕРВОГО РЕКОРДА
 СЧ 0
 ЗП 42(М7)
 УИА -LРЕК+1(М14)
У0РЕК НОП ,
 МОДА (М16)
 ЗП Н+LРЕК-1(М14)
 ЦИКЛ У0РЕК(М14)
 СЧ Е46Е45
 ЗП (М16)
 СЧИ М13
 СЛЦ П15
 ЗП 41(М7)        КОНЕЧНЫЙ АДРЕС
 УИИ М11(М1)       М11 := <М1>
 УИИ М3(М13)       М3  := <М13>
 УИИ М1(М16)
 СЧ 0
 ЗП ТСВ(М1)        ДЛЯ В.Т.
* ТСВ ДЛЯ О С = 0
 СЛИА Н(М1)
 ПВ ДЛЯЭ71(М16)
 УИИ М13(М3)       ВОССТАНОВИЛИ М13
 УИИ М1(М11)       ВОССТАНОВИЛИ М1
 СЧ 0
 УИА 0
 ЗП (М13)         ОСВОБОДИЛИ КВАНТ ЛТВЫД
 УИА 3
 УИА ОЧТЕР(М15)    М15 := ОЧТЕР
 СЧ Е17
 ПБ ГАШЗАН
*
*
*//////////////////////////////////////////////////////////*
*//////////////////////////////////////////////////////////*
*   О Б Щ И Е   В С П О М О Г А Т Е Л Ь Н Ы Е   Б Л О К И
*//////////////////////////////////////////////////////////*
*//////////////////////////////////////////////////////////*
*
*
*  УСТАНОВКА НАЧАЛЬНОГО СОСТОЯНИЯ 1-ОГО РЕКОРДА ТЕРМИНАЛА
*
УСТНСР НОП ,
 СЧ ТСЛ(М2)
 УИ М16
 ЗП ТБЛ(М2)
 СЧ 0
 УИА -30(М15)
ПР МОДА (М16)         РОСПИСЬ
 ЗП 31(М15)
 ЦИКЛ ПР(М15)
 СЧ Е46Е45        УСТ. НАЧ. ЗНАЧЕНИЯ СЧЕТЧИКА
 ЗП (М16)
 СЧ РАЗРЕК
 ЗП ДЛ(М16)
 ПБ (М14)
*
*
* ЗАНЕСЕНИЕ NЗАД В М5 ,АДР.ИПЗ НА М3
*
NКАН СЧ ТСЛ(М2)
 СДА 64+20
 И П77
 УИ М5
 МОД ТУСП-1(М5)
 УИА (М3)
 ПБ (М17)
*
*
*
*
*
 ВХОД ИРЕЗОТ,NКАН,УСТНСР
*
*//////////////////////////////////////////////////////////*
* КОНСТАНТЫ И РАБОЧИИ ЯЧЕЙКИ РЕЗОТ-А
* НАХОДЯЩИЕСЯ   М О Т С В С
МОТТ ВНЕШ ОТЛТЕР,Т,НОМФУН
МОТТ ВНЕШ ШРЕЗОТ,РРЕЗОТ,РЯРОТ,Н3724,П15
МОТТ ВНЕШ ШКРАБН,ШКДОП,ШКДИА,ШКАС,ЕДОП,ЕДИА,ШКСТАР,ЕСТАР
*//////////////////////////////////////////////////////////*
*
*
*
* В Н Е Ш Н И И
ДИСП70 ВНЕШ ТАС,ТСЛ,ТБЛ,ВППС,ТРР,ТРР1,КЛР
ДИСП70 ВНЕШ П5,П7,П17,П37,П60,П77,П1777,П77777
ДИСП70 ВНЕШ Е15П11,Е18П1,Е46П48,Е46Е45
ДИСП70 ВНЕШ ШСБРОС,ШЗН,ТАБТИП,(Q,Е18П16)
ДИСП70 ВНЕШ ВСЕЕД,ТУСП,ЗАНЯТА,СМ,ШКИВ,ГОД,ШГП
ДИСП70 ВНЕШ ТЕРЕЗА,ЯЧОК,ЕОК,ЯЧОПР,ЕОПР
ДИСП70 ВНЕШ ЗАНОСМ,ЗАНПРП,ГАШЗАН,ТБУФ,БИНОТ2,ЗАПВЕТ
ДИСП70 ВНЕШ ИВ25,Е48П43
*
*
МОТТ ВНЕШ  ШКОРАБ,ШКЗАПР,ШБУДИЛ,ШКНОВ,ШКЗАКВ
МОТТ ВНЕШ ВТОРШК,ШКОКВД,ШКЗАК
МОТТ ВНЕШ ШКУСТР,ШКЭ71,ШКВЭ71,ШКВЫД,ШКОПТТ
МОТТ ВНЕШ ШЗНКТ,ШЗНКТ1,ШVD130,ШНР,АСОТ
МОТТ ВНЕШ ШСТRLО,ШКОТЛ,ШТО,ШСВЯТ,СЛОN,НУЖРЕК,ШЗПРИМ
МОТТ ВНЕШ МЫЖДЕМ,ЦЕПТЕР,ШКОЛА,ДАЙРЕК
МОТТ ВНЕШ ПЕОТ,КОТ,Е48Е24,LINFЕЕ,LF130,КГШТСЛ
МОТТ ВНЕШ СБРОСБ,ПРЛТВ,УМАМПД,УСТВД1
МОТТ ВНЕШ ЕЗВ,ДЕЛЮ1
МОТТ ВНЕШ ЕПРИМ,ЕПРИГ,ЕСЛВ,ЕСВД,ЕЗВОН,ЕТАВ
МОТТ ВНЕШ Н,ДЛ,АДРТСВ,ТСВ,НАЧТЕК,ВН,РАЗРЕК,LРЕК
*
ТЕРМ ВНЕШ ДЛЯЭ71,ШКВТ
*
БОЭК1 ВНЕШ ЗАПВЫХ
*
ВИСП ВНЕШ ВШГП
*
КАЧКА ВНЕШ Е48П17,ШКРЗ
*
КИТ ВНЕШ (БАЙТ,КОНСБР)
*
СОСТАВ ВНЕШ ЕВМПД,ЕНРОТ,ЕНЕРОТ
СОСТАВ ВНЕШ АТ
*
АВОСТ ВНЕШ СТ426
*
 Е
 М
*КОНЕЦ ЧАСТИ

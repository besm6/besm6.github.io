АВРАСП СТАРТ ’10000’
 Б
 АIF (∧ВS ЕQ 0).АВР
∧С SЕТВ 1
 АGО .АВРСП
.АВР АNОР
∧С SЕТВ 0
.АВРСП АNОР
 Е
 М
 УПОТР АВРАСП(7)
ИПЗ КОНД В’0’
 КОНД М41В’3’
 ПАМ 3
 КОНД В’3’          БЛОК. ПРИП, ЗАЩИТЫ
 ПАМ 16
ПР38 КОНД В’46’
 ПАМ 1
ОШМ СЧ П5            ПРИЗНАК ОШИБКИ ПОЛЬЗОВ.
 ПБ ФИНС1
 КОНД В’0’
Е16П22 КОНД М15В’177’
ЗВЕЗД КОНД  П’     0’В’175’
К3703 КОНД М24В’3703’
*С М13:=0
Э74 УИА (М13)
 ПБ БУФ
 КОНД В’714244’
ДЛЯПЧ КОНД В’0’
 КОНД М15В’1’
 КОНД М47В’1’
РЯ1 КОНД В’0’
РЯ3 КОНД В’0’
ИНФСЛ КОНД В’0’
П700 КОНД М6В’7’
СБМБ УИА 48(М2)
 ПБ ПРОВВР        СБОЙ МБ
П120 КОНД В’122’
АВРСП ЗП ИПЗ+25
 УИА 1027
 ЗП РЭ2
 УИ М6
 И ЕЗП
 ПО СТ435         УХОД В АВРАСП ОТ НЕРЕЗИДЕНТА
 СЛИА -ПЗ-1(М6)
 УИА ’42’(М16)
 СЧ ШЗАПОС
 И Е48(М6)
 ПЕ СТ434         УХОД В АВРАСП З-ЧИ ИЗ ШЗАПОС
 УИА (М11)
 СЧ ТУСП(М6)
 УИ М1            АИПЗ
 СЧ 36(М1)
 СДА 64+41
 УИ М2
 ВЧ П120
 ПЕ ВЕР
 СЧ Е41П1
 И 36(М1)
 ЗП 36(М1)
 УИА (М2)
ВЕР НОП 0
 УИИ М3(М1)
 СЧ АВОС(М1)
 СДА 64+33
 ПЕ НАЧАЛО        ОТЛАДЧИК
 ЗП РЯ3
 ЗП ПОЛЕ
 УИА (М10)
 УИИ М3(М2)
 СЧ ЯЧИУПР(М1)
 И ЕИУПР
 ПО ВИДАВ
 СЧ 15(М1)
 ЗП 3(М1)
 СЧ 5(М1)
 И П77777
 НТЖ 5(М1)
 ЗП 5(М1)
 ПБ ВЫХОД1
ВИДАВ СЧ ЯЧОТБ(М1)
 И ЕОТБ
 ПЕ СБМБ          *С СБОЙ МБ ПРИ ПОДКАЧКЕ
 МОДА АПРЕ          *С ПРИЧИНА ’КОНЕЦ ЗАДАЧИ’
 ПИО (М2)    КЗ
 УИА 3
 СЛИА -’24’(М3)
 СЧИ М3
 СДА 64+2
 ПЕ НАДОС         НЕ КК, НЕ СЧ, НЕ ЗП, НЕ КРА
 СЧИ М3
 СЛЦ Е1
 ЗП РЯ3           *С РЯ3:=1∨2∨3∨4
НАДОС СЧ 5(М1)
 И Е3
 ПО НАДОСЛ
 СЧ 4(М1)
 СЛЦ П77777
 И П77777
 ЗП 4(М1)         Э=Э-1
 ЗП 3(М1)         АП=Э-1
НАДОСЛ СЧ 36(М1)
 И Е37
 ПЕ ЕСПО          *С ЕСТЬ СПО
 СЧ РЯ3
 ПО ВИДПР         НЕ КК, НЕ ЗП, НЕ СЧ, НЕ КРА
 НТЖ Е1
 ПО КК
 УИА 1(М10)        ПРИЗНАК Э67
 МОДА ФИНС2А
 УИА (М16)
ФИНС СЧ РЯ3
 ПО ОШМ           *С НЕ ОСТАНОВ ДЛЯ СПО ИЛИ Э67
ФИНС1 СДА 64-40
 УИА 3
 ЗП ИНФСЛ         *С 48÷41РР-ВИД АВОСТА
 СЧ 5(М1)         И27
 И Е5            ПРЕД. КОМАНДА МОД ИЛИ МОДА
 ПО ФИНС2         *С ПРЕД. КОМАНДА НЕ МОД И НЕ МОДА
 СЧ 6(М1)
 СДА 64-15
 ИЛИ ИНФСЛ
 ИЛИ Е37           ПРИЗНАК ПРЕДЫД. КОМАНДА МОД ИЛИ МОДА
 ЗП ИНФСЛ         *С 30÷16РР-<М20>, Е37-ПРИЗНАК МОД(А)
ФИНС2 СЧ 5(М1)
 И Е9
 СДА 64-22
 ИЛИ ИНФСЛ         *С Е9-ПРИЗНАК ПРАВОЙ ИЛИ ЛЕВ. ПОЛОВ.
 ЗП ИНФСЛ
 ПБ (М16)
ФИНС2А ИЛИ 3(М1)
 ЗП 38(М1)
 СЧ 36(М1)
 И Е38
 ПО ВРВИПЗ        *С НЕТ СТАНД. ВЫДАЧИ
 ПБ ВИДПР
ЗПИНФ ИЛИ 3(М1)
 ЗП ИНФСЛ         *С 15÷1РР-АДРЕС ВОЗВРАТА
 СЛИА -1(М5)
 УИА ’2000’        СНЯТИЕ БЛОКИР. ПРИПИСКИ
 ЗП (М5)          *С СЛОВО В ПОЛЕ СПО
 УИА 3
 СЧ РЯ3
 ПЕ ВРВИПЗ        НА ГАШ. МОДИФ. 34 И 35
ПРОВВР СЧИ М2
 НТЖ ПР38
 УИИ М16(М2)
 ПЕ ФТЕК2         *С ПРИЧИНА НЕ ’ИСТЕКЛО ВРЕМЯ’
 СЧ 39(М1)        ИСТЕКЛО ВРЕМЯ
 ПО ФТЕК2         *С НЕТ РЕЗЕРВА ВРЕМЕНИ
 СДА 64-25
 СЛЦ 39(М1)
 ПЕ СНЯТ          ВРЕМЯ ИСТЕКЛО
 И Е48П24
 ЗП РЯ3
 СЧ 33(М1)
 СДА 64-25
 СДА 64+25
 ИЛИ РЯ3
 ЗП 33(М1)        *С ОСТАТОК ВРЕМЕНИ-ПОЛЬЗОВ.
 УИА 40(М2)        *С ПРИЧИНА:ИСТЕКЛО ВРЕМЯ ПО ЭКС
 УИА 40(М16)
СНЯТ СЧ 0
 ЗП 39(М1)        *С ГАШ. РЕЗЕРВА ВРЕМЕНИ
ФТЕК2 СЛИА -13(М16)
 УИИ М13(М16)
 СЛИ М16(М16)
 СЛИ М13(М16)      *С М13-ОТНОС.АДРЕС РАСПЕЧАТКИ
БУФ УИА 1027
 СЧ ШМАРС
 ИЛИ Е48(М6)
 НТЖ Е48(М6)
 ЗП ШМАРС
 СЧ ИЗАХВ(М1)
 И Е27
 ПО БУФ1          ЕNQ НЕТ
 СЧ ИЗАХВ(М1)
 СДА 64-22
 РЗБ НАПРМГ
 ЗП ПОЛЕ
 СЧ ИЗАХВ(М1)
 СДА 64+20
 И П37
 УИ М17
 СЧ ИЗАХВ(М1)
 И Е26
 СДА 64+25
 УИ М16
 СЧ Е32(М17)
 НТЖ ЕNQМДС(М16)
 ЗП ЕNQМДС(М16)
 СЛИА 3(М16)
 СЧ ИЗАХВ(М1)
 И О27П21
 ЗП ИЗАХВ(М1)
 СЧ ШЗМД-3(М16)
 И Е32(М17)
 ПЕ БУФ1
 УИИ М4(М6)
 СЧ ЕАВРАС
 ПВ ЗАНПАД(М15)
 СЧ ПОЛЕ
 ПВ ОСВМД(М15)
 СЧ ЕАВРАС
 ПВ ЗАНПРТ(М15)
БУФ1 МОДА РАСПЕЧ
 УИА (М16)
ФТЕК3 МОДА ПОЛЕ+1
 УИА (М4)
 СЧ ЗВЕЗД
 ЗП -1(М4)
 УИА -2(М12)
 СЛИ М13(М16)
 МОДА ФТЕК5
 УИА (М16)
ПЕРЕС СЧ (М13)         БЛОК ПЕРЕСЫЛКИ
 УИА 3
 ЗП (М4)
 СЛИА 1(М4)
 СЛИА 1(М13)
 МОДА ПЕРЕС
 ЦИКЛ (М12)
 ПБ (М16)
ФТЕК7 СЛИА -5(М16)
 МОДА ФТЕК6
 ПИНО (М16)
 ПБ ФТЕ
ФТЕК5 УИА -15(М12)
 УИИ М16(М2)
 СЛИА -37(М16)
 МОДА ФТЕК7
ФТЕ ПИНО (М16)
 СЧ ИНФАВ(М1)
 СДА 64-27
 РЗБ =М47В’1’М24В’77’В’37777’
 ЗП (М1)
 СЧ ИНФАВ(М1)
 СДА 64+21
 СДА 64-8
 ИЛИ =П’ 0000 ’
ФТЕК6 НТЖ ЕТХ1
 ЗП -1(М4)
 МОДА ЗАГОЛ
 УИА (М13)
 СЛИА 2(М4)
 СЧ (М1)          *С <СМ> В МАССИВ ДЛЯ ПЕЧАТИ
 ЗП ПОЛЕ+4
 ЗП ПОЛЕ+5
 МОДА ПЕРЕС         *С ЗАСЫЛКА ЗАГОЛОВКА
 ПВ (М16)
 УИА -14(М12)
 УИИ М13(М1)
 УИИ М17(М4)
 СЧ 3(М1)         АП
 СЧМ 4(М1)       Э
 СЧМ 1(М1)      РК
 СДА 64-1
 СДА 64+42
ПЕРЕС1 СЧМ 21(М13)
 СЛИА -1(М13)
 МОДА ПЕРЕС1
 ЦИКЛ (М12)
*С КРА, ЗПСЧ
 СЧМ 22(М1)
 СЧМ 23(М1)
 СЧИМ М7
 УИИ М12(М17)
 СЛИА -1(М12)
 ЗП ДЛЯПЧ         ПРИПИСКА ВТОРОМУ ЛИСТУ
 СЧИ М12
 И П1777
 УИ М12
 МОДА ВРВИПЗ        *С ЕСТЬ Э67
 ПИНО (М10)
 СЧ 23(М1)
 И Е16П22
 УИИ М16(М2)
 СЛИА -48(М16)
 МОДА БЫЛСББ
 ПИО (М16)         *С БЫЛ СБОЙ МБ
 ПЕ ВРВИПЗ        *С ПРИЧИНУ СНЯТИЯ НЕ ЗАНОСИТЬ
 СЧИ М2
 СДА 64-15
ПРЧВИП ИЛИ 23(М1)
 ЗП 23(М1)        *С ПРИЧИНУ В ИПЗМ
ВРВИПЗ СЧ ИПЗ+33
 НТЖ Е48
 СЛЦ 33(М1)
 ЗП 33(М1)        *С ВРЕМЯ АВРАСПА-ПОЛЬЗОВ.
 СЧ Е48
 ЗП ИПЗ+33
ПРИК1 СЧ 5(М1)
 И П77777
 НТЖ 5(М1)
 ЗП 5(М1)         *С ГАШЕНИЕ М27
ВРВ1 МОДА КЗ
 ПИО (М2)          КОНЕЦ ЗАДАЧИ
 СЧ ЯЧЧП(М1)
 И ЕЧП
 ПЕ КЗ            БЫЛ СБОЙ, СНЯТЬ ЗАДАЧУ
 СЛИА -38(М2)
 МОДА КЗ
 ПИО (М2)          ИСТЕКЛО ВРЕМЯ
 СЛИА -11(М2)
 МОДА КЗ
 ПИО (М2)   НЕВЕР.ПАРОЛЬ
 СЛИА 49-64(М2)
 МОДА КЗ
 ПИО (М2)
 СЛИА 64-39(М2)
 СЧ 22(М1)
 И Е48П16
 ЗП 22(М1)        И 34:=0
 СЧ 23(М1)
 И Е48П16
 ЗП 23(М1)        И35:=0
 МОДА МЕТР
 ПИО (М2)          *С ПРИЧИНА ’ДАЙ МЕТРЫ’
 СЧ 36(М1)
 СДА 64+41
 НТЖ П77
 НТЖ П30
 ПО КЗ            *С ИСТЕКЛИ МЕТРЫ
 СЧ ЯЧОТБ(М1)
 И ЕОТБ
 ПЕ КЗ            СБОЙ МБ ПРИ ПОДКАЧКЕ
 СЛИА -26(М2)
 МОДА КЗ            СБОЙ, ВВЕДИ СНОВА
 ПИО (М2)
 СЛИА 46(М2)
 МОДА АВ1
 ПИНО (М2)          *С НЕ’СНЯТА ОПЕРАТОРОМ’
 СЧ 33(М1)        СНЯТА ОПЕРАТОРОМ
 И Е22
 ПЕ КЗ            БЫЛ ППРИКАЗ О СНЯТИИ
 СЧ 33(М1)
 ИЛИ Е22
 ЗП 33(М1)
АВ1 МОДА АУХСПО
 ПИНО (М11)         ЕСТЬ СПО
 МОДА АУХСПО        СПО НЕТ
 ПИНО (М10)         ЕСТЬ Э67
АВ МОДА ГАШАВ
 УИА (М13)
 СЧ 34(М1)
 ПЕ АВОСТ         ЕСТЬ АВ. ВЫДАЧА
 ПБ КЗ            НЕТ СПО И АВОСТНЫХ ВЫДАЧ
БЫЛСББ НТЖ 23(М1)
 ЗП 23(М1)
 СЧИ М2
 СДА 64-15
 УИА (М2)
 ПБ ПРЧВИП
МЕТР СЧ 33(М1)
 И Е23
 ПЕ КЗ            *С ДАЙ МЕТРЫ НА АВ. ВЫДАЧЕ
 СЧ 33(М1)
 ИЛИ Е23
 ЗП 33(М1)
 СЧ П777
 СДА 64-7
 ИЛИ 22(М1)
 ЗП 32(М1)
 ПБ АВ
АВОСТ2 МОДА СЧАВ
 ПВ (М15)         *С НА СЧЕТЧИК АВОСТОВ
 УИА АНВО1Е(М17)  АДРЕС ВХОДА В ПРОГР. ОБМЕНА
 ПО АУХ           *С ПОСЛЕДНИЙ АВОСТ
 МОДА ВЫХОД         *С М13:=ВЫХОД
 УИА (М13)
АУХ СЧ 34(М1)
 СДА 64+24
 И П77777
 ЗП ИНФСЛ         А ВХОДА В ПРОГР. АВ. ВЫДАЧИ
 ЗП 4(М1)
 СДА 64-24
 НТЖ 34(М1)
 ЗП РЯ3
 СЧ ИНФСЛ
 СДА 64-33
 РЗБ К3703
 ИЛИ РЯ3
 ЗП (М1)          КУС ЧТ ПРОГР. АВ. ВЫДАЧИ
 СЧ ИВ25
 И П77777
 ИЛИ 5(М1)
 ЗП 5(М1)
 ПБ (М13)
АУХСПО СЧ 36(М1)
 СДА 64+21
 УИ М17           *С М17:=АДРЕС ВХОДА В СПО
 ПБ ВЫХОД
ГАШАВ СЧ 0
 ЗП 34(М1)
ВЫХОД СЧИ М17
 ЗП 3(М1)
 СЧ ПОЛЕ
 ПО ВЫХОД1        НЕ ПЕЧАТАТЬ
 УИА 1(М14)
 Э64 ПЕЧ-АВРАСП+2048 ПЕЧ. АВ. ВЫДАЧИ
ДКЗ СЧ 0
 ПВ ТБУФ(М15)
 СДА 64-8
 УИ М5
 СДА 64-2
 ИЛИ КПЧКЗ       ПЧКЗ
 ИЛИ НОММБ1
 ПВ ФИЗОБМ(М16)
 ПБ НАЧПЧ-ПЧКЗ(М5)
ЭГДР СЧ 23(М1)
 СДА 64+15
 И П177
 УИ М15
 И П7
 ИЛИ Е48
 УМН П6
 СЧМР 64
 УИ М16
 СЧИ М15
 СДА 64+3
 УИ М15
 МОДА ТАБГДР
 СЧ (М15)
 СДА 64(М16)
 И П77
 ИЛИ (М1)
 ЗП (М1)
 ПБ (М14)
ТАБГДР КОНД В’0’
 КОНД М36В’302’
 КОНД В’75750073141305’
 КОНД В’0’
 КОНД В’72002000747000’
 КОНД В’72’
 КОНД (10)В’0’
КЗ НОП 0    М333
 СЧ 23(М1)
 СДА 64+15
 И П177
 НТЖ ПРИУПР
 ПО УПРЯ
 ПБ  М333
КЗ221 СЧ ИПЗ+25
 ИЛИ Е48
 ЗП ИПЗ+25
 Э64 ПЕЧ-АВРАСП+2048
КЗ222 УИА (М14)
 СЧ ИПЗ+25
 И Е47П1
 ЗП ИПЗ+25
 ПБ ДКЗ
ВПЧКЗ МОДА НАПЧ1
 ПИО (М14)
ВЫХОД1 СЧ Е48(М6)
 ПВ ВШГП(М15)
ВЫХАВ СЧ Е33
 ПБ ОТКАЗН
УПРЯ СЧ ЗОНЫ(М1)
 ЗП 21(М1)    ЯЧ ИН ИЗ МАЛ КАТ
НАПЧ1 УИА 1027
 СЧ ТЕРЕЗА(М1)
 ЗП Р
 ПО ММ
 МОДА ЕОС
 УИА (М16)
 МОДА КУЗ
 УИА (М15)
КИЗ НЕД П77777
 УИ М14
 СДА 64-40
 ИЛИ ЕОС1
 ЗП ЕОС
 УИИ М32(М15)
 СЧ Е23
 ПБ ТВ40
КУЗ УИА 1027
 СЧ ВСЕЕД
 НТЖ Е48(М14)
 ЗП РЯ1
 СЧ ШЗПРИМ
 И РЯ1
 ЗП ШЗПРИМ
 СЧ ТЕРЕЗА(М1)
 И РЯ1
 ЗП ТЕРЕЗА(М1)
 СЧ ШКЭ71
 И Е48(М14)
 ИЛИ ШЗНКТ1
 ЗП ШЗНКТ1
 СЧ ШЗНКТ
 НТЖ Е48(М14)
 ИЛИ ШЗНКТ1
 ЗП ШЗНКТ
ПБ НОП 0
 СЧ ТСЛ(М14)
 И ГЗ
 ЗП ТСЛ(М14)
 СЧ Р
 НТЖ Е48(М14)
 ЗП Р
 ПЕ КИЗ
 ПБ НАПЧ1
ММ СЧ Е48(М6)
 НТЖ ЕЗАР
 ПО КЗ2
 СЧ ШЗНМБ(М1)
 И ЕМАРС
 ПО ММ8
 СЧ НОМАРС+1
 ПО НМАРС
 ИЛИ Е40
 ПВ ЗАПВЕТ(М16)
НМАРС УИА ’2003’
 СЧ ШАРКЗМ
 ИЛИ Е48(М6)
 ЗП ШАРКЗМ
 ПБ ММ1
ММ8 СЧ ШЗНМБ(М1)
 И АХ
 ПО КЗ2
 СЧ Е48(М6)
 УИА 1027
 ИЛИ ШАРКЗ
 ЗП ШАРКЗ
 СЧ СОБА40
 ПВ ЗАПВЕТ(М16)
ММ1 СЧ ВСЕЕД
 НТЖ Е48(М6)
 ЗП РЯ1
 УИА -МЧПЗ(М16)
М СЧ ШКРЗ
 И Е48+МЧПЗ(М16)
 ПО МН
 МОД ТУСП+МЧПЗ(М16)
 СЧ ПОДЧ
 И РЯ1
 МОД ТУСП+МЧПЗ+НПЗ(М16)
 ЗП ПОДЧ
МН МОДА М
 ЦИКЛ (М16)
 СЧ ШКОК
 И Е48(М6)
 ПЕ ВЫХАВ
 СЧ И(М1)
 СДА 64+21
 И П77
 УИ М16
 СЧ ШКРЗ
 И Е48-1(М16)
 ПО Л
 СЧИ М16
 ИЛИ Е35
 ПВ ЗАПВЕТ(М16)
Л СЧ ШКОК
 ИЛИ Е48(М6)
 ЗП ШКОК
 ПБ ВЫХАВ
КЗ2 УИА 1027
 СЧ ШКИВ
 ИЛИ Е6
 ЗП ШКИВ КЗ
 СЧ ШККЗ
 ИЛИ Е48(М6)
 ЗП ШККЗ
 ПБ ММ1
АВОСТ СДА 64+15
 ПЕ АВОСТ2        АВОСТ 2 РОДА
 СЧ 36(М1)
 СДА 64+41
 УИ М17
 СЛИА -37(М17)
 СЧИ М17
 ПО О
 СЛИА -5(М17)
 СЧИ М17
 ПО О
 СЧ 3(М1)
 СДА 64-24
 ИЛИ Е23
 ЗП (М1)          ДЛЯ АЛГОЛА
О СЧ 34(М1)
 УИ М17           АДРЕС ВХОДА В АВОСТ 1 РОДА
 СЧ ЯЧЭК(М1)
 И ЕВАМОS
 ПО  АВОСТ1
 МОДА ЭГДР
 ПВ (М14)
АВОСТ1 МОДА СЧАВ
 ПВ (М15)         *С НА СЧЕТЧИК АВОСТОВ
 ПО ГАШАВ         *С ПОСЛЕДНИЙ АВОСТ
 ПБ ВЫХОД
СЧАВ СЧ ЯЧЧАВ(М1)
 СДА 64+41
 ПО (М15)         АВОСТ ОДИН
 СЧ ЯЧЧАВ(М1)
 СЛЦ Е42
 ПЕ ВХ
 СЧ ЯЧЧАВ(М1)
 И Е41П1
ВХ ЗП ЯЧЧАВ(М1)
 ПБ (М15)
ПРСПО УИА 1(М11)        ПРИЗНАК НАЛИЧИЯ СПО
 МОДА ЗПИНФ
 УИА (М16)
 ПБ ФИНС
ЕСПО СЧ 36(М1)
 СДА 64+21
 УИ М5            АДРЕС СПО
 СЧ РЯ3
 ПО ОШ            ОШ. МАТЕМ
 НТЖ Е1
 ПЕ ПРСПО         НЕ КК
 СЧИ М5
 ВЧОБ 3(М1)
 ПЕ ПРСПО         КК НЕ В СПО
 ВЧ П700
 ПО ПРСПО         КК НЕ В СПО
 УИА 24(М2)
 ПБ ПРОВВР
ОШ МОДА СЧАВ
 ПВ (М15)
 ПЕ ПРСПО         АВОСТ БУДЕТ ОБРАБОТАН
 ПБ ВИДПР
АПРЕ СЧ СУММАВ(М1)
 УИА (М16)
 СДА 64+33
 ПО Э74           ЗАКОНЧИТЬ ЗАДАЧУ
 УИ М17
 СЧ 5(М1)
 И П77777
 НТЖ 5(М1)
 ЗП 5(М1)
 ПБ ВЫХОД
КК МОДА ККМ
 УИА (М16)
 СЧИ М16
 ПВ ЗАПВЫХ(М15)
 СДА 64+8
 ЗП 4(М7)
 СЧ 3(М1)
 УИ М15
 СЧ Е11
 УИ М21
 СЧ (М15)
ККМ УИА 3
 ЗП (М1)
 СЧ ПР13
 УИ М21
 ПБ ВИДПР
ПЕЧ ЗП ПОЛЕ-АВРАСП+2048 НАЧАЛО МАССИВА
 ЗП 2048(М12)     КОНЕЦ МАССИВА
 КОНД В’0’          БЦ ПЕЧ. ТЕК ОБ ОШИБКЕ
 КОНК К’030240020’
 КОНК К’000200000’
 КОНК К’020520020’
 КОНК В’0’
 КОНД В’0’          БЦ ПЕЧ. ЗАГОЛОВКА
 КОНК К’020000005’
 КОНК К’100060023’
ЗАГОЛ КОНД М32П’  ’М16П’АП’М8В’173’В’7’
 КОНД М40П’Э’М32В’173’М8П’,РК’В’173’
 КОНД М40В’23’М24П’И1’М16В’173’П’*И’
 КОНД М40П’2’М32В’173’М24В’37’М8П’И3’В’173’
 КОНД М24П’ЕИ4’М16В’173’П’ЛИ’
 КОНД М40П’5’М32В’173’М8П’СИ6’В’173’
 КОНД М24П’ЧИ7’М16В’173’М8В’75’П’И’
 КОНД М32П’10’М24В’173’М16В’103’П’И1’
 КОНД М40П’1’М32В’173’М24В’111’П’И12’
 КОНД М40В’173’М32В’117’М8П’И13’В’173’
 КОНД М40В’125’М16П’И14’М8В’173’В’133’
 КОНД М24П’И15’М16В’173’М8В’141’П’И’
 КОНД М32П’16’М24В’173’М16В’147’П’И1’
 КОНД М40П’7’М32В’173’М24В’155’П’КРА’
 КОНД М40В’173’М32В’162’П’ЗПСЧ’
 КОНД М40В’172’
ПРИУПР КОНД В’103’
О27П21 КОНД В’-1’М20В’177’
*
*
*   О Т Л А Д Ч И К   *
* БЛОК РЕАКЦИИ НА ОСТОНОВ В ПРОГРАММЕ ПОЛЬЗОВАТЕЛЯ*
*
*
*
НАЧАЛО УИ М2            * В Е G I N *
 УИА (М13)         М2:= АИПО. М4,М5,М13,М17:= 0.
 МОДА АВАВ
 УИА (М15)
 СЧИ М15
 ПВ ЗАПВЫХ(М15)
 УИА (М17)         (М3 = АИПЗ, М6 = N ЗАДАЧИ)
 УИА (М5)
 УИА -4(М4)
*
 РЖА 3             БЛОКИРОВКА НОРМ.ОКРУГ.
*
 СЧ 5(М3)
 И Е3            РЕЖ.Э - 3Р М27
 ЗП S3
 ПО НАЧ           АВОСТ НЕ В ЭКСТРАКОДЕ
 СЧ 5(М3)         S3≠0 - ПРИЗНАК АВОСТА В ЭКСТРАКОДЕ
 И Е30П16        ГАШЕНИЕ М27, М33:=<М32>-1
 ЗП 5(М3)
 УИА 1(М17)
НАЧ СЧИ М17           М17:= N ПРИЧИНЫ ОСТАНОВА,М2:=АИПО
 МОДА (М17)
 ВЧОБ 3(М3)
 ЗП 3(М3)
 СЧ ’44’(М3)
 СДА 64+41
 УИ М17
 УИИ М1(М17)
 СЧ ТЕРЕЗА(М3)
 И ШКОТЛ
 НЕД П77777
 ЗП R2
 СДА 64-40
 ЗП S4
 УИА 1-ДИП(М12)
 УИА (М14)
 СЧ 0
 ЗП S5
КСИ СЛЦ S5            КОНТРОЛЬНОЕ СУММИРОВАНИЕ ИНФОРМАЦИИ
 ЗП S5
КСИ1 УИА ’2000’        СЧИТЫВАНИЕ СЛОВА
 МОДА (М14)
 СЧ 1(М2)
 УИА 3
 СЛИА 1(М14)
 МОДА КСИ
 ЦИКЛ (М12)         ОСНОВНОЕ ПОЛЕ
 МОДА КСИ2
 ПИНО (М4)          НЕ СУММИРОВАЛИ 5 СЛОВ ОСН.ПОЛЯ
 ПЕ КСИ           ИНФОРМАЦИЯ ОБ УЗЛАХ
 УИА ’2000’        ЗАКОНЧИЛИ СУММИРОВАНИЕ
 СЧ (М2)
 УИА ’2003’
 НТЖ S5
 ПЕ АВАВ1         ИСКАЖЕНИЕ ИНФОРМАЦИОННОГО ПОЛЯ
 СЛИА ДИП(М2)       М2:= АИПО + ДИП
 СЧИ М2
 УИИ М14(М2)
 ВЧОБ 3(М3)
 СДА 64+1
 СЛИА -20(М1)
 ПЕ АА            АВОСТ НЕ НА ИПО
 УИА ’2000’        СЧИТЫВАНИЕ 1ГО СЛОВА
 СЧ 1(М2)
 УИА 3
 ЗП 3(М3)
 СДА 64+24
 МОДА КК1
 ПИО (М1)          КК НА ИПО
 ПО СООБЩ         АВОСТ ПРИ ВЫПОЛНЕНИИ НЕСТ.ДЕЙСТВ.
 ВЧ Е1            ПОДПРАВКА АДРЕСА АВОСТА (М33)
 ЗП 3(М3)
АА СЧ 3(М3)         АНАЛИЗ АВОСТА
 УИ М4            М4:=<М33>
 МОДА ОБРОС
 УИА (М10)
 МОДА АК
 УИА (М12)
 СЛИА 2(М14)
 СЧИ М1
 ПЕ НЕКОК
 УИА 51(М17)       ЗАМЕНА N РАСПЕЧАТКИ (УЗЕЛ)
 СЛИА 5(М14)
 ПБ ССИПО         НА ПОИСК ИНФОРМАЦИИ
КСИ2 УИИ М12(М4)       ПЕРЕСКОК ЧЕРЕЗ 2 СЛОВА
 УИА (М4)
 СЛИА 1(М14)
 ПБ КСИ1
НЕКОК СДА 64+2
 УИА 3
 ПЕ СООБЩ         НЕ КК, КРА, ЗПСЧ
 СЛИА 53(М17)
 СЧИ М1
 НТЖ П3
 ПО ССИПО         ОСТАНОВ ПО КРА
 СЛИА 2(М14)        ОСТАНОВ ПО ЗПСЧ
 УИА 1(М5)
 ПБ ССИПО         НА ПОИСК
ПИКУ РЖА ’13’          АНАЛИЗ <S>
 СЛИА 1(М14)
 ПЕ ССИПО         <S> - КРАТНЫЙ УЗЕЛ
 СЛИА 1(М14)        <S> - УСЛОВНЫЙ УЗЕЛ
ССИПО УИА ’2000’        СЧИТЫВАНИЕ СЛОВА ИПО
 СЧ (М14)
 УИА 3
 ПО СНЯТИЕ        ИНФОРМАЦИИ НЕТ - ПРОСТОЙ ОСТАНОВ
 ЗП S             S:=СЛОВО ИПО
 МОДА ЕСТЬИ
 ПИНО (М1)          НЕ КК
 НТЖ 3(М3)         ПОИСК ИНФОРМАЦИИ К УЗЛУ <М33>
 УИ М15
 СЧ S
 МОДА ПИКУ
 ПИНО (М15)         <S> ≠ <М33>
ЕСТЬИ СЛИА ЗАП-ОБРОС(М10) НАШЛИ ИНФОРМАЦИЮ К ОСТАНОВУ
 РЖА ’13’
 ПО УСОС          ОСТАНОВ ПО УСЛОВИЮ
 ВЧ Е16           КРАТНЫЙ ОСТАНОВ
 ЗП S
 СДА 64+15
 И П77777
 ПО СНЯТИЕ        КРАТНОСТЬ ИСЧЕРПАНА
 СЧ S             УМЕНЬШЕНИЕ КРАТНОСТИ
 РЖА ’23’
 ПО ЗАП           ’НОРМАЛЬНАЯ’КРАТНОСТЬ
 УИА -1(М13)       ОСТАНОВ ТОЛЬКО ПОСЛЕ ИСЧЕРПАНИЯ КРАТ.
 СЧ Е16           ПОДПРАВКА КОНТР.СУММЫ ИПО
 НТЖ ВСЕЕД
 УИА ’2000’
 СЛЦ -ДИП(М2)
 ЗП -ДИП(М2)
 УИА 3
 СЧ S
ЗАП УИА ’2000’        ЗАПИСЬ НА ИПО
 ЗП (М14)
 УИА 3
 МОДА ОБРОС
 ПИО (М11)         ОСТАНОВ НЕ СНЯТ
 СЛИА 1(М14)        ГАШЕНИЕ ИНФОРМАЦИИ О СНЯТОМ ОСТАНОВЕ
 МОДА ЗАП
 ЦИКЛ (М13)         УСЛОВНЫЙ ОСТАНОВ
ОБРОС УИА ’2000’        S2:= << М33 >>
 СЧ (М4)
 УИА 3
 ЗП S2
 МОДА ЗИПОН
 УИА (М10)
В УИА (М14)         ПОДГОТОВКА КОМАНДЫ ВОЗВРАТА
 СЛИА 1(М4)         S1:= 00 30 <М33>+1 00 00 00000
 СЧИ М4
 СЛИА -1(М4)
 СДА 64-24
 ИЛИ Е44Е43
 ЗП S1
 СЧИ М1
 ПО АК            ОСТАНОВ ПО УЗЛУ
 НТЖ П3
 ПО (М12)         ОСТАНОВ ПО КРА
 СЧ 23(М3)        ОСТАНОВ ПО ЗПСЧ - ГАШЕНИЕ М35
 И П77777        S1:= 00 30 <М33>+1 00 00 <М35>
 ИЛИ S1
 ЗП S1
 И П77777
 НТЖ 23(М3)
 ЗП 23(М3)
 СЧ 5(М3)
 И Е9
 ПЕ ЗПСЧ          ОСТАНОВ НА ПРАВОЙ.ПРК (9Р М27)=1
 СЧ S3            ПРК = 0
 ПЕ ЗПЭ75         ОСТАНОВ ПО ЗАПИСИ В Э75
 УИА -24(М14)      АНАЛИЗ ПРАВОЙ !
АК СЧ S2            АНАЛИЗ СТРУКТУРЫ КОМАНДЫ
 СДА 64+36(М14)    М15:= КОП
 И П377
 УИ М15
 И Е8
 УИ М12
 ПО АК1           КОМАНДА 1-Й СТРУКТУРЫ (М12 = 0)
 СЧИ М15           М12 ≠ 0
 СДА 64+3
 УИ М15
 НТЖ П31
 ПЕ АК1           НЕ 31 КОМАНДА (ПВ)
 СЛИА ЗИПОКК-ЗИПОН(М10) ИЗМЕНЕНИЕ ВОЗВРАТА (ЧЕРЕЗ КК)
 СЧ S2
 СДА 64+24(М14)
 ЗП S2
К СДА 64-24         ПРИВЕЛИ К ОБЩЕМУ ВИДУ
 И Е48П45
 ПО ЗИПО          N ИР-А =0 - НИЧЕГО НЕ МЕНЯЕМ
 ИЛИ S1            ЗАМЕНА ИР 31 АДРЕС
 ЗП S1            НА     00 22 АДРЕС. N ИР-А -> S1
 СЧ ВСЕЕД
 НТЖ Е24П16
 И S2
 ИЛИ Е17
 ИЛИ Е20
АЗ ЗП S2            КОМАНДА ГОТОВА
 ПБ ЗИПО          АНАЛИЗ ЗАКОНЧЕН
АК1 МОДА АПК
 ПИНО (М14)         ПРАВАЯ КОМАНДА
 УИА -24(М14)
 МОДА АК
 ПИНО (М1)          ОСТАНОВ ПО КРА - АНАЛИЗ ПРАВОЙ
* АНАЛИЗ ЛЕВОЙ КОМАНДЫ УЗЛА
 СЧИ М15
 МОДА К2С
 ПИНО (М12)         КОМАНДА 2Й СТРУКТУРЫ
 И П37
 УИ М16
 ИЛИ Е2
 НТЖ П3
 ПО ПРМАГ2        ОПЕРАЦИИ ЗПМ,УИМ, СЧМ,СЧИМ
 СЧИ М15
 ВЧ П30
 ПО ЭКСТ          НЕ МАГАЗИННАЯ КОМАНДА: КОП > 27
ПРМАГ СЧ S2            М16:=А
 СДА 64+24
 И П7777(М12)
 УИ М16
 СЧ 5(М3)
 И Е5
 ПО ПРМАГ1        ПРЕДЫДУЩАЯ КОМАНДА НЕ МОД(А)
 МОД 6(М3)         М16:= А+<М20>
 СЛИА (М16)
 ПБ ПРМАГ1
ПРМАГ2 СЛИА -3(М16)       ОПЕРАЦИИ 001,041(+),003,043(-)
 УИИ М15(М16)
МАГ СЧ 7(М3)         М16:= <М17> ИЗ ИЗПМ
 УИ М16
 МОДА УММАГ
 ПИО (М15)         ЗП ПО МАГАЗИНУ
УВМАГ СЛИА 1(М16)        М17:=М17+1 В ИПЗМ
 СЧИ М16
 ЗП 7(М3)
 ПБ АС1           НА АНАЛИЗ ПРАВОЙ КОМАНДЫ УЗЛА
УММАГ СЛИА -2(М16)       М17:=М17-2 В ИПЗМ
 ПБ УВМАГ
К2С УИА П77777-П7777(М12) ЛЕВАЯ КОМАНДА С ДЛИННЫМ АДРЕСОМ
 НТЖ П23
 ПО ПРМАГ         ЭТО МОД (23)
 НТЖ Е1
 ПО МОДА          ЭТО МОДА (22)
 УИА -3(М12)       ВОЗМОЖНО Э20,Э21
ЭКСТ СЧ S3            ЛЕВАЯ КОМАНДА НЕ ЭКСТАКОД *
 ПО АС1           НЕТ
 СЧИ М15           ЗАМЕНА ИР ЭКС АДРЕС
 СДА 64-36(М12)    НА     16 ЭКС С НУЛЕВЫМ АДРЕСОМ
 ИЛИ Е46П48
 ИЛИ Е43
 НТЖ Е43
 ПБ АЗ            АНАЛИЗ ЗАКОНЧЕН
МОДА СЧ 5(М3)         22-Я УСПЕЛА ВЫПОЛНИТЬСЯ ДО КК
 ИЛИ Е9            УСТАНОВКА ПРК - ПРОПУСТИМ 22-Ю
 ЗП 5(М3)
 ПБ АС1
АПК МОДА ЗПСЧ
 ПИО (М12)         ПРАВАЯ КОМАНДА 1Й СТРУКТУРЫ
 СЧИ М15
 НТЖ П23
 СДА 64+1
 ПО ВОЗВКК        ЭТО МОД(А)
ЗПСЧ СЧ S1            ЕСЛИ ОСТАНОВ ПО ЗПСЧ СНЯТ,
 И П77777        ВОЗВРАТ ЧЕРЕЗ КК НА ИПО НЕ НУЖЕН
 ПЕ ВОЗВКК        ЗПСЧ НЕ СНЯТ
ЗИПО СЧ S2            ЗАПИСЬ ПОДГОТОВЛЕННЫХ КОМАНД НА ИПО
 УИИ М11(М2)
ЗИПО1 УИА ’2000’        (ПОДКАЧКА ЛИСТА ИПО)
 ЗП (М11)
ЗИПО2 УИА ’2003’        ЗАПИСЬ КОМАНДОЙ
 РЕГ ’101’
 УИА ’2000’
 ЗП (М11)
 УИА ’2003’
 РЕГ ’107’
 СЧ S1
 СЛИА 1(М11)
 РЖА 3
 ПБ (М10)
ЗИПОН СЛИА ИЗМ33-ЗИПОН(М10) ЗАПИСЬ ВОЗВРАТА
 ПБ ЗИПО2
ЗИПОКК УИА ’2000’        ЗАПИСЬ ЧИСЛОМ (КК НА ИПО)
 ЗП (М11)
ИЗМ33 УИА 3
 СЧИ М2
 ЗП 3(М3)
 ПБ АС
ЗПЭ75 СЧ М16Э75        S2:= 16 075 0000 00 000 0000
 ЗП S2
ВОЗВКК СЛИА ЗИПОКК-ЗИПОН(М10) ВОЗВРАТ К МАТЕМАТИКУ
 ПБ ЗИПО          ВОЗМОЖЕН ТОЛЬКО ЧЕРЕЗ КК НА ИПО
КК1 СЧ 3(М3)         КК НА ИПО => ВОЗВРАТ В ПРОГРАММУ
 И П77777        ПОЛЬЗОВАТЕЛЯ. ВОССТАНОВЛЕНИЕ М33
 ЗП S2
 НТЖ 3(М3)
 УИА ’2003’
 ПО КК2           ВЫПОЛНИЛИ НЕСТАНДАРТНОЕ ДЕЙСТВИЕ
 СДА 64+24
 ЗП 3(М3)
 СДА 64+20
 ПО ЗПСЧ1         КОМАНДЫ ПВ (31) НЕ БЫЛО
 НТЖ П17           М33:=<М20>,ИР:=<М33>+1,
 УИ М16           ГАШЕНИЕ ПРИК(РК) - В ИПЗМ
 СЧ 3(М3)
 МОДА 7(М3)
 ЗП (М16)
 СЧ 6(М3)
 ЗП 3(М3)
 СЧ 5(М3)
 НТЖ Е5
 ЗП 5(М3)
ЗПСЧ1 СЧ S2            ЧТО БЫЛО *
 ПО ОТЗ           КРА,КК
 СЧ 23(М3)        ПОСЛЕ ЗПСЧ
 И П77777
 ПЕ ОТЗ           <М35> ≠0 => УСТАНОВИЛИ НОВЫЙ ЗПСЧ
 СЧ S2            ВОССТАНОВЛЕНИЕ М35
 ИЛИ 23(М3)
 ЗП 23(М3)
ОТЗ СЧ Е48(М6)       ОТКРЫТИЕ ЗАДАЧИ В ШГ
 ПВ ВШГП(М15)
ВЫХ СЧ Е48
 ЗП ИПЗ+33
 ПБ ВЫХАВ
* БЛОК ПРОВЕРКИ ВЫПОЛНЕНИЯ УСЛОВИЯ ОСТАНОВА
УСОС СДА 64+38
 И П7
 УИ М16           М16:= ИДЕНТИФИКАТОР ЛЕВОЙ ЧАСТИ
 СДА 64+1
 МОДА -2(М16)
 ПЕ УСМ           СМ,МР,РК
 СЧ S             АД,ИР
 СДА 64+30
 И П17
 ПО УАД           В УСЛОВИИ АДРЕС БЕЗ ИР-А
 НТЖ П17
 УИ М15
 МОДА 7(М3)
 СЧ (М15)
 МОДА УСОС1
 ПИНО (М16)         ИР
УАД СДА 64-15
 СЛЦ S
 СДА 64+15
 УИ М16
УАД1 УИА ’2000’        СЧИТЫВАНИЕ СЛОВА
 СЧ (М16)
 УИА 3
 ПБ УСОС1
УСМ СЧ (М3)
 ПБ УСОС1
УСОС1 ЗП S1            S1:= ЗНАЧЕНИЕ ЛЕВОЙ ЧАСТИ
 РЖ S             (БЛОК.НОРМ.И ОКР.)
УСОС2 СЧ S             ВЫБОРКА ЗНАКА УСЛОВИЯ
 СДА 64+35
 И П3
 УИ М16
 МОДА (М16)
 СЧ ПРУС
 УИА ’2003’
 РЕГ ’101’
 ЗП УСОС3
 УИА -7(М15)
ВЫБРЗ ЗП 1             ВЫТАЛКИВАНИЕ БРЗ
 МОДА ВЫБРЗ
 ЦИКЛ (М15)
 РЕГ ’107’
 УИА ’2000’
 СЧ 1(М14)
 УИА 3
 УИА -1(М13)
УСОС3 ЗП 0             ПОЛЕ ДЛЯ ПРОВЕРКИ УСЛОВИЯ
 ЗП 0             ( ВЫПОЛНЕНО => СНЯТИЕ )
 РЖА 3
 ПБ ОБРОС         УСЛОВИЕ НЕ ВЫПОЛНЕНО
СНЯТИЕ УИИ М11(М4)       М11 ≠ 0 - ПРИЗНАК СНЯТИЯ ОСТАНОВА
 МОДА СНУ
 ПИО (М1)          СНИМАЕТСЯ УЗЕЛ
 СЛИ М5(М3)        КРА,ЗПСЧ
 СЧ 22(М5)
 И Е48П16
 ЗП 22(М5)
 СЛИА СООБЩ-АК(М12) ЕСЛИ КРА СНЯТ - ВЫПОЛНЯЕМ НА МЕСТЕ
 СЧ
 ПБ (М10)
СНУ СЧ Е48           СМ:=<<М33>>.КОМАНДЫ СНЯТОГО
 ЗП S1            УЗЛА ВЫПОЛНЯЮТСЯ НА СВОЕМ МЕСТЕ
 УИА ’2000’
 СЧ (М4)
 УИА ’2003’
 ПБ ЗИПО2         НА ЗАПИСЬ
ПРМАГ1 МОДА АС1
 ПИНО (М16)         АИСП ≠ 0
 СЧ S2
 И Е48П45
 НТЖ Е48П45
 ПО МАГ           N ИР-А = ’17’
АС1 МОДА АК
 ПИО (М11)         УЗЕЛ НЕ СНЯТ
АС МОДА ОТЗ
 ПИНО (М13)         СООБЩЕНИЕ НЕ ВЫДАВАТЬ
СООБЩ СЧ 25(М3)        БЛОК ВЫДАЧИ НА ТЕРМИНАЛ
 И Е27
 ПЕ ЕNQ
 СЧИ М17
 НТЖ П60
 НТЖ Е1
 ПО СПЕЦАВ
 НТЖ П17
 ПО СПЕЦАВ
 УИА -2(М15)
 СЧИ М4            МОЖНО ВЫДАТЬ
 СДА 64-33
 РЗБ РАДР
 МОДА СОО
 ПИО (М17)         КОНЕЦ ЗАДАЧИ
 СЛИА -13(М17)
СОО ИЛИ Л175          ПОДГОТОВКА СООБЩЕНИЯ
 ЗП ПОЛЕ
 МОДА (М17)
 МОДА (М17)
 МОДА РАСПЕЧ
 СЛИА (М17)
СООБ МОДА (М17)         ПЕРЕНОС ТЕКСТА СООБЩЕНИЯ
 СЧ 2(М15)
 МОДА ПОЛЕ
 ЗП 3(М15)
 МОДА СООБ
 ЦИКЛ (М15)
 НТЖ ЕТХ1
 ЗП ПОЛЕ+3
 МОДА ВТТ1
 УИА (М15)
 УИА ’2000’
 СЧ 6(М2)
 УИА 3
 ЗП R2
 РЖА ’23’
 ПО ВТТ2          СИТУАЦИЮ НА ЭКРАН НЕ ВЫДАВАТЬ
 СЛИА VIDЕО-ВТТ1(М15) ПОДПРАВКА А ВОЗВРАТА ПОСЛЕ ТВ40
 ПБ ВТТ
КК2 МОДА ИСК           ПЕЧАТАЕМ ТОЛЬКО -.
 УИА (М15)
ВТТ1 СЧ МИС           ПЕЧАТЬ МИНУСА
 ЗП ПОЛЕ
ВТТ СЧ R2            НУЖНА ЛИ ПАРАЛЛЕЛЬНАЯ ВЫДАЧА НА АЦПУ
 РЖА ’13’
 ПЕ ВТТ2          НЕТ - 48Р.6-ГО СЛОВА ИПО = 0
 СЧИ М7            ПЕЧАТЬ - Э64
 ЗП ДЛЯПЧ
 Э64 РRINТ-АВРАСП+2048
ВТТ2 МОДА ПОЛЕ          М16:= А ТЕКСТА
 УИА (М16)
 УИИ ’32’(М15)     М32:= ВОЗВРАТ ИЗ ТВ40
 СЧ ПОЛЕ          УСТАНОВКА N ТЕРМИНАЛА
 И Е40П1
 ИЛИ S4
 ЗП ПОЛЕ
 СЛИА ИСК-ВТТ1(М15)
 СЧ Е23
 ПБ ТВ40          ВЫДАЧА СТРОКИ
ИСК СЧ Е48(М6)       РАЗРЕШЕНИЕ НАБОРА ОЧЕРЕДНОЙ
 ИЛИ ШКРДО         ДИРЕКТИВЫ : Е ЗАДАЧИ->ШКРДО
 ЗП ШКРДО
 СЧ ДСТОП         ГАШЕНИЕ ПРИЗНАКА <СТОП> - СТРАХОВКА
 ИЛИ Е48(М6)       ЕСЛИ ПОЛЬЗОВАТЕЛЬ НЕ ДОЖДАЛСЯ
 НТЖ Е48(М6)       ОСТАНОВА - СРАБОТАЛ <НРОТ>
 ЗП ДСТОП
 ПБ ВЫХ
АВАВ1 УИА 3             СНТ.БЛПР. ВЫДАЧА СООБЩЕНИЯ: НЕТ ИПО
 МОДА СООБЩ
 УИА (М16)
 УИИ ’32’(М16)
 МОДА САВ
 УИА (М16)
 СЧ (М16)
 И Е40П1
 ИЛИ S4
 ЗП (М16)
 МОД 3(М3)
 УИА (М4)
 СЧ Е23
 ПБ ТВ40
СПЕЦАВ УИИ М14(М17)      М14:= НОМЕР АВОСТА
 УИА ’2003’
 ПБ ИМКНЦ
АВАВ УИА 77(М14)       М14:= 77
 ПБ ИМКНЦ
ЕNQ УИА 78(М14)       М14:= 78
 УИА ’2003’
ИМКНЦ МОД R2            ИММИТАЦИЯ ОПЕРАЦИИ < КНЦ > Е34->ШКИВ
 УИА (М16)         УСТАНОВКА ШЗПРИМ,ШКВЫД,ШТО.
 СЧ ШЗПРИМ
 ИЛИ Е48(М16)
 ЗП ШЗПРИМ
 СЧ ШКВЫД
 ИЛИ Е48(М16)
 НТЖ Е48(М16)
 ЗП ШКВЫД
 СЧ ШТО
 ИЛИ Е48(М16)
 ЗП ШТО
 СЧ ЕНРОТ
 ИЛИ ШКИВ
 ЗП ШКИВ
 СЧИ М14
 ИЛИ КНЦ
 МОД ТСЛ(М16)
 ЗП Н
 ПБ ИСК
*
* БЛОК ВЫДАЧИ СИТУАЦИИ НА ЭКРАН   V I D Е О Т О N-А - 3 4 0 *
*
VIDЕО МОДА ПОЛЕ          ВЫДАЧА 2-Й СТРОКИ
 УИА (М17)         *** КОМАНДА ***
 СЧ SРS5К
 ЗП (М17)
 СДА 64+8
 ЗП 5(М17)
 УИА ’2000’
 СЧ (М4)
 УИА 3
 ЗП R
 СДА 64-24
 ЗП R1
 УИА -1(М1)
КОМ1 МОДА (М1)
 СЧ R1
 УИА 15(М12)
 УИА -1(М5)
 И Е44
 МОДА РАДР
 УИА (М14)
 УИА Е44П41(М4)
 ПЕ КОМ2
 СЛИА КРК1-РАДР(М14)
 МОДА SР2
 УИА (М4)
КОМ2 МОДА (М1)
 СЧ R1
 РЗБ КРК
 ИЛИ SР14
 МОДА (М1)
 СЧМ R1
 СДА 64-9
 РЗБ (М14)
 ИЛИ (М4)
 ЗП (М17)
 МОДА КОМ1
 ЦИКЛ (М1)
 СЧ КСМ           * СМ В ВИДЕ НОРМАЛИЗОВАННОГО ЧИСЛА *
 МОДА БРЧ3
 УИА (М11)
 СЧМ (М3)
 ЗП R
 УИИ М1(М17)
 УИА (М17)
 РЖА ’23’
 ПО БРЧ2          <СМ> > 0
 УИА 11(М12)
БРЧ2 СЧИ М12
 ПБ ЗС
БРЧ3 СЧ (М3)          НОРМАЛИЗАЦИЯ
 РЖА ’40’
 ВЧАБ
 РЖА 4
 УИА -9(М4)
 СЛИА БРЧ7-БРЧ3(М11)
 ПО БРЧ6          <СМ> = 0
 УИА 1(М17)
БРЧ4 ЗП R
 ВЧ Ч10Д
 ПЕ БРЧ5
 СЛИА 10(М17)
 СЧ R
 УМН Ч10М10
 ПБ БРЧ4
БРЧ5 СЧ R
 ВЧ Ч1Д
 ПО БРЧ6
 СЛИА -1(М17)
 СЧ R
 УМН Ч10Д
 ЗП R
 ПБ БРЧ5
БРЧ6 РЖА 3
 СЧ R
 СЛ ЭК9
 И П17
 ПБ ЗС
БРЧ7 СЧ R1
 ИЛИ ЭК9
 РЖА 0
 ВЧОБ R
 УМН Ч10Д
 ЗП R
 МОДА БРЧ6
 ЦИКЛ (М4)
 УИА 10(М12)
 СЛИА БРЧ8А-БРЧ7(М11)
 РЖА 3
 СЧИ М17
 И Е6
 ПО БРЧ2
 УИА 11(М12)
 СЧИ М17
 НТЖ П77777
 УИ М17
 СЛИА 1(М17)
 ПБ БРЧ2
БРЧ8А УИИ М12(М17)
 СЛИА -10(М12)
 СЧИ М12
 СДА 64+4
 И Е1
 НТЖ Е1
 ПЕ БРЧ9
 СЛИА 10(М12)
БРЧ9 МОДА ЗС
 ПВ (М11)
 МОДА БРЧ2
 ПВ (М11)
 СЧ (М3)          * СМ В ВИДЕ ВОСЬМИРИЧНОЙ КОНСТАНТЫ *
 ЗП R
 МОДА VIDЕО1
 УИА (М15)
БРС УИА -15(М4)       ДЛЯ СМ И РМР ( ’8’ - ВИД )
 СЧ П17
 МОДА ЗС
 ПВ (М11)         ПРОБЕЛ
 МОДА БРС1
 УИА (М11)
БРС2 СЧ R             ОЧЕРЕДНАЯ ЦИФРА
 СДА 64-3
 ЗП R
 СЧМР
ЗС ЗП R1            ЗАПИСЬ ОДИНОЧНОГО СИМВОЛА
 СЧ (М1)
 СДА 64-8
 ИЛИ R1
 ЗП (М1)
 МОДА (М11)
 ЦИКЛ (М5)
 УИА -5(М5)        СЛОВО ГОТОВО
 СЛИА 1(М1)
 ПБ (М11)
БРС1 МОДА БРС2          ЕЩЕ НЕ ВСЕ
 ЦИКЛ (М4)
 МОДА ВТТ           ЕТХ. НА ВЫДАЧУ СТРОКИ
 УИА (М11)
 СЧ ЕТХ
 ПБ ЗС
VIDЕО1 МОДА ПОЛЕ          ВЫДАЧА 3-Й СТРОКИ
 УИА (М17)         *** ИНДЕКС-РЕГИСТРЫ С 1 ПО 7 ***
 СЧ КИР
 ЗП (М17)
 СЛИА РМР-ВТТ(М11)
 УИА ’25’(М14)
 УИА -6(М4)
 МОДА (М17)
 УИА 8(М1)
ИР МОДА (М3)          ОЧЕРЕДНОЙ ИР
 СЧ (М14)
 СДА 64+15
 СЧМР
 РЗБ РАДР
 ИЛИ Е44П41
 ЗП (М17)
 СЛИА -1(М14)
 МОДА ИР
 ЦИКЛ (М4)
 ПБ (М11)
РМР СЧ КРМР          *** РМР В ВОСЬМИРИЧНОМ ВИДЕ ***
 ЗП (М17)
 СЧ 2(М3)
 ЗП R
 МОДА БРС
 ПВ (М15)
VIDЕО2 МОДА ПОЛЕ          ВЫДАЧА 4-Й СТРОКИ
 УИА (М17)         *** ИНДЕКС-РЕГИСТРЫ С 10 ПО 17 ***
 СЧ ’16’(М3)
 СДА 64+15
 СЧМР
 РЗБ РАДР
 ИЛИ Е44П41
 УИА ’15’(М14)
 СЧМ 1(М3)   ПР.АУ
 СДА 64+8
 РЗБ РАДР
 СДА 64-8
 ИЛИ ЕТХ
 СДА 64-16
 ИЛИ Е44П41
 ЗП ’10’(М17)
 СЧ РК
 ЗП 7(М17)
 УИА -6(М4)
 МОДА ВТТ1
 УИА (М15)
 ПБ ИР
*
*
*
П31 КОНД В’31’
М16Э75 КОНД М36В’7075’
Е30П16 КОНД М15В’77777’
МИС КОНД М32П’ -’М24В’172’
РАДР КОНД П’77777’
SРS5К КОНД П’     К’
SР2 КОНД М32П’ ’
КРК КОНД П’017037’
КРК1 КОНД П’707777’
SР14 КОНД П’ 00 00’
КСМ КОНД П’    СМ’
Ч10Д КОНД Е’10’
Ч10М10 КОНД Е’Е-10’
КИР КОНД П’ ИР   ’
КРМР КОНД П’   РМР’
РК КОНД П’    РК’
Л175 КОНД М40В’175’
РRINТ ЗП ПОЛЕ-АВРАСП+2048
 ЗП ПОЛЕ-АВРАСП+2048
 КОНД М23В’1’
ПРУС НТЖ S1
 ПО СНЯТИЕ        =
 НТЖ S1
 ПЕ СНЯТИЕ        ≠
 ВЧ S1
 ПЕ СНЯТИЕ        >
 ВЧОБ S1
 ПЕ СНЯТИЕ        <
КНЦ КОНД М24П’КНЦ’М16В’377’
ЕОС КОНД В’0’
 КОНД П’ СЕАНС’
 КОНД М40П’А’М32В’377’
ЕОС1 КОНД П’КОНЕЦ’
ГЗ КОНД М20В’77’В’-1’М36В’1’
 ЛИТЕР
*
*
*
*
*
*      О С О Б Е Н Н О С Т И   А П П А Р А Т У Р Ы
*
*
* 1.   КОНТРОЛЬ КОМАНДЫ (КК)
*
* КК ПРОИСХОДИТ ТОЛЬКО НА ЛЕВОЙ КОМАНДЕ. ОТЛАДЧИК ПРОГРАММНЫМ
* ПУТЕМ ИСКЛЮЧАЕТ ВОЗМОЖНОСТЬ ПЕРЕДАЧИ УПРАВЛЕНИЯ НА ПРАВУЮ
* КОМАНДУ УЗЛА (СМ.П.2.1.).  ИЗЛОЖЕННОЕ НИЖЕ ОТНОСИТСЯ ТОЛЬКО
* К ЛЕВОЙ КОМАНДЕ УЗЛА.
*
* 1.1. КОМАНДА МОДА (22) ВЫПОЛНЯЕТСЯ,ФОРМИРУЕТСЯ ПРИК (5Р М27)
*
* 1.2. КОМАНДА МОД (23) ВО ВНИИП НЕ ИМЕЕТ ОСОБЕННОСТЕЙ,ЕСЛИ ОНА
* НЕ МАГАЗИННАЯ (СМ.П.1.5.). В ДРУГИХ ОРГАНИЗАЦИЯХ: ЕСЛИ ПЕРЕД
* НЕЙ СТОИТ КОМАНДА 22(23),ТО УКАЗАННЫЙ В НЕЙ АДРЕС УЖЕ ЗАНЕ-
* СЕН НА М20
* ИР 22(23) АДРЕС   -   М20 = К
* ---------------
* КК: ИР 23 АDRЕS   -   М20 = К + АDRЕS
*
* 1.3. КОМАНДА ПВ(31) УСТАНАВЛИВАЕТ ПРК.
*
* 1.4. ЭКСТРАКОДЫ ФОРМИРУЮТ М27 = 02007,НА М32 ЗАНОСИТСЯ АДРЕС
* ВОЗВРАТА,НА М16 - ИСПОЛНИТЕЛЬНЫЙ АДРЕС.
*
* 1.5. МАГАЗИННЫЕ КОМАНДЫ УСПЕВАЮТ ИЗМЕНИТЬ СЧЕТЧИК МАГАЗИНА.
*
*
*
* 2.   ОСТАНОВ ПО СОВПАДЕНИЮ АДРЕСА КОМАНДЫ С СОДЕРЖИМЫМ М34
*      (КРА)
*
* ПРИ ПЕРЕДАЧЕ УПРАВЛЕНИЯ НА ПРАВУЮ КОМАНДУ <М34> ПРЕРЫВАНИЕ
* НЕ ВОЗНИКАЕТ,НО ПЕРВЫЙ ЖЕ АВОСТ (ПО ЛЮБОЙ ПРИЧИНЕ) ИДЕНТИФИ-
* ЦИРУЕТСЯ КАК ОСТАНОВ ПО КРА (12Р ГРП). ПОЭТОМУ
*
* 2.1. ПРИМЕЧАНИЕ: ПРИ ОСТАНОВЕ ПО СОВПАДЕНИЮ АДРЕСА ЗАПИСИ(СЧИ
* ТЫВАНИЯ) ОПЕРАНДА С СОДЕРЖИМЫМ М35 НА ЛЕВОЙ КОМАНДЕ,НА БУФЕРЕ
* ОТЛАДЧИКА ВЫПОЛНЯЮТСЯ (ПРИ М35 = 0) И ЛЕВАЯ И ПРАВАЯ (!)
* КОМАНДЫ.
*
*
*
*
РАСПЕЧ ТЕКСТ П’ КОНЕЦ ЗАДАЧИ’     0
 ТЕКСТ П’ ДЕЛЕНИЕ НА НУЛЬ’  14
 ТЕКСТ П’ ПЕРЕПОЛНЕНИЕ АУ’  15
 ТЕКСТ П’ ЧИС.В ЧУЖ.ЛИСТЕ’  16
 ТЕКСТ П’ КОМ.В ЧУЖ.ЛИСТЕ’  17
 ТЕКСТ П’ ЗАПРЕЩ.КОМАНДА’   18
 ТЕКСТ П’ СНЯТА ОПЕРАТОРОМ’ 19
 ТЕКСТ П’ КОНТРОЛЬ КОМАНДЫ’ 20
 ТЕКСТ П’ ОСТАНОВ ПО СЧИТ.’ 21
 ТЕКСТ П’ ОСТАНОВ ПО ЗАП.’  22
 ТЕКСТ П’ ОСТАНОВ ПО КРА’   23
 ТЕКСТ П’ ЗАТЕРТО СПО   ’   24
 ТЕКСТ П’ ВЫВОД ЗАПРЕЩЕН  ’   25
 ТЕКСТ П’ НЕСУЩ.ВИД РАБОТЫ’ 26
 ТЕКСТ П’ СМЕНИЛСЯ ВИД РАБ’ 27
 ТЕКСТ П’ СБОЙ   АРХИВА   ’ 28
 АIF (∧НИ).СВ8
 ТЕКСТ П’ МАС.НЕ ВХ.В ПОЛЕ’ 29
 АGО .СВ1
.СВ8 АNОР
 ТЕКСТ П’НЕ ЗАДАН РЕЖИМ’     29
.СВ1 АNОР
 ТЕКСТ П’ НЕТ КОНЦА БЦ.ИНФ’ 30
 ТЕКСТ П’ ДЛ.МАССИВА>6200’  31
 ТЕКСТ П’ ДАЙ ТРАКТЫ    ’   32
 ТЕКСТ П’ ОБРАЩ.К НЕЗАК.МЛ’ 33
 ТЕКСТ П’ ОШ.В ИНФ.СЛ.ЭКСТ’ 34
 ТЕКСТ П’ ОБРАЩ.К НЕСУЩ.СП’ 35
 ТЕКСТ П’ ЗАПРЕЩ.ЭКСТРАКОД’ 36
 ТЕКСТ П’ ОШИБКА МЛ ЭВМ   ’   37
 ТЕКСТ П’ ИСТЕКЛО ВРЕМЯ’    38
 ТЕКСТ П’ ДАЙ МЕТРЫ АЦПУ!’  39
 ТЕКСТ П’ ИСТЕК.ВРЕМ.ПО ЭК’ 40
 ТЕКСТ П’ ЧИСЛ.ВЫД.ПК>4096’ 41
 ТЕКСТ П’ ОШИБКА МД ЭВМ   ’   42
 ТЕКСТ П’ НЕТ МЛ В ЭК.РЕС’  43
 ТЕКСТ П’ В ЭК.РЕС.ТРАК>32’ 44
 ТЕКСТ П’ СНЯТА ПОЛЬЗОВАТ.’ 45
 ТЕКСТ П’ ДАЙ РАБ.В ЭК.РЕС’ 46
 ТЕКСТ П’ ЗАПРЕЩ.НАПР.В ЭК’ 47
 ТЕКСТ П’ ОШИБКА МБ ЭВМ   ’   48
 ТЕКСТ П’ НЕВЕРНЫЙ  ПАРОЛЬ’  49
 ТЕКСТ П’ ДАЙ ЗАПИСЬ НА МЛ’ 50
 КОНД М8П’ УЗЕЛ’В’172’     51
ЕТХ КОНД В’172’
 КОНД В’0’          ПОРТИТСЯ ПРИ ВЫДАЧЕ НА ТЕРМИНАЛ
 ТЕКСТ П’ АRСSIN(Х):|Х|>1’  52
 ТЕКСТ П’ КОРЕНЬ(Х): Х <0’  53
 ТЕКСТ П’ ЛОГАРИФМ(Х):Х≤0’  54
 ТЕКСТ П’ ЕХР(Х): Х≥44’     55
 ТЕКСТ П’ ЧИСЛО ЗОН>200’    56
 ТЕКСТ П’ ИНФ.СЛ.В ЧУЖ.ЛИС’ 57
 ТЕКСТ П’ ЛИСТ В ЭК.ЧУЖОЙ’  58
 ТЕКСТ П’ МАССИВ НЕ СУЩ-Т.’ 59
 ТЕКСТ П’ СНЯЛАСЬ  ГЛАВНАЯ’  60
 ТЕКСТ П’ НЕТ МЕСТА В КАТА’ 61
 ТЕКСТ П’ СНЯТА  ГЛАВНОЙ  ’    62
 АIF (∧НИ).СВ9
 ТЕКСТ П’ М220 НЕ ПЕРЕДАЛА’ 63
 АGО .СВ2
.СВ9 АNОР
 ТЕКСТ П’ НЕВЕРНОЕ ОБРАЩЕН ’     63
.СВ2 АNОР
 ТЕКСТ П’ ЧУЖАЯ ИНФОРМАЦИЯ’ 64
 ТЕКСТ П’ СБО,ВВЕДИ ПО ИНС’ 65
 ТЕКСТ П’ ЭКСТР.>ДОПУСТ.’   66
 ТЕКСТ П’ ЗАДАЧА УПРЯТАНА’  67
 АIF (∧НИ).СВ10
 ТЕКСТ П’ НЕТ БОБИНЫ СВЯЗИ’ 68
 АGО .СВ3
СВ10 АNОР
 ТЕКСТ П’НЕТ ТРЕБ СЕАН СВ’        68
.СВ3 АNОР
 АIF (∧НИ).СВ11
 ТЕКСТ П’ КЧ В РЕЖ.ТЕСТА  ’   69
 АGО .СВ4
.СВ11 АNОР
 ТЕКСТ П’НЕТ СИНХРО         ’     69
.СВ4 АNОР
 ТЕКСТ П’ НЕ ЗАКАЗАН ТЕРМ.’ 70
 ТЕКСТ П’ НЕКОР.ОБРАЩЕНИЕ’  71
 ТЕКСТ П’ ОБРЫВ СВЯЗИ   ’   72
 ТЕКСТ П’ ОШ.ЗП НА МЛ   ’   73
 КОНД П’ ПО СЧ’            74
 КОНД М24П’ИТ.’М16В’172’
 КОНД В’0’          ПОРТИТСЯ ПРИ ВЫДАЧЕ НА ТЕРМИНАЛ
 КОНД П’ ПО ЗА’            75
 КОНД М32П’П.’М24В’172’
 КОНД В’0’          ------- /// -------
 КОНД М16П’ КРА’М8В’172’   76
ЕТХ1 КОНД В’165’
 КОНД В’0’          ------- /// -------
 ТЕКСТ П’ НЕТ ИНФ. ДЛЯ ОТЛ’ 77
 ТЕКСТ П’ ЕNQ<=ОСТАН.=>DЕQ’ 78
САВ ТЕКСТ П’0ИСКАЖЕНИЕ ИНФОРМ.’
 КОНД М8П’ ПОЛЯ’В’377’
 АIF (∧G ЕQ 0).Е1
 ПАМ 2
 ТЕКСТ П’ ОШ. УЧЕТА    ’    81
.Е1 АNОР
КПЧКЗ КОНД М17В’3’М2В’23’  23 ТР
Р КОНД В’0’
Р1 КОНД В’0’
БОЭК1 ВНЕШ ЗАПВЫХ,АРХМЛ,М333
ДИСП70 ВНЕШ ПОДЧ,Е40П1,Е44П41,Е44Е43,Е24П16,Е48П16
ДИСП70 ВНЕШ Е46П48,ВСЕЕД,ТВ40,НОММБ1,ШГ,ТЕРЕЗА,Ч1Д
ДИСП70 ВНЕШ ЭК9,ШКИВ,ТСЛ,РЭ2,ШКОК,ОСПОД,ЗОНЫ
АВОСТ ВНЕШ СТ434,СТ435
ДИСП70 ВНЕШ ЗАПВЕТ,АВОС,ШККЗ,ШКСВН,ТУС,ТБУФ,П777,П177
ДИСП70 ВНЕШ П77,ПР13,П5,СУММАВ,ВИСП16,ИВ25,П7777
ДИСП70 ВНЕШ П77777,П37,П1777,П30,П7,П17,П3,П377,П23
ДИСП70 ВНЕШ Е24П1,ШЗНМБ,ШМАРС,ЯЧЭК,ЕВАМОS
ДИСП70 ВНЕШ Е41П1,П60,Е47П1,П6,И,НАПРМГ
ДИСП70 ВНЕШ ЯЧИУПР,ЕИУПР,ТУСП,МЧПЗ,НПЗ,ШАРКЗ
ДИСП70 ВНЕШ НОМАРС,ШАРКЗМ
ДИСП70 ВНЕШ Е48П24,ОТКАЗН,ШЗАПОС,СОБА40,ПЗ,ЕЗП,ЕЗАР
ВИСП ВНЕШ ВШГП
КАЧКА ВНЕШ ШКРЗ,Е48П17
КИТ ВНЕШ ШКРДО,ДСТОП
МОТТ ВНЕШ Е48П45,Н,ШЗПРИМ,ШЗНКТ,ШКВЫД,ШТО,ШКВИД
МОТТ ВНЕШ ШКОТЛ
МОТТ ВНЕШ ДИП,ШКЭ71,ШЗНКТ1,ЗАНПАД,ЗАНПРТ
ВЗУ ВНЕШ ФИЗОБМ,АНВО1Е
ДИСКИ ВНЕШ ЕNQМДС,ШЗМД,ЗУС,ОСВКМД
ПЧКЗ ВНЕШ ПЧКЗ,(НАЧПЧ,Н)
ПВВ ВНЕШ ОСВМД
СОСТАВ ВНЕШ ЕНРОТ,ИНФАВ,ИЗАХВ,ЕАВРАС
СОСТАВ ВНЕШ ЕМАРС,АХ
СОСТАВ ВНЕШ ЕОТБ,ЯЧОТБ,ЯЧЧП,ЕЧП,ЯЧЧАВ
 ВХОД КЗ221,КЗ222,РАСПЕЧ,ВЫХОД
 ВХОД ДКЗ,Р,Р1,ДЛЯПЧ,НАЧАЛО,ВПЧКЗ
ПОЛЕ ПАМ 41
 КОНД В’0’
 ЭКВИВ (R,ПОЛЕ+13),(R1,ПОЛЕ+14),(R2,ПОЛЕ+15),(S,ПОЛЕ)
 ЭКВИВ (S1,ПОЛЕ+1),(S2,ПОЛЕ+2),(S3,ПОЛЕ+3),(S4,ПОЛЕ+16)
 ЭКВИВ (S5,ПОЛЕ+17),(СН,ОБРОС),(ВИДПР,ПРОВВР)
*КОНЕЦ ЧАСТИ

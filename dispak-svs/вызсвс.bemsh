*
*      С Ч П
*
 МАСRО
∧М СЧП ∧А
∧М КК ’033’,∧А
 МЕND
*
*      З П П
*
 МАСRО
∧М ЗПП ∧А
∧М КК ’032’,∧А
 МЕND
*
*      О Б М М Д
*
 МАСRО
 ОБММД ∧КУС,∧АЗАЯВ
 СЧИ ∧АЗАЯВ
 ЗП Р16
 МОД БАЗАОС
 СЛИА (М10)
 СЧ ∧КУС
 ПВ ПВВМД(13)
 ПАМ 3
 МОДА КО∧SNDХ
 УИА (13)
 ПБ ЖДИПР
КО∧SNDХ НОП
 УИА ’2003’
 УИ ∧АЗАЯВ
 СЧ Е1
 ЗП ПРИВОС
 ВОСЛМД ∧КУС,∧АЗАЯВ
 СЧ 0
 ЗП ПРИВОС
 СЧ Р16
 УИ ∧АЗАЯВ
 МЕND
*
*      В О С Л М Д
*
 МАСRО
 ВОСЛМД ∧КУС,∧АЗАЯВ
 СЧ ∧КУС
 ЗП (∧АЗАЯВ)
 СЧИ ∧АЗАЯВ
 ПВ ВОСЛМД(13)
 ПАМ 3
 МЕND
ВЫЗСВС СТАРТ 1024
НАЧАЛО НОП
 РЕГ ’101’
 ПБ НЧ0
 СТРН
*////////////////////////////////////////////////////////*
*///                                                  ///*
*///                                                  ///*
*///                 С   Т   О   П   Ы                ///*
*///                  00200  -  00277                 ///*
*///                                                  ///*
*////////////////////////////////////////////////////////*
СТ200 НОП ,     ****** БЫЛО   К  Ч
 РЕГ ’237’
 СТОП ’00200’
 ПБ СТ200
*
СТ201 НОП ,     ****** НЕБОРАБАТЫЫВАЕМЫЕ ВНЕШИЕ ПРЕРЫВАНИЯ
 РЕГ ’247’
 СТОП ’00201’
 ПБ СТ201
*
СТ202 НОП ,             БНС ПРИ ОБМЕНЕ С МД
 СЧ КУСД
 СТОП ’00202’
 ПБ НАЧАЛО
*
СТ203 НОП ,             ПЛОХОЙ КЛЮЧ В СЛ.СЛОВАХ
 СЧ КУСД
 СТОП ’00203’
 ПБ НАЧАЛО
*
СТ204 НОП ,             ПЛОХАЯ КОНТР.СУММА
 СЧ КУСД
 СТОП ’00204’
 ПБ НАЧАЛО
*
СТ205 НОП ,             НЕВЕРНО ЗАДАНА ИНФ.О МБ НА 2-ОМ Т.Р.
 СЧ ТУМБ
 СТОП ’00205’
 ПБ НАЧАЛО
*
СТ206 НОП ,             МБ НЕТ В КОНФИГУРАЦИИ (ТУС В АДАП-Е)
 СЧ ТУМБ
 СТОП ’00206’
 ПБ НАЧАЛО
*
СТ207 НОП ,             НЕТ ЗАДАННОГО ВАРИАНТА ПАРАМЕТРОВ
 СЧ ТУМБ
 СТОП ’40207’       ПРИ ПРОТАЛКИВАНИЕ ВАРИАНТ ИЗ ВЫЗСВСС
 ПБ РАБМБ
*
СТ210 НОП ,             В РЕЗЕРВЕ ТОУ(ТЛУ) НЕТ МЕСТА
 СЧ ТУМБ
 СТОП ’00210’
 ПБ НАЧАЛО
*
*////////////////////////////////////////////////////////*
 СТРН
*
НЧ0 НОП ,
 СЧ ЯЧ501
 ЗП ’501’
 СЧ ЯЧ500
 ЗП ’500’
 СЧ К2013
 УИ ’21’
 УИА 0(М3)         М3 := 0 - АДРЕС ВОЗВР.ПРИ ВНУТР.ПРЕР.
 РЖА 3
 ДК ’36’,НЧ1
НЧ1 НОП
*
 СЧ 7
 И =М36Х’FFF’
 ЗП ПРИСОС
 СЧ 7
 СБР =М36Х’111’
  СДА 64+45
 ЗП СЕКОС
 СДА 64-7
 ЗП БАЗАОС
*
*
*
* ЧТЕНИЕ ЗОНЫ (750) С ПАРАМЕТРАМИ ГЕНС-А
*
 МОДА НАПРУС
 СЧ 0
 ИЛИ КУСПАР
 ЗП КУСД
 ПВ ДАЙПРМ(М15)
 УИА 2(М10)        М10:=2,ПАРАМЕТРЫ ЧИТАТЬ ВО 2-ОЙ ЛИСТ
 ОБММД КУСД,14
 УИА 1(М10)
 ПВ ЧТСЛ(М6)
 НТЖ КЛЮЧ
 И Е39П25
 ПЕ СТ203         КЛЮЧ НЕ СОВПАЛ
 ПВ ПОДКС(М6)
 ЗП РЯ1           РЯ1:=КС ЗОНЫ
 УИА 3(М10)
 ПВ ЧТСЛ(М6)
 НТЖ РЯ1
 ПЕ СТ204         НЕ СОВПАЛ КС
*
* ПОИСК ВАРИАНТА ГЕНС-А
*
 ПВ МОЙНОМ(М15)   М2:=НОМСВС:= N СВС
 СЧ ТУМБ
 СДА 64+24
 И П17
 УИ М6           М6 = N ВАРИНТА
 ПО ПЕРВАР
 СЛИА -1(М6)
ПЕРВАР УИА 1-ИНФГЕН(М4)
 УИА ’4000’(М15)
ПОИСКВ СЧ NМАШ(М15)
 НТЖ НОМСВС
 И П7
 ПЕ НЕТЭВМ
 ПИО НАШЛИВ(М6)
 СЛИА -1(М6)
НЕТЭВМ СЛИА ИНФГЕН(М15)
 СЧИ М15
 И П777
 ПЕ ПОИСКВ
* НЕ НАШЛИ ЗАДАННЫЙ ВАРИАНТ
 ПБ СТ207
НАШЛИВ МОДА (М15)
 СЧ ИНФГЕН-1(М4)
 ЗП ИНФО+ИНФГЕН-1(М4)
 ЦИКЛ НАШЛИВ(М4)
*
* РАБОТА С     М  Б
*
РАБМБ НОП
 СЧ ТУМБ
 СДА 64-6
 И =М36В’7777’    12 МБ,УСТРОЙСТВА С 0 ПО В
 ЗП УСТРМБ
 ЗП МБВСЕ
 ПО СТ205         НЕТ МБ
 ЧЕД 0
 ВЧ Е4
 ПО СТ205         МБ > 3
*
 УИА СТЕК(М17)
КОНФМБ СЧ МБВСЕ
 ПО ФОРТЛУ
 НЕД П77777
 УИ М4            М4:= N МБ
 СЧ МБВСЕ
 НТЖ Е48(М4)
 ЗП МБВСЕ
 СЧИ М4
 СДА 64-38
 МОДА КАНМБ
 ИЛИ 0
 МОДА ЕПВВ
 ИЛИ 0
 ПВ ИНТУСФ(М15)
 ПО СТ206         НЕТ В КОНФИГУРАЦИИ
 УИ М5            М5:= АДР.ТУСЗ
 СЧП (М5)
 И =М6В’1777’
 ПО КОНФМБ        МБ СВОБОДЕН
 СЧМ Е16
 СДА 64(М2)
 НТЖ =М6В’1777’
 НТЖ (М17)
 ПЕ СТ205         КТО-ТО КРОМЕ МЕНЯ
 ПБ КОНФМБ
*
* ФОРМИРОВАНИЕ ТЛУ,РЕЗЕРВ ДЛЯ   М Б
*
ФОРТЛУ НОП
 СЧ УСТРМБ
 ЗП МБВСЕ
 НЕД П17
 И П17
 СДА 64-38
 ЗП МБСВС         МБСВС := N ПЕРВОГО МБ
 СЧИ М2
 УМН Ч13
 СЧМР 64
 УИ М16
 СЛИА ТЛУ+1(М16)    М16:=АДР.ТЛУ МБ ДАННОГО СВС
 МОД АРЕЗЕР
 УИА  0(М14)        М14:= АДР.РЕЗЕРВА
 УИА 0(М4)         М4:=0, N СЛОВА РЕЗЕРВА
 УИА 0(М1)         М1 - СЧЕТЧИК   М Б
*
ЦРЕЗЕР НОП
 СЧ МБВСЕ
 НЕД П77777
 И П77777
  УИ М6            М6:= N МБ
 СДА 64-38         УСТРОЙСТВО
 МОДА КАНМБ
 ИЛИ 0
 МОДА ЕПВВ
 ИЛИ 0             КОНФИГ ПВВ
  ПВ ИНТУСФ(13)    ЗА ИНДЕКСОМ В ТУСЗ
 ПО СТ205           НЕ МОЖЕТ БЫТЬ; СВС НЕ РАБОТАЕТ
  УИ М5            М5 := АДРЕС В ТУСЗ
 СЧ Е16
 СДА 64(М2)        М2- N СВС
 НТЖ =М6В’1777’
 ЗП (М17)
 СЧП (М5)
 И =В’-1’М6В’1777’
 ИЛИ (М17)
 ЗП (М17)
 СЧП (М5)
 СЧ (М17)
 ЗПП (М5)          ЗАПИСЛИ ТУСЗ
 СЧИ М5
 МОДА АТУСЗ
 ВЧ 0 АДРЕС В ТУСЗ
 ИЛИ Е16           ЧИСЛО МБ ДЛЯ ТЛУ
  ЗП (М16)         ССЫЛКА В ТУС ГОТОВА
 СЛИА 1(М1)
 СЧ МБВСЕ
 НТЖ Е48(М6)
 ЗП МБВСЕ
 ПО КРЕЗЕР        ВСЕ  МБ ОБРАБОТАНЫ
ПОИСКМ СЛИА 1(М4)
 СЛИА 1(М14)
 СЧ (М14)
 ПО ЕСТЬМ         СЛОВО СВОБОДНО
 СЧИ М4
 ЗП (М17)
 УИА КРЕЗ(М15)
 СЧИ М15
 ВЧОБ (М17)
 ПО СТ210         В РЕЗЕРВЕ НЕТ МЕСТА
 ПБ ПОИСКМ
ЕСТЬМ НОП ,
 СЧИ М4       НОМЕР СЛОВА В РЕЗЕРВ
 СДА 64-35
 ИЛИ (М16)    В ПОЛЕ ССЫЛКИ
 ЗП (М16)    ЭЛ-ТА ЦЕПОЧКИ ТЛУ ДЛЯ МБ
 УИИ М16(М14)
 ПБ ЦРЕЗЕР
КРЕЗЕР СЧИ М1
 ЗП ЧИСЛМБ
*
*
*    ЧТЕНИЕ    Г  Е  Н  С - А
*
*
 МОДА НАПРУС
 СЧ
 ИЛИ КУСГЕН
 ЗП КУСД
 ПВ ДАЙПРМ(13)
 УИА ’34’(М10)   НОМЕР ЛИСТА
 ОБММД КУСД,14
 УИА 1(М10)
 ПВ ЧТСЛ(М6)
 НТЖ КЛЮЧ
 И Е39П25
 ПЕ СТ203         ПЛОХОЙ КЛЮЧ
 ПВ ПОДКС(М6)
 ЗП РЯ1           РЯ1 := КС
 УИА 3(М10)        НОМЕР СЛ.СЛОВА
 ПВ ЧТСЛ(М6)
 НТЖ РЯ1
 ПЕ СТ204          ПЛОХАЯ КС
* ПЕРЕНОС В ГЕНС 12,13,14(8) ЯЧЕЕК ПАРАМЕТРОВ
 СЧ ИНФО+10
 ЗП ИНФО12
 СЧ ИНФО+11
 ЗП ШАС128
 СЧ ИНФО+12
 ЗП ШАС128+1
*
************************************************************
*
*       З А К Р Ы Т И Е     П А М Е Т И
*
************************************************************
 СЧ 7             7 ТУМБ.РЕГ.
 И =М20В’1’      21 РАЗРЯД
 ПО ОЗУПАР
* СЕКЦИИ ЗАДАНЫ НА 7-ОМ ТУМБ.РЕГИСТРЕ:
* 20 Р - 0 СЕК, 19 Р - 1 СЕК ,...,13 Р - 7 СЕК
*
 СЧ 0
 ЗП ШКАМОД
  УИА -7(М15)
 СЧ =М28Х’F’
  ЗП РЯ
ОЗУТР НОП ,
 СЧ =М19В’1’
 СДА 64+7(М15)
 И 7
 ПО СЛЕДСК
* СЕКЦИЯ ЗАДАНА
 СЧ РЯ
 ИЛИ ШКАМОД
 ЗП ШКАМОД
СЛЕДСК НОП ,
 СЧ РЯ
 СДА 64+4
 ЗП РЯ
 ЦИКЛ ОЗУТР(М15)
 ПБ ДОББЗ
ОЗУПАР НОП ,            СЕКЦИИ ЗАДАНЫ В ПАР.ГЕНС-А
 СЧ ИНФО+9
 СДА 64+16
 ЗП ШКАМОД        ШКАМОД :=  РАЗРЕШЕННЫЕ МОП
*
ДОББЗ НОП ,
 СЧ СЕКОС
 СДА 64-2
 УИ М15           М15 := N БАЗОВОЙ СЕКЦИИ  О С * 4
 СЧ =М28В’17’
 СДА 64(М15)
 ИЛИ ШКАМОД
 ЗП ШКАМОД
*
 СЧ СЕГМ0
 ЗП ИНФО+9
 СЧ 0
 ЗП ИНФО+10
 ЗП ИНФО+11
 ЗП ИНФО+12
 СЧ 2
 И =М22В’1’
 ПО ОЗУ128
*
* 23 РАЗРЯД НА 2-ОМ ТУМБ.РЕГ. - РАБООТА  О С  НА 64 К
*
 СЧ =В’-1’В’177777’
 ЗП ИНФО+11
 ЗП ИНФО+12
 СЧ =М28В’3’
 СДА 64(М15)
 НТЖ ШКАМОД
 ЗП ШКАМОД         ЗАКРЫЛИ 2 И 3МОДУЛИ БАЗ.СК.О С
ОЗУ128 НОП ,
 МОДА ПРАДАП
 СЧ 0
 СБР =М36Х’111’
 СДА 64+45
 НТЖ СЕКОС
 ПЕ НЕУНАС        АДАП НЕ В НАШЕЙ БАЗОВОЙ СЕКЦИИ
 СЧ =М44В’17’
 ЗП ИНФО+10       ЗАКРЫЛИ 40,41,42,43 ЛИСТЫ БАЗ.СЕК.ОС
НЕУНАС НОП ,
*
* ПЕРЕПИСЬ ПАРАМЕТРОВ  ДЛЯ   Г Е Н С - А
*
 УИА 1-ИНФГЕН(М6)
ИНФГ НОП ,
 СЧ ИНФО+ИНФГЕН-1(М6)
 ЗП ПАРОС+ИНФГЕН-1(М6)
 ЦИКЛ ИНФГ(М6)
*
 СЧ УСТРМБ
 ЗП УПРМБ
*
 СЧ ЧИСЛМБ
 ЗП ГЕНСМБ
*
 СЧ ПРИСОС
 ЗП ПРИСОГ
*
 СЧ СЕКОС
 ЗП СЕКОСГ
*
 СЧ ШКАМОД
 ЗП ШКАМОГ
*
 ПБ ’70000’       У Х О Д    В    Г  Е  Н  С
*
*////////////////////////////////////////////////////////*
*///                                                  ///*
*///         ОБРАБОТКА   П Р Е Р Ы В А Н И Й          ///*
*///                                                  ///*
*////////////////////////////////////////////////////////*
*
*  ОБРАБОТКА ВНУТРЕННИХ ПРЕРЫВАНИЙ
*
ЯЧ500 НОП ,
 ЗП СМ
 ПБ ПРКЧ
*
ПРКЧ НОП ,
 СЧ ЯЧКЧ
 ПО ПРКЧ1
  СЧ 0
 РЕГ ’37’
 МОД ЯЧКЧ
 ПБ 0
*
ПРКЧ1 НОП ,
 СЧ ПРИВОС
 ПО ПРКЧ2         ПОД-МА ’ВОСЛМД’ НЕ РАБОТАЕТ
 ПВ ВОСРКЧ(М15)
 СЧ 0
ПРКЧ2 НОП ,
 ЗП ПРИВОС
 ПИО СТ200(М3)
 ПБ (М3)
*
*
*   ОБРАБОТКА ВНЕШНИХ ПРЕРЫВАНИЙ
*
ЯЧ501 НОП ,
 ЗП СМ
 ПБ ВНЕШПР
*
ВНЕШПР НОП
 СЧМР
 ЗП РМР
 СЧРЖ ’77’
 ЗП РК
 СЧИ ’20’
 ЗП И20
 СЧИ 15
 ЗП И17
 СЧИ 14
 ЗП И16
 СЧИ 13
 ЗП И15
 СЧИ 12
 ЗП И14
 РЕГ ’247’  РЕГ ВНЕШН. ПРЕРЫВ.
 МОДА МГРП
 И
 ЗП РВП
 И =М3В’1’
 ПЕ ПРВРЕМ  ВРЕМЕННОЕ ПРЕРЫВ
 СЧ РВП
 И =М7В’1’
 ПЕ ПРЕСВС ПРЕРЫВ ОТ СВС
 СЧ РВП
 И =М2В’1’
 ПЕ ПРЕПВВ
 СЧ РВП
 И =М5В’1’
 ПЕ ПРЕРВС   АВАРИЯ СВС
 СЧ РВП
 ПБ СТ201         ПРЕРЫВ НЕ ОБРАБАТЫВАЕМ
ВЫХПР НОП --- ВЫХОД ИЗ ПРЕРЫВАНИЙ
 СЧ И15
 УИ 13
 СЧ И16
 УИ 14
 СЧ И17
 УИ 15
 СЧ И14
 УИ 12
 СЧ И20
 УИ ’20’
 СЧ РМР
 НТЖ 0
 СЧ СМ
 РЖ РК
 ВЫПР (3)
ПРЕПВВ УИА ВОЗПРЕ(13)
 ПБ ПРПВВ       ПРЕРЫВ. ОТ ПВВ
ПРЕСВС ПВ ПРСВС(13)
 ПАМ 4
ВОЗПРЕ НОП
 ПО ВЫХПР ОТ ДРУГОГО СВС
ВХПВВП НОП ----
 УИ 14    АДРЕС БВВ
 ЗП СМ
 СЧ КУСД
 ПВ ДАЙПРМ(М15)
 СЧ И15
 УИ М15
 УИИ ’33’(М15)
 КК ’033’,(М16)
 СЧМР ,
 И Е33
 ПЕ СТ202         Б Н С
 ПБ ВЫХПР
*
ПРЕРВС НОП ----
 СЧ РВП
 УИА ВОЗПРЕ(13)
 ПБ АВСВС       АВАРИЯ ОТ СВС
ПРВРЕМ НОП ----
 УИА ВЫХПР(13)
 СЧ =В’37777750000’
 РЕГ ’57’
 ПБ СКАН
ЖДИПР НОП ---
 СЧ ВСЕЕД
 НТЖ 0
ЖДИПР1 НОП ,
 РЕГ ’140’
 СЧ КУСД
 УИА 3
 ПБ ЖДИПР1
*
*////////////////////////////////////////////////////////*
*///                                                  ///*
*///          П О Д П Р О Г Р А М М Ы                 ///*
*///                                                  ///*
*////////////////////////////////////////////////////////*
*
*
* ДАЙ НОМЕР СВС
МОЙНОМ НОП
 РЕГ ’250’
 СДА 64-6
 НТЖ ВСЕЕД
 НЕД =В’77777’
 И =В’77777’
 УИ М2
 ПЕ НЕНОЛЬ
 СЧ =М2В’1’       НУЛЕВОЙ НОМЕР --> В ЧЕТВЕРТЫЙ
НЕНОЛЬ ЗП НОМСВС
 ПБ (М15)
*
* ДАЙ ПРИПИСКУ 1-ОГО МАТ.ЛИСТА
*
ДАЙПРМ НОП ,
 И =М12В’37’
 СДА 64+7
 ИЛИ ПРИСОС
 ДК ’36’,РП1
РП1 НОП ,
 РЕГ ’20’
 УИ ’0’
 ПБ (М15)
*
*
* ЧТНЕНИЕ СЛУЖЕБНОГО СЛОВА
*
*
ЧТСЛ НОП ,
 УИА КЧСЛ(М3)      М3 := КЧСЛ
 ПВ УСТПОК(М16)
 СЧ КУСД
  СДА 64+38
 И Е4
 УИ М16
 МОДА ’30’(М16)
 СЧ (М10)
КЧСЛ  НОП ,
 УИА 0(М3)         М3 := 0
 ПБ (М6)
*
*
* ПОДСЧЕТ КОНТР. СУММЫ
*
*
ПОДКС НОП ,
 УИА КЧКС(М3)     М3 := КЧКС
 ПВ УСТПОК(М16)
 СЧ КУСД
 И =М12В’37’
 СДА 64+2
 СЛЦ =В’2000’
 УИ М17
 УИА -1023(М16)
 СЧ 0
ПОДКС1 НОП ,
 СЛЦ (М17)
 ЦИКЛ ПОДКС1(М16)
КЧКС НОП ,
 УИА 0(М3)         М3 :=0
 ПБ (М6)
*
*
* УСТАНОВКА  21 РЕГИСТРА
*
*
УСТПОК НОП ,
 СЧ 2
 И Е1
 СДА 64-3
 ИЛИ К2003
 УИ ’21’
 ПБ (М16)
*
*
*
*
*
*/////////////////////////////////////////////////////*
*///                                               ///*
*///        ПАРАМЕТРЫ        Г  Е  Н  С  - А       ///*
*///                                               ///*
*/////////////////////////////////////////////////////*
*
ИНФО КОНД М24В’560’В’4005’
 КОНД М47В’1’В’4005’
 КОНД М24В’100’В’4005’
 КОНД М24В’760’В’4005’
 КОНД В’6’
 КОНД М47В’1’
 КОНД В’0’
 КОНД В’21’
 КОНД В’22’ ПРОГР.МБ
 КОНД М16Х’0F000000’    МОДУЛИ  1-ОЙ СЕКЦИИ
 КОНД В’0’
 КОНД В’0’
 КОНД В’0’
 КОНД М47В’1’М31В’0’М27В’6’В’67777’   ПРЕДЕЛ
 КОНД М39В’177’В’2’      СВС-2
 КОНД В’26005670’        1985 ГОД
 КОНД М40В’175’П’0 БАРЫ’
 КОНД В’0’
 КОНД М39В’776’
 КОНД М39В’776’В’3320’
 КОНД В’0526400502334005’  5 КУСКОВ С 1300, 2 МБ С 1540
 КОНД П’БИНО  ’
 КОНД В’101010101010101’
ШКУСТР КОНД М36В’7400’      АЦД 7906
ШКОПТТ КОНД М39В’740’       ОПЕРАТОРСКИЕ ТЕРМ.
 КОНД В’-1’
 КОНД М12В’177760’
АСОТ КОНД В’0’       ОПЕРАТОРСКИЕ ТЕРМ.АС
 ПАМ 2
 КОНД М18В’74’
 КОНД В’0’     ТЕРМ.АС
КИНФО НОП
ИНФГЕН ЭКВ КИНФО-ИНФО
*
**************************************************************
СЕКОС  ПАМ 1             N СЕКЦИИ ОС С 1 Р.
БАЗАОС ПАМ 1             N СЕКЦИИ ОС С 8 Р.
ПРИСОС ПАМ 1             ПРИПИСКА СЕКЦИИ ОС
ШКАМОД ПАМ 1             ШКАЛА МОДУЛЕЙ ОП ДОСТУПНЫХ ОС
**************************************************************
*
*    К О Н С Т А Н Т Ы
*
СЕГМ0 КОНД В’7570700735600000’
* ЛИСТЫ :
*        0 1 2 3 - ДИСП80,ВЗУ,ТЕРМ
*        5 Л.0 АБЗ.          - АВОСТ
*        5(1,2,3 АБЗ) 6      - СЕКТОРНЫЕ НЕР-ТЫ
*        7 10    - БОЭК1,ВИСП,АВМД
*        14 15 16- РЕКОРДЫ
*        25 26 27- ЕСМЛ,ДМЛМБ,ХЛАМ,РЕЗОТ
*        31 32 33- ИПЗ МАТ.ЗАДАЧ
*        35 36   - КАЧКА,КИТ,ДИСКИ,ТЕСТЭК,АСПВВ
*        37      - МОТСВС
*
П7 КОНД В’7’
КУСГЕН КОНД М17В’3’М12В’34’М24В’0555’   ГЕНС
КУСПАР КОНД М17В’3’М12В’02’М24В’0754’   ПАРАМЕТРЫ ГЕНС-А
П777 КОНД В’777’
П17 КОНД В’17’
ВСЕЕД КОНД В’-1’
К2003 КОНД В’2003’
К2013 КОНД В’2013’
КУСД КОНД В’0’
Е39П25 КОНД М24В’77777’
КЛЮЧ КОНД М24В’70707’
П77777 КОНД В’77777’
Ч13 КОНД М47В’1’В’13’
*
* Р А Б О Ч И И
*
СМ ПАМ 1
РК ПАМ 1
РМР ПАМ 1
И20 ПАМ 1
И17 ПАМ 1
И16 ПАМ 1
И15 ПАМ 1
И14 ПАМ 1
РВП ПАМ 1
Р16 ПАМ 1
УСТРМБ ПАМ 1
МБВСЕ ПАМ 1
МБСВС ПАМ 1
ЧИСЛМБ ПАМ 1
ПРИВОС ПАМ 1
НОМСВС ПАМ 1
РЯ КОНД В’0’
РЯ1 КОНД В’0’
СТЕК ПАМ 5
*
* В Н Е Ш Н И И
ГЕНС ВНЕШ СЕКОСГ,ПРИСОГ,ШКАМОГ,ПАРОС,(ГЕНСМБ,ЧИСЛМБ)
ГЕНС ВНЕШ УПРМБ,ИНФО12,ШАС128
ПВВ ВНЕШ КАНМБ
ПВВ ВНЕШ ИНТУСФ,АТУСЗ,ЯЧКЧ,ЕПВВ,ПВВМБ,ПВВМД,АВСВС,ВОСЛМБ
ПВВ ВНЕШ ВОСРКЧ,ВОСЛМД,СКАН,ПРАДАП,МГРП,АРЕЗЕР,НАПРУС
ПВВ ВНЕШ ТЛУ,ПРПВВ,ПРСВС,КРЕЗ
*
*
*   Э К В
*
*
ТУМБ ЭКВ 2
NМАШ ЭКВ 14
 М
 ОПРЕ
*КОНЕЦ ЧАСТИ

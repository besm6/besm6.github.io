 МАСRО
 ОБММБ ∧КУС,∧АЗАЯВ
 СЧИ ∧АЗАЯВ
 ЗП Р16
 СЧИ М10
 ЗП ЗМ14   УПРЯТТ.   М10
 СЧ ∧КУС
 СДА 64+12
 И П37
 ИЛИ БАЗАОС
 УИ М10    ПАРАМ.  ПВВМБ
 СЧ ∧КУС
 ПВ ПВВМБ(13)
 ПАМ 3
 МОДА КО∧SNDХ
 УИА (13)
 СЧ ∧КУС
 ПБ ЖДИПР
КО∧SNDХ НОП
 УИА ’2003’
 УИ ∧АЗАЯВ
 СЧ ’500’
 ЗП Я500
 СЧ ЯЧ500
 ЗП ’500’
 ДК ’36’,М1∧SNDХ
М1∧SNDХ НОП
 ВОСЛМБ ∧КУС,∧АЗАЯВ
 СЧ Я500
 ЗП ’500’
 ДК ’36’,М2∧SNDХ
М2∧SNDХ НОП
 СЧ Р16
 УИ ∧АЗАЯВ
 СЧ ЗМ14
 УИ М10   ВОССТ.  М10
 МЕND
 МАСRО
 ОБММД ∧КУС,∧АЗАЯВ
 СЧИ ∧АЗАЯВ
 ЗП Р16
 СЧИ М10
 ЗП ЗМ14   УПРЯТ  М10
 СЧ ∧КУС
 СДА 64+12
 И П37
  ИЛИ БАЗАОС
 УИ М10    ПАРАМ ПВВМД
 СЧ ∧КУС
 ПВ ПВВМД(13)
 ПАМ 3
 МОДА КО∧SNDХ
 УИА (13)
 СЧ ∧КУС
 ПБ ЖДИПР
КО∧SNDХ НОП
 УИА ’2003’
 УИ ∧АЗАЯВ
 СЧ ’500’
 ЗП Я500
 СЧ ЯЧ500
 ЗП ’500’
 ДК ’36’,М1∧SNDХ
М1∧SNDХ НОП
 ВОСЛМД ∧КУС,∧АЗАЯВ
 СЧ Я500
 ЗП ’500’
 ДК ’36’,М2∧SNDХ
М2∧SNDХ НОП
 СЧ Р16
 УИ ∧АЗАЯВ
 СЧ ЗМ14
 УИ М10   ВОССТ. М10
 МЕND
 МАСRО
 ВОСЛМБ ∧КУС,∧АЗАЯВ
 СЧ ∧КУС
 ЗП (∧АЗАЯВ)
 СЧИ ∧АЗАЯВ
 ПВ ВОСЛМБ(13)
 ПАМ 3
 МЕND
 МАСRО
 ВОСЛМД ∧КУС,∧АЗАЯВ
 СЧ ∧КУС
 ЗП (∧АЗАЯВ)
 СЧИ ∧АЗАЯВ
 ПВ ВОСЛМД(13)
 ПАМ 3
 МЕND
ГЕНС СТАРТ ’70000’
 Б
 М
 Е
∧МЫ SЕТА ∧ВS
∧С SЕТВ 1   ИМЕЕТ ДР.СМЫСЛ,ЧЕМ ДЛЯ ОБЩИХ ДИСКОВ СВС
*∧Ф SЕТВ 0
*∧НИ SЕТВ 0
 GВLВ ∧Р,∧МВВ
∧МВВ SЕТВ 0
∧Р SЕТВ 0
*
 СЧ КНАПР
 ЗП КНАПРД
 СЧ КНАПР+1
 ЗП КНАПРД+1
* * * * * * * * * *
 СЧ СЕКОСГ
  СДА 64-7
 ЗП БАЗАОС
* * * * * * * * * *
 УИА 1(М16)
 СЧИ М16
 СДА 64-47
 УИА -47(М16)
ИИ ЗП ’103’+47(М16)
 СДА 64+1
 ЦИКЛ ИИ(М16)
 РЕГ ’101’
 СЧ ЯЧ501
 ЗП ’501’
 СЧ НОВ500
 ЗП ’500’
 ДК ’36’,НЧ1
НЧ1 РЖА 3
 ПБ ФК1МЫ
 СТРН
**********************************************
****                                      ****
****           С   Т   О   П   Ы          ****
****             00300 - 00377            ****
****                                      ****
**********************************************
*
СТ300 НОП ,
* НЕОБРАБАТЫВАЕМОЕ ВНУТРЕННЕЕ ПРЕРЫВЫНИЕ
*   СМ - РЕГИСТР ВНУТРЕННИХ ПРЕРЫВАНИЙ;
*   М33 - АДРЕС ПРЕРЫВАНИЯ.
 РЕГ ’237’
 СТОП ’00300’
 ПБ СТ300
*
СТ301 НОП ,
* НЕОБРАБАТЫВАЕМОЕ ВНЕШНЕЕ ПРЕРЫВЫНИЕ
*   СМ - РЕГИСТР ВНЕШНИХ ПРЕРЫВАНИЙ;
*   М33 - АДРЕС ПРЕРЫВАНИЯ.
 РЕГ ’247’
 СТОП ’00301’
 ПБ СТ301
*
СТ302 НОП ,
* НЕВЕРНЫЙ КЛЮЧ В СЛУЖЕБНЫХ СЛОВАХ ЗОНЫ МД
*   СМ - КУС МД
 СЧ КУСR
 СТОП ’00302’
 ПБ СТ302
*
СТ303 НОП ,
* НЕВЕРНЫЙ КОНТРОЛЬНАЯ СУММА В СЛУЖЕБНЫХ СЛОВАХ ЗОНЫ МД
*   СМ - КУС МД
 СЧ КУСR
 СТОП ’00303’
 ПБ СТ303
**********************************************
 СТРН ,
ДАЙПРМ НОП ---- ДАЙ ПРИП. УПРЯТ. РЕГ.
 И ПРИПВ
 СДА 64+7
 ИЛИ ПРИСОГ
 ДК ’36’,РП1
РП1 НОП
 РЕГ ’20’  УСТАНОВИЛ 1-МУ МАТЛИСТУ
 ПБ (М15)
РОС0 НОП
 СЧ СЧВРБ-8(М10)
 И Е48+31(М5)
 ПО НРОС  НЕ РОСП
МБРОС УИА -1023(М1)
 СЧ КАННА5
РПВВ СЛЦ К5АННА
 МОДА 4096
 ЗП 1023(М1)
 ЦИКЛ РПВВ(М1)
 ПВ ЗКМБ(М11)
 СЧ ЗПМ2
 УИ М2
 СЧ ОТВЕТ
 ПЕ КЧРОС     ОШ. В ДИР.
 УИА -1023(М1)
 СЧ
ОРОС МОДА 4096
 ЗП 1023(М1)
 ЦИКЛ ОРОС(М1)
 СЧ КУСБ-1(М16)
 ИЛИ Е18
 ЗП КУСБ-1(М16)
 ПВ ЗКМБ(М11)
 СЧ ЗПМ2
 УИ М2
 СЧ ОШБ
 ПО РПВ    НЕТ ОШ. ОБМЕНА
КЧРОС СЧИ М2
 ПВ НОМСЧ1(М1)
 СЧ СЧВРБ-8(М10)
 ИЛИ Е48+31(М5)
 НТЖ Е48+31(М5)
 ЗП СЧВРБ-8(М10)
РПВ СЛИА 1(М14)
 ЦИКЛ РОС(М5)
 СЧИ М14
 НТЖ Е9
 ПЕ РОС2
 СЧИ М2
 ПО МБ100    ВСЕ МБ ПРОВЕРЕНЫ
 СЛЦ Е1
 И П7
 УИ М2
 ПБ РОС3   НА СЛЕДУЮЩЕЕ НАПРАВЛЕНИЕ
МБ100 НОП
 УИА LШЗНМБ(М5)
 СЧИ М5
 ПБ ЗПГРБ
ВСЕМБ СЧИ М2
 ПВ НОМСЧ1(М1)
 СЛИА -8(М10)
 СЧИ М10
ЗПГРБ НОП
 ПИО ВЫХ12(М13)
 СЛЦ Е1
ВЫХ12 НОП
 ЗП ГРАНБ
 ВЧ Е5
 ПО НОВПР
 СЧ Е5
 ЗП ГРАНБ
 ПБ (М12)
НОВПР НОП ,             НОВАЯ ПРОВЕРКА 15.11.87
 ВЧОБ =В’50’
 ПО (М12)
 СЧ =В’70’
 ЗП ГРАНБ
 ПБ (М12)
*
НРОС СЧ СЧВРБ-8(М10)
 ПБ РПВ
******КОНЕЦ БЛОКА******
***** КОНЕЦ ЧАСТИ *****
* ИНФОРМАЦИЯ К ГЕНС:
*
 АIF (NОТ ∧ИАЭ).Э1
БОС ЭКВ ’71733’ НОМЕР ХОДА В КМД
МД29 ЭКВ ’71734’
МД58 ЭКВ ’71736’
.Э1 АNОР
NМЛ ЭКВ ’71740’ НОМЕРА СИСТЕМНЫХ ЛЕНТ
МДС ЭКВ ’71741’ НОМЕР МД СТАТИСТИКИ
МВВ ЭКВ ’71742’ НОМЕР МД ВВОДА
МДВ ЭКВ ’71743’ НОМЕР МД ВЫВОДА
ПРОГОН ЭКВ ’71744’
СЛОЙК ЭКВ ’71745’ ВИД НАБИВКИ П/К,N МЛ ДЛЯ СВЯЗИ
МБКЧТ ЭКВ ’71746’ НАЧ.N ТРАКТА ДЛЯ КОНТРОЛЯ ЗП НА МЛ
ДЛТ ЭКВ ’71747’ ДЕЛЬТА ДЛЯ РАБМБ МДРЗО
NМБ ЭКВ ’71747’
ПМБ ЭКВ ’71750’ НОМЕР ПРОГРАМНОГО МБ
РАСПП ЭКВ ’71751’ ТАБЛИЦА СТРАНИЦ ОП
РАСП1 ЭКВ ’71752’
РАСП3 ЭКВ ’71754’
ТРАКТЫ ЭКВ ’71755’ МАХ ЧИСЛО ТРАКТОВ И ЛИСТОВ ДЛЯ1ЗАДАЧ
NБЭСМ ЭКВ ’71756’ НОМЕР МАШИНЫ,ПРИЗНАК ЧАСОВ ИПМ
КГОД ЭКВ ’71757’ МЛ.ЦИФРА ГОДА,ЧИСЛО И НАЧ.N ОБЩ.ДИСК
ВЛА ЭКВ ’71760’ ПЕРВ. СИМВОЛЫ ВЛАДЕЛЬЦА ЭВМ
ВИДМ ЭКВ ’71761’ ВИД ПОДКЛ.УВВК (ЗАВОДСКОЙ ИЛИ ИПМ)
УСТРМД ЭКВ ’71762’  NN УСТР.МД (48-17РР) 3-ГО НАПР.
МБС5 ЭКВ ’71763’ NN УСТР.МД (48-17РР) 4-ГО НАПР.
НОМРД ЭКВ ’71764’ МД РАБ ЗОН,РАБ МД
ВЛА1 ЭКВ ’71765’ ПОСЛ.6 СИМВ.ВЛАДЕЛЬЦА ЭВМ
НОМУСТ ЭКВ ’71766’ ТАБЛИЦА УСТРОЙСТВ ТТ И СОNSUL
ШКУСТР ЭКВ ’71767’
ШКОПТ ЭКВ ’71770’
ШКДТ ЭКВ ’71771’
ШКСВ ЭКВ ’71772’ ШКСВМ ДЛЯ ДИСП70 (+1 ЯЧЕЙКА ДЛЯ ТБ)
NТАС ЭКВ ’71773’ ФИЗ. НОМЕРА ТЕРМ. АС
ШКТТ ЭКВ ’71774’ В 1-Й ЯЧ ЗАНЯТ 48
ШКТТ2 ЭКВ ’71775’
ШКVТ ЭКВ ’71776’ ЗАНЯТЫ 48
ШКVТ2 ЭКВ ’71777’
*
*
ФК1МЫ НОП
 СЧ ТР3
 И П16
 ЗП КОДТР
 МОДА НАПРУС
 СЧ 0
 ЗП КУСГП
 УИА 0(М16)
РОСМБ УИА -15(М3)
 ПВ РБ(М12) ОБОБЩ.ПРИЗН.РОСПИСИ МБ
 УИА (М14)
 УИА СТАТИ(М12)
 СЧ РОСПБ
 ПЕ РОСПВВ
СТАТИ НОП
 СЧ 0
 ЗП РОСПБ
 УИА -15(М4)
 УИА 2(М1)
 ПВ ЧТГП(М11) ЧТ.561-600 ЗОН НА 11-30 ЛЛ.
 УИА -15(М4)
 УИА 0(М1)
 ПВ ОБМПМБ(М15)   ЗАПИСЬ ТР. 0-17
 УИА -7(М4)
 УИА 24(М1)
 ПВ ЧТГП(М11) ЧТ.601-610 ЗОН НА 21-30 ЛЛ.
 УИА ’50000’(М17)
 СЧ ВЛА
 ЗП ВЛАД-ПЧКЗ(М17)
 ЗП ВЛИ-ЭКСТ-1024(М17)
 СЧ ВЛА1
 ЗП ВЛАД+1-ПЧКЗ(М17)
 ЗП ВЛИ-ЭКСТ-1023(М17)
 СЧ ВЕ-ПЧКЗ(М17)
 ЗП ВЕ1-ЭКСТ-1024(М17)
 СЧ П17
 СДА 64-20
 И КГОД
 СДА 64-4
 ЗП РЯ3
 СЧ П17
 СДА 64-16
 И КГОД
 СДА 64-16
 ИЛИ РЯ3
 ИЛИ ПЧГОД-ПЧКЗ(М17)
 ЗП ПЧГОД-ПЧКЗ(М17)
 УИА -7(М4)
 УИА 1(М1)
 ПВ ОБМПМБ(М15) ЗП.ТРАКТОВ 20-27
 УИА -7(М4)
 УИА 25(М1)
 ПВ ЧТГП(М11) ЧТ.ЗОН 611-620 НА 11-20 ЛЛ.
 УИА -7(М4)
 УИА 14(М1)
 ПВ ОБМПМБ(М15) ЗП ТРАКТОВ 30-37
АЦП УИА (М1)
*
 ПВ ОБМИМБ(М15) ЧТ.НА 23Л 6ТР ИНФ.МБ
 СЧ УПРМБ
 МОДА МК
 ЗП МБМК          СОХРАНЯЮ ДО СЛЕДУЮЩЕЙ ГЕНЕРАЦИИ
 СЧ ГРАНБ
 МОДА МК
 ЗП УПРГРА        БЕЗ РОСПИСИ МБ ГРАНБ БЕРЕТСЯ ИЗ М.К.
 УИА -6(М4)
 УИА 1(М1) НА 1-7 СТР
 ПВ ЧТГП(М11)    ЧТ-Е 41-47 ЗОН(ДИСП,КАЧКА,ДИСКИ,КИТ)
*БЛОК ВЫЗОВА НЕРЕЗИДЕНТНЫХ ЧАСТЕЙ
*********************************
 УИА 22(М1)      ЧТЕНИЕ 1-ГО НЕРЕЗИДЕНТА
М244 УИА (М4)
 СЧ ЛСТБАЗ
 СДА 64-2
 ИЛИ КУСЧТЗ(М1)
 ЗП КУСЧТЗ(М1)
 СЧИ М7
 ЗП ЗАПМ7
 ПВ ЧТГП(М11)  ВЫЗОВ НЕР.ЧАСТИ
 СЧ ЗАПМ7
 УИ М7
 СЧ ЛСТБАЗ
 УИ М6
 ПБ (М6)       НАЧИНАЕТ РАБОТАТЬ НЕРЕЗИДЕНТ
КОН1 СЧ ЛСТБАЗ
 СДА 64-2
 ИЛИ КУСГ2
 СЛЦ НОММЛ
 УИИ М16(М6)
 ПБ ФИЗОБМ
*** *** *** *** *** *** *** *** *** *** *** ***
*  ПО ОКОНЧАНИИ ГЕНЕРАЦИИ ВЫХОД ЧЕРЕЗ ГЕНС2   *
*** *** *** *** *** *** *** *** *** *** *** ***
А СЧ ЛСТБАЗ
 УИ М6
 ПБ АЛИСТЫ-ГЕНС1(М6)
ПОИСК СЧ ЛСТБАЗ
 УИ М6
 ПБ ПОИСКС-ГЕНС1(М6)
ОБЩГ СЧ ЛСТБАЗ
 УИ М6
 ПБ АОБЩГ-ГЕНС1(М6)
* БЛОК ЧТЕНИЯ С МД ЗАГРУЗКИ
* НОМЕР КУС НА М1
* ЧИСЛО ЗОН НА М4,ВЫХОД ПО М11
ЧТМДВ УИА КУСГП(М6)
 ПБ ЧТСМД ЧТ С МД ВВОДА
ЧТГП УИА КУСГП(М6)
 ПБ ЧТСМД
ЧТСМД СЧ (М6)
 ИЛИ КУСЧТЗ(М1)
 СЛЦ Е27
ЧТМД1 ЗП КУСД1 ФОРМ.КУС
 ПВ ЧТМД(М1)
 СЧ КУСД1
 СЛЦ Е13
 СЛЦ Е25
 ЦИКЛ ЧТМД1(М4)
 СЧ ЛСТБАЗ
 УИ М6
 ПБ (М11)
КУСЧТЗ КОНД М24В’546’М17В’3’М12В’20’ Б.КАТ С ’ТЕЛЕ’(0)
 КОНД М24В’621’М17В’3’М12В’1’ ДИСП70,КИТ,ДИСКИ,КАЧКА (3)
 КОНД М24В’561’М17В’3’М12В’11’ ЗОНЫ 561-600
 КОНД М24В’545’М17В’3’М12В’22’ КАТАЛОГ ШИФРОВ И ЗОН ВВОДА
 КОНД М24В’540’М17В’3’М12В’24’   ФИО
 КОНД М24В’465’М17В’3’М12В’25’     ЕСМЛ,ДМЛМБ,ХЛАМ
 КОНД М24В’706’М17В’3’М12В’20’
 КОНД М24В’717’М17В’3’М12В’30’
 КОНД М24В’677’М17В’3’М12В’30’ ДОПОЛН.РЕЗИДЕНТ
 КОНД М24В’520’М17В’3’М12В’04’ ТАБКОД
 КОНД М24В’526’М17В’3’М12В’05’ АВОСТ
 КОНД М24В’611’М17В’3’М12В’1’     ПГС
 КОНД М24В’730’М17В’3’М12В’30’    ГП1 ГП4
 КОНД М24В’726’М17В’3’М12В’14’
 КОНД М24В’725’М17В’3’М12В’1’
 КОНД М24В’531’М17В’3’М12В’30’    ЦИРКУЛ
 КОНД М24В’712’М17В’3’М12В’1’  712 ЗОНА :КОНФУС НА СВС
 КОНД М24В’534’М17В’3’М12В’30’    ПРИК4
 КОНД М24В’550’М17В’3’М12В’20’
 КОНД М24В’520’М17В’3’М12В’07’    19 ТУПР
 КОНД М24В’475’М17В’3’М12В’7’    БОЭК1-ВИСП-АВМД
 КОНД М24В’751’М17В’3’М12В’21’      751 ЗОНА
 КОНД М24В’552’М17В’3’  КУС ЧТЕН.1 НЕРЕЗ.ГЕНСА
КУСГ2 КОНД М24В’471’М17В’1’     ГЕНС2
  КОНД М24В’601’М17В’3’М12В’21’ ЗОНЫ 601-610 (24)
  КОНД М24В’611’М17В’3’М12В’11’ ЗОНЫ 611-620 (25)
ЧТМД СЧ КУСД1
 СДА 64+7
 И П7
 УИ М17 N МД
 НТЖ П7
 УИ М15
 СЧ КУСД1
 СДА 64+41
 СЛЦ П3
 УИ М16     N НАПРАВЛЕНИЯ
ЗАПР НОП
 УИИ М6(М16)
 УИА -7(М6)
 СЧ 0
ОЧСЛ МОД КНАПР-1(М16) РОСП.СЛ.СЛОВ
 ЗП 7(М6)
 ЦИКЛ ОЧСЛ(М6)
 УИА -7(М6)
ВБРЗ ЗП 1 ВЫТАЛК.БРЗ
 ЦИКЛ ВБРЗ(М6)
 СЧ КУСД1
 УИИ М10(М15)
 УИИ М6(М16)
 ЗП КУСR
 ПВ ДАЙПРМ(М15)
 ОБММД КУСR,М16
 УИИ М15(М10)
 УИИ М16(М6)
 ПБ ПРЕР
ЧТСЛ СЧ К2003   ЧТ. СЛ. СЛОВ
 УИ ’21’
 МОД КНАПР-1(М16)
 СЧ (М10)
 УИ 0
 ПБ (М6)
ПРЕР НОП ,
 СЧ КУСД1
 ПВ ДАЙПРМ(М15)
 УИА КССС(М3)
 УИА 1(М10)
 ПВ ЧТСЛ(М6)
КССС ЗП РЯ2
 НТЖ =М24В’70707’
 И Е39П25
 ПЕ СТ302         НЕВЕРНЫЙ КЛЮЧ В  СЛ.СЛ.
 СЧ РЯ2
 И Е41
 ПЕ НОВС
 УИА -1(М10)
КСС1 УИА ПОВТД(М3)
 СЧ Е11
 УИ ’21’
 УИА -255(М6)
 СЛИА 2(М10)
 СЧ 0
КСС МОДА (М12) КОНТР.СУММИР.
 СЛЦ 1279(М6)
 ЦИКЛ КСС(М6)
 УИА ’2003’
 ЗП РЯ1
 УИА КСС2(М3)
 ПВ ЧТСЛ(М6)
КСС2 ЗП РЯ2
 НТЖ РЯ1
 И П7777
 ПЕ ОШК
 СЛИА 256(М12)
КС ЦИКЛ КСС1(М2)
 УИА ГАШ(М6)
ОСВКМД СЧ К2013 ОСВОБ.КМД ,ПР.ПОК
 УИ ’21’
 ПБ (М6)
ГАШ СЧ 0 ГАШ.ГРП И МГРП
 ПБ (М1)
ОШК СЛИА -1(М10)
 УИА ОШК1(М3)
 ПВ ЧТСЛ(М6) В ЧЕТН СЛ КС,ЗАПИСАН ПО-СТАРОМУ
ОШК1 НТЖ РЯ1
 И П7777
 ПЕ СТ303         НЕСОВПАДЕНИЕ  К С
 СЛИА 1(М10)
 СЛИА 256(М12)
 ПБ КС
НОВС УИА -1023(М6)
 УИА ПОВТД(М3)
 СЧ Е11
 УИ 17
 СЧ 0
КСН СЛЦ 2047(М6)
 ЦИКЛ КСН(М6)
 УИА 1027
 ЗП РЯ1
 УИА КСП(М3)
 УИА 3(М10)
 ПВ ЧТСЛ(М6)
КСП НТЖ РЯ1
 ПЕ СТ303         НЕСОВПАДЕНИЕ  К С
 УИА ГАШ(М6)
 ПБ ОСВКМД
ПОВТД УИА ЗАПР(М6) ПОВТ.ОБМЕН ПРИ КЧ
 ПБ ОСВКМД
НОВ500 НОП ,
 ЗП СМ
 ПБ (М3)
Я500 ПАМ 1
ЯЧ500 ЗП СМ
 ПБ ПРКЧ
ЯЧ501 ЗП СМ
 ПБ ВНЕШПР
* БЛОК РОСПИСИ МБ
*
РБ УИА (М1)
 УИА 0(М4)
 СЧ 0
 МОДА МК
 ЗП КЛЮМК
 СЧ КОДТР
 И Е4
 ПЕ РБ1А          ОС ВЫЗВАНА С РОСПИСЬЮ МБ
 СЧ Е1
 ЗП ПРМК
 ПВ ОБМИМБ(М15)  ЧТ. М.К. НА 23 Л.
РБ1А СЧ 0
 ЗП ПРМК
 УИА 1-КОЛМБ(М1)
 УИА -13(М3)
 МОДА МК
 СЧ КЛЮМК
 НТЖ NМЛ+2
 ПО РБА           КЛЮЧ ВЕРЕН
РОСМК СЧ Е4
 ЗП КОДТР
РБ1 СЧ Е48П17 НЕ РАСП.ТРАКТ С ГЕНС
 ЗП СЧВРБ+13(М3)
 МОДА РБ1
 ЦИКЛ (М3)
 СЧ =М41В’177’
 ЗП СЧВРБ+14
 УИА -1023(М3)
 СЧ 0
ОБМК МОДА ’46000’
 ЗП 1023(М3)
 ЦИКЛ ОБМК(М3)
 УИА (М4)
 УИА 2(М1)
 ПВ ОБМИМБ(М15)  ОБНУЛЕНИЕ МАЛОГО КАТАЛОГА
 УИА (М4)
 УИА 4(М1)
 ПВ ОБМИМБ(М15)    ОБНУЛЕНИЕ 5 ТР. ИНФ. МБ
 СЧ ШКПРТР
 НТЖ Е43
 ЗП ШКПРТР           5 ТРАКТ ПРОВЕРИЛИ
 СЧ NМБ
 И П37
 УИ М2
 СЧ СЧВРБ-8(М2)
 ИЛИ ТРСОХР
 НТЖ ТРСОХР
 ЗП СЧВРБ-8(М2)
 СЧ ЧТЗП
 СЛЦ Е25
 ЗП ЧТЗП
 ДК ’36’,РБ11     ВМЕСТО ПРОВЕРКИ - ОБНУЛЕНИЕ
РБ11 НОП
*-ПРОВЕРОЧНОЕ ЧТЕНИЕ ТРАКТОВ ИНФ. МБ
ВЫБОРТ СЧ ШКПРТР
 ПО КНЦПРТ  ВСЕ ТРАКТЫ ПРОВЕРИЛИ
 НЕД П37
 И П37
 УИ М4
 СДА 64-2
 ЗП ТРАКТ    3:7 РР - НОМЕР ТРАКТА
 СЧ Е48(М4)
 НТЖ ШКПРТР
 ЗП ШКПРТР
 СЧ Е1
 ЗП ПРИНТ   ПРИЗНАК ПРОВЕРКИ ТРАКТОВ
ЧТЗП УИА 6(М1)
ОБМИНТ УИА (М4)
 СЧ КУСИМБ(М1)
 И ОБТРАК
 СЛЦ ТРАКТ
 ЗП КУСИМБ(М1)
 ПВ ОБМИМБ(М15)
 ПБ ВЫБОРТ       НА ВЫБОР СЛЕД. ТРАКТА
КНЦПРТ ЗП ПРИНТ   КОНЕЦ ПРОВЕРКИ
 СЧ КОДТР
 СДА 64+3
 И Е1
 ПЕ ЗРОСБ         РОСПИСАТЬ МБ
 МОДА МК
 СЧ МБМК          ПРОШЛАЯ ГЕНЕРАЦИЯ
 НТЖ УПРМБ
 ПО ЗРОСБ         БЕЗ РОСПИСИ МБ
 СЧ 0
 УИА 15-КОЛМБ(М5)
ЦОСЧВ ЗП СЧВРБ+КОЛМБ-1(М5) ОБНУЛЕНИЕ ИНФОРМАЦИИ ИЗ М.К.
 ЦИКЛ ЦОСЧВ(М5)
 СЧ Е1
ЗРОСБ ЗП РОСПБ         РЕЖИМ РОСПИСИ МБ
 НТЖ Е1
 ПО НМГРА
 МОДА МК
 СЧ УПРГРА
 ЗП ГРАНБ
НМГРА УИА 0(М5)
 СЧ 3
 И Е12
 ПО (М12)
 СЧ 4  12Р 3ТР - ПРИЗН ИСКЛЮЧ МБ
 И П37 НОМ МБ В 1-3 РР 4 ТР
 УИ М2  48-17 РР - ТРАКТЫ
 СЧ 4
 И Е48П17
 НТЖ Е48П17
 И СЧВРБ-8(М2)
 ЗП СЧВРБ-8(М2)
 СЧИ М2
 НТЖ NМБ
 И П7
 ПЕ (М12)
 СЧ СЧВРБ-8(М2)
 И ТРСИСТ
 ЗП ОТБРТР
 ПБ (М12)
РБА МОДА МК ОТБРАК.ТРАКТЫ НЕ ПРОВ
 СЧ БРАКТР+КОЛМБ-1(М1)
 ЗП СЧВРБ+КОЛМБ-1(М1)
 МОДА РБА
 ЦИКЛ (М1)
 МОДА МК
 СЧ БРАКТР+14
 И =М41В’177’
 ЗП СЧВРБ+14
 СЧ NМБ
 И П37
 УИ М2 ИНФМБ
 СЧ СЧВРБ-8(М2)
 НТЖ ТРСИСТ
 ЗП ОТБРТР 1-ТРАКТ ОТБРАКОВАН
 ИЛИ ТРСОХР
 НТЖ Е48П17
 ЗП СЧВРБ-8(М2)
 ПБ РБ11
ПОДГ4Б СЧ РОСПБ
 ПО (М12)  ПОДГОНМЛ И РОСПИСЬ МБ НЕ ЗАКОНЧЕНА
РОСПМБ ПИНО РОСБ2(М5) ИДЕТ КОНТРОЛЬНОЕ ЧТЕНИЕ С МБ
 СЧИ М14
 И Е1
 СЛЦ Е1
 УИ М16 N НАПР
 СЧ КУСБ-1(М16)
 И Е48
 ПЕ РОСБ2 НЕ ОБРАБОТАН ПРЕДЫДУЩИЙ ЗАКАЗ К НАПР
 СЧИ М14
 СДА 64+1
 СДА 64-2 ФОРМИРОВАНИЕ КУС ЗП НА МБ
 ИЛИ Е19
 ИЛИ Е17(М16) (10-Я ИЛИ 4-Я СТР.СООТВ.НАПР)
 ЗП КУСБ-1(М16)
 ПВ НОМСЧ(М1)
 УИА -1023(М1)
 СЧИ М10
 НТЖ NМБ
 И П37
 ПЕ РОСБ НЕ ИНФ.МБ
 СЧ СЧВРБ-8(М10)
 И Е48(М13)
 ПО РОСБ12 ТРАКТ НЕ РАСПИС.
 СЧ 0
РОСБА МОД Е15(М16)
 ЗП 1023(М1) ОБНУЛ СТР.ПРИ РОСП.МБ
 ЦИКЛ РОСБА(М1)
 ПБ РОСБ2
РОСБ СЧ СЧВРБ-8(М10)
 И Е48(М13)
 ПО РОСБ12 ТРАКТ НЕ РАСПИС.
РОСБ1 СЧ КРОСП
 СЛЦ КРОСП
 ЗП КРОСП
 МОД Е15(М16) РОСПИСЬ СТРАНИЦЫ ЗП НА МБ
 ЗП 1023(М1)
 ЦИКЛ РОСБ1(М1)
РОСБ2 УИА -1(М16)
РОСБ2А СЧ КУСБ+1(М16)
 И Е48
 ПО РОСБ10 ЗАКАЗ К НАПРАВЛ.ЕЩЕ НЕ ВЫДАН
 СЧ КУСБ+1(М16)
 И Е47
 ПЕ РОСБ5А ЗАКАЗ УЖЕ ОБРАБОТАН
 РЕГ ’237’
 И Е45(М16)
 ПЕ РОСБ6 ОБМЕН ОКОНЧЕН,НА ОБРАБ.
 СЛИА 2(М16)
 ПВ НОМСЧ(М1)
 СЧ СЧВРБ-8(М10)
 И Е15П1
 ЗП РЯ1
 СЧ СЧВРЕМ
 ВЧ РЯ1
 ВЧ П13
 СЛИА -2(М16)
 ПЕ РОСБ4
 СЧ 0 МБ ОТСУТСТВУЕТ
 ЗП СЧВРБ-8(М10)
РОСБ5 СЧ КУСБ+1(М16)
 ИЛИ Е45 УСТ.ПРИЗНАК БЛОКИРОВКИ КОНТР.ЧТ
 ИЛИ Е47
 ЗП КУСБ+1(М16)
РОСБ5А ЦИКЛ РОСБ2А(М16)
 СЛИА 2(М16)
 ПБ РОСБ7
РОСБ4 ЦИКЛ РОСБ2А(М16)
 ПБ ПОДГ4 НА БЛОК ПОДГОНА МЛ
РОСБ6 РЕГ ’37’ ГАШ.46(45)Р ГРП
 СЛИА 2(М16)
 ПВ ПРБ(М11) НА БЛОК ОБРАБОТКИ ОБМЕНА С МБ
 ПИО РОСБ7(М5) БЫЛА ЗП
 СЧ ОШБ
 ПО РОСБ7 ЧТ.БЕЗ ОШИБОК
 ПВ НОМСЧ(М1)
РОСБ6В СЧ СЧВРБ-8(М10)
 ИЛИ Е48(М13)
 НТЖ Е48(М13)
 ЗП СЧВРБ-8(М10) ТРАКТА НЕТ
РОСБ7 СЧ КУСБ-1(М16)
 ИЛИ Е47 УСТ.ПРИЗНАК ВЫП.ЗАКАЗА
 ЗП КУСБ-1(М16)
 И КУСБ
 И КУСБ+1
 И Е47
 ПО ПОДГ4 ВЫПОЛНИТЬ ЗАКАЗ ПО ДРУГОМУ НАПР.МБ
 ПИНО РОСБ9(М5) КОНТР.ЧТ.ОКОНЧЕНО НА ОБОИХ НАПР.
 УИА 1(М5)
 УИА -1(М16)
РОСБ8 СЧ КУСБ+1(М16)
 И Е46П1 ФОРМИРОВАНИЕ КУС КОНТРОЛЬНОГО ЧТЕНИЯ
 ИЛИ Е22Е18
 ЗП КУСБ+1(М16)
 ЦИКЛ РОСБ8(М16)
 ПБ ПОДГ4
РОСБ9 СЧ 0
 ЗП КУСБ
 ЗП КУСБ+1
 УИА 0(М5) УСТ.РАЗРЕШЕНИЕ ЗП НА МБ
 СЧИ М14
 НТЖ Е10
 ПЕ ПОДГ4
 ЗП РОСПБ РОСПИСЬ МБ ОКОНЧЕНА
 ПБ СТАТИ
РОСБ10 СЛИА 2(М16)
 ПВ НОМСЧ(М1)
 СЧ СЧВРБ-8(М10)
 И Е48П17 УСТ.ВРЕМЯ ВЫДАЧИ КОМАНДЫ ОБМЕНА С МБ
 ЗП СЧВРБ-8(М10)
 СЧ СЧВРЕМ
 И П77777
 ИЛИ СЧВРБ-8(М10) УСТАН.ВРЕМЯ ВЫДАЧИ КОМАНДЫ ОБМЕНА СМБ
 ЗП СЧВРБ-8(М10)
 СЧ КУСБ-1(М16)
 ИЛИ Е48
 ЗП КУСБ-1(М16) УСТ.ПРИЗНАК ВЫДАННОГО ЗАКАЗА
 ПИО РОСБ11(М5) ПРИ ЗП
 СЧ КУСБ-1(М16)
 И Е45
 ПЕ РОСБ6В
РОСБ11 ПВ ЗКМБ(М11) ВЫДАЧА ЗАКАЗА НА ОБМЕН
 ПИНО ПОДГ4(М5) ПРИ ЧТ.
 СЛИА 1(М14) +1 К СЧ-КУ ЗАКАЗОВ
 ПБ ПОДГ4
РОСБ12 СЧ КУСБ-1(М16)
 ИЛИ Е48 ИММИТАЦИЯ ВЫДАЧИ ЗАКАЗА
 ИЛИ Е45
 ЗП КУСБ-1(М16)
 СЛИА 1(М14)
 ПБ РОСБ7
НОМСЧ СЧИ М16
НОМСЧ1 НОП
 СДА 64-3
 ПЕ НЕМБФ
 СЧ Е7
НЕМБФ НОП
 УИ М10
 СЧ КУСБ-1(М16)
 СДА 64+7
 И П7 МБ
 УИ М13
 СЛИ М10(М13)
 СЧ КУСБ-1(М16)
 СДА 64+2
 И П37 ТРАКТ
 УИ М13
 ПБ (М1)
*
* БЛОК ОБМЕНА С ПРОГРАММНЫМ МБ
*
* ВХОДНАЯ ИНФ:
* НОМЕР ИСХОДНОГО КУС В М1
* ЧИСЛО ЦИКЛОВ ОБМЕНА В М4
* ВЫХОД ПО М15
*
ОБМПМБ СЧ NМБ+1
 СДА 64+3
 И Е3П1
 УИ М16 N НАПР.ПРОГРАММНОГО МБ
 СЧ NМБ+1
 И Е3П1
 СДА 64-7 N ПРОГРАММНОГО МБ
 ИЛИ КУСПМБ(М1) ИСХОДНЫЙ КУС ОБМЕНА
Б1 ЗП КУСБ-1(М16)
 ПВ ЗКМБ(М11) ОБМЕН С ПРОГРАММНЫМ МБ
 СЧ КУСБ-1(М16)
 СЛЦ Е13 +1 К N СТР.
 СЛЦ Е3 +1 К N ТР
 ЦИКЛ Б1(М4)
 СЧ ЛСТБАЗ
 УИ М6
 ПБ (М15) ВЫХОД
КУСПМБ КОНД М18В’1’М12В’11’М2В’0’ ТР.0-17 (0)
       КОНД М18В’1’М12В’21’М2В’20’ ТР.20-27 (1)
 ПАМ 1    СВ-НА
 КОНД М10В’2’М12В’30’В’37’ ЕСПИ80
 КОНД М10В’3’М12В’30’В’33’ ЕСПЛ
 КОНД М17В’1’М12В’6’В’24’ 5 ТР 0С- ИПЗ,ХЛАМ
 КОНД М18В’1’М12В’1’В’140’       ПГС
 КОНД М12В’30’М2В’13’В’1’ ГП1
 КОНД М12В’30’М10В’1’В’132’   ГП4
 КОНД М12В’7’М17В’3’М2В’16’     БОЭК
 КОНД М12В’1’М18В’1’М2В’31’
 КОНД М12В’30’М18В’1’М2В’37’
 КОНД М18В’1’М12В’1’М2В’32’   КОНФУС РАБОТАЕТ НА СВС
 КОНД М18В’1’М12В’30’М2В’36’
 КОНД М18В’1’М12В’11’М2В’30’ ТР.30-37 (14)
*
*
* БЛОК ОБМЕНА С ИНФОРМАЦИОННЫМ МБ
*
* ВХОДНАЯ ИНФ:
* НОМЕР ИСХОДНОГО КУС В М1
* ЧИСЛО ЦИКЛОВ ОБМЕНА В М4
* ВЫХОД ПО (М15)
*
ОБМИМБ СЧ NМБ
 СДА 64+3
 И Е3П1
 УИ М16 N НАПР
 СЧ NМБ
 И Е3П1
 СДА 64-7 N МБ
 ИЛИ КУСИМБ(М1)
 ПБ Б1
КУСИМБ КОНД М17В’3’М12В’23’М2В’6’
 КОНД М17В’3’М12В’20’
 КОНД М18В’1’М12В’23’М2В’6’
 КОНД М18В’1’М12В’1’М2В’6’
 КОНД М18В’1’М12В’23’М2В’5’
 КОНД М18В’1’М12В’07’М2В’3’    ТУПР
 КОНД М17В’3’М12В’24’   ЧТ-Е 4-5,10-11,20-25,36-37 ТРАКТОВ
 КОНД М18В’1’М12В’23’  ОБН-Е 3-5,10-11,20-25,36-37 ТРАКТОВ
*
*               БЛОК ОБМЕНА С МБ
*
* ВХОДНАЯ ИНФ: М16 -N НАПРАВЛЕНИЯ ОБМЕНА, В ЯЧЕЙКЕ КУСБ НАПР.
* СФОРМИРОВАННЫЙ КУС. ВЫХОД ПО (М11)
*
ЗКМБ НОП
 СЧ РОСПБ
 ПЕ РПБ
 СЧИ М16
 СДА 64-39
 ИЛИ КУСБ-1(М16)
 ЗП КУСБ-1(М16)
РПБ СЧ КУСБ-1(М16)
 И Е18
 ПО МБ5 ЗП НА МБ
 УИА -7(М1)
 УИА -7(М10)
 СЧ 0
МБ4 МОД КНАПР-1(М16)
 ЗП 7(М1) ОЧИСТИТЬ ЯЧЕЙКИ СЛУЖ.СЛОВ НАПР.
 ЦИКЛ МБ4(М1)
 ПБ МБ7
МБ5 СЧ КУСБ-1(М16)
 ПВ КСМБ(М10) НА КОНТР.СУММИРОВАНИЕ МАССИВА ОБМ.
 УИА -8(М1)
 СЧ КУСБ-1(М16)
 СБР БТС
МБ6 МОД КНАПР-1(М16)
 ЗП 8(М1) ЗАПИСЬ ФИЗ.АДРЕСА СЕКТОРА В ЧЕТН.СС
 СЛЦ Е36 +1 К NСЕКТОРА ДЛЯ СЛЕД.СС.ФИЗ.АДР.
 ЗП РЯ2
 СЧ РЯ1 КОНТР.СУММА
 СДА 64-12
 ЗП РЯ1
 СЧМР 0
 МОД КНАПР-1(М16)
 ЗП 9(М1) ЗАПИСЬ КОНТР.СУММЫ СЕКТОРА В НЕЧ.СС
 СЛИА 2(М1)
 СЧ РЯ2 ФИЗ.АДРЕС ДЛЯ СС СЛЕД.СЕКТОРА
 ПИНО МБ6(М1) ПРОДОЛЖИТЬ РОСПИСЬ СС
 УИА -7(М10)
МБ7 ЗП 1
 ЦИКЛ МБ7(М10) ВЫТ С БРЗ
МБ7А НОП ---
 УИИ М10(М15)
 УИИ М1(М16)
 СЧ КУСБ-1(М16)
 ЗП РЯ2
 ИЛИ Е24
 НТЖ Е24
 ЗП КУСR
 ПВ ДАЙПРМ(М15)
 ОБММБ КУСR,М16
 УИИ М15(М10)
 УИИ М16(М1)
 СЧ РЯ2
 ЗП КУСБ-1(М16)
 СЧ РОСПБ
 ПЕ ПРБ1  ВЫХОД ПРИ РОСПИСИ МБ
 УИА -25000(М1)
 ПБ ПРБ1
ПОВТОБ УИА ЗКМБ(М7) НЕТ ОБМ.
 СЧ ПРВОС
 ПЕ (М11) РОСП.ТРАКТА ПРИ ВОССТАНОВЛ
 ПБ (М7)
*
* ОБРАБОТКА ПРЕРЫВАНИЯ ПО ОКОНЧАНИЮ ОБМЕНА
*
ПОДГ4 ЭКВ ПОДГ4Б
ПРБ НОП
ПРБ1 СЧ КУСБ-1(М16)
 И Е18
 ПО ПРБ6 ЗП НА МБ
 УИА ПРБ7(М3) ДЛЯ ВЫХОДА ПРИ КЧ
 СЧ К2003
 УИ ’21’ ГАШ.ПОК
 СЧ КУСБ-1(М16)
 ПВ КСМБ(М10) НА КОНТР.СУММИРОВАНИЕ
 УИА 0(М2) НОМЕР СС
 УИА 0(М6) N СЕКТОРА
 УИА -3(М1) ЧИСЛО АБЗАЦЕВ
 СЧ КУСБ-1(М16)
 И Е19
 ПЕ ПРБ3 ПРИ ОБМ.СТРАНИЦЕЙ
 УИА 0(М1) ОДИН АБЗАЦ
 СЧ КУСБ-1(М16)
 И Е2Е1
 УИ М6 N СЕКТОРА
 СДА 64-1
 УИ М2 N СС
ПРБ3 СЧ КУСБ-1(М16)
 СБР БТС
 ЗП РЯ2
 СЧИ М2
 СДА 64-34
 ИЛИ РЯ2
 МОД КНАПР-1(М16)
 НТЖ (М2)
 СДА 64-3
 ПЕ ПРБ7 НЕСОВП.СС
 УИА -1(М17)
 СЧ РЯ1 КОНТР СУММА
ПРБ4 СД КСД2(М6)
 И Е12П1
 МОД КНАПР-1(М16)
 НТЖ 1(М2)
 ПО ПРБ4А КСМ ПРАВИЛЬНАЯ
 ПИНО ПРБ7(М17) ИДЕТ СРАВНЕНИЕ СО СЧИТАННОЙ КОНТР.СМ
 СЧ РОСПБ
 ПЕ ПРБ7 ЗАФИКСИРОВАТЬ ОШ.ПРИ РОСП.МБ
 СТОП 0
 ПБ ПРБ7 ПОВТОРИТЬ ЗАПИСЬ НА МБ ПО ПУСКУ
ПРБ4А СЧ КУСБ-1(М16)
 И Е24 КОНТРОЛЬНОЕ ЧТЕНИЕ*
 ПО ПРБ4Б НЕТ
 СЧ КСУМБ-1(М16) КОНТР.СУММА,СФОРМИРОВАННАЯ ПРИ ЗП
 ЦИКЛ ПРБ4(М17)
ПРБ4Б СЛИА 2(М2)
 СЛИА 1(М6)
 ЦИКЛ ПРБ3(М1)
 СЧ 0
ПРБ6 ЗП ОШБ
 СЧ К2013
 УИ ’21’ УСТ ПОК
 СЧ КУСБ-1(М16)
 И Е18
 ПЕ ПРБ7А
 СЧ РОСПБ
 ПЕ (М11) ВЫХОД ПРИ РОСПИСИ МБ
 СЧ ПРВОС
 ПЕ (М11) РОСП.ТРАКТА(НЕ ЗАНЯТОГО)ПРИ ВОССТАНОВ
 СЧ КУСБ-1(М16)
 И КСТР
 ЗП СТРЗП-1(М16) СОХР.N СТРАНИЦЫ ЗП
 СЧ ВСЕЕД
 НТЖ КСТР
 И КУСБ-1(М16)
 ИЛИ СТРЧТ-1(М16) УСТ.СТРАНИЦУ КОНТР.ЧТЕНИЯ (31,32)
 ИЛИ Е22Е18 УСТ.ПРИЗН.КОНТР.ЧТ (КОНСТ.С 24Р)
 ЗП КУСБ-1(М16)
 ПБ ЗКМБ
ПРБ7 СЧ Е1
 СЛЦ ОШБ
 ЗП ОШБ
 СЧ К2013
 УИ ’21’ УСТ.ПОК
ПРБ7А СЧ РОСПБ
 ПЕ (М11) ВЫХОД ПРИ РОСПИСИ МБ
 СЧ КУСБ-1(М16)
 И Е24
 ПО ПРБ8
 СЧ ВСЕЕД
 НТЖ КСТР
 НТЖ Е22Е18 ГАШ.ПРИЗНАКОВ КОНТР.ЧТЕНИЯ
 И КУСБ-1(М16)
 ИЛИ СТРЗП-1(М16) УСТ.СТР.ЗП
 ЗП КУСБ-1(М16)
ПРБ8 СЧ ОШБ
 ПО (М11)
 НТЖ Е5
 ПЕ ПОВТОБ
 ЗП ОШБ
 СЧ ПРМК
 ПО ПРБ9
 СЧ 0
 ЗП ПРМК
 МОДА МК
 ЗП КЛЮМК
 ПБ РБ1А          НА ОБНУЛЕНИЕ М.К.
ПРБ9 СЧ ПРИНТ   ПРИЗНАК ПРОВЕРКИ ИНФ. ТРАКТОВ
 ПО ПОВТОБ    НЕТ
 УИА 7(М1)
 ПБ ОБМИНТ  НА ОБНУЛЕНИЕ
*
* БЛОК КОНТРОЛЬНОГО СУММИРОВАНИЯ ПРИ ОБМЕНЕ С МБ
*
* ВХОДНАЯ ИНФ: НА СМ СФОРМИРОВАННЫЙ КУС.ВЫХОД ПО (М10).КС В РЯ1
* ИСПОЛЬЗУЕМЫЕ МОДИФИКАТОРЫ: 1,2,3,6,7,10.
*
КСМБ НОП ---
 УИИ М6(М15)
 ПВ ДАЙПРМ(М15)
 УИИ М15(М6)
 СЧ КУСБ-1(М16)
 И Е18
 ЗП ПРЧТМБ
 УИА (М6)    АДРЕС В ЛИСТЕ
 УИА 0(М2)    N СЕКТОРА
 УИА -3(М1)   ЧИСЛО АБЗАЦЕВ
 СЧ КУСБ-1(М16)
 И Е19
 ПЕ КСМБ1  ОБМЕН СТРАНИЦЕЙ
 УИА 0(М1)    ОДИН АБЗАЦ
 СЧ КУСБ-1(М16)
 И КАБЗ
 СДА 64+2
 ЗП РЯ2
 СЧИ М6
 ИЛИ РЯ2
 УИ М6 НАЧАЛЬНЫЙ АДРЕС АБЗАЦА
 СЧ КУСБ-1(М16)
 И Е2Е1
 УИ М2 N СЕКТОРА
КСМБ1 СЧ 0
 ЗП РЯ1 ОЧИСТИТЬ ДЛЯ КС
КСМБ2 СЧ 0
 УИА -255(М7)
 ЗП РЯ2
 УИА 1024
КСМБ3 НОП ---
 МОДА (М6)
 СЛЦ ’2377’(М7)
 МОДА КСМБ3
 ЦИКЛ (М7)
 УИА ’2003’
КСМБ4 ЗП РЯ3 КОНТР.СУММА АБЗАЦА
 И Е12П1
 СЛЦ РЯ2
 ЗП РЯ2
 СЧ РЯ3
 СДА 64+12
 ПЕ КСМБ4
 СЧ РЯ2 СВЕРТКА КОНТР.СУММЫ В 12 Р.
 ЗП РЯ3
 СДА 64+12
 ПЕ КСМБ4А
 СЧ РЯ2
 СД КСД1(М2)
 ИЛИ РЯ1
 ЗП РЯ1
 СЛИА 256(М6) ИЗМ.НАЧ.АДРЕС АБЗАЦА
 СЛИА 1(М2) +1 К N СЕКТОРА
 ЦИКЛ КСМБ2(М1) СУММИРОВАТЬ СЛЕД.АБЗ.
 СЧ ПРЧТМБ
 ПЕ (М10)
 СЧ РЯ1
 ЗП КСУМБ-1(М16)
 ПБ (М10)
КСМБ4А ЗП РЯ2
 СЧ РЯ3
 И Е12П1
 ПБ КСМБ4
РОСПВВ ПАМ 0
 УИА 1(М2)
РОС3 УИА (М14)
 УИА 1(М16)
 СЧИ М2
 И Е1
 ПЕ НАПР1
 УИА 2(М16)
НАПР1 НОП
РОС2 УИА -31(М5)
РОС СЧИ М14
 СДА 64-2
 ИЛИ Е19
 ИЛИ Е15
 ЗП КУСБ-1(М16)
 СЧИ М2
 СДА 64-39
 ИЛИ КУСБ-1(М16)
 ЗП КУСБ-1(М16)
 ПВ ОПРОСБ(13)
 ЗП ОТВМБ       48Р=1 - НЕ ДОСТУПЕН
 И ЕNЛОГ
 ЗП NЛОГ         ЛОГ. НОМЕР МБ
 СЧИ М2
 ЗП ЗПМ2
 ПВ НОМСЧ1(М1)
 СЧ ЧИСЛМБ       ЧИСЛО МБ НА 2 Т.Р.
 ВЧ Е1
 ВЧ NЛОГ
 ПЕ ВСЕМБ          ТРАКТ НЕ НАШ
 СЧ NЛОГ
 ПО РОС0    0-Й МБ РАСПИСЫВАЕМ ВСЕГДА
 СЧ ОТВМБ
 И Е48
 ПЕ РПВ    ТРАКТ НЕ ДОСТУПЕН
 СЧ СЧВРБ-8(М10)
 ИЛИ Е48(М13)
 ЗП СЧВРБ-8(М10)  ОТКРЫВАЕМ ТРАКТ
 ПБ МБРОС
ВНЕШПР НОП
 СЧМР
 ЗП РМР
 СЧРЖ 63
 ЗП РК
 РЕГ ’244’
 ЗП РТЕГ         УПРЯТ. РЕГ. ТЕГОВ
 СЧИ 16
 ЗП И20
 СЧИ М17
 ЗП И17
 СЧИ М16
 ЗП И16
 СЧИ М15
 ЗП И15
 СЧИ М14
 ЗП И14
 РЕГ ’247’   РВП
 МОДА МРВП
 И
 ЗП РВП
 И Е4
 ПЕ ПРВРЕМ
 СЧ РВП
 И Е8
 ПЕ ПРЕСВС
 СЧ РВП
 И Е3
 ПЕ ПРЕПВВ
 СЧ РВП
 И Е6
 ПЕ ПРЕРВС
 ПБ СТ301         НЕОБРАБ.ВНЕШНИЕ ПРЕРЫВАНИЯ
ВЫХПР НОП
 СЧ И15
 УИ М15
 СЧ И16
 УИ М16
 СЧ И17
 УИ М17
 СЧ И14
 УИ М14
 СЧ И20
 УИ ’20’
 СЧ РТЕГ
 РЕГ ’44’     ВОСС. РЕГ. ТЕГОВ
 СЧ РМР
 НТЖ
 СЧ СМ
 РЖ РК
 ВЫПР (М3)
ПРЕПВВ УИА ВОЗПРЕ(М15)
 ПБ ПРПВВ
ПРЕСВС ПВ ПРСВС(М15)
 ПАМ 4
ВОЗПРЕ ПО ВЫХПР
 УИ М16
 ЗП СМ
 СЧ КУСR
 ПВ ДАЙПРМ(М15)
 СЧ И15
 УИ М15
 УВВ 5(М16)
 СЧМР
 И Е33
 ЗП ОТВЕТ
 УИИ ’33’(М15)
 ПБ ВЫХПР
ПРЕРВС СЧ РВП
 УИА ВОЗПРЕ(М15)
 ПБ АВСВС
ПРВРЕМ УИА ВЫХПР(13)
 СЧ =В’37777750000’
 РЕГ ’57’
 ПБ СКАН
ЖДИПР РЕГ ’140’
 ЗП СМЖДЕМ
 СЧ СЧЖДЕМ
 СЛЦ Е1
 ЗП СЧЖДЕМ
 НТЖ 0
 СЧ СМЖДЕМ
ЖДЕМ НОП ,
 РЕГ ’140’
 УИА 3
 ПБ ЖДЕМ
ПРКЧ СЧРЖ ’177’
 ЗП РК
 СЧ ЯЧКЧ
 ПО ПРКЧА
 СЧ
 РЕГ ’37’
 СЧ СМ
 РЖ РК
 МОД ЯЧКЧ
 ПБ
ПРКЧА СЧИ М15
 ЗП И15
 ПВ ВОСРКЧ(М15)
 СЧ РОСПБ
 ПО СТ300         КЧ
 СЧ И15
 УИ М15
 ПБ 3(М15)
Е19Е20 КОНД М18В’3’
*
К2003 КОНД В’2003’
К2013 КОНД В’2013’
КСТР КОНД М12В’37’
Е39П25 КОНД М24В’77777’
П1735 КОНД В’1735’
Е48П17 КОНД М16В’37777777777’
П13 КОНД В’13’
ПРИП КОНД М33В’1’М29В’1’М5В’1777’
БТС КОНД М39В’7’М2В’377’
СТРЧТ КОНД М12В’07’      ЛИСТЫ БОЭК,АВМД,ВИСП
 КОНД М12В’10’
Е22Е18 КОНД М23В’1’М21В’1’М17В’1’
КАБЗ КОНД М10В’3’
НАПД КОНД М37В’37’М7В’7’
ЗАП КОНД П’,,,,,,’
КОДТР КОНД В’0’
ТРСОХР КОНД М41В’7’      1-ТР.СОХР,48Р.-ТР-0
ТРСИСТ КОНД М18В’3003740377’   1 - ТАКТ ПОЛЬЗ
 КОНД В’0’
 АIF (∧ИАЭ).Э15
КОСВ КОНД М9В’1’В’50’
 АGО .Э16
.Э15 АNОР
КОСВ КОНД В’50’
ДЦИЛ ПАМ 1
К641 КОНД М39В’405’
К64 КОНД М42В’64’
ЧД10 КОНД Е’10’
ЧД20 КОНД Е’20’
НАПРМ КОНД М37В’3’М7В’7’
Е13П1 КОНД В’17777’
.Э16 АNОР
Е9Е11 КОНД М8В’5’
ПРМК КОНД В’0’
ИД КОНД В’0’ ДЛЯ ДОР.ЗАМЕНЫ Е6
П16 КОНД В’16’
ИСКАТ КОНД В’0’
К КОНД М38В’1’М18В’1’М26В’1’
 КОНД А(РАБМД)
 КОНД А(НОМСВ)
СБ КОНД А(НОМЛД)
 КОНД А(НОММЛ)
ПРРО КОНД В’1’
*
*
 АIF (NОТ ∧НИ).СВ6
КОКОС КОНД В’62000740000’
КОКОСМ КОНД В’62100740000’
ГЕНР1 КОНД В’0’
ГЕНР2 УВВ ’4017’
 ПБ ПР31
ГЕНР3 КОНД М19В’1’М9В’1’
ПР31 ВНЕШН КОСА.ПР31
.СВ6 АNОР
КЛСВ   КОНД  В’57600740000’
 АIF (NОТ ∧АС).АС3
КЛСВ1 КОНД В’61200740000’
.АС3 АNОР
Р3 КОНД В’0’
Е32П25 КОНД М24В’377’
 КОНД М16В’377’
 КОНД М8В’377’
 КОНД В’377’
NЛОГ КОНД В’0’
ГРАНБ КОНД В’0’
ОТВМБ КОНД В’0’
РТЕГ КОНД В’0’
ЗПМ2 ПАМ 1
 ЛИТЕР
УХСАLС МОД ТУС
 ЗП 0
ПРВОС КОНД В’0’
 ЛИТЕР
 АIF  (NОТ  ∧Ф).Щ3
КЛ2  УИА  1027
 ПБ АВРАС1
ЛИСТСВ ВНЕШ АВРАС1
.Щ3 АNОР
Р8 ПАМ 1
КУС КОНД М17В’1’М24В’445’М12В’22’
КУСТ КОНД М17В’1’М24В’446’М12В’23’
КУС1 КОНД М17В’1’М24В’447’М12В’23’
КАТР КОНД М17В’1’М24В’450’М12В’23’
КУСП КОНД М17В’1’М24В’651’М12В’23’
ККИЕВ КОНД М17В’1’М24В’435’М12В’22’
ЛИСТА3 КОНД М28В’3’М19В’1’ ЛИСТЫ 22,23,34
ОУПР КОНД М47В’1’М23В’1’В’-1’
КУСПА КОНД М24В’600’М17В’1’
НОМЛИС НОП
 КОНД В’6063’    ПУСКАЧ
 КОНД В’6046’             ПЕЧАТЬ
 КОНД В’6055’    РОБОТ
 КОНД В’6065’    ЭКВВОД
 КОНД В’6062’
 КОНД В’6070’
 КОНД В’6066’      РЕСУРС
 КОНД В’6064’
 КОНД В’6074’
КОЛЛИС ЭКВ *-НОМЛИС-1
КУСЛИС НОП
 КОНД В’4047401400000’    ПУСКАЧ
 КОНД В’4051601400000’    ПЕЧАТЬ
 КОНД В’4052101400000’    РОБОТ
 КОНД В’4053301400000’    ЭКВВОД
 КОНД В’4056001400000’
 КОНД В’4067701400000’
 КОНД В’4055701400000’
 КОНД В’4056101400000’
 КОНД М15В’40534014’     ПРИКАЗ
СМЖДЕМ ПАМ 1
СЧЖДЕМ ПАМ 1
СЕКОСГ ПАМ 1
ПРИСОГ ПАМ 1
ШКАМОГ ПАМ 1
БАЗАОС ПАМ 1
ЧИСЛМБ ПАМ 1
Е2Е1 КОНД В’3’
Е3П1 КОНД В’7’
П17 КОНД В’17’
Е5П1 КОНД В’37’
Е10П1 КОНД В’1777’
Е12П1 КОНД В’7777’
Е15П1 КОНД В’77777’
Е46П1 КОНД М46В’3’В’-1’
КСД1 КОНД М41В’34’
 КОНД М41В’50’
 КОНД М41В’64’
 КОНД М41В’100’
КСД2 КОНД М41В’144’
 КОНД М41В’130’
 КОНД М41В’114’
 КОНД М41В’100’
ШКПРТР КОНД М15В’7140374006’
ОБТРАК КОНД В’-1’М2В’37’
ТРАКТ КОНД В’0’
ПРИНТ КОНД В’0’
КАННА5 КОНД Х’А5А5А5А5А5А5’
К5АННА КОНД Х’5А5А5А5А5А5А’
ЗМ14 КОНД В’0’
Р1 КОНД В’0’
ПРИП1 КОНД В’1042104000001740’
ПРИПВ КОНД М12В’37’
*****
ДИС501 ПАМ 1
РМР ПАМ 1
РК ПАМ 1
И20 ПАМ 1
И17 ПАМ 1
И16 ПАМ 1
И15 ПАМ 1
И14 ПАМ 1
РВП ПАМ 1
КУСR ПАМ 1
ОТВЕТ ПАМ 1
Р16 ПАМ 1
ЛВЗЯЛИ КОНД В’0’
НАПРКЧ КОНД (2)В’0’    ИСПОЛЬЗУЕТСЯ В КЧ
ПВВ ВНЕШ ОПРОСБ,ПРПВВ,ПРСВС,ПВВМБ,ПВВМД,АВСВС
ПВВ ВНЕШ ВОСЛМБ,ВОСЛМД,ВОСРКЧ,СКАН,ОТМТЕС,ЯЧКЧ
ПВВ ВНЕШ (МРВП,МГРП)
 КОНД М41В’100’
ВСЕЕД КОНД В’-1’
* РАБОЧИЕ ЯЧЕЙКИ:
СМ КОНД В’0’
КУСД1 КОНД В’0’
СТРЗП КОНД В’0’
 КОНД В’0’
 КОНД В’0’ СВОБ
РЯ1 КОНД В’0’
РЯ2 КОНД В’0’
ЗАП20Р КОНД В’0’
ЛСТБАЗ КОНД М12В’6’
ЗАПМ7 КОНД В’0’
СЧВРЕМ КОНД В’0’
ПРЧТМБ КОНД В’0’
КУСБ КОНД (2)В’0’
ОШБ КОНД В’0’
РОСПБ КОНД В’0’
КСУМБ КОНД (2)В’0’
КРОСП КОНД В’1’
КНАПР КОНД А(СС1Н)
 КОНД А(СС2Н)
 КОНД В’30’
 КОНД В’40’
 КОНД В’50’
 КОНД В’60’
СЧВЫД ПАМ 2
РЯ3 ПАМ 1
ОТБРТР ПАМ 1
П17777 КОНД В’17777’
СЧВРБ КОНД (64)В’0’
КОЛМБ ЭКВ *-СЧВРБ       МАКСИМАЛЬНОЕ ЧИСЛО МБ
МБМК  ЭКВ ’310’         СОХРАНЕНИЕ НОМЕРОВ МБ НА 2-М Т.Р.
УПРГРА ЭКВ ’311’         СОХРАНЕНИЕ ГРАНЬБ
УПРМБ ПАМ 1             НОМЕРА МБ НА 2-М Т.Р.
ЕNЛОГ ЭКВ Е10П1
ПРПЕ КОНД В’0’
 АIF (∧Э).Э39
СУС КОНД В’40110’
 АGО .Э40
.Э39 АNОР
СУС КОНД В’2000’
П56 КОНД В’56’
Е32П17 КОНД М16В’177777’
 КОНД В’177777’
НАЛОГ ПАМ 2
РЯ5 ПАМ 1    N ГР
.Э40 АNОР
ИНФО12 ПАМ 1             12(8) ЯЧЕЙКА ПАРАМЕТРОВ
ШАС128 ПАМ 2             ТЕРМИНАЛЫ МПД 100-177
 АДРЕС ГЕНС+’1740’
ПАРОС НОП
 ПАМ 32
 ЭКВИВ (П1777,Е10П1),(МК,’46000’)
ДИСП70 ВНЕШ (СММ,СМ),МД5,ТУС
ДИСП70 ВНЕШ СС1Н,СС2Н,(КНАПРД,КНАПР)
ПВВ ВНЕШ НАПРУС
 ЭКВИВ (ТР3,3),(П37,Е5П1),(МДЗАГР,’1027’)
 ЭКВИВ (Д29М,’1032’),(П3,Е2Е1),(П7,Е3П1),(П7777,Е12П1)
ВЗУ ВНЕШ ФИЗОБМ
ДИСП70 ВНЕШ НОММЛ,ТБУФ
*
ЭКСТ ВНЕШ ВЕ1,ВЛИ,ЭКСТ
ПЧКЗ ВНЕШ ПЧКЗ,(ВЛАД,ОРГ1),ВЕ,(ПЧГОД,ГОД)
КИТ ВНЕШ КЛЮМК,БРАКТР
ДИСКИ ВНЕШ РАБМД,НОМЛД,НОМСВ
 ЭКВИВ (Н,1024),(П77777,Е15П1),(КУСГП,’70000’)
ГЕНС1 ВНЕШ ГЕНС1,ПОИСКС,АЛИСТЫ,АОБЩГ
СОСТАВ ВНЕШ LШЗНМБ
 ВХОД ОУПР,ИД,АЦП,ДЛТ,ИСКАТ,КУС,КУСПА,КУСТ,НОМРД
 ВХОД РОСМБ,КУСГП,ПРРО,Р3,СБ,NМЛ,Е32П25
 ВХОД ЛИСТА3,П1735,П1777,Е39П25,Е19Е20,КОДТР
 ВХОД ШКОПТ,NТАС,ШКТТ2,ШКVТ2,А,КОН1,ВЛА,ВЛА1
 ВХОД НОМЛИС,КУСЛИС,КОЛЛИС
 ВХОД NБЭСМ,NМБ,ТРСИСТ,УСТРМД,ШКТТ,ШКУСТР,РАСПП
 ВХОД ОБМИМБ,ОТБРТР,ПРОГОН,РЯ1,РЯ2,РЯ3,СЛОЙК
 ВХОД ОБЩГ,ПОИСК,ПАРОС
 ВХОД КУСИМБ,КУСПМБ,КУСД1,ЧТГП,ЧТМД
 ВХОД ЛСТБАЗ,СУС,СЧВРБ,ВСЕЕД
 ВХОД ШКVТ,ЧТМДВ,ТРАКТЫ,ЗАП,КГОД,КНАПР
 ВХОД КУСR,ДИС501,РМР,ОТВЕТ,ЛВЗЯЛИ,НАПРКЧ
 ВХОД Р16,СМ,ПРКЧ,ВНЕШПР,ЗМ14,ЖДИПР
 ВХОД ПРИСОГ,СЕКОСГ,ШКАМОГ,ЧИСЛМБ
 ВХОД ИНФО12,УПРМБ,МБМК,УПРГРА
 ВХОД ГРАНБ,РК,ШАС128
 ВХОД ККИЕВ,КАТР,КУСП,КУС1,ЗАП20Р
 АIF (∧МВВ ЕQ 0).МВВ5
ПРЭ71 СЧ ПРИМВВ
 И ЕМВВ
 ПЕ ПРЭ71А
 ЗП ПРМВВ
ПРЭ71А СЧ ПРМВВ
 ПО СТАТИП
 И Е15П1
 СЛЦ НЗАДМК
 МОДА ’25400’
 ЗП INР-Э71ТК
 ПБ СТАТИП
НЗАДМК СЧ Е48Д(М2)
 ПБ INРМК-МКАНАЛ
ПРДИМК СЧ ПРМВВ
 ПО КОНПР
 УИ М13
 СДА 64+42
 ЗП НЗАДМК
 СЧИ М13
 СДА 64-2
 ИЛИ КУСМК
 ИЛИ КУСГП
 ПВ ЧТМД1(М11)
 УИА -7(М4)
 СЧИ М13
 МОДА ТАФИТЕ-МКАНАЛ(М13)
 ПВ (М11)
 МОДА ТАФИТЕ-МКАНАЛ(М13)
 УИА (М17)
ЗАПТФТ СЧ ТФТ+7(М4)
 УИА -3(М1)
ЗПТФТ РЗБ МАСТФТ-МКАНАЛ(М13)
 СЧМ ТФТ+7(М4)
 СДА 64-12
 ЗП ТФТ+7(М4)
 ЦИКЛ ЗПТФТ(М1)
 ЦИКЛ ЗАПТФТ(М4)
 СЧИ М13
 СДА 64+10
 УИ М13
 СЧ Е48(М13)
 ИЛИ ТЗФСТР
 ЗП ТЗФСТР
 СЧ Е48(М13)
 ИЛИ РАСПП
 ЗП РАСПП
КОНПР УИА ’12000’(М1)
 ПБ БЕЗСПП
КУСМК КОНД М24В’611’М17В’3’
Э71ТК ВНЕШН Э71ТК.
INР ВНЕШН Э71ТК.INР
МКАНАЛ ВНЕШН МКАНАЛ.
INРМК ВНЕШН МКАНАЛ.INРМК
ТАФИТЕ ВНЕШН МКАНАЛ.ТАФИТЕ
 ВХОДН ПРМВВ
 ВХОДН НЗАДМК
 ЛИТЕР
 АДРЕС ГЕНС+’3767’
ПРМВВ КОНД М42В’31’М12В’5’
ТФТ КОНД В’2222222222222222’
 КОНД В’2222222222222222’
 КОНД В’2222222222222222’
 КОНД М24В’22222222’
 КОНД В’4444444444444444’
 КОНД М45В’5’М30В’5’М21В’6’
 КОНД М45В’3’
 КОНД В’0’
ПРИМВВ ЭКВ ПРМВВ
ЕМВВ ЭКВ Е15
.МВВ5 АNОР
 ФИНИШ
*КОНЕЦ ЧАСТИ

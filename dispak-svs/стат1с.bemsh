СТАТ1С СТАРТ ’11400’
 М
 Е
 Б
 УПОТР СТАТ1С-768(М7)
НАЧСТ СЧ ’5001’
 УИА 12(М4)        30 ДНЕЙ В МЕСЯЦЕ
 СЧ ВРЕМЯ
 ВЧ СУТКИ
 ПЕ ДАТ          СУТКИ НЕ КОНЧИЛИСЬ
 ЗП ВРЕМЯ
 СЧ ГОД
 СДА 64+24
 И П37
 УИ М3 МЕСЯЦ
 НТЖ Е2
 ПЕ ИЗМД
 СЧ ГОД
 СДА 64+20
 И П17
 ЗП РАБ
 СЧ ГОД
 И Е35П17
 СДА 64+12
 ИЛИ РАБ
 И П377
 ПВ ИЗ10В8(М15)
 ЗП РАБ
 УИА 6(М4)         29 ДНЕЙ В МЕСЯЦЕ
 И П3
 ПО ИЗМД
 УИА 0(М4)         28 ДНЕЙ В МЕСЯЦЕ
ИЗМД СЧ ШК
 И Е48-1(М3)
 ПО НЕ31
 СЛИА 6(М4)         31 ДЕНЬ В МЕСЯЦЕ
НЕ31 СЧ ЧИСЛОД
 СДА 64-29(М4)
 НТЖ ГОД
 И Е35П30
 ПО КОНМЕС
 СЧ ГОД
 И Е35П30
 СДА 64+29
 МОДА Е35П30
 УИА (М13)
 ПВ ИЗ10В8(М15)
 СЛЦ Е1           +1 ДЕНЬ
 ПВ ПЕРЕВ(М15)
 СБР КОН
 СДА 64+13
ИЗМ НТЖ ГОД
 И (М13)
 НТЖ ГОД           НОВОЕ ЧИСЛО(МЕСЯЦ)
 ПБ КОНЕЦ
МЕСЯ СЛИА ’22’(М3)
 УИА Е36П25(М13)
 СЧИ М3
 ПВ ИЗ10В8(М15)
 СЛЦ Е1           +1 МЕСЯЦ
 ПВ ПЕРЕВ(М15)
 СБР КОН
 СДА 64+18
 ИЛИ Е30
 ПБ ИЗМ
КОНМЕС СЛИА -’22’(М3)
 МОДА МЕСЯ
 ПИНО (М3)
 СЧ РАБ
 СЛЦ Е1
 ПВ ПЕРЕВ(М15)
 ЗП РАБ
 СДА 64-12
 ИЛИ РАБ
 И Е24П1
 ИЛИ ЯНВ01
 НТЖ ГОД
 И Е35П17
 НТЖ ГОД
КОНЕЦ ЗП ГОД
 УИА ’2003’
 СЧ ФЛАТЕР
 ИЛИ Е48
 ЗП ФЛАТЕР
 СЧ ШКИВ
 ИЛИ ЕПУСК
 ЗП ШКИВ
ДАТ УИА ’2003’
 СЧ Е36
 ИЛИ ШКИВ
 ЗП ШКИВ
 УИА 3
 УИА -3(М16)
ДАТ2 СЧ ТУСП+НПЗ+МЧПЗ(М16)
 УИ М15          А ИПЗ
 СЧ 35(М15)
 СДА 64+8
 ИЛИ ЯЧВР(М15)
 СДА 64+39
 ПО ДАТ1
 СЧ 36(М15)
 И Е41
 ПЕ ДАТ1
 УИА ’2003’
 СЧ 36(М15)
 СЛЦ Е39          СОХРАН.ПЕЧАТИ ДЛЯ СЧЕТН.ЗАДАЧ
 ЗП 36(М15)
 УИА 3
ДАТ1 МОДА ДАТ2
 ЦИКЛ (М16)
 СЧ ГОД           ФОРМ-Е КОНСТ.СДВИГА
 И П7
 ВЧ Е1
 ИЛИ Е48
 УМН П6
 СЧМР 64
 ЗП КСДВ
 ВЧОБ 0
 УИ М10
 СЧ ГОД
 И Е35П17
 ЗП ДАТА
 СЧ 0             ЗАПРОС ЛИСТА ПОД РАБ.ЗОНУ СТ-КИ
 ПВ ТБУФ(М15)
 НТЖ 0
 ПО В-Н(М7)    НЕТ ЛИСТОВ
 СДА 64-8
 УИ М2
 СДА 64-2
 ИЛИ НОММЛ
 ИЛИ Е18
 ЗП РАБ
 СЧ НОММЛ
 ПВ ЕNQ(М15)
 МОДА НОРМ
 УИА (М11)
 СЧ СТЗОН
 И П77
 ПЕ ЧТТЕКЗ
 СЧ П30
 МОДА ЧТЗОНЫ
 ПВ (М15)         ЧТ.30-Й ЗОНЫ(КАТАЛОГ)
 СЧ ГОДСТ(М2)
 ПЕ ЕСТЬ30         30-Я ЗОНА НЕ ЧИСТИЛАСЬ
 МОДА НАЧС30
 ПВ (М15)         30-Я ЗОНА - РАБОЧАЯ
 ЗП ЧИСТ          ЧИСТКА НЕ НУЖНА
 ПБ НОРМ
ЕСТЬ30 СЧ ШКРЗСТ(М2)
 МОД КСДВ
 СДА 64
 И П77
ЧТТЕКЗ ЗП ЗОНЫСТ
 ПО ДАЙНОВ        НЕТ ТЕК.ЗОНЫ
 МОДА ЧТЗОНЫ        ЧТ.ТЕК.ЗОНЫ ЭВМ
 ПВ (М15)
 СЧ ВРЕМЯ
 ВЧОБ ГРСУТ
 ПО ГОТОВА        ВРЕМЯ < ПЕРЕХОДНОГО
 СЧ ГОД
 НТЖ СПЕЦ(М2)
 И Е35П17
 ПО ГОТОВА        ДАТУ УЖЕ СМЕНИЛИ
 ПБ НОВАЯ         НОВАЯ ДАТА - НОВАЯ ЗОНА
***   ВЫБОР НОВОЙ ЗОНЫ С ЗАПИСЬЮ КАТАЛОГА НА МД
ДРУГАЯ СЧ ШКЗЗСТ(М2)
 И П77
 ПО НАЧС30        ШКАЛА=0, 30 -> СТРЕЛКУ
 НТЖ В67
 ПЕ НЕ67
 СЧ ШКЗЗСТ(М2)    ПОСЛЕ 67-Й ИДЕТ 30-Я
 ИЛИ П77
 НТЖ П77
НАЧС30 ИЛИ П30
 ПБ ПРОВ
НЕ67 СЧ ШКЗЗСТ(М2)
 СЛЦ Е1
ПРОВ ЗП ШКЗЗСТ(М2)    N НОВОЙ ЗОНЫ В 1-6 РР
 И П77
 ЗП ЗОНЫСТ
 ВЧ П30
 УИ М16
 СЧ ШКЗЗСТ(М2)
 И Е48(М16)
 ПЕ ДРУГАЯ        ЗОНА ЗАНЯТА
 ЗП ЧИСТ          НУЖНА ЧИСТКА
 СЧ ШКЗЗСТ(М2)
 ИЛИ Е48(М16)
 ЗП ШКЗЗСТ(М2)    ОТМЕТИЛИ В ШКАЛЕ
 СЧ ШКРЗСТ(М2)
 МОД КСДВ
 СДА 64
 НТЖ ЗОНЫСТ
 И П77
 СДА 64(М10)
 НТЖ ШКРЗСТ(М2)
 ЗП ШКРЗСТ(М2)
***   ЗАПИСЬ НА МД СЧИТАННОЙ ЗОНЫ С АСТРОН.ДАТОЙ
НАДИСК СЧ ГОД
 СДА 64+24
 ПО НАЗАП
 СЧ ГОД
 ЗП ГОДСТ(М2)     ДАТА АСТРОНОМИЧЕСКАЯ
НАЗАП СЧ КУССТ-Н(М7)
 ПВ ФИЗЗП(М16)    ЗП ЗОНЫ НА МД
 УИА 3
 СЧ ЗОНЫСТ
 ПБ (М15)
***   ЧТЕНИЕ ЗОНЫ С КОНТРОЛЕМ И ЧИСТКОЙ(ПО ЗАКАЗУ)
ЧТЗОНЫ СДА 64-24
 СЛЦ РАБ
 ЗП КУССТ-Н(М7)
 ПВ ФИЗОБМ(М16)
 УИА 3
 УИА -1022(М16)
 СЧ КЛЮЧ
 НТЖ КЛЮЧСТ(М2)
 ПЕ РОСПИС        КЛЮЧ В ЗОНЕ ИСПОРЧЕН
 СЧ ЧИСТ
 ПЕ (М15)         НЕТ ЗАКАЗА НА ЧИСТКУ
 УИА -1020(М16)
РОСПИС СЧ 0
 МОДА (М2)
 ЗП 1023(М16)
 МОДА РОСПИС
 ЦИКЛ (М16)         НУЛЕНИЕ ЗОНЫ
 СЧ ГОД
 СДА 64-36
 ИЛИ ДАТА
 ИЛИ Е10
 ЗП СПЕЦ(М2)      ДАТА МАШИННАЯ
 СЧ КЛЮЧ
 ЗП КЛЮЧСТ(М2)
 ЗП ЧИСТ          ЧИСТКУ ПРОИЗВЕЛИ
 ПБ (М15)
*** ВАРИАНТЫ ПЕРЕХОДА К НОВОЙ ЗОНЕ
ПОЛНАЯ МОДА НАДИСК
 ПВ (М15)         ЗП ПОЛНОЙ ЗОНЫ
 НТЖ П30
 ПО ОСВОБ         РАБОТАЛИ С 30-Й ЗОНОЙ,ЕNQ ЕСТЬ
 СЧ НОММЛ
 ПВ ЕNQ(М15)
НОВАЯ СЧ П30
 МОДА ЧТЗОНЫ
 ПВ (М15)         ЧТ.30-Й ЗОНЫ(КАТАЛОГ)
 СЧ ЗОНЫСТ
 ВЧ П30
ОСВОБ УИ М16
 СЧ ШКЗЗСТ(М2)
 ИЛИ Е48(М16)
 НТЖ Е48(М16)
 ЗП ШКЗЗСТ(М2)    ОСВОБ.ЗОНУ В ШКАЛЕ
ДАЙНОВ МОДА ДРУГАЯ
 ПВ (М15)         ЗАПРОС НОВОЙ ЗОНЫ
 МОДА ЧТЗОНЫ
 ПВ (М15)         ЧТ.НОВОЙ ЗОНЫ С ЧИСТКОЙ
ГОТОВА СЧ ЗОНЫСТ
 НТЖ П30
 ПО (М11)         РАБ.С 30-Й ЗОНОЙ,МД НЕ ОСВОБ.
 СЧ НОММЛ
 УИИ М15(М11)
 ПБ DЕQ
НОРМ СЧ ГОД
 ЗП ДАТГЕН(М2)    ДАТА ДЛЯ ПЕРЕВЫЗОВА
 СЧ ПРЕДЕЛ
 И Е41
 ПЕ МБСТ0
 УИА 1027
 СЧ Е41
 ИЛИ ПРЕДЕЛ
 ЗП ПРЕДЕЛ
 УИА 3
 СЧ Е1
 СЛЦ СЧЕТЧ(М2)
 ЗП СЧЕТЧ(М2)
МБСТ0 УИА -7(М4)
МБСТ УИА ’2003’   ПЕРЕНОС МБ СТ-КИ
 СЧ ОБРМБ+7(М4)
 МОДА НАРМБ(М2)
 СЛЦ 7(М4)
 МОДА НАРМБ(М2)
 ЗП 7(М4)
 СЧ
 ЗП ОБРМБ+7(М4)
 УИА 3
 МОДА МБСТ
 ЦИКЛ (М4)
 УИА -15(М1)
МБСБОИ УИА 1027
 СЧ СБОИМБ+15(М1)
 МОДА ОШМБ(М2)
 СЛЦ 15(М1)
 МОДА ОШМБ(М2)
 ЗП 15(М1)
 СЧ
 ЗП СБОИМБ+15(М1)
 УИА 3
 МОДА МБСБОИ
 ЦИКЛ (М1)
 УИА 1027
 УИА -9(М1)
ВУСБОЙ УИА 1027
 СЧ П77
 ЗП РАЗРЯД   РАБОЧАЯ
 УИА -7(М4)
СБОЙ1 СЧ СТУСТР+9(М1)
 И РАЗРЯД
 ЗП СТУСТ6   РАБОЧАЯ
 МОДА ОШВУ(М2)
 СЧ 9(М1)
 И РАЗРЯД
 СЛЦ СТУСТ6
 ИЛИ РАЗРЯД
 НТЖ РАЗРЯД
 ПЕ ДАЛЬШЕ
 МОДА ОШВУ(М2)
 СЧ 9(М1)
 СЛЦ СТУСТ6
 МОДА ОШВУ(М2)
 ЗП 9(М1)
ДАЛЬШЕ СЧ РАЗРЯД
 СДА 64-6
 ЗП РАЗРЯД
 МОДА СБОЙ1
 ЦИКЛ (М4)
 СЧ
 ЗП СТУСТР+9(М1)
 УИА 3
 МОДА ВУСБОЙ
 ЦИКЛ (М1)
 СЧ ВРМАТ
 СДА 64+23
 И Е24П1
 СЛЦ ОСДВ(М2)
 ЗП ОСДВ(М2)     Т НА ОС
 СЧ ЧИП
 СЛЦ ПОДКАЧ(М2)
 ЗП ПОДКАЧ(М2)
 СЧ 0
 ЗП ВРМАТ
 ЗП ЧИП
 СЧ ОАС
 ИЛИ Е16
 ЗП ОАС
 УИА 3
* Б Л О К  ЗАПОЛНЕНИЯ БУФЕРА НАКОПЛЕНИЯ ИНФ. ПО МД И МЛ
 СЧ СПЕЦ(М2)
 И П1777
 УИ М1
 СЛИ М1(М2)       АДР. СВОБОДНОГО СЛОВА
 УИА -31(М4)
ПАБ СЧ ОАС          ПЕРЕНОС ИНФ. ПО СБОЯМ МЛ И МД
 НТЖ Е16
 ПО ПЕНА1
 УИА ’2003’
 СЧ ОАС
 ВЧ Е1
 ЗП ОАС
 МОД Р
 СЧ СТМГ+’140’
 ЗП РАБ1
 СЧ
 МОД Р
 ЗП СТМГ+’140’
 УИА 3
 СЧ Р
 СЛЦ Е1
 ЗП Р
 МОДА ПАБ
 УИА (М5)
 ПБ ЗПМД
ПЕНА1 СЧ СТМГ+31(М4)
 ПО ПЕНА2
 ЗП РАБ1
 СЧ
 ЗП СТМГ+31(М4)
 МОДА ЗПМД
 ПВ (М5)
ПЕНА2 МОДА ПЕНА1
 ЦИКЛ (М4)
*** ПЕРЕНОС СТ-КИ ПО НАРАБОТКЕ МД ***
 УИА -63(М4)
ЦК1 УИА ’2003’
 СЧ СТМД+63(М4)
 ПО КЦК1         НАРАБОТКИ НЕТ
 ЗП ПРОМ1
 И МАСУСТ
 ЗП ПРОМ
 СЧ
 ЗП СТМД+63(М4)  0 - НИЕ В ДИСПЕ
 УИА 3
 МОДА 63(М4)
 СЧ НАРМД(М2)
 ПЕ МЕТ1
 СЧ ПРОМ1        ВКЛЮЧИЛИ НОВОЕ УСТР-ВО
 ПБ МЕТ2
МЕТ1 ЗП РАБ1
 И МАСУСТ
 НТЖ ПРОМ
 ПЕ ЗАМЕН        НА УСТР-ВЕ ЗАМЕНИЛИ БОБИНУ
 СЧ ПРОМ1
 И МАСКНР
 СЛЦ РАБ1
 ЗП ПРОМ
 И Е46
 ПЕ ЗАМЕН        + - НИЕ НАРАБОТКИ НЕВОЗМОЖНО
 СЧ ПРОМ
МЕТ2 МОДА 63(М4)
 ЗП НАРМД(М2)
КЦК1 УИА 3
 МОДА ЦК1
 ЦИКЛ (М4)
 УИА 1027
 СЧ
 ЗП ОАС
 СЧ СТЗОН
 И Е48П17
 ИЛИ ЗОНЫСТ
 ЗП СТЗОН
 УИА 3
*  ВЫХОД В СТАТ2 ЧЕРЕЗ НОМБОБ
 ПБ КНЦСТ-Н(М7)
*** ЗАМЕНА СЛОВА НАРАБОТКИ ***
ЗАМЕН СЧ ПРОМ1        ИЗ СТМД->НАРМД
 МОДА 63(М4)
 ЗП НАРМД(М2)
 МОДА КЦК1
 УИА (М5)
* Б Л О К  ЗАНЕСЕНИЯ СЛОВА СТ-КИ НА БУФЕР
ЗПМД УИА 3    И ЗАПИСИ ЗОНЫ.  ВЫХОД ПО М5
 СЧ РАБ1
 ЗП (М1)
 СЧ СПЕЦ(М2)
 СЛЦ Е1
 ЗП СПЕЦ(М2)
 СЛИА 1(М1)
 И П1777
 ПЕ (М5)         ЗОНА НЕ ЗАПОЛНЕНА
 СЧ РАБ1
 СДА 64+45
 НТЖ П5
 ПЕ НЕСБМД       ПОСЛ. СЛОВО НЕ СБОЙ МД
 ЗП -1(М1)       ПРИ СБОЕ НУЛИМ ПОСЛ. СЛОВО
 СЧ Р
 ВЧ Е1
 ЗП Р
 СЧ РАБ1
 МОД Р
 ЗП СТМГ+’140’
НЕСБМД СЧ СПЕЦ(М2)
 И Е35П12
 СЛЦ Е12
 ЗП ДАТА
 МОДА ПОЛНАЯ
 ПВ (М11)
 СЧ СПЕЦ(М2)
 И П1777
 УИ М1
 СЛИ М1(М2)
 ПБ (М5)
*
Е35П12 КОНД М11В’77777777’
МАСУСТ КОНД М6В’7777’
МАСКНР КОНД М25В’3777777’
ПРОМ КОНД В’0’
ПРОМ1 КОНД В’0’
В67 КОНД В’67’
ЧИСТ КОНД В’1’
КСДВ КОНД В’0’
СУТКИ КОНД М6В’203654’
Е35П17 КОНД М16В’1777777’
ГРСУТ КОНД В’5200000’
КЛЮЧ КОНД П’КЛЮЧСТ’
РАЗРЯД ПАМ 1
СТУСТ6 ПАМ 1
РАБ КОНД В’0’
РАБ1 КОНД В’0’
ДАТА КОНД В’0’
Р КОНД В’0’
ЗОНЫСТ КОНД В’0’
ШК КОНД М39В’526’М30В’5’
ЧИСЛОД КОНД В’61605150’
КОН КОНД В’1417’
ЯНВ01 КОНД М29В’1’М24В’1’
Е35П30 КОНД М29В’77’
*
 ЭКВИВ (УН,СТАТ1С-768)
ВЗУ ВНЕШ ФИЗЗП,ФИЗОБМ
ДИСКИ ВНЕШ ЕNQ,DЕQ
ДИСП70 ВНЕШ ШКИВ,ТБУФ,СТЗОН,СБОИМБ
ДИСП70 ВНЕШ ОБРМБ,СТМГ,ВРЕМЯ,НОММЛ,СТУСТР,ВРМАТ,ПРЕДЕЛ
ДИСП70 ВНЕШ (ЗАНОСС,ЗАНОСМ),П7,П77,П777,П1777,П7777,Е48П43
ДИСП70 ВНЕШ ВСЕЕД,ОАС,Т,ГОД,МГРП,ИЗ10В8,ПЕРЕВ,Е24П1,Е36П25
ДИСП70 ВНЕШ П17,П37,П377,ТУС,Д,П3,НОММБ1,П5,СТМД
ДИСП70 ВНЕШ П6,П30,ЯЧВР,ЕЗАР,ТУСП,НПЗ,МЧПЗ
КАЧКА ВНЕШ ШКРЗ,Е48П17,ЧИП
МОТТ ВНЕШ ФЛАТЕР
СОСТАВ ВНЕШ ЕПУСК
НОМБОБ ВНЕШ КНЦСТ,(Н,НОМБОБ),КУССТ,В
СОСТАВ ВНЕШ СПЕЦ,НАРМБ,ШКРЗСТ,ШКЗЗСТ,ОСДВ,КЛЮЧСТ,ГОДСТ
СОСТАВ ВНЕШ ОШМБ,ОШВУ,НАРМД,СЧЕТЧ,ОБМЛМД,ПОДКАЧ
СОСТАВ ВНЕШ ДАТГЕН
 ВХОД НАЧСТ,УН
         ФИНИШ ,    В ВАШЕМ ФИНИШЕ ОШИБКА ИЛИ ЕГОНЕТ
*КОНЕЦ ЧАСТИ

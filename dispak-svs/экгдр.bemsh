ЭКГДР СТАРТ ’10000’
 М
 Е
 Б
  УПОТР ЭКГДР(М1)
 УИА 3
 СЧ ВРЕМЯ
 ЗП БУДГДР
 СЧ П-Р(М7)
 ПО ПП
 СЧ 36(М10)
 И П77777
 ЗП ДМ7
 ВЧОБ ЧЭК
 УИА 36(М3)
 ПЕ КП25-Р(М7)
 СЧ ДМ7
 ВЧ Е6
 ПЕ ТГДР
 УИ М3
 ВЧ П60
 ПО ТГД
 СЧ ШКАЛА
 И Е48(М3)
 ПО ТГД
 МОДА ЕСПОД
 ПВ (М17)
ТГД СЧ ДМ7
 ВЧОБ П50
 ПО ТГДР
ПП СЧ ОПТИМ+1
 И П77777
 ПЕ НЭКС
 ПВ ТБУФ(М15)
 СДА 64-8
 ЗП ДМ7
 ИЛИ ОПТИМ+1
 ЗП ОПТИМ+1
 СЧ ДМ7
 СДА 64-2
 ИЛИ КС
 СЛЦ НОММБ1
 ПВ ФИЗОБМ(М16)
НЭКС МОД ОПТИМ+1
 УИА (М5)
ТГДР СЧИ М7
 ЗП ДМ7
 СЧ ККШ
 ЗП КШ
 ЗП ЗШ
 СЛЦ Е25
 ЗП БК
 СЧ П-Р(М7)
 ПО КАН-К(М5)
 СЧ 36(М10)
 И П77777
 ВЧ П60
 ПО КАН-К(М5)
 МОДА НЕТЛ
 УИА (М15)
 СЧИ М15
 ПВ ЗАПВЫХ(М15)
 СЧ (М10)
  МОД 36(М10)
 ПБ ЭКСТ-1
ЭКСТ СЧ 0
 ПБ ВЫХОД
 СЧ (М10) СМ ПОЛЬЗ
  ПБ ВЫХОД
 СЧ (М10)
 ПБ ВЫХОД
 СЧ (М10)
  ПБ ВЫХОД
 СЧ 9(М10)
 ПБ ЗАБКАН
 СЧ 9(М10)
 ПБ ОБМАН
 СЧ 9(М10) М15
 ПБ ОАБКАН
 СЧ (М10)
 ПБ ПЕРВХ
 СЧ (М10)
 ПБ ЗАКЛНТ
 СЧ (М10)
 ПБ ВЫХОД
 СЧ ВЕРСИЯ
 ПБ ВЫХОД
 СЧ (М10)
 ПБ ВЫХОД
 СЧ (М10)
 ПБ ВЫХОД
 СЧ 35(М10)
 ПБ ЭКВРЕ
 СЧ (М10)
 ПБ ВЫХОД
 СЧ (М10)
 ПБ ВЫХОД
 СЧ (М10)
 ПБ ВЫХОД
  СЧ  33(М10)
 ПБ ЭКВР
 СЧ (М10)
 ПБ ВЫХОД
 СЧ (М10)
 ПБ ВЫХОД
 СЧ (М10)
 ПБ ВЫХОД
 СЧ (М10)
 ПБ ВЫХОД
 СЧ (М10)
 ПБ ВЫХОД
       СЧ    (М10)
       ПБ    Э6230
 СЧ 0
 ПБ ДАЙБ ЗАКАЗ БУФ. СТРАНИЦЫ
 СЧ (М10)
 ПБ ОСВЛ ОСВОБОЖ.СТРАНИЦЫ
 СЧ (М10)
 ПБ ВЫХОД
 СЧ (М10)
 ПБ ВЫХОД
 СЧ (М10)
 ПБ ВЫХОД
 СЧ (М10)
 ПБ ВЫХОД
 СЧ 0
 ПБ ПЕРВХ
 УИА -1(М15)
 ПБ ЭКСЗ
 СЧ 0
       ПБ НАЧ  41 ПОТОК  ВВОДА-ВЫВОДА
       СЧИ   М6 ВЫДАЧА НА РЕАЛ. УСТР.
       ПБ    Э6442
 СЧ П77777
 ПБ Э43
 СЧ Е48
 ПБ Э44    ПРОПУСК ВЫВОДА  44
 УИА 3
 ПБ Э45   ОТКАЗ ОТ СП  45
 СЧ 9(М10)
 ПБ Э462
 СЧ 9(М10)
 ПБ К47  М34 М35
 СЧ 9(М10)
 ПБ Э50    ИР1-17
 СЧ 9(М10)
 ПБ Э51    М20 М27 М33
 СЧ 9(М10)
 ПБ Э52    ОЗУ
 УИИ М12(М10)
 ПБ Э6253    Э62 ’53’
 СЧ 36(М12)
 ПБ Э6254    54
 ПО ДЛГЛАВ
 ПБ Э55    55
       СЧ    ШЗНКТ
       ПБ    ВЫХОД
       СЧ    (М10)
       ПБ    Э57
ЕСПОД УИА 3
 СЧ (М10)
 СДА 64+6
 ПЕ МСП
 СЧ (М10)
 УИ М11
 ПО НЕТЗАД
 ВЧ П60
 ПО НЕТЗАД
 ПО НЕТЗАД
 СЧ Е48-1(М6)
 НТЖ ЕЗАР
 ПЕ НЕАРХ
 СЧ Ш1
 И Е48-1(М11)
 ПЕ АРХ
НЕТЗАД СЧ 0
 ПБ ВЫХОД
НЕАРХ НОП ---
 СЧ ПОДЧ(М10)
 И Е48-1(М11)
 ПЕ АРХ
 СЧ ШКГР
 И Е48-1(М6)
 ПО НЕТЗАД
 УИА -15(М12)
СЛГРУ СЧ ГЛГРУП+15(М12)
 И Е48-1(М6)
 ПО НЕТТ
 СЧ ЗАГРУП+15(М12)
 И Е48-1(М11)
 ПЕ АРХ
НЕТТ МОДА СЛГРУ
 ЦИКЛ (М12)
 ПБ НЕТЗАД
АРХ НОП ---
 МОД ТУСП-1(М11)
 УИА (М12)
 ПБ (М17)
МСП СЧ ПОДЧ(М10)
НЕЧЕТ ПО НЕТЗАД
 ЗП РАБ
 НЕД 0
 УИ М11
 МОД ТУСП-1(М11)
 УИА (М12)
 СЧ ШИФРМ(М12)
 НТЖ (М10)
 ПО (М17)
 СЧ РАБ
 НТЖ Е48-1(М11)
 ПБ НЕЧЕТ
Э6230  МОДА  БУФЕР
       ПВ    (М3)
 СЛИ М2(М6)
       СЧ    (М10)
       И     П37
       УИ    М16
       СЧ    Е48(М16)
 И 224-32(М2)
       ПЕ    ПЛОХО
 СЧ 224-32(М2)
       ИЛИ   Е48(М16)
 ЗП 224-32(М2)
Э301 СЧ 0
       ЗП    РАБ
ПИШИ1 МОД ДМ7
 УИА (М7)
 СЧ РАБ
 ПБ ВЫХОД
Э6442  ЗП    25(М7)
       Э64
 СЧ ЯЧИНКО(М10)
 И ЕИНКО
 ПО К300-Р(М7)
 УИА 1027
 СЧ ЯЧАЦПУ(М10)
 И Е48П17
 ЗП ЯЧАЦПУ(М10)
 СЧ ДАЦПУ(М10)
 СДА 64+32
 ИЛИ ЯЧАЦПУ(М10)
 ЗП ЯЧАЦПУ(М10)
 ПБ К300-Р(М7)
МОГБ СЧ (М10)
 УИ М15
 СЧ  РАБ
 УИА 1024
 ЗП 4(М15)
 УИА 3
 ПБ ВЫХОД
ЗАКЛНТ СДА 64-18
 СДА 64+42
 ПЕ ДСК
 ПБ ВЫХОД
ДСК СЧ Е47
 ПБ ВЫХОД
ДАЙБ МОДА БУФЕР
 ПВ (М3)
 УИА 3
       УИА   32(М16)
ВЫБОР  МОДА  (М2)
       СЧ    224-32(М6)
       И     Е17-32(М16)
       СЛИА  -1(М16)
       ПО    ЗАК
       МОДА  ВЫБОР
       ПИНО  (М16)
 ПБ Э301
ЗАК    МОДА  (М2)
       СЧ    224-32(М6)
  ИЛИ  Е17-31(М16)
       МОДА  (М2)
       ЗП    224-32(М6)
       СЧИ   М16
ЗАКИ ЗП РАБ
 ПБ ПИШИ
ЭКВРЕ СДА 64+32
 ЗП РАБ
 СЧ 26(М10)
 СДА 64+39
 СДА 64-16
 ИЛИ РАБ
 ЗП РАБ
 СЧ 33(М10)
 НТЖ Е48П16
 СДА 64+23
 ВЧОБ  РАБ
  ЗП РАБ
 СДА 64-24
 ИЛИ РАБ
 ЗП РАБ
 ПБ МОГБ
ЭКВР НТЖ Е48П16
 СДА 64+23
 ИЛИ ПОРЧ
 УИА 1027
 РЖА 0
  УМН =Е’0.02’
 РЖА 3
 УИА 3
 ЗП РАБ
  ПБ ВЫХОД
ЗАДНЕТ СЧ Е1
 ПБ ВЫХОД
БУФЕР СЧ Е48-1(М6)
 СДА 64+17
 ПЕ ПЛОХО
 СЧ ОПТИМ
 СДА 64+15
 УИ М2
 ПЕ СТАР
 СЧ Е1
 ПВ ТБУФ(М15)
 СДА 64-8
 УИ М2
 УИА -255(М15)
 СЧ 0
РИСП МОДА (М2)
 ЗП 255(М15)
 МОДА РИСП
 ЦИКЛ (М15)
СТАР СЧИ М2
 СДА 64-15
 ИЛИ Е48-32(М6)
 ИЛИ ОПТИМ
 ЗП ОПТИМ
 УИА 3
 ПБ (М3)
РМ КОНД В’0’
КС КОНД М17В’3’М2В’34’ КУС ЧТЕНИЯ КАНАЛА
РАБ КОНД В’0’
ОАБКАН УИ М11
 ВЧОБ Е4
 ПЕ ВЫХОД
 МОДА БУФЕР
 ПВ (М3)
 УИА 3
 СЧИ М6
 ИЛИ Е48
 УМН П11
 СЧМР 64
  УИ  М3
ДАЙКА МОДА (М11)
  МОДА  (М3)
  СЧ -288(М2)
 ЗП РАБ
 И Е48
 ПО ПИШИ
 СЧ РАБ
 УИ М11
 ПБ ДАЙКА
ОСВЛ ПО ВЫХОД
       МОДА  БУФЕР
       ПВ    (М3)
       УИА   3
       СЧ    (М10)
       И     П37
       УИ    М16
  ЗП  РАБ
       МОДА  (М2)
       СЧ    224-32(М6)
       И     Е48(М16)
 ПО ПИШИ1
       МОДА  (М2)
       НТЖ   224-32(М6)
       МОДА  (М2)
       ЗП    224-32(М6)
 ПБ ПИШИ
ПЕРВХ МОДА БУФЕР
 ПВ (М3)
 УИА 1027
 СЧ ЯЧЭК(М10)
 И ЕВАМОS
 ПЕ Э6210
 СЧ Е48
 ИЛИ Е17
 МОДА (М2)
 ЗП 224-32(М6)
Э6210 СЧ ЯЧЭК(М10)
 ИЛИ ЕВАМОS
 ЗП ЯЧЭК(М10)
 УИА 3
 МОДА (М2)
 СЧ 240-32(М6)
 ИЛИ Е48
 НТЖ Е48
 МОДА (М2)
 ЗП 240-32(М6)
 СЧИ М6
 ИЛИ Е48
 УМН П11
 СЧМР 64
 УИ М3
 УИА -8(М16)
 СЧ 0
ОЧИ МОДА (М2)
 МОДА 8(М16)
 ЗП -288(М3)
 МОДА ОЧИ
 ЦИКЛ (М16)
 ПБ ЗАКИ
ЗАБКАН СДА 64+4
 ПЕ ВЫХОД
 СЧ 9(М10)
 И Е4
 ПО МОЖНО
 СЧ 9(М10)
 И П7
 ПЕ ВЫХОД
МОЖНО МОДА БУФЕР
 ПВ (М3)
 СЧИ М6
 ИЛИ Е48
 УМН П11
 СЧМР 64
  УИ  М3
 СЧ 9(М10)
 УИ М4
 МОДА (М4)
 МОДА (М2)
 СЧ -288(М3)
 ЗП РАБ
 ПО ПУСТО
 И Е48
 ПО ПРД
 СЧ РАБ
 УИ М4
 МОДА (М4)
 МОДА (М2)
 СЧ -288(М3)
 ЗП РАБ
ПРД СЧ РАБ
 СДА 64-38
Д1 СДА 64+38
 ЗП РАБ
 СЧ (М10)
 СДА 64+10
 СДА 64-10
 ИЛИ РАБ
ЗПС МОДА (М4)
 МОДА (М2)
 ЗП -288(М3)
 СЧ (М10)
 ЗП РАБ
 ПБ ПИШИ
ПУСТО УИА -8(М5)
МУН СЧ (М10)
 МОДА (М2)
 МОДА 8(М5)
 НТЖ -288(М3)
 И КОНСТ
 ПО НШЛИ
 МОДА МУН
 ЦИКЛ (М5)
 МОДА (М2)
 СЧ 240-32(М6)
 ЗП РАБ
 МОД 9(М10)
 СЧ РАЗ37
 ИЛИ РАБ
 МОД 9(М10)
 НТЖ РАЗ37
 ЗП РАБ
 СЧ (М10)
 СБР РАЗ37
 МОД 9(М10)
 РЗБ РАЗ37
 ИЛИ РАБ
 МОДА (М2)
 ЗП 240-32(М6)
 СЧ 0
 ПБ Д1
НШЛИ СЛИА 8(М5)
 СЧИ М5
 ИЛИ Е48
 ПБ ЗПС
КОНСТ КОНД М15В’77777’
ЭКСЗ МОДА ЧЛ1
 УИА (М15)
 СЧИ М15
 ПВ ЗАПВЫХ(М15)
 СЧ 9(М10)
 УИ М14
 УИА 0
 СЧ (М14)
 СЧ 8(М14)
 УИА 3
 СЧ 0
 ПВ ТБУФ(М15)
 СДА 64-8
 УИ М2
 УИА -1(М3)
 УИА -256(М5)
 МОДА ВШИ
 УИА (М13)
ЧТК СДА 64-2 ЧТЕНИЕ КАТАЛОГА
 ИЛИ НОММЛ1
 СЛЦ КШ
 ЗП КШ
 ПВ ФИЗОБМ(М16)
 УИА 3
 ПБ (М13)
ВШИ СЧ (М10)
 ПО КОНЭ
 СДА 64+8
 ПЕ ПШ
 УИА -1(М3)
 СЧ (М10)
 УИА (М5)
 ВЧ Е8
 УИ М4
 СЛИА 1(М4)
 ПЕ ПЕРВ
 УИА (М3)
 СЛИА -128(М4)
 УИА 256(М5)
 ПБ ПЕРВ
ПШ УИА -126(М4) ПОИСК ШИФРА
 СЛИА 256(М5)
ПШ1 МОДА (М2)
 МОДА (М5)
 СЧ 127(М4)
 НТЖ (М10)
 ПО ЕСТЬ 3АДАЧУ НАШЛИ
 МОДА ПШ1
 ЦИКЛ (М4)
 МОДА ПШ
 ЦИКЛ (М3)
 СЧ 0
ШН ЗП (М10)
 ПБ КОНЭ
ПЕРВ МОДА (М2)
 МОДА (М5)
 СЧ 127(М4)
 ПО ШН
ЕСТЬ МОДА 1(М3)
 СЧ КАТ
 ЗП КШ
 СЧИ М2
 МОДА ЧТК
 ПВ (М13)
 СЛИА 127(М4)
 СЧИ М4
 СДА 64-3
 УИ М13
 ЗП РЯ
 МОДА КАТ1
 ПИО (М5)
 СЛИА 128(М4)
КАТ1 СЧИ М4
 ЗП (М10)
 СЛИ М13(М2)
 СЧ 6(М13)
 И Е31
 ПЕ ВРЕШ ЗАДАЧА В РЕШ
 УИА (М5)
 УИА (М16)
ОЧЕР СЛИ М16(М2) ПОДСЧЕТ ОЧЕРЕДИ
 СЧ 6(М16)
 И Е32
 ПЕ ОЧЕР1 СО ЖДЕМ
 СЧ 6(М16)
 И Е31
 ПЕ ОЧЕР1
 СЛИА 1(М5)
ОЧЕР1 СЧ 7(М16)
 И П1777
 ПО НЕИЗ ЗАДАЧИ НЕТ
 УИ М16
 НТЖ РЯ
 ПЕ ОЧЕР
 СЧИ М5
 УИА ’2000’
 ЗП (М14) НОМ ОЧЕРЕДИ
 УИА 3
 СЧ Е47
 ИЛИ (М10)
 ЗП (М10)
 УИА (М11)
 СЧ 6(М13)
 И Е32
 ПЕ ЖДУЩ
 УИА -7(М16)
РОСП УИА ’2000’
 МОДА 8(М16)
 ЗП (М14)
 УИА 3
 МОДА РОСП
 ЦИКЛ (М16)
 УИА -3(М3)
 УИА 48(М5)
ПОМЛ УИА -Б(М4)
 УИИ М12(М14)
 СЛИА -16(М5)
 МОДА 3(М3)
 СЧ 1(М13)
 СДА 64(М5)
 ЗП РЯ1
 И Е13
 ПО ПОМЛ2       КУСОК
ПОМЛ1 СЧ РЯ1
 НТЖ ТЗНМЛ+Б(М4)
 И П7777
 ПО БУСТ
 МОДА ПОМЛ1
 ЦИКЛ (М4)
 УИА 2048(М11)
 СЛИА 4(М12)
 НТЖ ТЗНМЛ+Б(М4)
ТЕКП ЗП РЯ
 СЧ РЯ1
 И Е14
 ПЕ БОБАРХ
 СЧ РЯ
 И П7777
 ПВ ПЕРЕВ(М15)
ТЕКП1 НОП
 СБР =П’00    ’
 СДА 64(М5)
 УИА ’2000’
 МОДА 3(М3)
 ИЛИ 1(М12)
 МОДА 3(М3)
 ЗП 1(М12) НОМ ЗАНЯТЫХ БОБ В (А+1)-(А+4)
 УИА 3
 СЧИ М11 НОМ НЕ УСТ БОБ В А+5-А+8
 ИЛИ (М10)
 ЗП (М10)
ПОМЛ2 МОДА ПОМЛ
 ПИНО (М5)
 УИА 48(М5)
 МОДА ПОМЛ
 ЦИКЛ (М3)
 СЧ МГРП
 И ЕСТОП
 ИЛИ (М10) ПР’СТОП’-Е1
 ЗП (М10)
 СЧ (М13)
 СДА 64+34
 И П37 ЧИСЛО ЛИСТОВ
 СЛЦ Е1
 ЗП РЯ
 СЧ (М13)
 СДА 64+25
 И П777 ТРАКТЫ
 ЗП РЯ1
 УИА -2(М16)
 УИА ’2003’
 СЧ ОЗУ
 ВЧ РЯ
 ПЕ НЕТР
 СЧ ТОБ
 ВЧ РЯ
 ВЧ РЯ1
 ПЕ НЕТР НЕТ РЕСУРСОВ
 СЧ 7(М13)
 СДА 64+10
 И П37 Ч КУСКОВ
 ЗП РЯ
 СЧ КУСКИ
 ЧЕД 0
 ВЧ РЯ
 ПЕ НЕТР НЕТ КУСКОВ
 МОДА КОНЭ
 ПИНО (М11) НЕТ Б ИЛИ ЗАНЯТЫ
 СЧ Е14
 ПБ КОНЭ1
БУСТ СЧ ТЗНМЛ+Б(М4)
 И Е48
 ПО ПОМЛ2
 СЧ ТЗНМЛ+Б(М4)
 УИА 1024(М11)
 ПБ ТЕКП
БОБАРХ СЧ Б9999
 ПБ ТЕКП1
ВРЕШ СЧ Е48
 ИЛИ (М10)
 ЗП (М10)
 СЧ 7(М13)
 СДА 64+45
 НТЖ ГОД НОМ БЭСМ
 И П7
 ПЕ КОНЭ2 НА ДР8БЭСМ
 УИА -МЧПЗ(М5)
ПОИС0 СЧ ШКРЗ
 И Е48+МЧПЗ(М5)
 ПО ПОИС1
 СЧ ТУСП+МЧПЗ(М5)
 УИ М16
 СЧИ М4
 СДА 64-39
 НТЖ ЯЧНОМ(М16)
 СДА 64-1
 И Е48П41
 ПО ТА
ПОИС1 МОДА ПОИС0
 ЦИКЛ (М5)
НЕИЗ СЧ Е47
 ИЛИ Е14
 ЗП (М10)
 ПБ КОНЭ
ТА СЧ ШИФРМ(М16)
 ПО НЕИЗ
 СЧ 26(М16)
 СДА 64+39
 СДА 64-16 9СТ РАЗР ВРЕМЕНИ
 ЗП РЯ
 СЧ 35(М16)
 СДА 64+32 МЛ 16 РАЗР
 ИЛИ РЯ
 ЗП РЯ ЗАКАЗ ВРЕМЯ
 СЧ 39(М16)
 СДА 64-25
 СЛЦ 39(М16)
 СЛЦ 33(М16)
 СДА 64+23
 СЛЦ РЯ
 И Е1П25
 УИА ’2000’
 ЗП (М14) ВРЕМЯ РЕШ
 УИА 3
 ВЧОБ РЯ
 УИА ’2000’
 ЗП 1(М14)
 УИА 3 Т ДО КОНЦА СЧЕТА
 СЛИА ЧПЗ+НПЗ(М5)
 СЧИ М5
 СДА 64-15
 ПБ КОНЭ1
ЖДУЩ СЧ ККШ
 ЗП КШ
 СЛИА 1(М3)
 СЧИ М2
 МОДА ЧТК
 ПВ (М13)
 СЧИ М3
 СДА 64-7
 УИ М5
 СЛИ М4(М5)
 СЛИ М4(М2)
 СЧ ’200’(М4)
 УИА ’2000’
 ЗП 1(М14) ШИФР
 УИА 3
 СЧ Е15
 ПБ КОНЭ1
КОНЭ2 НТЖ ГОД
 И П7
 ПО НЕИЗ
 СДА 64-9
КОНЭ1 ИЛИ (М10)
 ЗП (М10) ПР ЗАНЯТ КАНАЛ
КОНЭ СЧ КШ
 МОДА К300-Р(М7)
 УИА (М15)
 ПБ ТБУФ
НЕТР СЧ Е13
 ПБ КОНЭ1
ЧЛ1 УИА 58(М3)
 ПБ КП25-Р(М7)
АПЕН СЧ 4(М10)
 УИ М14
 СЧИ М6
 ИЛИ Е48
 УМН П5
 СЧМР 64
 УИ М4
 УИА -4(М13)
ВОСС МОДА 144(М2)
 МОДА -160(М4)
 СЧ 4(М13)
 УИА ’2000’
 РЕГ 65
 МОДА (М14)
 ЗП 1(М13)
 УИА 3
 МОДА ВОСС
 ЦИКЛ (М13)
 МОДА (М2)
 СЧ 240-32(М6)
 ИЛИ Е48
 НТЖ Е48
 МОДА (М2)
 ЗП 240-32(М6)
 СЧ 4(М10)
 СЛЦ =В’77775’
 И П77777
 ЗП 4(М10)
 ЗП 3(М10)
 МОДА (М11)
 СЧ РАЗ37
 ЗП РАБ
 МОДА (М2)
 И 240-32(М6)
 СЛЦ РАБ
 И РАБ
 ЗП РАБ
 МОДА (М2)
 СЧ 240-32(М6)
 МОДА (М11)
 ИЛИ РАЗ37
 МОДА (М11)
 НТЖ РАЗ37
 ИЛИ РАБ
 МОДА (М2)
 ЗП 240-32(М6)
 СЧ (М10)
 ЗП РАБ
 СДА 64+24
 НТЖ РАБ
  И П77777
 ПО ПИШИ
  СЧ (М10)
 СЛЦ Е25
 ЗП (М10)
 ПБ БЭСМ
ОБМАН УИ М11
 ВЧОБ Е4
 ПЕ ВЫХОД
 МОДА БУФЕР
 ПВ (М3)
 МОДА (М2)
 СЧ 240-32(М6)
 И Е48
 ПЕ АПЕН
БЭСМ СЧИ М6
 ИЛИ Е48
 УМН П11
 СЧМР 64
 УИ М3
НАКА МОДА (М11)
 МОДА (М3)
 СЧ -288(М2)
 ЗП РАБ
 УИ М17
 И Е48
 ПО ПРБУФ
 СЧ РАБ
 УИ М11
 ПБ НАКА
ПРБУФ МОДА (М2)
 СЧ 240-32(М6)
 МОДА (М11)
 И РАЗ37
 ПО ПИШИ
 СЧ РАБ
 СДА 64+27
 И П7
 СЛЦ Е1
 УИА 1(М5)
 И Е3
 ПЕ ПРМБ
 УИА (М5)
ПРМБ СЧ РАБ
 СЛИ М5(М1)
 СБР МАСМБ-Э(М5)
 ЗП АДРЗА
 СЧ (М10)
 И П77777
 ЗП ЕНД
 СЧ (М10)
 СДА 64+24
 УИ М7
 ВЧОБ ЕНД
 СЛЦ Е1
 СД СДМБ-Э(М5)
 СЛЦ АДРЗА
 НТЖ ПЫРМБ-Э(М5)
 И ПЫРМБ-Э(М5)
 ПО ПИШИ
ПЕРЕПЕ УИА ’2000’
 СЧ (М7)
 ЗП (М17)
 УИА 3
 СЧИ М17
 И П1777
 ПО ЗПБУФ
 СЧИ М7
 НТЖ ЕНД
 ПО ВЫПНОШ
 СЛИА 1(М7)
 ПБ ПЕРЕПЕ
ЗПБУФ СЧ АДРЗА
 РЗБ МСКМБ-Э(М5)
 ЗП ИНФ
 СЧ РАБ
 СДА 64+10
 И П37
 СДА 64-10
 УИ М17
 СДА 64-20
 ИЛИ ИНФ
 ЗП ИНФ
 СЧ АДРЗА
 СЛЦ ЕМБ-Э(М5)
 ЗП АДРЗА
 МОДА ОБЭ70
 ПВ (М12)
 МОДА (М2)
 СЧ 240-32(М6)
 ИЛИ Е48
 МОДА (М2)
 ЗП 240-32(М6)
ВЫПНОШ СЧ РАБ
 И П3147
 ЗП РАБ
 СЧ АДРЗА
 РЗБ МАСМБ-Э(М5)
 СДА 64+10
 СДА 64-10
 ИЛИ РАБ
 ЗП РАБ
 СЧИ М17
 ИЛИ РАБ
 МОДА (М11)
 МОДА (М3)
 ЗП -288(М2)
 ЗП РАБ
 ПБ ПИШИ
МИР МОДА 0
 Э70 0
МИР1 МОДА 0
 СЧ 0
ФОРМ МОДА
 Э70
 МОДА 0
 СЧ 0
 Э62 6
ИНФ ЗП
ИНФ2 КОНД В’0’
ОБЭ70 СЧ 4(М10)
 УИ М14
 СЛЦ П3
 СДА 64-24
 ИЛИ МИР
 ЗП ФОРМ
 СЧ 4(М10)
 СЛЦ Е3
 СДА 64-24
 ИЛИ МИР1
 ЗП ФОРМ+1
 УИА -4(М13)
 СЧИ М6
 ИЛИ Е48
 УМН П5
 СЧМР 64
 УИ М4
ОТМ УИА ’2000’
 МОДА (М13)
 СЧ 4(М14)
 УИА 3
 МОДА 144(2)
 МОДА -160(М4)
  ЗП 4(М13)
 МОДА ОТМ
 ЦИКЛ (М13)
 СЧИ М7
 СДА 64-24
 ИЛИ ЕНД
 ЗП ИНФ2
 УИА -4(М13)
ОТК МОДА ФОРМ+4
  СЧ (М13)
 УИА ’2000’
 РЕГ 65
 МОДА 4(М13)
 ЗП (М14)
 УИА 3
 МОДА ОТК
 ЦИКЛ (М13)
 ПБ (М12)
ЧЭК КОНД В’136’
ВЕРСИЯ КОНД В’2647372’
П3147 КОНД М30В’377777’
ДМ7  КОНД  В’0’
МАСМБ КОНД М24В’37’М15В’37’В’1777’
МАСМЛ КОНД М24В’77’М15В’777’В’1777’
АДРЗА КОНД В’0’
ЕНД КОНД В’0’
СДМБ КОНД М41В’44’
СДМЛ КОНД М41В’51’
ПЫРМБ КОНД М46В’3’
ПРМЛ КОНД М45В’7’
МСКМБ КОНД М12В’37’В’37’
МСКМЛ КОНД М12В’77’В’777’
ЕМБ КОНД М38В’1’
ЕМЛ КОНД М33В’1’
КШ КОНД М24В’445’М17В’1’ КАТ ШИФРОВ
КАТ КОНД М24В’446’М17В’1’
 КОНД М24В’447’М17В’1’ НЕ РАЗД С КАТ
ККШ КОНД М24В’445’М17В’1’
КУСМК КОНД М17В’1’В’30’ МАЛНКАТ
ПРЭК КОНД В’0’
НАЧ ЗП R2
Ч ЗП ПРЭК
 СЧ (М10)
 СДА 64+24    НЗАД
       И     П377
 ПО ПРОВ3
 УИ М2
 ПБ ПРВ
Э43 ЗП ПРЭК
 СЧ Е8
 ПВ ЗАХЗАН(М15)
 СЧ НОММЛ1
 ПВ ЕNQ(М15)
ПРОВ3 СЧ ЯЧНОМ(М10)
 СДА 64+39
 И П377
 УИ М2  НОМ В КАТ ВВОДА
ПРВ МОДА V
 УИА (М12)
 УИИ М5(М2)
 СЧИ М2
 СДА 64+7
 ПО ПРНЗАД
 СЧ БК
 СЛЦ Е25
 ЗП БК
 СЛИА 128(М5)
ПРНЗАД  СЧ   0
       ПВ    ТБУФ(М15)      ЛИСТ
       СДА   64-8
       УИ    М4
ПРН    СДА   64-2
       ИЛИ  НОММЛ1
 МОД ПРЭК
       СЛЦ  ЗШ
ВСТ1   ЗП  РЯ
       ПВ   ФИЗОБМ(М16)
       УИА  3
       ПБ    (М12)           ЧТЕН. ШИФРОВ
V СЧ ПРЭК
 ПЕ СЛЕД
 МОДА (М4)
 СЧ (М5)
       ПЕ    ПРОВ2           ЕСТЬ  ШИФР
ПРНЗ2  СЧ    РЯ
       ПВ    ТБУФ(М15)
ОШ1    СЧ    Е1
 ПБ ВЫХОД
ПЗА СЛИА 1(М13)
 МОДА ПОИСЗ
 ЦИКЛ (М12)
 ПБ ОШ1
ПРОВ2    СЧ   РЯ
       ПВ  ТБУФ(М15)      ОТДАТЬ КАТ.ШИФРОВ
 СЧ R2
 ПЕ ЭЭ
       СЧ (М10)
 СДА 64+2
 И Е15П11
 УИ М5
 МОДА ЧЛ1
 УИА (М15)
 СЧИ М15
 ПВ ЗАПВЫХ(М15)
 МОД НЗАД-Р(М7)
 УИА (М17)
 ПВ ЗАНПРП(М15)
 УИА 0
 СЧ 20(М5)
 УИА 3
 СЧИ М5
 СДА 64+10
 НТЖ П37
 УИ М5
ЭЭ УИА -МЧПЗ(М12)
 СЧИ М2
 СДА 64-40
 ЗП R1
 УИА 1(М13)
ПОИСЗ СЧ ШКРЗ
 И Е48-1(М13)
 ПО ПЗА
 МОД ТУСП-1(М13)
 СЧ ЯЧНОМ
 СДА 64-1
 НТЖ R1
 И Е48П41
 ПЕ ПЗА  НЕ ТА
 СЧИ М10
 НТЖ ТУСП-1(М13)
 И П77777
 ПО СС
 СЧ ПОДЧ(М10)
 И Е48-1(М13)
 И ШГП
 И ОСПОД
 ПО ОШ1
СС СЧ (М10)
       СДА  64+18
 И П77
 УИ М16
 ВЧОБ Е3
 ПЕ ПРОВ5
 СЧИ М16  0-ВВОД, 1-АЦПУ, 2-ПИ, 3-ГП, 4-ПЛ
 ПЕ ВЫА
ВВД СЧ R2
       ПЕ ПРОВ5  НЕВЕРНЫЙ ВИД РАБОТЫ
       СЧ    БК
      ЗП   ЗШ
      МОДА ПРНЗАД
       ПВ    (М12)
       СЧИ   М2
 И П177
       СДА   64-3
       УИ    М12
       МОДА  (М4)
       СЧ    5(М12)
       И     П1777          А СЛ.С НОМЕРАМИ ЩОН
       УИ М11
 СЧ КШ
 ЗП ЗШ
       СЧИ   М4
       МОДА  ПРН
       ПВ    (М12)
       СЧ (М10)
       И     П1777
       УИ М16
       СЛИА  1(М16)
ИЩУЗВ1 СЧИ М11
 И Е1
 УИ М12
 СЧИ М11
 СДА 64+1
 УИ М15
 СЛИ М15(М4)
 СЧ 512(М15)
 СД КСО(М12)
       И     П1777
       СЛИА  -1(М16)
       ПО    НЕТИНФ
 МОДА ИЩУЗВ2
 ПИО (М16)
 УИ М11
 ПБ ИЩУЗВ1
ИЩУЗВ2 СЧИ  М11
       СДА   64-24
 СЛЦ НОММЛ2
 ПБ ЗА
МЗ СЧ РИБ
 СЛЦ НОММЛ3
ЗА ЗП R1
       СЧ    РЯ              ОТДАТЬ КАТ.ЗОН ВВОДА
       ПВ    ТБУФ(М15)
 МОД НЗАД-Р(М7)
 УИА (М17)
 ПВ ЗАНПРП(М15)
 СЧИ М5
       НТЖ   П37
       УИ    М16
       СДА   64+2
       НТЖ   П7
       УИ    М12
       СЧИ   М16
       И     П3
       УИ    М15
       СЧИ   М16
       СДА   64-10
       УИ    М17
       УИА   ’2000’
       СЧ    40(М17)
       УИА   ’2003’
       МОДА  (М10)
       СЧ    24(М12)
       СБР   ПРИП(М15)
 СДА 64+2
 ЗП РМР
       СЧ    32(М10)
       ИЛИ   Е48(М5)
       ЗП    32(М10)
 СЧ R1
 ИЛИ Е18
 ЗП РЯ
 ПВ ФИЗУР(М16)
ХОР    УИА   ’2003’
       СЧ      32(М10)
       НТЖ   Е48(М5)
       ЗП  32(М10)
ХОРОШО СЧ    П77777
  ПБ  ВЫХОД
НЕТИНФ МОДА  НЕТИН1
       ПИНО  (М16)
 СЧ 512(М15)
 СД КСО(М12)
       И     Е16
       ПЕ    ИЩУЗВ2
НЕТИН1 СЧ    0
      ЗП (М10)
ВЫХ2 СЧ РЯ
 МОДА К300-Р(М7)
 УИА (М15)
       ПБ  ТБУФ
ВЫА ЗП RR
 СДА 64-36
 ЗП КВ
 СЧ R2
 ПО ИЗ2
 СЧ КВ
 НТЖ Е37
 ПО АЦПУ
 НТЖ Е37
 СДА 64-4
 МОД ТУСП-1(М13)
 ИЛИ ЗОНЫ
 МОД ТУСП-1(М13)
 ЗП ЗОНЫ
 ПБ ХОРОШО
АЦПУ МОД ТУСП-1(М13)
 СЧ АВОС
 ИЛИ Е32
 МОД ТУСП-1(М13)
 ЗП АВОС
 ПБ ХОРОШО
ИЗ2 СЧ RR
 СДА 64-2
 ИЛИ RR
 СДА 64-1
 УИ М12
 МОД ТУСП-1(М13)
 СЧ ЗОНЫ
 СДА 64-10(М12)
 И П1777
 ПО ПРОВ5    НЕТ ИН-ЦИИ ВЫВОДА
 УИ М11
 СЧИ М13
       СДА   64-42
       ИЛИ   КВ
       ЗП    КВ
       СЧ    КВЫ
       ЗП    ЗШ
       МОДА ПРНЗАД
       ПВ    (М12)           ЧТЕН.КАТ.ВЫВОДА
 МОДА (М4)
 СЧ (М11)
 И Е44Е43
 СДА 64+18
 МОДА (М4)
 СЛЦ (М11)
 И Е36П25
 ЗП RR
 СЧ (М10)
 УИИ М12(М11)
 И П1777
 УИ М11
ИЩУЗОН МОДА (М4)
 СЧ (М12)
 СДА 64+11
 И П3777
 УИ М13
 МОДА ИЩ
 ПИО (М13)
 УИИ М12(М13)
 ПБ ИЩУЗОН
ПРОВ5 СЧ Е2
 ПБ ВЫХОД
НЕН МОДА КСС
 ПИО (М16)
 СЛИА -1(М16)
 СЧ РИБ
 СЛЦ Е25
 ЗП РИБ
 СЛИА -1(М11)
 ПБ МИК
КСС МОДА НЕТИН1
 ПИО (М3)
 УИИ М12(М3)
 СЛИА -1(М11)
ИЩ МОДА (М4)
 СЧ (М12)
 ЗП РБ
 И Е36П25
 ЗП РИБ
 СЧ РБ
 И П3777
 УИ М3
 СЧ РБ
 СДА 64+42
 И П3
 УИ М16
МИК МОДА НЕН
 ПИНО (М11)
 МОДА МЗ
 ПИНО (М16)
 МОДА МЗ
 ПИНО (М3)
 СЧ КВ
 СДА 64+36
 И П7
 ЗП R1  1-АЦПУ, 2-ПИ, 3-ГП, 4-ПЛ
 НТЖ Е1
 ПЕ НЕТИН3    ДЛЯ АЦПУ ОБНУЛИТЬ
  ЗП R1
НЕТИН3 СЧ    КВ
       СДА   64+36
       И     П12Е7
       ИЛИ   Е6
  ИЛИ R1
       ПВ    ТБУФ(М15)
       СДА   64-8
       УИ    М14   А БУФЕРА ВЫВОДА
       СЧ    (М14)
 УИА (М12)
 ПО МЗ1
 УИА 1(М12)
       СЧ    РЯ
       ПВ    ТБУФ(М15)
 МОД НЗАД-Р(М7)
 УИА (М17)
 ПВ ЗАНПРП(М15)
       СЧ    (М10)
       СДА   64+2
 И Е15П11
       УИ    М13
 УИА ’2000’
 СЧ  40(М13)
 УИА ’2003’
       УИА   -’1777’(М15)
ПЕРЕП  МОДА  (М15)
       СЧ    ’1777’(М14)
       УИА
       МОДА  (М15)
       ЗП    ’1777’(М13)
       УИА   3
       МОДА  ПЕРЕП
       ЦИКЛ  (М15)
МЗ1 СЧ RR
 СЛЦ НОММЛ3
 ЗП R1
 СЧИ М14
 СДА 64-2
 СЛЦ R1
 ЗП R1
 СЧ КВ
 И Е48П43
 ИЛИ R1
 ПВ ТБУФ(М15)
 СЧИ М12
 ПО МЗ
 ПБ ХОРОШО
СЛЕД СЧИ М2
 И П177
 СДА 64-3
 УИ М2
 СЛИ М2(М4)
 СЧ УКАЗД(М10)
 И ЕТЕЛ
 ПО МУМ
 УИА 1027
 СЧ АВОС(М10)
 ИЛИ Е33
 ЗП АВОС(М10)
 СЧ 6(М2)
 ИЛИ Е32
 ЗП 6(М2)
 СЧ РЯ
 НТЖ Е18
 ПВ ФИЗОБМ(М16)
МУМ СЧ Е8
 ПВ ГАШЗАН(М15)
 СЧ РЯ
 ПВ DЕQ(М15)
 ПБ ВЫХ2
П12Е7  КОНД  М6В’77’
Е37П39 КОНД  М36В’7’
Э44    ЗП    R2
       МОДА  ПРОВ3
       УИА   (М12)
 СЧ 0
 ПБ Ч
R2     КОНД  В’0’  ДЛЯ ПРИЗНАКА Э62 ’44’
Э45 СЧ Е2
 ПВ ЗАХЗАН(М15) РАБОТА С СП
  УИА (М11) БЭР НЕТ
  УИА (М12) НЕТ СП МБ
  УИА -3(М4)
 УИА -СП+1(М5)
  СЧ СПR
 ВЧОБ КСП
  ПО НЕТНАБ В ОЗУ
  СЧ СПR
 ВЧ КСП
  УИ М12 ЧИСЛО НА МБ
  СЧ Е1
  ПВ ТБУФ(М15)
  УИА 3
  СДА 64-8
  УИ М11
  СДА 64-2
  ИЛИ НОМСЕК
  ИЛИ НОММБ
  ЗП РАБ
  ПВ ФИЗОБМ(М16)
НЕТНАБ УИА 3
 СЧ СПНОМ+СП-1(М5)
 СДА 64+1
 НТЖ ТУСП-1(М6)
 И Е47П42
  ПЕ НЕТОЙЗ
 ЗП СПНОМ+СП-1(М5)
 ЗП СТРЕЛ
  СЧ СПR
  ВЧ Е1
  ЗП СПR
  МОДА НЕТОЙЗ
  ПИО (М12) СП МБ НЕТ
  СЛИА -1(М12)
  МОДА (М12)
  СЧ (М11)
 ЗП СПНОМ+СП-1(М5)
  СЧ 0
  МОДА (М12)
  ЗП (М11)
  ПБ НЕТНАБ
НЕТОЙЗ МОДА НЕТНАБ
  ЦИКЛ (М5)
  МОДА ПУСТМБ
  ПИО (М11) НА МБ СП НЕТ
   СЛИ М12(М11)
ОСВМБ СЧ (М11)
  ПО КОНСП ВСЕ СП
 СДА 64+1
 НТЖ ТУСП-1(М6)
 И Е47П42
  ПЕ ОСВМБ1
  СЧ СПR
  ВЧ Е1
    ЗП СПR
  СЧ -1(М12)
  ЗП (М11)
  СЧ 0
  ЗП -1(М12)
  СЛИА -1(М12)
  ПБ ОСВМБ
ОСВМБ1 СЛИА 1(М11)
  ПБ ОСВМБ
КОНСП СЧ РАБ
  НТЖ Е18
  ЗП РАБ
  ПВ ФИЗОБМ(М16)
  СЧ РАБ
  ПВ ТБУФ(М15)
ПУСТМБ СЧ Е2
  ПВ ГАШЗАН(М15)
 ПБ ВЫХОД
МУЗА СЧ ПОДЧ(М12)
 ИЛИ Е48
 ЗП (М10)
 И Е47П1
 ЗП РАБ
 ПО НЕТЗАД
 И Е17П1
 ПО К300-Р(М7)
ДСЛП НЕД П77777
 УИ М17
ПОИС СЧ ШКРЗ
 И Е48(М17)
 ПО НЗА
 СЧ ТУСП(М17)
 УИ М15
 СЧ ШИФРМ(М15)
НЗА УИА 0
 МОДА -46(М17)  НА 16 ПЗ  ВРЕМЕН
 ЗП (М16)
 УИА 3
 СЧ РАБ
 НТЖ Е48(М17)
 ЗП РАБ
 ПЕ ДСЛП
 ПБ К300-Р(М7)
Э462 И П77
 УИ М2   N ТЕРМ
 ВЧОБ П60
 ПЕ ПРОВ5
 СЧ 9(М10)
 СДА 64+12
 НТЖ Е2
 УИА 1027
 ПО ОТДВТ           ОТДАТЬ ВСЕ ТЕРМ. ПОДЧ. З-ЧЕ
 НТЖ П6
 ПО ВЗВТ            ВЗЯТЬ ВСЕ ТЕРМ. У ПОДЧ. З-ЧИ
 МОДА ПРОВ5
 ПИО (М2)           N ТЕРМ.=0
 НТЖ П5
 ПО ОТДNТ
 НТЖ Е2
 ПО ВЗNТ
ПРИ СЧ Е2
 ПБ ВЫХОД
ВЗВТ СЧ ТЕРЕЗА(М12)
 ПО ЗАДНЕТ
 И ШКЭ71
 ПЕ НЕТЛ
 СЧ ТЕРЕЗА(М12)
  ИЛИ ТЕРЕЗА(М10)
 ЗП ТЕРЕЗА(М10)
 УИИ М11(М6)       N ДАННОЙ З-ЧИ
О2 СЧИ М11
 СДА 64-20
 ЗП R1
О1 СЧ ТЕРЕЗА(М12)
 ПО ХОРОШО
 НЕД П77777
 УИ М15            N ТЕРМ.
 СЧ Е48(М15)
 НТЖ ТЕРЕЗА(М12)
 ЗП ТЕРЕЗА(М12)    ПОГАСИТЬ N ТЕРМ.
 СЧ ТСЛ(М15)       N З-ЧИ В ТСЛ
 И NЗТСЛ
 ИЛИ R1
 ИЛИ КОТ
 НТЖ КОТ
 ИЛИ Е20
 ЗП ТСЛ(М15)
 ПБ О1
NЗТСЛ КОНД М20В’77’В’-1’
ОТДВТ СЧ ТЕРЕЗА(М10)
 ПО ЗАДНЕТ
 И ШКЭ71
 ПЕ НЕТЛ
 СЧ ТЕРЕЗА(М10)
 ИЛИ ТЕРЕЗА(М12)
  ЗП ТЕРЕЗА(М12)
 УИИ М12(М10)      А ИПЗ ДАННОЙ
 ПБ О2             М11=N ПОДЧ. З-ЧИ -’40’
ОТДNТ СЧ Е48-1(М2)
 И ТЕРЕЗА(М10)
 ПО ЗАДНЕТ
 И ШКЭ71
 ПЕ НЕТЛ
 СЧ Е48-1(М2)
 НТЖ ТЕРЕЗА(М10)
 ЗП ТЕРЕЗА(М10)
 СЧ Е48-1(М2)
 ИЛИ ТЕРЕЗА(М12)
 ЗП ТЕРЕЗА(М12)
 ПБ О3
ВЗNТ СЧ Е48-1(М2)
 И ТЕРЕЗА(М12)
 ПО ЗАДНЕТ
 И ШКЭ71
 ПЕ НЕТЛ
 СЧ Е48-1(М2)
 НТЖ ТЕРЕЗА(М12)
 ЗП ТЕРЕЗА(М12)
 СЧ Е48-1(М2)
 ИЛИ ТЕРЕЗА(М10)
  ЗП ТЕРЕЗА(М10)
О4 УИИ М11(М6)
О3 НОП
 СЧИ М11
 СДА 64-20
 ЗП R1
 СЧ ТСЛ-1(М2)      М2=N ТЕРМ
 И NЗТСЛ
 ИЛИ R1
 ИЛИ КОТ
 НТЖ КОТ
 ИЛИ Е20
 ЗП ТСЛ-1(М2)      N З-ЧИ В ТСЛ
 ПБ ХОРОШО
ДЛГЛАВ УИИ М12(М10)
 ПБ ЭКС55
К47 МОДА ДИНФ
 ПВ (М3)
 СЧИ М2
 ПО МН
 СЧ 0
 УИА ’2000’
 ЗП (М11)
 УИА 3
МН УИА (М13)
 СЧ R1
 СДА 64+15
 И П7   ВИДЫ РАБОТ
 НТЖ Е1
 ПО КРА   Е16
 НТЖ П3
 ПО ЗП   Е17
 НТЖ П6
 ПЕ ПРОВ5   НЕВЕРН. ВИД РАБОТ
 МОДА СЧ
 ПИО (М2)     ЗАНЕСТИ В М35
ЧТМ21 УИА ’2003’
 СЧ 5(М12)
 СДА 64-28
 И Е48
 УИА ’2000’
 ЗП (М11)
ЧТИР УИА ’2003’
 МОДА (М13)   4ТЕНИЕ ИР
 СЧ 23(М12)
 И П77777
 УИА ’2000’
 ИЛИ (М11)
 ЗП (М11)
 УИА 3
 ПБ ХОРОШО
КРА    УИА   -1(М13)
 МОДА ЧТИР
 ПИНО (М2)
ЗПИР УИА ’2003’   ЗАПИСЬ В ИР
 МОДА (М13)
 СЧ 23(М12)
 И Е48П16
 МОДА (М13)
 ЗП 23(М12)
 СЧ R1
 И П77777
 МОДА (М13)
 ИЛИ 23(М12)
 МОДА (М13)
 ЗП 23(М12)
 ПБ ХОРОШО
СЧ УИА ’2003’
 СЧ 5(М12)
 ИЛИ Е20
 НТЖ Е20
СЧЧ ЗП 5(М12)
 ПБ ЗПИР
ЗП МОДА ЧТМ21   СЧИТАТЬ
 ПИНО (М2)
 УИА ’2003’
 СЧ 5(М12)
 ИЛИ Е20
 ПБ СЧЧ
НЕТЛ СЧ Е3
 ПБ ВЫХОД
ДИНФ УИ М11
 СЧ
 ЗП (М10)
 УИА 1024
 СЧ (М11)
 УИА 3
 ЗП R1
 СДА 64+47
 УИ М2
 СЧ R1
 ПБ (М3)
Э50 МОДА ДИНФ
 ПВ (М3)
 СЧ R1
 И П17
 УИ М5  N ИР
 ПО ПРОВ5
 СЧ R1
 СДА 64+6
 И П17
 УИ М4   КОЛ-ВО ИР
 ПО ПРОВ5
 УИ М16
  УИ М17
 СЛИ М17(М5)  N ПОСЛ. ИР+1
 СЧИ М17
 ВЧОБ Е5
 ПЕ ПРОВ5
 СЛИ М11(М16)
 СЧИ М5
 ВЧОБ
 УИ М5  -N ИР
 СЛИА -1(М4)
 СЧИ М4
 УИА (М13)
 ВЧОБ 0
  УИ М4  - КОЛ. ИР+1
       УИА   ’2003’
 МОДА Э502
 ПИО (М2)    ЗАПИСЬ
ЧИР  МОДА (М5)    СЧИТЫВАНИЕ
 МОДА (М4)
 СЧ 22(М12)
 УИА ’2000’
 МОДА (М13)
 ЗП (М11)
 УИА 3
 СЛИА -1(М13)
 МОДА ЧИР
  ЦИКЛ (М4)
 ПБ ХОРОШО
Э502 УИА ’2000’   ЗАПИСЬ
 МОДА (М13)
 СЧ (М11)
 УИА 3
 МОДА (М5)
 МОДА (М4)
 ЗП 22(М12)
 СЛИА -1(М13)
 МОДА Э502
 ЦИКЛ (М4)
 ПБ ХОРОШО
Э51 МОДА ДИНФ
 ПВ (М3)
 УИ М5
 МОДА Э511
 ПИО (М2)    ЗАПИСЬ
 УИА -6(М13)   СЧИТЫВАНИЕ
Ц МОДА 6(М13)
 СЧ (М12)
 УИА 1024
 МОДА 6(М13)
 ЗП (М5)
 УИА 3
 МОДА Ц
 ЦИКЛ (М13)
 ПБ ХОРОШО
Э511 УИА ’2000’
 СЧ 5(М5)  ДЛЯ ЗАПИСИ В М33 ИМ27 ДОЛЖНО БЫТЬ
 УИА 3
 И П77777
 ЗП (М10)
 И П17     Р.1-4=0
 ПЕ ПРОВ5
 СЧ 5(М12)   ДОБАВИТЬ М21
 СДА 64+15
 СДА 64-15
 ИЛИ (М10)
 ЗП (М10)
 УИА -6(М13)    ЗАПИСЬ
Э512 УИА ’2000’
 МОДА 6(М13)
 СЧ (М5)
 УИА 1027
 МОДА 6(М13)
 ЗП (М12)
 МОДА Э512
 ЦИКЛ (М13)
 СЧ (М10)
 ЗП 5(М12)
 ПБ ХОРОШО
НЛП СЧ Е4
 ПБ ВЫХОД
Э52 УИИ М13(М11)
 МОДА ДИНФ
 ПВ (М3)
 УИ М5  А ЯЧ. В ПОДЧ.’З-ЧЕ
 СДА 64+15
 УИ М4   А ЯЧ. В ГЛ. З-444Е
 СДА 64+15
 И Е1
 УИ М3   0-КОМАНДА,1-ЧИСЛО
 СЧИ М5
 СДА 64+10
 НТЖ П37
 УИ М15    НОМ ЛИСТА ПОДЧ ЗАДАЧИ В ОБР КОДЕ
 СЧ 32(М12)
 НТЖ 22(М12)
 И Е48(М15)
 ПЕ ВЫХОД    ЛИСТ ПОДЧ ЗАДАЧМ В ОБМЕНЕ
 МОДА Э52НЕТ
 УИА (М15)
 СЧИ М15
 ПВ ЗАПВЫХ(М15)
 МОДА СЧЯЧ   СЧИТАТЬ ЯЧ.
 ПИНО (М2)
 УИА 2(М2)
СЧЯЧ1 СЧ
 ЗП РАБ
 УИА 1024
 СЧ (М4)
  УИА 3
 ЗП R1
 УИИ М17(М13)
 ПВ ЗАНПРП(М15)
 СЧ П12
 ЗП РАБ
 СЧ R1
 УИА ’2000’
 МОДА ЗПЧ
 ПИНО (М3)
 УИА 1027
 РЕГ ’101’   ЗП КОМАНДОЙ
 УИА 1024
 ЗП (М5)
 УИА 1027
 РЕГ 65
 УИА 1024
ЗПЧ ЗП (М5)
 УИА 3
 РЕГ ’107’
 ПБ ХОРОШО
СЧЯЧ СЧИ М4
 УИИ М4(М5)   - А ЯЧ. В ПОДЧ. З-ЧЕ
 УИ М5     А ЯЧ. В ГЛ. З-ЧЕ
 УИИ М17(М13)
 УИА 8(М2)
 ПВ ЗАНПРП(М15)
 УИИ М13(М6)
 ПБ СЧЯЧ1
Э52НЕТ СЧИ М2
 НТЖ РАБ
 ПБ ВЫХОД
Э6253 СЧ (М10)
 ПО Э531   ДАННАЯ ЗАДАЧА
 МОДА ЕСПОД
 ПВ (М17)
Э531 УИА 1027
 СЧ 33(М12)
 И П11Е21
 ЗП 33(М12)
 СЧ 9(М10)
 И П3777
 СДА 64-10
 ИЛИ 33(М12)
 ЗП 33(М12)
 ПБ ХОРОШО
Э6254 СДА 64+41
 ИЛИ Е48
 ПБ ВЫХОД
Э55 МОДА ЕСПОД
 ПВ (М17)
ЭКС55 СЧ 34(М12)
 И П77777
 ЗП (М10)
 СЧ 70(М12)
 СДА 64+33
 СДА 64-24
 ИЛИ (М10)
 ИЛИ Е24
ВЫХОД УИА 1027
 ЗП (М10)
 ПБ К300-Р(М7)
Э57    ПО    ОШ1
       НЕД
       УИ    М16
       СЧ    ШЗНКТ
       И     Е48-1(М16)
 ПЕ ПРИ
 СЧ Е48-1(М6)
 НТЖ ЕЗАР
 ПО АРХТТ
 СЧ ШКОПТТ
       И     Е48-1(М16)
       ПЕ    ОПЕР
АРХТТ НОП ,
       СЧ    ШКУСТР
 ИЛИ ШКАС
       И     Е48-1(М16)
       ПО    ОШ1
       УИА   ’2003’
       ИЛИ   ТЕРЕЗА(М10)
  ЗП   ТЕРЕЗА(М10)
       СЧ    Е48-1(М16)
       ИЛИ   ШЗНКТ
       ЗП    ШЗНКТ
 УИИ М2(М16)    N ТЕРМ
 ПБ О4          ЗАНЕСЕНИЕ N З-ЧИ В ТСЛ
ЭК63 ПАМ 0
 СЧИ М16
 НТЖ Е7
 ПО ВЫХОД
 НТЖ Е7
 СДА 64+3
 ПО ПОПИ
 СДА 64+3
 ПО ВЫХОД
 СЧИ М16
 СДА 64+10
 УИ М15
 СЧ ШКЗЛ
 И Е48(М15)
 ПЕ ПОПИ
 СЧ ТЕРЕЗА(М10)
ЗС ЗП РЯ
 ПО ВЫХОД
 НЕД 0
 УИ М15
 СЧИ М16
 НТЖ ТСЛ-1(М15)
 СДА 64+5
 И П1777
 ПО ПОПИ
 СЧ РЯ
 НТЖ Е48-1(М15)
 ПБ ЗС
ПОПИ СЧ К2013
 УИА 1027
 УИ М21
 СЧ (М16)
 ПБ ВЫХОД
Е17П1 КОНД В’377777’
РАЗ37 КОНД В’37’
 КОНД М5В’37’
 КОНД М10В’37’
 КОНД М15В’37’
 КОНД М20В’37’
 КОНД М25В’37’
 КОНД М30В’37’
 КОНД М35В’37’
 КОНД М40В’37’
ШКАЛА КОНД М24В’764037’М18В’37’В’145’
*   48Р - А40,47Р-А41 И Т.Д.
П11Е21 КОНД В’-1’М10В’3777’
НОМСЕК КОНД М17В’1’В’170’ 36Т УС
РБ ПАМ 1
РИБ ПАМ 1
КВ ПАМ 1
RR ПАМ 1
БК  КОНД  М17В’1’М24В’446’  БОЛ.  ’КАТАЛОГ
ЗШ КОНД М17В’1’М24В’445’  ЗОНА ШИФРОВ
КВЫ КОНД М17В’1’М24В’450’ ВЫВОД
Б9999 КОНД П’009999’
Е47П42 КОНД М41В’77’
 ЭКВИВ (Б,95),(ПИШИ,ПИШИ1),(R1,РМ),(ОПЕР,НЕТЛ)
 ЭКВИВ (РЯ,РАБ),(РЯ1,РМ),(ПЛОХО,Э301),(Э,ЭКГДР)
 ЭКВИВ (Н,32),(ШКР,’201’)
БОЭК1 ВНЕШ ЗАПВЫХ
ГРУППА ВНЕШ ШКГР,ГЛГРУП,ЗАГРУП
ДИСП70 ВНЕШ П7777,П777,П60,НОММБ1,БУДГДР,ВРЕМЯ,ТЗНМЛ
ДИСП70 ВНЕШ Е47П1
ДИСП70 ВНЕШ ГОД,Е48П16,ОПТИМ,П37,ШГ,П7,ТБУФ,
ДИСП70 ВНЕШ П77777,П1777,П377,АВОС,ЗАХЗАН,ГАШЗАН,СП,КСП
ДИСП70 ВНЕШ СТРЕЛ,Е15П11,ТСЛ,П6,П17,ЗАНПРП
ДИСП70 ВНЕШ ТЕРЕЗА,ЗОНЫ,ОЗУ,(ПОРЧ,ЭК9),ТОБ,НОММБ,ТУС
ДИСП70 ВНЕШ НОММЛ1,ШИФРМ,П5,ПЕРЕВ,МГРП,П50,П3,СПR,ЕСТОП
ДИСП70 ВНЕШ СПНОМ,Е44Е43,МАЛКАТ,ЯЧНОМ
ДИСП70 ВНЕШ Е48П43,МАСГЛ,НОММЛ2,КСО,Е36П25,НОММЛ3,П3777
ДИСП70 ВНЕШ ПОДЧ,Е24П1,ПРИП,ЗАПВЕТ,И,П77,ОСПОД,П177,УКАЗД
ДИСП70 ВНЕШ К2013,Ш1,РМР,П12,ШКЗЛ,ЯЧЭК,ЕВАМОS
ДИСП70 ВНЕШ МИНЗ,МАКЗ,ЕЗАР,ТУСП,МЧПЗ,НПЗ,ЧПЗ,ШГП
КАНАЛ ВНЕШ (К,КАНАЛ),КАН
МОТТ ВНЕШ ШКУСТР,ШЗНКТ,ШКОПТТ,ШКАС,ШКЭ71,КОТ,ШСВЯТ
КИТ ВНЕШ Е1П25,Е48П41
ВЗУ ВНЕШ ФИЗОБМ,ФИЗУР
БОП1 ВНЕШ ЕТЕЛ
ДИСКИ ВНЕШ КУСКИ,П11,ЕNQ,DЕQ
РЕСУРС ВНЕШ (Р,РЕСУРС),(П,РЯ1),РЯ6,К300,КП25,НЗАД
КАЧКА ВНЕШ ШКРЗ,Е48П17
СОСТАВ ВНЕШ ЯЧИНКО,ЕИНКО,ЯЧАЦПУ,ДАЦПУ
 ВХОД ЕСПОД,КАТ,КШ,РЯ,ВЫХ2,КОНЭ,ЧТК,ВЫХОД,НЕТЛ
 ВХОД НЕТЗАД,ХОРОШО,ОШ1,МУЗА,НЛП,ЭК63
*КОНЕЦ ЧАСТИ

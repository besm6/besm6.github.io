*^
*^
*^
**********************************************^
*                                            *^
*    Д И А Л О Г О В А Я    С И С Т Е М А    *^
*                К  Р  А  Б                  *^
*                                            *^
*--------------------------------------------*^
*  ПРОГРАММА СБОРЩИК. ЧАСТЬ 3.               *^
*  ЗАДАЧА СБОРКИ МАССИВОВ И ФОРМИРОВАНИЯ     *^
**********************************************^
*^
*^
*^
***************************^
*  О Б Щ И Е   Б Л О К И  *^
***************************^
*^
*  БЛОК ВЫБОРА СИМВОЛА^
*  -------------------^
*^
ВС НОП^
*^
* ВХОДНЫЕ ЗНАЧЕНИЯ:^
*   КОДВС - ПРИЗНАК ПЕРЕКОДИРОВКИ:^
*            0    - БЕЗ ПЕРЕКОДИРОВКИ^
*          100(8) - ISО  --> ГОСТ^
*           70(8) - ГОСТ --> ISО^
*   М3    - АДРЕС СЛОВА^
*   М10   - НОМЕР БАЙТА^
*^
 СЧ КОДВС^
 ПЕ ВСП ТРЕБУЕТСЯ ПЕРЕКОДИРОВКА^
 СЧ (М3)^
 СБР П377(М10)^
ВС0 НОП^
 МОДА (М15)^
 ЦИКЛ (М10) СЛОВО НЕ ИСЧЕРПАНО^
 СЛИА 1(М3)^
 УИА -5(М10)^
 ПБ (М15) ВЫХОД^
ВСП НОП ПЕРЕКОДИРОВКА^
 СЧ (М3)^
 СБР П377(М10)^
 СДА 64+40^
 УИ М2^
 СЧ ТАБКОД(М2)^
 МОД КОДВС^
 СДА 0^
 И Е48П41^
 ПБ ВС0^
*^
*  БЛОК ЗАПИСИ СИМВОЛА^
*  -------------------^
*^
ЗС НОП^
*^
* ВХОДНЫЕ ЗНАЧЕНИЯ:^
*   М1    - АДРЕС СЛОВА^
*   М4    - НОМЕР БАЙТА^
*^
 РЗБ П377(М4)^
 ИЛИ (М1)^
 ЗП (М1)^
 МОДА (М15)^
 ЦИКЛ (М4) СЛОВО НЕ ИСЧЕРПАНО^
 СЛИА 1(М1)^
 УИА -5(М4)^
 СЧ 0^
 ЗП (М1)^
 ПБ (М15)^
*^
*  БЛОК ЗАПИСИ СИМВОЛА^
*  -------------------^
*^
ЗСТ НОП^
*^
* ВХОДНЫЕ ЗНАЧЕНИЯ:^
*   М1    - АДРЕС СЛОВА^
*   М4    - НОМЕР БАЙТА^
*^
 ЗП РЯ6^
 СЧ (М1)^
 ИЛИ П377(М4)^
 НТЖ П377(М4)^
 ЗП (М1)^
 СЧ РЯ6^
 РЗБ П377(М4)^
 ИЛИ (М1)^
 ЗП (М1)^
 МОДА (М15)^
 ЦИКЛ (М4) СЛОВО НЕ ИСЧЕРПАНО^
 СЛИА 1(М1)^
 УИА -5(М4)^
 ПБ (М15)^
*^
*  БЛОК ВЫБОРА СИМВОЛА ИЗ БУФЕРА ЧТЕНИЯ^
*  ------------------------------------^
*^
ВСБ НОП^
*^
* ВХОДНЫЕ ЗНАЧЕНИЯ:^
*   КОДВС - ПРИЗНАК ПЕРЕКОДИРОВКИ:^
*            0    - БЕЗ ПЕРЕКОДИРОВКИ^
*          100(8) - ISО  --> ГОСТ^
*           70(8) - ГОСТ --> ISО^
*   М3    - АДРЕС СЛОВА^
*   М10   - НОМЕР БАЙТА^
*   М12   - СЧЕТЧИК СЛОВ^
*^
 СЧ КОДВС^
 ПЕ ВСБП ТРЕБУЕТСЯ ПЕРЕКОДИРОВКА^
 СЧ (М3)^
 СБР П377(М10)^
ВСБ0 НОП^
 МОДА (М15)^
 ЦИКЛ (М10) СЛОВО НЕ ИСЧЕРПАНО^
 СЛИА 1(М3)^
 УИА -5(М10)^
 МОДА (М15)^
 ЦИКЛ (М12) ЛИСТ НЕ ИСЧЕРПАН^
 ЗП РЯВС^
 УИА -1023(М12)^
 УИА СЛОВО(М3)^
 СЧ НОМЗОН^
 СЛЦ Е1^
 ЗП НОМЗОН^
 МОД НОМЗОН^
 СЧ ЗОНЫ^
 ПО ВСБ2 КОНЕЦ МАССИВА^
 СЛЦ СЛЧТМ^
 ЗП РЯ1^
 Э70 РЯ1^
 СЧ КОНМАС^
 ПО ВСБ1^
 СДА 64+10^
 И П3^
 НТЖ НОМЗОН^
 ПЕ ВСБ1^
 СЧ КОНМАС^
 И Е10П1^
 СЛЦ АСЛОВО^
 УИ М16^
 СЧ ПП340^
 ЗП 1(М16)^
ВСБ1 НОП^
 СЧ РЯВС^
 ПБ (М15)^
ВСБ2 НОП^
 СЧ ПП340^
 ПБ (М15)^
ВСБП НОП ПЕРЕКОДИРОВКА^
 СЧ (М3)^
 СБР П377(М10)^
 СДА 64+40^
 УИ М2^
 СЧ ТАБКОД(М2)^
 МОД КОДВС^
 СДА 0^
 И Е48П41^
 ПБ ВСБ0^
*^
*  БЛОК ЗАПИСИ СИМВОЛА В БУФЕР ЗАПИСИ^
*  ----------------------------------^
*^
ЗСБ НОП^
*^
* ВХОДНЫЕ ЗНАЧЕНИЯ:^
*   М1    - АДРЕС СЛОВА^
*   М4    - НОМЕР БАЙТА^
*   М11   - СЧЕТЧИК СЛОВ^
*^
 РЗБ П377(М4)^
 ИЛИ (М1)^
 ЗП (М1)^
 МОДА (М15)^
 ЦИКЛ (М4) СЛОВО НЕ ИСЧЕРПАНО^
 СЛИА 1(М1)^
 УИА -5(М4)^
 ЦИКЛ ЗСБК(М11) ЛИСТ НЕ ИСЧЕРПАН^
 Э70 ТРМБ^
 СЧ ТРМБ^
 СЛЦ Е1^
 ЗП ТРМБ^
 И Е6^
 ПЕ НЕТМБ ВСЕ ТРАКТЫ ЗАНЯТЫ^
 УИА -1023(М11)^
 УИА ЛИСТМБ(М1)^
ЗСБК НОП^
 СЧ 0^
 ЗП (М1)^
 ПБ (М15)^
*^
* БЛОК ВЫБОРА ПАРАМЕТРОВ ИЗ УПРАВЛЯЮЩЕЙ ШАПКИ^
*--------------------------------------------^
*^
* ВЫХОДНЫЕ ЗНАЧЕНИЯ:^
*   РЯ1 - ПАРАМЕТР^
*   РЯ2 - МАСКА ПАРАМЕТРА^
*   М11=0 - ПРИЗНАК ОШИБКИ^
*^
ВЫБПАР НОП^
 СЧ 0^
 ЗП РЯ1^
 ЗП РЯ2^
ВП1 НОП ВЫБОР ПАРАМЕТРА И ФОРМИРОВАНИЕ МАСКИ^
 ПВ ВСБ(М15)^
 СЛИА -1(М11)^
 ПИО ВПК(М11)^
 ЗП СИ^
 НТЖ ПП340^
 ПО ВПК КОНЕЦ МАССИВА^
 ПВ ПРР1(М15)^
 ПИО ВПК(М11)^
 ПО ДОДПАР РАЗДЕЛИТЕЛЬ^
 СЧ СИ^
 СДА 64+40^
 ЗП СИ^
 СЧ РЯ1^
 СДА 64-8^
 ИЛИ СИ^
 ЗП РЯ1^
 СЧ РЯ2^
 СДА 64-8^
 ИЛИ П377^
 ЗП РЯ2^
 ПБ ВП1^
*^
ДОДПАР НОП ДОДВИГ ПАРАМЕТРА И МАСКИ^
 СЧ РЯ2^
 ПО ВПК НЕТ ПАРАМЕТРА^
ВП3 НОП^
 СЧ РЯ2^
 И Е48П41^
 ПЕ ПРАЗД ПАРАМЕТР И МАСКА ГОТОВЫ^
 СЧ РЯ1^
 СДА 64-8^
 ИЛИ П17^
 ЗП РЯ1^
 СЧ РЯ2^
 СДА 64-8^
 ЗП РЯ2^
 ПБ ВП3^
*^
ПРАЗД НОП ПРОПУСК РАЗДЕЛИТЕЛЕЙ^
 СЧИ М12^
 ЗП РЯ3^
 СЧИ М3^
 ЗП РЯ4^
 СЧИ М10^
 ЗП РЯ5^
 ПВ ВСБ(М15)^
 СЛИА -1(М11)^
 ПИО ВПК(М11)^
 ЗП СИ^
 НТЖ ПП17^
 ПО ПРАЗД ПРОБЕЛ^
 СЧ СИ^
 И Е48^
 ПЕ ПРАЗД ПРОБЕЛЫ ИЛИ СЛУЖЕБНЫЙ СИМВОЛ^
 СЧ РЯ3^
 УИ М12^
 СЧ РЯ4^
 УИ М3^
 СЧ РЯ5^
 УИ М10^
 СЛИА 1(М11)^
 ПБ (М17)^
*^
ВПК НОП ОШИБКА В ШАПКЕ^
 УИА (М11)^
 ПБ (М17)^
*^
* БЛОК ПРОВЕРКИ РАЗДЕЛИТЕЛЕЙ^
*---------------------------^
*^
ПРР1 НОП ВХОД N 1^
 СЧ СИ^
 НТЖ ПП17^
 ПО (М15) ПРОБЕЛ^

 СЧ СИ^
 НТЖ ПП201^
 ИЛИ ПП17^
 НТЖ ПП17^
 ПО (М15) ПРОБЕЛЫ^
 СЧ СИ^
 НТЖ ПП343^
 ПО (М15) ТАБУЛЯЦИЯ^
ПРР2 НОП ВХОД N 2^
 СЧИ М15^
 ЗП ПРРВЫХ АДРЕС ВЫХОДА^
ПРР3 НОП^
 СЧ СИ^
 НТЖ ПП344^
 ПО ПРР4 ОТМЕНЕННАЯ СТРОКА^
 СЧ СИ^
 НТЖ ПП346^
 ПЕ ПРР5 НЕ ПУСТОЙ СИМВОЛ^
ПРР4 НОП^
 ПВ ВСБ(М15)^
 СЛИА -1(М11)^
 ПИО ПРР6(М11) КОНЕЦ ФАЙЛА^
 ЗП СИ^
 ПБ ПРР3^
ПРР5 НОП^
 СЧ СИ^
 НТЖ ПП341^
 ПО ПРР6 КОНЕЦ СТРОКИ^
 СЧ СИ^
 НТЖ ПП350^
 ПО ПРР6 КОНЕЦ СТРОКИ^
 СЧ СИ^
 НТЖ ПП343^
 ПО ПРР6 ТАБУЛЯЦИЯ^
 СЧ СИ^
 НТЖ ПП345^
 ПО ПРР6 НЕНУМЕРОВАННАЯ СТРОКА^
ПРР6 НОП^
 МОД ПРРВЫХ^
 ПБ 0 ВЫХОД^
ПРРВЫХ КОНД В'0' АДРЕС ВЫХОДА^
*^
* БЛОК ПЕРЕВОДА ИЗ 10 В 8^
*------------------------^
*^
ИЗ10В8 НОП^
*^
*  ВХОДНЫЕ ЗНАЧЕНИЯ:^
*    РЯ1 - 10 ЧИСЛО (ПО 4 РАЗРЯДА)^
*  ВЫХОДНЫЕ ЗНАЧЕНИЯ:^
*    РЯ2, СМ - 8 ЧИСЛО^
*^
       УИА   20(М16)^
       УИА   (М15)^
       СЧ    0^
       ЗП    РЯ2^
ПВЧ    СЧ    РЯ1^
       СДА   64(М16)^
       ЗП    РЯ3^
       СЧМР  0^
       СДА   64+28(М15)^
       ЗП    РЯ1^
       СЧ    РЯ3^
       СЛЦ   РЯ2^
       ЗП    РЯ2^
       МОДА  (М17)^
       ПИО   (М16)^
       СЛИА  -4(М16)^
       СЛИА  4(М15)^
       СЧ    РЯ2^
       УМН   К1+1^
       СЧМР  64^
       ЗП    РЯ2^
       ПБ    ПВЧ^
*^
*^
*  БЛОК ВЫДАЧИ АСТРОНОМИЧЕСКОГО ВРЕМЕНИ^
*--------------------------------------^
*^
ВРЕМЯ НОП^
*^
*  ВЫХОДНЫЕ ЗНАЧЕНИЯ:^
*    ВР8Ч - ЧАСЫ(8)^
*    ВР8М - МИНУТЫ(8)^
*^
       Э53   '10'^
       РЖА   6^
       ИЛИ   ЦНОЛЬ^
 ДЕЛ ВЕ3000^
       РЖА   3^
       СЛ    ЦНОЛЬ^
       ЗП    РЯ4 ВРЕМЯ В МИНУТАХ^
       РЖА   6^
       ДЕЛ   ВЕЩ60^
       РЖА   3^
       СЛ    ЦНОЛЬ^
       ЗП    РЯ3 ВРЕМЯ В ЧАСАХ^
       И     П77777^
 ЗП ВР8Ч ЧАСЫ В 8-ВИДЕ^
       СЧ    РЯ3^
       РЖА   6^
       УМН   ВЕЩ60^
       ВЧОБ  РЯ4^
       РЖА   3^
       СЛ    ЦНОЛЬ^
       И     П77777^
 ЗП ВР8М МИНУТЫ В 8-ВИДЕ^
       ПБ    (М15)^
*^
*  БЛОК ФОРМИРОВАНИЯ ИНФ. СЛОВ ОБМЕНА^
*------------------------------------^
*^
ФОРМСЛ НОП^
*^
 ПВ ЗАКСМД(М16)^
*^
 СЧ ЧТВВД^
 Э50 '105'^
 ЗП БОБОСН^
 СЧ ЯЧВВД^
 СДА 64+15^
 И П1777^
 ЗП РЯ1^
 ИЛИ ЧТВВД^
 ЗП ЧТВВД^
 СЧ РЯ1^
 ИЛИ ЗПВВД^
 ЗП ЗПВВД^
 СЧ РЯ1^
 ИЛИ ЧТПОЛ^
 ЗП ЧТПОЛ^
 СЧ РЯ1^
 ИЛИ ЧТМВВД^
 ЗП ЧТМВВД^
 СЧ РЯ1^
 ИЛИ ЗППОЛ^
 ЗП ЗППОЛ^
*^
 СЧ ПАРОСН^
 И Е24П1^
 ИЛИ ПЧПМД^
 ЗП ПЧПМД^
 СЧ ЧТГП1^
 Э50 '113'^
 СДА 64+24^
 ИЛИ ПЧСДВ^
 ЗП ПЧСДВ^
 СЧ ПЧАЦП0^
 ЗП ПЧАЦП^
 СЧ ПЧВРЕ0^
 ЗП ПЧВРЕ^
 СЧ БОБОСН^
 ЗП ПЧБДД^
 СДА 64-32^
 РЗБ ООПППП^
 ИЛИ ПЧБОБ^
 ЗП ПЧБОБ^
 Э50 '114'^
 ЗП РЯ1^
 И Е35П25^
 СДА 64-3^
 ЗП ДАТ114^
 СЧ РЯ1^
 И П7^
 ЗП НОММАШ^
 ПБ (М6)^
*^
*  БЛОК ЗАКАЗА МЛ ИЛИ МД^
*  ---------------------^
*^
ЗАКАЗ НОП^
*^
 Э72 ОТК40 ОТКАЗ ОТ БОБИНЫ ПО 40^
 СЧ 1(М7)^
 СДА 64+36^
 ЗП СДЗАК^
 СЧ 3(М7)^
 ЗП БОБЗАК^
 СЧ 1(М7)^
 И Е30^
 ПЕ ЗАКАЗБ ЗАКАЗ БИБЛИОТЕКИ^
 МОД 3(М7)^
 УИА (М16) НОМЕР АРХИВА^
 СЧ ТАРМДК-1(М16)^
 И Е16П1^
 ЗП БОБЗАК^
 СЧ ТАРМДК-1(М16)^
 СДА 64+16^
 И Е17П1^
 ЗП СДЗАК^
 СЧ ПАРКОН-1(М16)^
 СДА 64+24+12^
 СДА 64-16^
 ИЛИ БОБЗАК^
 ЗП БОБЗАК^
 СЧ ПАРКОН-1(М16)^
 СДА 64+24^
 И П7777^
 СДА 64-16-15^
 ИЛИ БОБЗАК^
 ЗП БОБЗАК^
ЗАКАЗБ НОП^
 СЧ БОБЗАК^
 И Е16П1^
 ИЛИ СЛЗАК^
 Э50 '131' ЗАКАЗ МЛ(МД) ПО НАПР. 40^
 ПО ЗАКАЗ2^
*^
ЗАКАЗ1 НОП^
 СЧ БОБЗАК^
 УИА '71717'(М3)^
 Э50 '1206'^
 ПЕ ЗАКАЗ МЛ(МД) УСТАНОВИЛИ^
 СЧ ПАУЗА^
 Э50 '7700'^
 ПБ ЗАКАЗ1^
*^
*  ПОДТВЕРЖДЕНИЕ ПАРОЛЕЙ^
ЗАКАЗ2 НОП^
 СЧ БОБЗАК^
 СДА 64+16^
 ИЛИ Е15^
 УИ М15^
 СЧ БОБЗАК^
 И Е16П1^
 Э50 '121' ПАРОЛЬ ЧТЕНИЯ^
 СЧ 1(М7)^
 И Е30^
 ПЕ ЗАКСДВ ЗАКАЗ БИБЛИОТЕКИ^
 СЧ БОБЗАК^
 СДА 64+16+15^
 УИ М15^
 СЧ БОБЗАК^
 И Е16П1^
 Э50 '121' ПАРОЛЬ ЗАПИСИ^
*^
*  УСТАНОВКА СДВИГА^
ЗАКСДВ НОП^
 СЧ СДЗАК^
 ЗП РЯСДВ^
 СЧ КСДВИГ^
 Э50 '156'^
 ПО МД725 ЛЕНТА ИЛИ МД 7.25 МГБ^
 СЧ СДЗАК^
 СДА 64+5^
 ЗП РЯСДВ^
МД725 НОП^
 СЧ РЯСДВ^
 ИЛИ КСДВИГ^
 Э50 '112'^
*^
 СЧ БОБЗАК^
 И Е16П1^
 ЗП БОБИНА НОМЕР БОБИНЫ^
 СЧ 1(М7)^
 И Е30^
 ПЕ ДАЙКОН ЗАКАЗ БИБЛИОТЕКИ^
 МОД 3(М7)^
 СЧ ПАРКОН-1^
 И Е17П1^
 ВЧ СДЗАК^
 ЗП КМД КОНЕЦ АРХИВА^
 ВЧОБ Е3^
 ЗП НКМД НАЧАЛО-КОНЕЦ^
 СЧ Е1^
 ЗП ТИПБ^
 ПБ (М17) ВЫХОД^
*^
ДАЙКОН НОП^
 СЧ 0^
 ЗП ТИПБ^
 СЧ 1(М7)^
 СДА 64+33^
 И П7^
 ПО (М17) ТИП: Л Е Н Т А^
 НТЖ Е1^
 ПЕ ОШЗАК НЕВЕРНЫЙ ТИП^
 СЧ Е1^
 ЗП ТИПБ^
 Э70 ЧТКАТ1 ЧТЕНИЕ КАТАЛОГА^
 СЧ КАТ1+1^
 И П7777^
 ЗП КМД КОНЕЦ БИБЛИОТЕКИ^
 ВЧОБ П3^
 ЗП НКМД НАЧАЛО-КОНЕЦ^
 ПБ (М17) ВЫХОД^
*^
СДЗАК ПАМ 1 СДВИГ НА БОБИНУ^
БОБЗАК ПАМ 1 НОМЕР БОБИНЫ^
РЯСДВ ПАМ 1^
*^
*  БЛОК ПОИСКА МАССИВА В ДИСКОВОМ АРХИВЕ^
*---------------------------------------^
*^
ДАЙЗМД НОП^
*^
*  ВЫХОДНЫЕ ЗНАЧЕНИЯ:^
*    КЗФ:    1 - 15 Р - КОЛ-ВО ЗОН У ФАЙЛА^
*           16 - 19 Р - ТИП КОДИРОВКИ^
*                       (ДЛЯ НЕТЕКСТОВОГО ФАЙЛА)^
*            43 Р = 1 - НЕТЕКСТОВЫЙ ФАЙЛ^
*   ЗОНЫ[N]: 1 - 10 Р - НОМЕР ЗОНЫ N-ОЙ ЧАСТИ ФАЙЛА^
*^
 Э70 ЧТКАТ1^
 Э70 ЧТКАТ2^
 СЧ КАТ1^
 НТЖ ВСЕЕД^
 ПЕ ЗАТКАТ ЗАТЕРТ КАТАЛОГ^
 ЗП КЗФ^
 УИА -31(М15)^
 СЧ 0^
ЕСИЗМ0 НОП^
 ЗП ЗОНЫ+31(М15)^
 ЦИКЛ ЕСИЗМ0(М15)^
*^
 МОД НКМД^
 УИА (М16)^
ЕСИЗМ НОП^
 МОД КМД^
 СЧ КАТ1(М16)^
 РЗБ ШЕСТЬ7^
 НТЖ (М7)^
 ПЕ ЕСИЗМ1 ИМЯ НЕ СОВПАЛО^
 МОД КМД^
 МОДА КАТ2^
 СЧ (М16)^
 НТЖ 1(М7)^
 И Е24П1^
 ПЕ ЕСИЗМ1 ХОЗЯИН НЕ СОВПАЛ^
 МОД КМД^
 СЧ КАТ1(М16)^
 И П37^
 УИ М15^
 СЧИ М16^
 СЛЦ КМД^
 И П7777^
 ЗП ЗОНЫ(М15)^
 МОД КМД^
 МОДА КАТ2^
 СЧ (М16)^
 И Е43^
 ИЛИ КЗФ^
 СЛЦ Е1^
 ЗП КЗФ^
ЕСИЗМ1 ЦИКЛ ЕСИЗМ(М16)^
 СЧ КЗФ^
 И Е43^
 ПО (М17) КОДИРОВКА МОНИТОРА АБОНЕНТОВ^
 Э70 ЧТКАТ3^
 МОД ЗОНЫ^
 МОДА КАТ3^
 СДА 64+32^
 И П17^
 СДА 64-15^
 ИЛИ КЗФ^
 ЗП КЗФ ТИП КОДИРОВКИ^
 ПБ (М17)^
*^
*^
*^
*  БЛОК ПОИСКА МАССИВА В ЛЕНТОЧНОМ АРХИВЕ^
*----------------------------------------^
*^
ДАЙЗМЛ НОП^
*^
 Э70 ЧТКАТ1^
 СЧ КАТ1^
 НТЖ ВСЕЕД^
 ПЕ ЗАТКАТ ЗАТЕРТ КАТАЛОГ^
 УИА -31(М16)^
 СЧ 0^
ИМЯМЛ0 НОП^
 ЗП ЗОНЫ+31(М16)^
 ЦИКЛ ИМЯМЛ0(М16)^
 УИА 4(М15)^
 УИА -339(М16)^
*^
ИМЯМЛ1 НОП^
 СЧ КАТ1(М15)^
 НТЖ ВСЕЕД^
 ПО (М17) КОНЕЦ КАТАЛОГА^
 СЧ КАТ1(М15)^
 НТЖ (М7)^
 СДА 64+6^
 ПЕ ИМЯМЛ2 НЕ СОВПАЛО ИМЯ^
 СЧ КАТ1+1(М15)^
 И Е48П25^
 ПО БЕЗХОЗ МАССИВ БЕЗ ХОЗЯИНА^
 СДА 64+24^
 НТЖ 1(М7)^
 И Е24П1^
 ПЕ ИМЯМЛ2 НЕ СОВПАЛ ХОЗЯИН^
БЕЗХОЗ НОП^
 СЧ КАТ1+1(М15)^
 И П777^
 ЗП ЗОНЫ^
 СЧ КАТ1+1(М15)^
 СДА 64+9^
 И П3^
 ВЧОБ 0^
 СЛЦ Е2^
 УИ М15 КОЛ-ВО ЗОН - 1^
 МОДА (М17)^
 ПИО (М15) ОДНА ЗОНА ==> ВЫХОД^
 УИА (М16)^
ИМЯМЛ3 НОП^
 СЧ ЗОНЫ(М16)^
 СЛЦ Е1^
 ЗП ЗОНЫ+1(М16)^
 СЛИА 1(М16)^
 ЦИКЛ ИМЯМЛ3(М15)^
 ПБ (М17)^
ИМЯМЛ2 СЛИА 3(М15)^
 ЦИКЛ ИМЯМЛ1(М16)^
 ПБ (М17)^
*^
*^
*  БЛОК ЧТЕНИЯ ПЕРВОЙ ЗОНЫ МАССИВА^
*---------------------------------^
*^
ЧИТАЙ НОП^
*^
 СЧ СЛЧТМ^
 СЛЦ ЗОНЫ^
 ЗП РЯ1^
 Э70 РЯ1^
 УИА СЛОВО+6(М3)^
 УИА -5(М10)^
 УИА -1023+6(М12)^
 СЧ 0^
 ЗП НОМЗОН^
 СЧ ПП340^
 МОДА СЛОВО^
 ЗП 1024^
 СЧ КОНМАС^
 ПО (М17) КОНМАС НЕ ФОРМИРОВАТЬ^
 СЧ СЛОВО^
 ЗП КОНМАС^
 И Е12П11^
 ПЕ (М17) КОНЕЦ МАССИВА НЕ В НУЛЕВОЙ ЧАСТИ^
 СЧ СЛОВО^
 И Е12П1^
 ЗП КОНМАС^
 СЛЦ АСЛОВО^
 УИ М16^
 СЧ ПП340^
 ЗП 1(М16)^
 ПБ (М17)^
*^
* БЛОК ПРОВЕРКИ ПРАВА ВЫПОЛНИТЬ ДЕЙСТВИЕ^
*---------------------------------------^
*^
ПРАВО НОП^
*^
*  ВХОДНЫЕ ЗНАЧЕНИЯ:^
*    РЯПР - НОМЕР ОБ'ЕКТА ДЕЙСТВИЙ^
*  ВЫХОДНЫЕ ЗНАЧЕНИЯ:^
*    СМ = 0 - МОЖНО ВЫПОЛНИТЬ ДЕЙСТВИЕ^
*^
 СЧ ШИФРП^
 ПО НЕТНОМ НЕ ЗАДАН НОМЕР^
 НТЖ П377^
 И П377^
 ПО (М17)^
 СЧ ШИФРП^
 И Е16П1^
 ПЕ НЕОТД НЕ АДМ-ТОР ОТДЕЛА^
 СЧ ШИФРП^
 НТЖ РЯПР^
 И Е48П41+3^
 ПБ (М17)^
НЕОТД СЧ ШИФРП^
 И П377^
 ПЕ НЕГРУП НЕ АДМ-ТОРГРУППЫ^
 СЧ ШИФРП^
 НТЖ РЯПР^
 ИЛИ П377^
 НТЖ П377^
 И Е24П1^
 ПБ (М17)^
НЕГРУП СЧ ШИФРП^
 НТЖ РЯПР^
 И Е24П1^
 ПБ (М17)^
НЕТНОМ СЧ РЯПР^
 ПБ (М17)^
*^
*  БЛОК РАЗБОРА ПАСПОРТА^
*-----------------------^
*^
*    ИМЕЕТ ДВА ВХОДА: РАЗПАС, ВЫБРП1^
*^
*  ВЫХОДНЫЕ ЗННАЧЕНИЯ:^
*    ВРЕЗАД - ВРЕМЯ^
*    ТРАЗАД - ТРАКТЫ^
*    ТЕЛЗАД - РАЗДЕЛ ТЕЛЕ^
*    АЦПЗАД - МЕТРЫ АЦПУ^
*    ЛИСЗАД - ЛИСТЫ ОЗУ^
*  ВЫХОД ПО М6^
*^
РАЗПАС НОП ВХОД ПРИ ФОРМИРОВАНИИ ЗАДАЧИ^
 СЧ 0^
 ЗП ТРАЗАД^
 ЗП ТЕЛЗАД^
 ЗП ТРАЗАД^
 ЗП ЛИСЗАД^
 СЧ П7^
 ЗП АЦПЗАД^
*^
 СЧ ПРКОНТ^
 И Е2^
 ПЕ КОНПАС БЫЛА ОШИБКА В ЗОНЕ СТПАСП^
*^
 УИА СЛОВО+1(М3)^
 УИА -5(М10)^
 УИА 1023(М11) МАКС. КОЛ-ВО СИМВОЛОВ В ПАСПОРТЕ^
*^
*  ВЫБОР НОМЕРА СТАНДАРТНОГО ПАСПОРТА^
 СЧ 0^
 ЗП РЯ1^
ВЫБСТ1 НОП^
 ПВ ВС(М15)^
 СЛИА -1(М11)^
 ПИО ДЛПАС(М11)^
 ЗП СИ^
 НТЖ ПП115^
 ПО ВЫБРП1 СТАНДАРТНЫЙ ПАСПОРТ НЕ УКАЗАН^
 СЧ СИ^
 НТЖ ПЗС^
 ПЕ ВЫБСТ1^
 УИА -1(М17)^
ВЫБСТ2 НОП^
 ПВ ВС(М15)^
 СЛИА -1(М11)^
 ПИО ДЛПАС(М11)^
 СДА 64+40^
 ЗП СИ^
 НТЖ П17^
 ПО ВЫБСТ2 ПРОБЕЛ^
 СЧ СИ^
 НТЖ П341^
 ПО ВЫБСТ2^
 СЧ РЯ1^
 СДА 64-8^
 ИЛИ СИ^
 ЗП РЯ1^
 ЦИКЛ ВЫБСТ2(М17)^
*^
ВЫБСТ6 НОП^
 ПВ ВС(М15)^
 СЛИА -1(М11)^
 ПИО ДЛПАС(М11)^
 НТЖ ПП115^
 ПЕ ВЫБСТ6 НЕ КОНЕЦ ШИФРА^
*^
*  ПОИСК В ТАБЛИЦЕ ПАСПОРТОВ^
 УИА -551(М4)^
 УИА (М15)^
ВЫБСТ4 НОП^
 МОДА СТАНП^
 СЧ (М15)^
 ПО ОШПАС КОНЕЦ ТАБЛИЦЫ^
 НТЖ РЯ1^
 И Е16П1^
 ПО ВЫБСТ5 ПАСПОРТ НАЙДЕН^
 СЛИА 2(М15)^
 ЦИКЛ ВЫБСТ4(М4)^
 ПБ ОШПАС^
ВЫБСТ5 НОП^
 МОДА СТАНП^
 СЧ (М15)^
 ИЛИ Е16П1^
 НТЖ Е16П1^
 ЗП ЛИСЗАД^
 МОДА СТАНП^
 СЧ 1(М15)^
 И П1777^
 ЗП ТРАЗАД^
 МОДА СТАНП^
 СЧ 1(М15)^
 СДА 64+9^
 И П7777^
 ЗП ВРЕЗАД^
 МОДА СТАНП^
 СЧ 1(М15)^
 И Е48^
 ЗП ТЕЛЗАД^
 МОДА СТАНП^
 СЧ 1(М15)^
 СДА 64+22^
 И П177^
 ЗП АЦПЗАД^
*^
*  ВЫБОР РАЗДЕЛОВ ПАСПОРТОВ^
*^
ВЫБРП1 НОП ВХОД ДЛЯ ПОДГОТОВКИ ТАБЛИЦЫ ПАСПОРТОВ^
*^
*    М3,М10 - КООРДИНАТЫ НАЧАЛА ПОИСКА^
*^
 УИА -2(М4)^
 СЧ 0^
 ЗП РЯ1^
ВЫБРАЗ ПВ ВС(М15)^
 СЛИА -1(М11)^
 ПИО ДЛПАС(М11)^
 СДА 64+40^
 ЗП СИ^
 НТЖ П341^
 ПО ВЫБРАЗ^
 СЧ СИ^
 НТЖ П17^
 ПО ВЫБРАЗ^
 СЧ РЯ1^
 СДА 64-8^
 ИЛИ СИ^
 ЗП РЯ1^
 ЦИКЛ ВЫБРАЗ(М4)^
*^
 СЧ РЯ1^
 НТЖ ТКПАС^
 ПО ВЫБРП1 ЭТО <ЕМЕ>^
 И Е48П41+3^
 ПО КОНПАС КОНЕЦ МАССИВА^
*^
*  ПОИСК РАЗДЕЛА ПАСПОРТА^
 УИА РПАС(М1)^
 УИА -КРПАС(М4)^
ПРПАС СЧ (М1)^
 НТЖ РЯ1^
 И Е24П1^
 ПО НАШРП НАШЛИ НА РУССКОМ^
 СЧ (М1)^
 СДА 64+24^
 НТЖ РЯ1^
 ПО НАШРП НАШЛИ НА ЛАТИНСКОМ^
 СЛИА 2(М1)^
 ЦИКЛ ПРПАС(М4)^
ПРПАС1 ПВ ВС(М15)^
 СЛИА -1(М11)^
 ПИО ДЛПАС(М11)^
 НТЖ ПП115^
 ПЕ ПРПАС1 РАЗДЕЛ НЕ ЗАКОНЧЕН^
 ПБ ВЫБРП1^
*^
НАШРП НОП РАЗДЕЛ НАЙДЕН^
 ПВ ВС(М15)^
 СЛИА -1(М11)^
 ПИО ДЛПАС(М11)^
 ЗП СИ^
 НТЖ ПП17^
 ПО 1(М1) НА ВЫБОР ПАРАМЕТРА^
 СЧ СИ^
 НТЖ ПП115^
 ПО 1(М1) ПАРАМ. НЕТ^
 ПБ НАШРП^
* ПЕРЕЛЮЧАТЕЛЬ^
КРПАС ЭКВ 6^
РПАС НОП^
*^
 КОНД П'ТIМВРЕ'^
 ПБ РАЗВРЕ^
*^
 КОНД П'ТRАТРА'^
 ПБ РАЗТРА^
*^
 КОНД П'ТЕLТЕЛ'^
 ПБ РАЗТЕЛ^
*^
 КОНД П'LРRАЦП'^
 ПБ РАЗАЦП^
*^
 КОНД П'РАGЛИС'^
 ПБ РАЗЛИС^
*^
 КОНД П'МЕМОЗУ'^
 ПБ РАЗОЗУ^
*^
 КОНД П'RЕРПОВ'^
 ПБ РАЗПОВ^
*^
РАЗВРЕ НОП РАЗДЕЛ ВРЕМЯ^
 ПВ НАШРПП(М17)^
 СЧ РЯ1^
 ЗП РЯ5 ВРЕМЯ В 2-10 ВИДЕ^
 СЧ 0^
 ЗП РЯ4 ВРЕМЯ В СЕКУНДАХ(8)^
 УИА 16(М1)^
РАЗВР1 СЧ РЯ5^
 СДА 64(М1)^
 И П377^
 ЗП РЯ1^
 ПВ ИЗ10В8(М17)^
 ПИО СЕКУНД(М1)^
 СЛЦ РЯ4^
 ИЛИ ЦНОЛЬ^
 РЖА 6^
 УМН ВЕЩ60^
 РЖА 3^
 СЛ ЦНОЛЬ^
 И Е24П1^
 ЗП РЯ4^
 СЛИА -8(М1)^
 ПБ РАЗВР1^
СЕКУНД СЛЦ РЯ4^
 ЗП ВРЕЗАД^
 ПБ ВЫБРП1^
*^
РАЗТРА НОП РАЗДЕЛ ТРАКТЫ^
 ПВ НАШРПП(М17)^
 ПВ ИЗ10В8(М17)^
 ЗП ТРАЗАД^
 ПБ ВЫБРП1^
*^
РАЗТЕЛ НОП РАЗДЕЛ ТЕЛЕ^
 СЧ Е48^
 ЗП ТЕЛЗАД^
 ПБ ВЫБРП1^
*^
РАЗАЦП НОП РАЗДЕЛ АЦПУ^
 ПВ НАШРПП(М17)^
 ПВ ИЗ10В8(М17)^
 ЗП АЦПЗАД^
 ПБ ВЫБРП1^
*^
РАЗЛИС НОП РАЗДЕЛ ЛИСТЫ^
 ПВ НАШРПП(М17)^
 СБР Х77777^
 СДА 64+33^
 ЗП РЯ5^
 СЧ СИ^
 НТЖ ПМИНУС^
 ПО ЗДИАП ЗАДАН ДИАПАЗОН^
 МОД РЯ5^
 СЧ Е48^
 ИЛИ ЛИСЗАД^
 ЗП ЛИСЗАД^
КАН1 НОП^
 СЧ СИ^
 НТЖ ПЗПТ^
 ПО РАЗЛИС^
 СЧ СИ^
 НТЖ П115^
 ПО ВЫБРП1 КОНЕЦ РАЗДЕЛА^
 ПБ ОШПАС^
ЗДИАП НОП^
 ПВ НАШРПП(М17)^
 СБР Х77777^
 СДА 64+33^
 ЗП РЯ1^
 ВЧ РЯ5^
 ПЕ ОШПАС^
 СЧ РЯ5^
 УИ М16^
 СЧ РЯ5^
 ВЧ РЯ1^
 УИ М17^
ЗДИАП1 СЧ Е48(М16)^
 ИЛИ ЛИСЗАД^
 ЗП ЛИСЗАД^
 СЛИА 1(М16)^
 ЦИКЛ ЗДИАП1(М17)^
 ПБ КАН1^
*^
РАЗОЗУ НОП РАЗДЕЛ ОЗУ^
 ПВ НАШРПП(М17)^
 ПВ ИЗ10В8(М17)^
 ЗП ОЗУЗАД^
 ПБ ВЫБРП1^
*^
РАЗПОВ НОП РАЗДЕЛ ПОВ^
 СЧ ШКТИП^
 И П7^
 ПЕ ПОВПЗ ПОВ В ПЗ^
 ПБ ВЫБРП1^
*^
НАШРПП НОП ВЫБОР ПАРАМЕТРА ПАСПОРТА^
 УИА -6(М16)^
 СЧ 0^
 ЗП РЯ1^
НАШРП3 ПВ ВС(М15)^
 СЛИА -1(М11)^
 ПИО ДЛПАС(М11)^
 СДА 64+40^
 ЗП СИ^
 НТЖ П115^
 ПО НАШРП2 ПАРАМЕТР ВЫБРАН^
 СЧ СИ^
 НТЖ ПЗПТ^
 ПО НАШРП2 ПАРАМЕТР ВЫБРАН^
 СЧ СИ^
 НТЖ ПМИНУС^
 ПО НАШРП2 ПАРАМЕТР ВЫБРАН^
 СЧ СИ^
 НТЖ П17^
 ПО НРП3 ПРОБЕЛ^
 СЧ РЯ1^
 СДА 64-8^
 ИЛИ СИ^
 ЗП РЯ1^
НРП3 ЦИКЛ НАШРП3(М16)^
 ПВ ВС(М15)^
 СЛИА -1(М11)^
 ПИО ДЛПАС(М11)^
 НТЖ ПП115^
 ПЕ ОШПАС ПАРАМ > 6^
НАШРП2 НОП^
 СЧ РЯ1^
 СБР ВСЕПР^
 СДА 64+24^
 ЗП РЯ1^
 ПБ (М17)^
*^
КОНПАС НОП КОНЕЦ ПАСПОРТА^
 СЧ 0^
 ПБ (М6)^
*^
ОШПАС НОП НОП ОШИБКА В ПАСПОРТЕ^
 СЧ Е1^
 ПБ (М6)^
*^
ДЛПАС НОП ДЛИНЫЙ ПАСПОРТ^
 СЧ Е2^
 ПБ (М6)^
*^
ПОВПЗ НОП ПОВ В ПЗ^
 СЧ П3^
 ПБ (М6)^
*^
*  БЛОК ПРОВЕРКИ ДОПУСТИМЫХ РЕСУРСОВ^
*-----------------------------------^
*^
ПРВПАС НОП^
*  ВХОДНЫЕ ЗННАЧЕНИЯ:^
*    ВРЕЗАД - ВРЕМЯ^
*    ТРАЗАД - ТРАКТЫ^
*    ТЕЛЗАД - ТЕЛЕ^
*    АЦПЗАД - АЦПУ^
*    ЛИСЗАД - ЛИСТЫ^
*    ОЗУЗАД - ОЗУ^
*  ВЫХОД ПО М6^
*^
 СЧ ТЕЛЗАД^
 ПО НЕТЕЛЕ НЕ ТЕЛЕ-ЗАДАЧА^
 УИА (М15)^
 СЧ Е1^
 ИЛИ Е7^
 И ШКТИП^
 ПЕ АНАЛИЗ ПЗ^
 СЧ ШКТИП^
 И Е7^
 ПО ПС ПС^
 СЧ КТГФ^
 И П7^
 ВЧОБ КАТЕГП^
 ПЕ ЗАПБЮД ТЕЛЕ ЗАПРЕЩЕНО ПО БЮДЖЕТУ^
ПС НОП^
 УИА 1(М15)^
 ПБ АНАЛИЗ^
*^
НЕТЕЛЕ НОП^
 УИА 3(М15)^
 СЧ ШКТИП^
 И Е5^
 ПЕ АНАЛИЗ СЧЕТ^
 УИА 2(М15)^
*^
АНАЛИЗ НОП^
 УИА ТВРЕ(М16)^
 СЧ ВРЕЗАД^
 ВЧОБ ВРЕОБЩ(М15)^
 ПЕ ОТВЕТ ВРЕМЯ > ДОПУСТ.^
 УИА ТТРА(М16)^
 СЧ ТРАЗАД^
 ВЧОБ ТРАОБЩ(М15)^
 ПЕ ОТВЕТ ТРАКТЫ > ДОПУСТ.^
 УИА ТЛИС(М16)^
 СЧ ЛИСЗАД^
 ЧЕД 0^
 ВЧОБ ЛИСОБЩ(М15)^
 ПЕ ОТВЕТ ЛИСТЫ > ДОПУСТ.^
 УИА ТАЦП(М16)^
 СЧ АЦПЗАД^
 ВЧОБ АЦПОБЩ(М15)^
 ПЕ ОТВЕТ АЦПУ > ДОПУСТ.^
 ПБ (М6)^
*^
ТВРЕ ТЕКСТ П'ВРЕМЯ > ДОПУ'^
 КОНД П'СТ0000'М24В'377'^
ТАЦП ТЕКСТ П'АЦПУ > ДОПУС'^
 КОНД П'Т00000'М32В'377'^
ТТРА ТЕКСТ П'ТРАКТЫ > ДОП'^
 КОНД П'УСТ000'М16В'377'^
ТЛИС ТЕКСТ П'ЛИСТЫ > ДОПУ'^
 КОНД П'СТ0000'М24В'377'^
*^
*  БЛОК ИММИТАЦИИ Э62 '40'^
*-------------------------^
*^
Э6240 НОП^
*^
*  У ТЮРИНА Э62 '40' РАБОТАЕТ КРАЙНЕ МЕРЗКО^
*^
*  ВХОДНЫЕ ЗНАЧЕНИЯ:^
*    РЯ1  - ШИФР^
*    РЯ2  - МАСКА^
*    КШОС - АДРЕС КАТАЛОГА ШИФРОВ ОС ДИСПАК^
*  ВЫХОДНЫЕ ЗНАЧЕНИЯ:^
*    РЯ3  - КОЛ-ВО ЗАДАЧ^
*  РАБОЧИЕ РЕГИСТРЫ:^
*    М7   - ЦИКЛ ПО КАТАЛОГУ^
*    М15  - НОМЕР В КАТАЛОГЕ^
*^
*^
 ПВ ЧТКШИФ(М16) КАТАЛОГ ШИФРОВ ОС ДИСПАК^
*^
 СЧ 0^
 ЗП РЯ3^
 УИА -1(М16)^
 УИА (М15)^
ПРШФ1 УИА -'177'(М7)^
ПРШФ МОДА (М15)^
 МОДА КШОС^
 СЧ '177'(М7)^
 НТЖ РЯ1^
 И РЯ2^
 ПЕ НШИФ ДРУГОЙ ШИФР^
 СЧ РЯ3^
 СЛЦ Е1^
 ЗП РЯ3^
НШИФ ЦИКЛ ПРШФ(М7)^
 УИА '400'(М15) ВТОРОЙ КАТАЛОГ^
 ЦИКЛ ПРШФ1(М16)^
 ПБ (М6)^
*^
*  БЛОК ПРВЕРКИ ГРАФИКА ФОРМИРОВАНИЯ (ДЛЯ ТЕЛЕ-ЗАДАЧ)^
*----------------------------------------------------^
*^
ГРАФИК НОП^
 ПВ ВРЕМЯ(М15)^
 СЧ ВР8Ч^
 СДА 64-1 УМНОЖ. 2^
 УИ М15^
 СЧ ВР8М^
 ВЧ П36^
 ПЕ ГРАФ1 ПЕРВАЯ ПОЛОВИНА ЧАСА^
 СЛИА 1(М15)^
ГРАФ1 НОП^
 СЧ ТЕЛЗАД^
 ПО (М6) НЕ ТЕЛЕ-ЗАДАЧА^
 СЧ Е1^
 ИЛИ Е7^
 И ШКТИП^
 ПО (М6) НЕ ПС И НЕ ТЕЛ^
 УИА ТЗАПТ(М16)^
 СЧ ГРАФ+1^
 И Е48(М15)^
 ПО ОТВЕТ^
 ПВ ЧТУПР1(М16)^
 И Е27^
 ПЕ ОТВЕТ^
 ПБ (М6)^
*^
* БЛОК ПРОВЕРКИ ЗАДАЧ НА БУФЕРЕ ВВОДА^
*------------------------------------^
*^
ПРБУФ НОП^
 ПБ (М17)^
*^
* КОМПЕНСАТОР ВЕРСИЙ ОС^
***********************^
*^
* ВКЛЮЧЕНИЕ РЕЖИМА ФОРМИРОВАНИЯ СЛУЖЕБНЫХ ЗАДАЧ^
*----------------------------------------------^
*^
ВКЛСЛ НОП^
 СЧИ М16^
 ЗП ИР16К^
 СЧ ТИПОС^
 НТЖ Е1^
 ПЕ (М16) НЕ ИПМ^
 Э62 '7236'^
 МОД ИР16К^
 ПБ 0^
*^
* ВЫКЛЮЧЕНИЕ РЕЖИМА ФОРМИРОВАНИЯ СЛУЖЕБНЫХ ЗАДАЧ^
*-----------------------------------------------^
*^
ВЫКСЛ НОП^
 СЧИ М16^
 ЗП ИР16К^
 СЧ ТИПОС^
 НТЖ Е1^
 ПЕ (М16) НЕ ИПМ^
 Э62 '7235'^
 МОД ИР16К^
 ПБ  0^
*^
* ЧТЕНИЕ 1-ОЙ УПРАВЛЯЮЩЕЙ ЯЧЕЙКИ^
*-------------------------------^
*^
ЧТУПР1 НОП^
 СЧИ М16^
 ЗП ИР16К^
 МОД ТИПОС^
 ПБ УПР1^
УПР1 НОП ПЕРЕКЛЮЧАТЕЛЬ^
 УИА 6(М16)^
 ПБ УПР10^
 УИА 1(М16)^
 ПБ УПР10^
 СЧ КТР1^
 ПБ УПР11^
*^
УПР10 НОП^
 Э65 (М16)^
УПР11 НОП^
 МОД ИР16К^
 ПБ 0 ВЫХОД^
*^
* ЧТЕНИЕ КАТАЛОГА ШИФРОВ^
*-----------------------^
*^
ЧТКШИФ НОП^
 СЧИ М16^
 ЗП ИР16К^
 МОД ТИПОС^
 ПБ ПЧТШ^
ПЧТШ НОП ПЕРЕКЛЮЧАТЕЛЬ^
 УИА ЧТШИТМ(М15)^
 ПБ ЧТКШ^
 УИА ЧТШИПМ(М15)^
 ПБ ЧТКШ^
 УИА ЧТШИТМ(М15)^
 ПБ ЧТКШ^
ЧТКШ НОП^
 Э70 (М15)^
 МОД ИР16К^
 ПБ 0 ВЫХОД^
ЧТШИТМ КОНД В'0010030000330545'^
ЧТШИПМ КОНД В'0010030000330000'^
*^
* ЗАКАЗ СИСТЕМНОГО МД ИЛИ ОБЛАСТИ ШИФРОВ^
*---------------------------------------^
*^
ЗАКСМД НОП^
 СЧИ М16^
 ЗП ИР16К^
 МОД ТИПОС^
 ПБ ПЗСМД^
ПЗСМД НОП ПЕРЕКЛЮЧАТЕЛЬ^
 СЧИ М3^
 ПБ ЗМДИТМ^
 СЧ ОБЛШ^
 ПБ ЗМДИПМ^
 СЧИ М3^
 ПБ ЗМДИТМ^
*^
* ВЕРСИЯ ИТМ^
ЗМДИТМ НОП^
 ИЛИ КМДОС^
 Э50 '131'^
 СЧИ М3^
 Э50 '121' ПАРОЛЬ ЧТЕНИЯ^
 МОД ИР16К^
 ПБ 0 ВЫХОД^
*^
* ВЕРСИЯ ИПМ^
ЗМДИПМ НОП^
 ИЛИ КМДОС^
 Э50 '131'^
 МОД ИР16К^
 ПБ 0 ВЫХОД^
*^
* ЗАПСИСЬ РАЗДЕЛА ПАСПОРТА "КОД СИС" ДЛЯ ВЕРСИИ ИПМ^
* ПРИ БЫСТРОМ ФОРМИРОВАНИИ, КОГДА УКАЗАН РАЗДЕЛ "ВЫВ"^
*----------------------------------------------------^
*^
ЗПКСИС НОП^
 СЧ ТИПОС^
 НТЖ Е1^
 ПЕ (М16) ВЕРСИЯ НЕ ИПМ^
 СЧ ПРВЫВ^
 И Е2^
 ПО (М16) НЕТ РАЗДЕЛА "ВЫВ"^
 УИА ТКСИС(М3)^
 УИА -5(М10)^
ЗПКС1 НОП^
 ПВ ВС(М15)^
 НТЖ Е48П41^
 ПО (М16) КОНЕЦ ВСТАВКИ^
 НТЖ Е48П41^
 ПВ ЗСБ(М15)^
 ПБ ЗПКС1^
ТКСИС КОНД П'КОД СИ'^
 КОНД П'С00000'М32В'115'М24В'341'М16В'377'^

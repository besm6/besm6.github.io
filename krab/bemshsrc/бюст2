*
*
*23456789012345678901234567890123456789012345678901234
**********************************************
*                                            *
*    Д И А Л О Г О В А Я    С И С Т Е М А    *
*                К  Р  А  Б                  *
*                                            *
*--------------------------------------------*
*  БЮДЖЕТНО-СТАТИСТИЧЕСАЯ ПОДСИСТЕМА.        *
*  Ч А С Т Ь  2                              *
*  БЛОК БЮДЖЕТА                              *
*
*********************************************
*
*
*+++++++++++++++++++++++++++++++++++++++++++++++++++++
*+++++++++++++++++++++++++++++++++++++++++++++++++++++
*
БЛОБЩ НОП ,  ВХОД ПО СТАРТОВОЙ ДИР-ВЕ "БЮД"
*            ОБЩАЯ ЧАСТЬ БЛОКА БЮДЖЕТ
*
 СЧ ОБОЛЬ
 ЗП РЯБОЛЬ  ПРИЗНАК ПРИГЛАШЕНИЯ
 СЧ ВСЕЕД
 ЗП ФЛОТД    ПРИЗНАК  ОБЩЕГО ДОСТУПА
*
 СЧ Е1
 ЗП РАБАРХ  ПОКА ПЕРВЫЙ АРХИВ
 ЗП АРХИВ
 ПВ ДИСКИ(М14)
 ПО КОНЕЦ   ЗАКАЗ НЕ ПРОШЕЛ
*
 Э70 ЧТСЛЗО  ЗОНА 11. БЮДЖЕТЫ ОТДЕЛОВ И ГРУПП
*
 СЧ РАБШИФ    ШИФР ЗАДАЧИ ПОЛЬЗОВАТЕЛЯ
 СДА 64+16
 ЗП ЗОНРЯ
 ПВ ИЗ10В8(М13)
 МОД РЯ2   НОМЕР ОТДЕЛА В 8-ИЧН.
 СЧ БЮДОТ2
 СБР Е24П1
 ПО АХ7    В ОТДЕЛЕ НЕТ АРХИВОВ ===>  АРХИВ N1
 НЕД
 ЗП РАБАРХ
 ПБ АРХНАЧ  ОТКАЗ ОТ АРХ 1 И ЗАКАЗ АРХ ОТДЕЛА
*
*=====================================================
*+++++++++++++++++++++++++++++++++++++++++++++++++++++
*
НАЧБЮД НОП , ВХОД ДЛЯ ПРИЕМА СЛЕДУЮЩЕЙ ДИРЕКТИВЫ
*
*+++++++++++++++++++++++++++++++++++++++++++++++++++++
*=====================================================
*
 СЧ 0
 ЗП БЮДЖП  БЛОК БЮДЖЕТА
 ЗП БЮДЖП+1
 ЗП БЮДЖП+2
ББ1 СЧ РАБШИФ
 ПО КОНЕЦ
 СЧ
 ЗП ГРУППА
ББ6 СЧ ФЛЗАХ
 ПО КОНЕЦ
 ПВ ПРИЕМ(М13)
ББ7 НОП
 ПВ ПРИГЛ(М15)
 ПВ НАЙПРО(М15)
 СЧ РЯВС
 НТЖ Е8П1
 ПО ББ6   ПРОПУСК ПУСТОЙ ПОСЫЛКИ
*
*  ВЫБИРАЕМ ДИРЕКТИВУ
*
 ПВ ВЫБДИР(М17)
 ПВ ПАМВС(М13)
*
*     ПОИСК ДИРЕКТИВЫ
*
   УИА 0(М5)
К2 СЧ   ПЕРЕКЛ(М5)
   ПО   ОШНАЧ  ДИРЕКТИВУ НЕ НАШЛИ
   НТЖ  ТЕКДИР   ТЕКУЩАЯ ДИРЕКТИВА
   И    МАСДИР
   ПО   ПЕРЕКЛ(М5)  НАШЛИ
   СЛИА 1(М5)
   ПБ   К2
*
ОШНАЧ НОП
      ПВ НЕТДИР(М13)
      ПБ ББ1
*
ПЕРЕКЛ НОП
       ПБ   НОВ       НОВ - ЗАВЕДЕНИЕ НОВ.ПОЛЬЗ.
       КОНК П'НОВ'
       ПБ ПРО         БЮД - ПРОСМ. БЮДЖ. ОТДЕЛА
       КОНК П'БЮД'
       ПБ   ОТД       ОТД - ЗАДАНИЕ БЮДЖ.ОТДЕЛА
       КОНК П'ОТД'
       ПБ   ПАР       ПАР - ПАРОЛЬ АДМ, АДМОТД
       КОНК П'ПАР'
       ПБ   ПАРДИС    ПРД - ПАРОЛЬ ДИСПЕТЧЕРА
       КОНК П'ПРД'
 ПБ ПЕЧ
 КОНК П'ПЕЧ'
       ПБ   КОНЕЦ     КОН - КОНЕЦ РАБОТЫ В БСС
       КОНК П'КОН'
       ПБ   АРХ       АРХ - УСТАНОВКА АРХИВА
       КОНК П'АРХ'
       ПБ   НОМ       НОМ - ВЫЗОВ БЮДЖ.ПОЛ.НА РП
       КОНК П'НОМ'
       ПБ   КБЮ       КБЮ - КОНЕЦ РАБОТЫ В БЮДЖ.
       КОНК П'КБЮ'
       ПБ   ИНФ       ИНФ - УСТАНОВКА ПРИЗН. ИНФ
       КОНК П'ИНФ'
 ПБ ШТР
 КОНК П'ШТР'
 ПБ ЗОН
 КОНК П'ЗОН'
 ПБ ДАЙ
 КОНК П'ДАЙ'
 ПБ ИСК
 КОНК П'ИСК'
      ПБ   СИС
      КОНК П'СИС'
      ПБ   ГРУ
      КОНК П'ГРУ'
      ПБ   ЧЕЛ
      КОНК П'ЧЕЛ'
      ПБ   БЛБЮД
      КОНК П'АДМ'
      ПБ   ДИСП
      КОНК П'ДИС'
      ПБ   УНЧ
      КОНК П'УНЧ'
      ПБ   ИТГ
      КОНК П'ИТГ'
      ПБ   КТГ
      КОНК П'КТГ'
      ПБ   ФИОИЗМ
      КОНК П'ФИО'
      ПБ   БЮДЭКР
      КОНК П'ЭКР'
      ПБ   БЮДАЦП
      КОНК П'АЦП'
*
       КОНД В'0'  КОНЕЦ ПЕРЕКЛЮЧАТЕЛЯ ДИР-В
*
*      ---------------------------------------
*
*
*$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
*
ДИСП НОП ,  ДИРЕКТИВА  "ДИС"
*
 СЧ Е9П1  '777' - МЕСТО РАСПОЛОЖЕНИЕ ПАРОЛЯ ДИСПЕТ.
 ЗП РЯ11  ДЛЯ "КТОАДМ"
 ПВ КТОАДМ(М10)
 ПЕ АДМ6  НЕВЕРНО ЗАДАН ПАРОЛЬ
*
* ПАРОЛЬ ПОДТВЕРЖДЕН
*
 СЧ Е1
 ЗП ФЛДИСП  УСТАНОВИЛИ ФЛАГ ДИСПЕТЧЕРА
 СЧ ДБОЛЬ
 ЗП РЯБОЛЬ  ЭТО ПРИГЛАШЕНИЕ
 ПБ НАЧБЮД
*
******************************************************
*
БЛБЮД НОП ,  ДИРЕКТИВА  "АДМ"
*
******************************************************
*
 СЧ
 ЗП ФЛДИСП СНЯЛИ РЕЖИМ ДИСП.
 ЗП РЯ11  0-Й ОТНОСИТ. АДР. СЛОВА ПАРОЛЯ АДМ-РА
 ПВ ПОДГОТ(М13)
 ПВ НАЙПРО(М15)
 СЧ РЯВС
 ЗП ЗОНРЯ
 НТЖ Е8П1
 ЗП ФЛОТД
 ПО АДМ3
*
АДМ2 УИА -1(М5)
 ПВ ЧИСЛА(М15)
 СЧ ЗОНРЯ
 ЗП ФЛОТД
 ЗП БОТ1
 ПВ ИЗ10В8(М13)
 СЧ РЯ2
 ЗП РЯ11  ОТНОСИТ. АДРЕС СЛОВА ПАРОЛЯ ОТДЕЛА
АДМ3 НОП
 ПВ КТОАДМ(М10)
 ПО ББ3 ПАРОЛЬ ПОДТВЕРЖДЕН
 СЧ ФЛОТД
 ПЕ АДМ4
АДМ6 СЧ Е26
 ЗП ДШТР
 ПБ АДМ5
АДМ4 СЧ РАБШИФ
 СДА 64+16
 НТЖ БОТ1
 ПЕ АДМ6
 СЧ БОТ1
 СДА 64-16
 НТЖ РАБШИФ
 ПО КОНЕЦ
 СЧ Е27
 ЗП ДШТР
АДМ5 СЧ РАБШИФ
 ЗП ШФР
 ПВ ПОЛКЛЮ(М13)
 СЧ РЯДУР
 ПО КОНЕЦ
 СЧ БЮДЖП+1
 ИЛИ ДШТР
 ЗП БЮДЖП+1
 СЧ
 ЗП РАБШИФ
 УИА ВЫВОШ(М1)
 ПВ ВЫВОД(М17)
 ПБ КОНЕЦ  <--------->   ПБ ЗАПШМ ЗАМЕНИТЬ!!!!!!
НОЛПАР УИА НЕМГ(М1)
 ПВ ВЫВОД(М17)
 ПБ КОНЕЦ
ББ3 НОП
 СЧ ФЛОТД
 ПО НАЧБЮД  АДМИНИСТРАТОР СИСТЕМЫ
*
 СЧ РЯ11
 ПВ ПЕРЕВ(М13)
 ЗП РЯ11
 ИЛИ ППППОО
 ЗП ЗГПОТ+2
 УИА ЗГПОТ(М1)
 ПВ ВЫВОД(М17)
 СЧ РЯ11
 СДА 64-32
 ИЛИ ОТБОЛЬ    ПРИГЛАШЕНИЕ ДЛЯ АДМ.ОТДЕЛА
 ЗП РЯБОЛЬ
 ПБ НАЧБЮД
*
*$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
*
БЮДЭКР НОП ,  ПЕРЕКЛЮЧЕНИЕ  Р Е Ж И М А   В Ы В О Д А
*
*
 ПВ ПОДГОТ(М13)
 ПВ ДАНЕТ(М16)
 ПВ ЭКР(М13)
 ПБ НАЧБЮД
*
*$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
*
БЮДАЦП НОП ,  ПЕРЕКЛЮЧЕНИЕ  Р Е Ж И М А   П Е Ч А Т И
*
*
 ПВ ПОДГОТ(М13)
 ПВ ДАНЕТ(М16)
 ПВ АЦП(М13)
 ПБ НАЧБЮД
*
*$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
*
АРХ НОП ,  БЛОК УСТАНОВКИ  А Р Х И В А
*
*
 ПВ ПОДГОТ(М13)
 ПВ НАЙПРО(М15)
 СЧ РЯВС
 НТЖ Е8П1
 ПО АРХТЕК   ПРОСМОТР НОМЕРА ТЕКУЩЕЙ ЧАСТИ АРХИВА
 ПВ ПРОЧИС(М13)
 СЧ РЯВС
 ЗП РАБАРХ
 ПВ ВС(М13)
 НТЖ Е8П1
 ПО АРХНАЧ
 ПВ ПРОЧИС(М13)
 СЧ РАБАРХ
 СДА 64-4
 ИЛИ РЯВС
 ЗП РАБАРХ
 ПВ ВС(М13)
 НТЖ Е8П1
 ПЕ ОШИФР    ОШИБКА В ПАРАМЕТРЕ
 СЧ РАБАРХ
 ПО НЕТБИБ     НЕТ АРХИВА 0
 ЗП ЗОНРЯ
 ПВ ИЗ10В8(М13)
 СЧ РЯ2
 ЗП РАБАРХ
АРХНАЧ СЧ РАБАРХ
 ВЧОБ КОЛАРХ
 ПЕ НЕТБИБ    НЕТ АРХИВА В ПАРАМЕТРАХ ГЕНЕРАЦИИ
АХ2 ПВ ДИСОТК(М13)
 ПВ ДИСКИ(М14)
 ПЕ АХ7  ЗАКАЗ ПРОШЕЛ
 ПВ НЕТДИС(М14)
 СЧ АРХИВ
 ЗП РАБАРХ
 ПВ ДИСКИ(М14)
 ПО КОНЕЦ
АХ7 НОП
 ПВ НАСАРХ(М17)  НАСТРОЙКА НА АРХИВ
 ПБ НАЧБЮД
*
АРХТЕК УИА НОМАРХ(М1)
 ПВ ВЫВОД(М17)
 ПБ НАЧБЮД
*
*
НЕТБИБ СЧ НЕБИБ+1
 И Е48П9
 ИЛИ РАБАРХ
 ЗП НЕБИБ+1
 УИА НЕБИБ(М1)
 ПВ ВЫВОД(М17)   "НЕТ АРХИВА N"
 СЧ АРХИВ
 ЗП РАБАРХ
 ПБ ББ1
*
*-------------------------------------------------
*
НАСАРХ НОП , НАСТРОЙКА НА АРХИВ: "ПВ НАСАРХ(М17)"
 ПВ ЧКТАРХ(М16)
 СЧ РАБАРХ
 ЗП АРХИВ
*
 УИ М14
 СЧ ТАРМДК-1(М14)
 СДА 64+16
 И Е12П1
 ЗП РЯ1   НАЧАЛО АРХИВА
 СЧ ПАРМД-1(М14)
 И Е12П1  КОНЕЦ  АРХИВА
 ВЧ РЯ1
 ВЧ КЗОНКТ  КОЛИЧ.ЗОН ОТВЕДЕННЫХ ПОД КАТАЛОГ
 СЛЦ Е1
 ЗП ДЛНАРХ  ДЛИНА АРХИВА
 ПБ (М17)  ВОЗВРАТ
*
*$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
*
ПРО НОП ,  БЛОК ПРОСМОТРА БЮДЖЕТА ОТДЕЛА
*
*
 ПВ ПОДГОТ(М13)
 ПВ НАЙПРО(М15)
*
 СЧ РЯВС
 ВЧОБ П11
 ПО ПРООТД   БЮДЖЕТ ОТДЕЛА
*
 СЧ РЯВС
 НТЖ СИСБЮД
 ПО ПРОСИС  БЮДЖЕТ СИСТЕМНОГО ВХОДА
 ПБ ОШИФР
*
ПРООТД СЧ РЯВС
 ЗП ЗОНРЯ
 УИА -1(М5)
 ПВ ЧИСЛА(М13)
 ЗП БОТ1 НОМЕР ОТДЕЛА В Д-Д
 ПВ КУКУ(М7)
 ПБ ПРОВЫД  НА ВЫДАЧУ СТРОКИ
*
ПРОСИС НОП
 ПВ ФОРСИС(М11)
*
ПРОВЫД НОП
 УИА ЗГБОТД(М1)
 ПВ ВЕРТУШ(М13)
 УИА БЮДОТД(М1)
 ПВ ВЕРТУВ(М13)  ВЕРТУШКА С ВЫВОДОМ
 ПБ НАЧБЮД
*
******************************************************
*
ФОРСИС НОП , ПОЛУЧЕНИЯ СТРОКИ БЮДЖЕТА СИС.ВХОДА
*
 СЧ Е8П1
 ЗП РЯ5  ШИФР ПОЛЬЗОВАТЕЛЯ
 ПВ НСПОЛ(М13)  ИМЕЕТ ЗОН: РЯ7 - БЗОН; РЯ8 - ЗЗОН
*
 СЧ РЯ7
 ЗП БЮДТ
 ЗП ИТБЗОН  ДЛЯ ИТОГА
*
 СЧ РЯ8
 ЗП РАСП
 ЗП ИТПЗОН  ДЛЯ ИТОГА
*
*    ГОТОВИМ СТРОКУ БЮДЖЕТА
*
 СЧ ТСИС
 ЗП БОТ1  ТЕКСТ "СИС"
*
 Э70 ЧТКЛЮ1
 СЧ КЛЮЧИ  Е48-Е24 ШКАЛА АРХИВОВ
 ПВ ДАЙАРХ(М17)
 СЧ ШКАРХ
 ЗП АРХОТ
 СЧ ШКАРХ+1
 ЗП АРХОТ+1
*
 УИА -1(М10)
ФОРС2 СЧ БЮДТ+1(М10) ПОДГОТОВКА ИНФ. О ЗОНАХ
 ПВ ПЕРЕВ(М13)
 СДА 64-8
 ИЛИ МАСЗОН
 ЗП БЮДТ+1(М10)
 ЦИКЛ ФОРС2(М10)
*
 СЧ ПППППЗ
 ЗП КЗОНОБ
*
 СЧ ППППЗЗ
 ЗП БНОМ
 ЗП ЗНОМ
 ЗП БКТГ
*
 СЧ ПППЗЗЗ
 ЗП БГР
 ЗП ЗГР
 ЗП КТРОТД
 ЗП КЗАД
 ЗП БФИО
 ЗП ЗФИО
*
 ПБ (М11)  ВОЗВРАТ
*
******************************************************
*
КУКУ НОП , ПОЛУЧЕНИЯ СТРОКИ БЮДЖЕТА ОТДЕЛА
*          А ТАКЖЕ И ИТОГОВЫХ ПАРАМЕТРОВ
*          ДЛЯ СВОДКИ ПО ГРУППЕ ОТДЕЛОВ
*
 СЧ БОТ1
 ЗП НОМОДД
 ЗП РЯ6
 ПВ НСОТ(М13)
*
 СЧ РЯ7   СВОБ.ЗОНЫ ОТДЕЛА
 СЛЦ РЯ8    РАСПРЕД.ПОЛЬЗ.
 ЗП БЮДТ  БЮДЖЕТ ЗОН ОТДЕЛА (8)
 СЛЦ ИТБЗОН
 ЗП  ИТБЗОН   ИТОГ:БЗОН
 СЧ РЯ8
 ВЧ РЯ12
 ЗП РАСП    РАСПРЕДЕЛЕНО ПОЛЬЗ.(БЕЗ ГРУПП)
 СЛЦ ИТПЗОН
 ЗП  ИТПЗОН   ИТОГ:ПЗОН
 СЧ РЯ12
 ЗП КЗОНОБ
 СЛЦ ИТГЗОН
 ЗП  ИТГЗОН   ИТОГ:ГЗОН
 СЧ РЯ9
 ЗП ЗАН
*
 ПВ ГРУЛЮД(М13)
 СЧИ М2
 ЗП ЗГР  ЗАНЯТО ГРУПП
 СЛЦ ИТЗГ
 ЗП  ИТЗГ     ИТОГ:ЗГР
*
 СЧ БОТ1
 ЗП РЯ5
 СЧ RСДВИГ
 ЗП РЯ4
 ПВ ЛЮДИ(М13)  ПОДСЧЕТ ЗАНЯТЫХ НОМЕРОВ
 СЧ РЯ6
 СЛЦ РЯ1
 ЗП ЗНОМ  РАСПРЕДЕЛЕНО НОМЕРОВ ГРУППАМ И 4-Х ЗНЧ ПОЛЬЗ
 СЛЦ ИТЗНМ
 ЗП  ИТЗНМ    ИТОГ:ЗНМ
 СЧ БОТ1
 ЗП ЗОНРЯ
 ПВ ИЗ10В8(М13)
 СЧ РЯ2
 ЗП НОМОТД
 МОД НОМОТД
 СЧ БЮДОТ1
 И Е47П45
 СДА 64-12
 СЧМР
 ИЛИ ЗЗППОП
 ЗП БКТГ   КАТЕГОРИЯ ОТДЕЛА
*
 СЧ РАБФИО
 ПО КУКУ21  А С ФИО РАБОТАТЬ ЗАПРЕЩЕНО
*
*  ВЫЧИСЛЕНИЕ ПАРАМЕТРОВ ПО ФИО
*
 МОД НОМОТД
 СЧ БЮДОТ1
 И Е8П1
 ПВ ПЕРЕВ(М13)
 СДА 64-24
 ИЛИ МСФИО
 ЗП БФИО
 ПВ ФИОЧИС(М13)
 СЧИ М3
 ПВ ПЕРЕВ(М13)
 СДА 64-24
 ИЛИ МСФИО
 ЗП ЗФИО   КОЛИЧЕСТВО ПОЛЬЗОВАТЕЛЕЙ ОТДЕЛА В ОС ДИСПАК
*
КУКУ21 НОП
 МОД НОМОТД
 СЧ БЮДОТ1
 СДА 64+8
 И Е8П1
 ЗП БНОМ    ЧИСЛО НОМЕРОВ ПО БЮДЖЕТУ
 СЛЦ ИТБНМ
 ЗП  ИТБНМ    ИТОГ:БНМ
*
 МОД НОМОТД
 СЧ БЮДОТ1
 И Е21П17
 СДА 64+16
 ЗП БГР  БЮДЖЕТ ПО ЧИСЛУ ГРУПП
 СЛЦ ИТБГ
 ЗП  ИТБГ     ИТОГ:БГ
*
 МОД НОМОТД
 СЧ БЮДОТ1
 И Е43П38
 СДА 64+37
 ЗП КЗАД
 СЛЦ ИТКЗ
 ЗП  ИТКЗ     ИТОГ:КЗ
*
*    ГОТОВИМ СТРОКУ БЮДЖЕТА
*
 УИА -2(М10)
ПРО3 СЧ БЮДТ+2(М10) ПОДГОТОВКА ИНФ. О ЗОНАХ
 ПВ ПЕРЕВ(М13)
 СДА 64-8
 ИЛИ МАСЗОН
 ЗП БЮДТ+2(М10)
 ЦИКЛ ПРО3(М10)
*
 СЧ БНОМ
 ПВ ПЕРЕВ(М13)
 СДА 64-16
 ИЛИ МАСБНГ
 ЗП БНОМ
*
 СЧ ЗНОМ
 ПВ ПЕРЕВ(М13)
 СДА 64-16
 ИЛИ МАСБНГ
 ЗП ЗНОМ
*
 СЧ БГР
 ПВ ПЕРЕВ(М13)
 СДА 64-24
 ИЛИ МСФИО
 ЗП БГР
*
 СЧ ЗГР
 ПВ ПЕРЕВ(М13)
 СДА 64-24
 ИЛИ МСФИО
 ЗП ЗГР
*
 СЧ КЗАД  КОЛИЧ. ЗАДАЧ
 ПВ ПЕРЕВ(М13)
 СДА 64-24
 ИЛИ МСФИО
 ЗП КЗАД
*
 СЧ КТРВКЛ  КТР.ВКЛЮЧЕН
 ЗП КТРОТД
 МОД НОМОТД
 СЧ БЮДОТ1
 И Е44
 ПЕ ФОРАРХ   КОНТРОЛЛЕР  ВКЛЮЧЕН
 СЧ КТРВЫК  КОНТРОЛЛЕР ВЫКЛЮЧЕН
 ЗП КТРОТД
*
ФОРАРХ НОП
 МОД НОМОТД  ТЕПЕРЬ ЗАНЕСЕМ АРХИВЫ
 СЧ БЮДОТ2
 СДА 64-24
 ПВ ДАЙАРХ(М17)
 СЧ ШКАРХ
 ЗП АРХОТ
 СЧ ШКАРХ+1
 ЗП АРХОТ+1
*
 СЧ БОТ1  НОМЕР ОТД. Д-Д
 СДА 64+8
 СЧМР
 РЗБ ОППООО
 ЗП БОТ1  НОМЕР ОТДЕЛА
*
 МОД НОМОТД
 СЧ БЮДОТ1
 И Е48
 ПЕ ПРОЗВ  ДИНАМ.БЮДЖЕТ
 СЧ БОТ1
 ИЛИ ПООЗЗЗ  - ПРОБЕЛ - СТАТ. БЮДЖ.
 ЗП БОТ1
 ПБ (М7)
*
ПРОЗВ СЧ БОТ1  ДИНАМИЧЕСКИЙ БЮДЖЕТ
 ИЛИ МАСДИН  МАСКА ДЛЯ ДИНАМИЧЕСКОГО БЮДЖ.
 ЗП БОТ1
 ПБ (М7)
*
*$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
*
ПЕЧ НОП ,  ПЕЧАТЬ СВОДОК
*
*
*    ШТШ  -  NЕ 0 - ВЫБОР ТОЛЬКО ОШТРАФОВАННЫХ ПОЛЬЗ.;
*            ЕQ 0 - ВЫБОР ВСЕХ;
*    ТЕКП -  NЕ 0 - ВЫБОР ТОЛЬКО ДЛЯ ТЕКУЩЕГО АРХИВА;
*            ЕQ 0 - ВЫБИРАЙ ВСЕХ.
*
*
 ПВ УПР(М17)  УПОРЯДОЧИВАНИЕ КЛЮЧЕЙ, ЕСЛИ НАДО!!
*
 СЧ ВСЕЕД
 ЗП РЯ0
 СЧ ПППППП
 ЗП РЯШФ
 СЧ
 ЗП ШТШ
 ЗП КООБЗ2
 ЗП ТЕКП
*
 ПВ ПОДГОТ(М13)
 ПВ НАЙПРО(М15)
 СЧ РЯВС
 НТЖ Е8П1
 ПО НАЧПЕЧ   ПЕЧАТАЕМ ПОЛНУЮ СВОДКУ
*
ППП0 НОП ,  ПОИСК ОЧЕРЕДНОЙ ПОДДИР-ВЫ
 ПВ ВЫБДИР(М17)
*
 УИА 0(М7)
ППП1 НОП
 СЧ ПЕРПЕЧ(М7)
 ПО ОШИФР  НЕТ ПАРАМЕТРА
 НТЖ ТЕКДИР
 И МАСДИР
 ПО ПЕРПЕЧ(М7)       НА ИСПОЛНЕНИЕ ПОДДИР-ВЫ.
 СЛИА 1(М7)
 ПБ ППП1
*
*      ПЕРЕКЛЮЧАТЕЛЬ  ПОДДИРЕКТИВ
*
ПЕРПЕЧ НОП
       ПБ ПЕЧШТР
       КОНК П'ШТР'
       ПБ ПЕЧОТД
       КОНК П'ОТД'
ЭТОГРУ ПБ ПЕЧГ
       КОНК П'ГРУ'
       ПБ ПЕЧТЕК
       КОНК П'ТЕК'
       ПБ ПЕЧКТГ
       КОНК П'КТГ'
*
       КОНД В'0'  КОНЕЦ ПЕРЕКЛЮЧАТЕЛЯ
*
*   ОПИСАНИЕ ПОДДИРЕКТИВ
*
*
*
АЦП1 ПВ НАЙПРО(М15)
 СЧ РЯВС
 НТЖ Е8П1
 ПО НАЧПЕЧ
 ПБ ППП0    НА ПОЛУЧЕНИЕ ОЧЕРЕДНОЙ ПОДДИРЕКТИВЫ
*
*
ПЕЧТЕК НОП
 МОД АРХИВ
 СЧ Е48-1
 ЗП ТЕКП
 ПБ АЦП1  НА ПРЕДВАР. ДЕЙСТВИЯ ПО ПОЛУЧ.ПОДДИР.
*
*
ПЕЧШТР НОП
 СЧ ВСЕЕД
 ЗП ШТШ <>0  ПРИЗНАК ВЫБОРА ОШТРАФОВАННЫХ
 ПБ АЦП1
*
*
ПЕЧКТГ НОП
 ПВ НАЙПРО(М15)
 СЧ РЯВС
 ВЧОБ Е3П1
 ПЕ ОШИФР
 СЧ РЯВС
 ЗП РЯ0  НОМЕР КАТЕГОРИИ
 ПВ НАЙПРО(М15)
 СЧ РЯВС
 НТЖ Е8П1
 ПО НАЧПЕЧ
 ПБ АЦП1  НА ПРЕДВАР. ДЕЙСТВИЯ ПО ПОЛУЧ.ПОДДИР.
*
*
ПЕЧОТД НОП
 СЧ
 ЗП РЯШФ
 СЧ RСДВИГ
 ЗП ПРИЗНК   СДВИГ
 ПВ НАЙПРО(М15)
 СЧ РЯВС
 НТЖ Е8П1
 ПО НАЧПЕЧ  ПЕЧАТЬ СВОДКИ ПО ОТДЕЛАМ
*
*  ЗАКАЗАНА СВОДКА ПО КОНКРЕТНОМУ ОТДЕЛУ
*
 УИА -1(М5)
 СЧ РЯВС
 ВЧОБ П11
 ПЕ ППП0
 СЧ РЯВС
 ЗП ЗОНРЯ
 ПВ ЧИСЛА(М13)
 СЧ ЗОНРЯ
 ЗП РЯШФ    НОМЕР ОТДЕЛА В Д-Д
 СДА 64-24
 РЗБ ПППППП
 СДА 64-32
 ИЛИ ООПППП
 ЗП ЗГПОТ+2
 СЧ РЯВС
 НТЖ Е8П1
 ПО НАЧПЕЧ
 ПБ АЦП1  НА ПРЕДВАР. ДЕЙСТВИЯ ПО ПОЛУЧ.ПОДДИР.
*
*
ПЕЧГ ПВ НАЙПРО(М15)
 СЧ РЯВС
 НТЖ Е8П1
 ПО ГРУВСЕ     СВОДКА ПО БЮДЖЕТУ ВСЕХ ГРУПП
 СЧ РЯВС
 ВЧОБ П11
 ПО ПЕЧГ00      НА ПОЛУЧЕНИЕ ШИФРА ГРУППЫ
 СЧ ВСЕЕД
 ЗП РЯШФ
 ПБ ППП0   НА ПОЛУЧЕНИЕ ОЧЕРЕДНОЙ ПОДДИРЕКТИВЫ
ПЕЧГ00 НОП
 СЧ RСДВИГ+1
 ЗП ПРИЗНК
 УИА -3(М5)
 СЧ РЯВС
 ЗП ЗОНРЯ
 ПВ ЧИСЛА(М13)
 СЧ ЗОНРЯ
 ЗП РЯШФ
 СДА 64-24
 РЗБ ПППППП
 СДА 64-8
 ИЛИ ПООООП
 ЗП НОМГР
 СЧ РЯВС
 НТЖ Е8П1
 ПО НАЧПЕЧ
 ПБ АЦП1  НА ПРЕДВАР. ДЕЙСТВИЯ ПО ПОЛУЧ.ПОДДИР.
*
*=====================================================
*
НАЧПЕЧ НОП ,  НАЧАЛО  ДИРЕКТИВЫ  "П Е Ч"
 СЧ 0
 ЗП СЧПОЛ  СЧЕТЧИК ПОЛЬЗОВАТЕЛЕЙ
 УИА -NSР+1(М5)  КОЛИЧЕСТВО СУММИР. (ИТОГ) ПАР.
ПОТД0 ЗП ИТОГ+NSР-1(М5)
 ЦИКЛ ПОТД0(М5)    ЧИСТИМ ПОЛЕ ДЛЯ ИТОГА
*
 УИА НОМАРХ(М1)
 ПВ ВЕРТУШ(М13)
 СЧ РЯШФ
 НТЖ ПППППП
 ПЕ ПП333
 ЗП РЯШФ
 ПБ ПП15
ПП333 СЧ РЯШФ
 ПО ПОТД     СВОДКА ПО ВСЕМ ОТДЕЛАМ
 НТЖ ВСЕЕД
 ПО ГРУВСЕ   СВОДКА ПО ВСЕМ ГРУППАМ
 СЧ ПРИЗНК
 НТЖ RСДВИГ
 ПО ПП95   СВОДКА ПО ПОЛЬЗОВАТЕЛЯМ ОТДЕЛА
 УИА СВОГРУ(М1) СВОДКА ПО ПОЛЬЗОВАТЕЛЯМ ГРУППЫ
 ПБ ПП999
ПП95 УИА ЗГПОТ(М1)
ПП999 ПВ ВЕРТУШ(М13)
ПП15 УИА ЗГБПОЛ(М1)
 ПВ ВЕРТУШ(М13)
*
*
ПП11 УИА -3(М2)
ПП12 УИА -1024(М5)
 Э70 ЧТКЛЮ1+3(М2)   ЧТЕНИЕ ОЧЕРЕДНОЙ ЗОНЫ
ПП19 СЛИА 2(М5)
 СЧ КЛЮЧИ+1022(М5)
 НТЖ ВСЕЕД
 ПО ПП73   КОНЕЦ СВОДКИ
 СЧ КЛЮЧИ+1022(М5)
 И Е24П1
 ЗП ШФР
 ПО ПП13
*
 СЧ РЯ0
 НТЖ ВСЕЕД
 ПО ПП197   НЕТ ПРОВЕРКИ НА КТГ
 СЧ КЛЮЧИ+1023(М5)
 СДА 64+4
 И Е3П1
 НТЖ РЯ0
 ПЕ ПП13   КАТАГОРИЯ ПОЬЗОВАТЕЛЯ <> КТГ. ВЫБОРА
ПП197 НОП
 СЧ ТЕКП
 ПО ПП190   НЕТ ПРОВЕРКИ НА ТЕКУЩИЙ АРХИВ
 СЧ КЛЮЧИ+1022(М5)
 И ТЕКП
 ПО ПП13    АРХИВ ЗАКРЫТ
ПП190 СЧ РЯШФ
 ПО ПП9
 СЧ ШФР
 СД ПРИЗНК
 НТЖ РЯШФ
 ПЕ ПП13
 СЧ ПРИЗНК
 НТЖ RСДВИГ+1
 ПЕ ПП9
 СЧ ШФР
 И Е8П1
 ПО ПП13    ПОЛЬЗОВАТЕЛЬ НЕ ГРУППОВОЙ
ПП9 НОП
 СЧ КЛЮЧИ+1022(М5)
 ЗП БЮДЖП+1
 СЧ КЛЮЧИ+1023(М5)
 ЗП БЮДЖП+2
 СЧ ШТШ
 ПО ПП99
 СЧ БЮДЖП+2
 СДА 64+24
 И Е4П1
 ПО ПП13     НЕТ ПРИЗНАКА ШТРАФА
ПП99 СЧИ М5
 ЗП ИР5
 СЧИ М2
 ЗП ИР2
 ПВ ФОРМ(М11)
*
 УИА БЮДПОЛ(М1)
 ПВ ВЕРТУШ(М13)
*
 СЧ ИР5  ВОССТАНОВЛЕНИЕ ИР-ОВ
 УИ М5
 СЧ ИР2
 УИ М2
ПП13 ПИНО ПП19(М5)   НА ОБРАБОТКУ СЛЕДУЮЩЕГО
 ЦИКЛ ПП12(М2)    ЧТЕНИЕ ОЧЕРЕДНОЙ ЗОНЫ КЛЮЧЕЙ
*
*-----------------------------------------------------
*
*    ПОДГОТОВКА ИТОГОВОЙ СТРОКИ ДЛЯ СВОДКИ ПО
* БЮДЖЕТАМ ПОЛЬЗОВАТЕЛЕЙ
*
ПП73 НОП
 УИА РЗБПОЛ(М1)  СТРОКА РАЗДЕЛИТЕЛЕЙ
 ПВ ВЕРТУШ(М13)
*
 УИА -1(М5)
ПП73А НОП , ПЕРЕВОД : БЗОН, ИЗОН
 СЧ ИТБЗОН+1(М5)
 ПВ ПЕРЕВ(М13)
 СДА 64-8
 ИЛИ МАСЗОН
 ЗП ИПБЗОН+1(М5)  ИТОГ ПОЛЬЗ. : БЗОН
 ЦИКЛ ПП73А(М5)
*
 УИА ИТГПОЛ(М1)
 ПБ ПЕЧЗАВ  НА ЗАВЕРШЕНИЕ ДИР-ВЫ "ПЕЧ"
*
******************************************************
*
*         ПЕЧАТЬ СВОДКИ ПО ВСЕМ ГРУППАМ
*
*
ГРУВСЕ НОП
 УИА ЗГБГРУ(М1)
 ПВ ВЕРТУШ(М13)
*
 УИА -99(М3)
ПЕЧГ1 СЧ БЮДГР1+99(М3)
 ПО ПЕЧГ2
 ЗП ГРУППА
 НТЖ ВСЕЕД
 ПО ПЕЧГ99
*
 СЧ БЮДГР2+99(М3)
 ЗП ГРУППА+1
 СЧ РЯ0
 НТЖ ВСЕЕД
 ПО ПЕЧГ4   НЕТ ПРОВЕРКИ НА КТГ
 СЧ ГРУППА
 СДА 64+44
 И Е3П1
 НТЖ РЯ0
 ПЕ ПЕЧГ2
ПЕЧГ4 НОП
 СЧ ТЕКП
 ПО ПЕЧГ3   НЕТ ПРОВЕРКИ НА ТЕКУЩИЙ АРХИВ
 СЧ ГРУППА+1
 СДА 64-24
 И ТЕКП
 ПО ПЕЧГ2   АРХИВ ЗАКРЫТ
ПЕЧГ3 НОП
 СЧИ М3
 ЗП ИР3
 ПВ ФОРГР(М11)
 СЧ ИР3
 УИ М3
 УИА БЮДГРУ(М1)
 ПВ ВЕРТУШ(М13)
ПЕЧГ2 ЦИКЛ ПЕЧГ1(М3)
*
ПЕЧГ99 НОП ,  КОНЕЦ СВОДКИ ПО БЮДЖ. ГРУПП
 УИА М40377(М1)
 ПБ ПЕЧЗАВ  НА ЗАВЕРШЕНИЕ ДИР-ВЫ "ПЕЧ"
*
******************************************************
*
ПОТД НОП ,  ПЕЧАТЬ СВОДКИ ПО ВСЕМ ОТДЕЛАМ
*
 УИА ЗГБОТД(М1)
 ПВ ВЕРТУШ(М13)
*
 ПВ ФОРСИС(М11)  ФОРМИРУЕМ ИНФ. О СИС.ВХОДЕ
 СЧ РЯ7  БЗОН
 ПО ПОТД1А  ЕСЛИ 0, ТО НЕ ВЫДАЮ
 УИА БЮДОТД(М1)
 ПВ ВЕРТУШ(М13)
*
ПОТД1А НОП
 УИА -98(М5) 0-Й ОТДЕЛ НЕ НУЖЕН
ПОТД1 НОП
 СЧ БЮДОТ1+99(М5)
 ИЛИ БЮДОТ2+99(М5)
 ПО ПОТД4  ОТДЕЛ НЕ ИМЕЕТ БЮДЖЕТА
*
 СЧ РЯ0
 НТЖ ВСЕЕД
 ПО ПОТД2  КАТЕГОРИЯ ЛЮБАЯ
*
 СЧ БЮДОТ1+99(М5)
 СДА 64-1
 СДА 64-3
 СЧМР
 НТЖ РЯ0
 ПЕ ПОТД4  ДРУГАЯ КАТЕГОРИЯ
*
ПОТД2 НОП ,  СМОТРЮ АРХИВ
 СЧ ТЕКП
 ПО ПОТД3   АРХИВ ЛЮБОЙ
 СДА 64+24
 И БЮДОТ2+99(М5)
 ПО ПОТД4   ТЕКУЩИЙ АРХИВ ЗАКРЫТ ДЛЯ ОТДЕЛА
*
ПОТД3 НОП ,  ОТДЕЛ ПОДХОДИТ
 МОДА (М5)
 УИА 99(М7)  99 + (-<М5>)  = НОМЕР ОТДЕЛА
 СЧИ М7
 ПВ ПЕРЕВ(М13)
 СДА 64-32
 СБР ПППППП
 СДА 64+40
 ЗП БОТ1
*
 СЧИ М5
 ЗП ИР5
 ПВ КУКУ(М7)  ФОРМИРУЕМ СТРОКУ БЮДЖ.ОТД.
 МОД ИР5
 УИА (М5)
*
 УИА БЮДОТД(М1)
 ПВ ВЕРТУШ(М13)
*
ПОТД4 ЦИКЛ ПОТД1(М5)
*
*:::::::::::::::::::::::::::::::::::::::::::::::::::::
*
*  ВЫДАЧА СТРОКИ ИТОГА ДЛЯ СВОДКИ ПО БЮДЖЕТАМ ОТДЕЛОВ
*
 УИА РЗБОТД(М1)  СТРОКА РАЗДЕЛИТЕЛЕЙ
 ПВ ВЕРТУШ(М13)
*
 УИА -2(М5)
ПОТД51 НОП , ПЕРЕВОД : БЗОН, ПЗОН, ГЗОН
 СЧ ИТБЗОН+2(М5)
 ПВ ПЕРЕВ(М13)
 СДА 64-8
 ИЛИ МАСЗОН
 ЗП ИОБЗОН+2(М5) 0ТОГ ОТДЕЛА : БЗОН
 ЦИКЛ ПОТД51(М5)
*
 УИА -1(М5)
ПОДТ52 НОП , ПЕРЕВОД : БНМ, ЗНМ
 СЧ ИТБНМ+1(М5)
 ПВ ПЕРЕВ(М13)
 СДА 64-16
 ИЛИ МАСБНГ
 ЗП ИОБНМ+1(М5)   ИТОГ ОТДЕЛА : БНМ
 ЦИКЛ ПОДТ52(М5)
*
 УИА -1(М5)
ПОДТ53 НОП , ПЕРЕВОД : БГ, ЗГ
 СЧ ИТБГ+1(М5)
 ПВ ПЕРЕВ(М13)
 СДА 64-24
 ИЛИ ПООЗЗЗ
 ЗП ИОБГ+1(М5)    ИТОГ ОТДЕЛА : БГ
 ЦИКЛ ПОДТ53(М5)
*
 СЧ ИТКЗ
 ПВ ПЕРЕВ(М13)
 ИЛИ ПППООО   В ИТОГЕ КОЛ.ЗАД. М.БЫТЬ > 100
 ЗП ИОКЗ
*
*
 УИА -1(М5)
ПОДТ54 НОП , ПЕРЕВОД : БФ, ЗФ
 СЧ ИТБФ+1(М5)
 ПВ ПЕРЕВ(М13)
 СДА 64-24
 ИЛИ ПООЗЗЗ
 ЗП ИОБФ+1(М5)    ИТОГ ОТДЕЛА : БФ
 ЦИКЛ ПОДТ54(М5)
*
 УИА ИТГОТД(М1)
 ПВ ВЕРТУШ(М13)
*
 ПВ ДАЙИТГ(М13)  ДАЙ ИТОГ ПО АРХИВУ
 УИА ИТГВЫВ(М1)
*
ПЕЧЗАВ НОП ,  ЗАВЕРШЕНИЕ ДИРЕКТИВЫ "ПЕЧ"
 ПВ ВЕРТУВ(М13)
 ПВ ПРОТЯГ(М17)  ПРОТЯЖКА АЦПУ ПОСЛЕ СВОДКИ
 ПБ НАЧБЮД
*
*$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
*
ИСК НОП ,  ИСКЛЮЧЕНИЕ ПОЛЬЗОВАТЕЛЯ
*
*
 СЧ КОНОТД
 ЗП РЯ10
 СЧ БЮДЖП+1
 ПО НЕТРП    НА РАБОЧЕМ ПОЛЕ НЕТ БЮДЖЕТА
 И Е24П1
 ЗП ШФР
 И Е8П1
 ПЕ ИСК05
 ПВ ПРАВО1(М13)
 ПБ ИСК5   4-Х ЗНАЧН.
ИСК05 НТЖ Е8П1
 ПО ОШИФР  ЭТО СИС.ВХ
 ПВ ПРАВО3(М13)
 СЧ П376
 ЗП РЯ10
ИСК5 НОП
 ПВ ХВАТЬ(М13)
 ПВ НАСАРХ(М17)
 МОД ДЛНАРХ
 УИА -1(М1)
ИСК2 НОП
 СЧ ПОЛКТ2(М1)
 И Е24П1
 НТЖ ШФР
 ПЕ ИСК4   НЕ ТОТ
 СЧ ШФР
 И Н377
 ИЛИ РЯ10    ПРИЗНАК ПОДРАЗДЕЛЕНИЯ
 ЗП ПОЛКТ2(М1)
 СЧ
 ЗП ПОЛКТ1(М1)
ИСК4 НОП
 ПИО ИСК3(М1)  МОЖНО ЗАПИСАТЬ
 СЛИА -1(М1)
 ПБ ИСК2
ИСК3 НОП
 ПВ ЗКТАРХ(М16)
 ПВ ОСВОБ(М13)
*
 ПВ ЗАХВАТ(М13)
 ПВ ПОЛКЛЮ(М13)
 МОД РАБАРХ
 СЧ Е48-1
 НТЖ ВСЕЕД
 И КЛЮЧИ+1022(М1)
 ЗП КЛЮЧИ+1022(М1)
 РЗБ Е24П1
 ПЕ ИСК1     ЕЩЕ ОСТАЛИСЬ ОТКРЫТЫЕ АРХИВЫ
 ЗП КЛЮЧИ+1023(М1)
 ЗП КЛЮЧИ+1022(М1)
ИСК1 Э70 ЗПКЛЮ
 ПВ ОСВОБ(М13)
*
 СЧ РАБФИО
 ПО НАЧБЮД  РАБОТА С КАТ. ФИО ЗАПРЕЩЕНА
*
 СЧ ШФР
 СДА 64+8
 ЗП РЯ1    ШИФР ОС ДИСПАК
*
*    ПРОВЕРКА: ЕСТЬ ЛИ ДАННЫЕ ПОЛЬЗОВАТЕЛИ ОС
*    В КАТАЛОГАХ  К Р А Б ' А.
*
 УИА -3(М1)
ИСКОС7 УИА -1023(М2)
 Э70 ЧТКЛЮ1+3(М1)
ИСКОС8 СЧ КЛЮЧИ+1023(М2)
 НТЖ ВСЕЕД
 ПО ИСКФ   НА ИСКЛ. ИЗ КАТ. ФИО
 СЧ КЛЮЧИ+1023(М2)
 И Е24П1
 НТЖ РЯ1   ШИФР  ОС ДИСПАК
 ПО НАЧБЮД  НАЙДЕН ПОЛЬЗОВАТЕЛЬ В КАТАЛОГЕ КЛЮЧЕЙ
 СЛИА 1(М2)
 ЦИКЛ ИСКОС8(М2)
 ЦИКЛ ИСКОС7(М1)
*
ИСКФ НОП
*
*    ДОЗАПРОС: ИСКЛ. ИЗ КАТ. ФИО ИЛИ НЕТ
*
 УИА -3(М1)
ИСКОС9 СЧ ИЗФИСК+4(М1)
 ЗП МАСПР+4(М1)   ПЕРЕНОС ПРИГЛАШЕНИЯ В БУФЕР ОБМЕНА
 ЦИКЛ ИСКОС9(М1)
 СЧ ИЗФИСК
 ПВ ПРИЕМ1(М13)
 УИА МАСПР(М2)
 УИА -5(М1)
 ПВ ДАНЕТ(М16)
 НТЖ Е1
 ПЕ НАЧБЮД
*
*    ИСКЛЮЧЕНИЕ ИЗ КАТАЛОГА  Ф И О
*
 ПВ ЗАХ253(М13)
 УИА -2(М1)
ИСКОС1 Э70 ЧТФИО+2(М1)
 УИА -1023(М2)
ИСКОС2 СЧ ПОЛФИО+1024(М2)
 РЗБ Е16П1
 НТЖ РЯ1
 ПЕ ИСКОС3    ПОИСК ФИО В КАТАЛОГЕ
 СЧ
 ЗП ПОЛФИО+1023(М2)
 ЗП ПОЛФИО+1024(М2)
 Э70 ЗПФИО+2(М1)
 ПВ ОСВОБ(М13)
 ПБ НАЧБЮД
ИСКОС3 НОП
 СЛИА 1(М2)
 ЦИКЛ ИСКОС2(М2)
 ЦИКЛ ИСКОС1(М1)
 ПВ ОСВОБ(М13)
 ПБ НАЧБЮД
*
*$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
*
НОВ НОП ,  ЗАВЕДЕНИЯ НОВОГО ПОЛЬЗОВАТЕЛЯ
*
*
 ПВ ПОДГОТ(М13)
 СЧ 0
 ЗП ВЫХНОМ  В НОМ ИЗ НОВ ПОКА НЕ ВЫХОЖУ
 ПВ НАЙПРО(М15)
 СЧ РЯВС
 ВЧОБ П11
 ПЕ НОВГР      НА ЗАВЕДЕНИЕ НОВОЙ ГРУППЫ
 ПВ ШНОВ2(М15)
 ПО ОШИФР
 СЧ ШФР
 СДА 64+16
 ЗП ЗОНРЯ
 ЗП НОМОДД
 ПВ ИЗ10В8(М13)
 СЧ РЯ2
 ЗП НОМОТД  НОМЕР ОТДЕЛА В 8-ИЧН. ВИДЕ
 СЧ ШФР
 СДА 64+8
 ЗП ФАМ2
 УИА ФАМ2(М7)
 СЧИ М7
 Э50 '104'
 СЧ ФАМ2
 ПЕ НОВ7  ПОЛЬЗОВАТЕЛЬ ОС ДИСПАК
 ПВ ПОЛЬЗ(М13)
 СЧ ШФР
 И Е8П1
 ПЕ НАЧБЮД
 СЧ РАБФИО
 ПО НАЧБЮД  РАБОТА С КАТ. ФИО ЗАПРЕЩЕНА
*
НОВОС9 НОП
 ПВ ПРАВО1(М13)
 Э70 ЧТСЛЗО
 МОД НОМОТД
 СЧ БЮДОТ1
 И Е8П1
 ЗП БЮФИО  БЮДЖЕТ ОТДЕЛА ПО ЧИСЛУ ПОЛЬЗОВАТЕЛЕЙ ОС
 ПВ ФИОЧИС(М13)
 СЧИ М3   ЧИСЛО ПОЛЬЗОВАТЕЛЕЙ ОС
 ВЧ БЮФИО
 ПО НБЮД
 СЧ ПРМФИО
 ПВ ПРИЕМ1(М13)   ПРИЕМ ТЕКСТА ФИО
 УИА МАСПР(М2)
 УИА -5(М1)
 ПВ ПДГФИО(М16)
 ПО НАЧБЮД    БЫЛ ОТКАЗ ОТ ВВОДА НОВОГО ПОЛЬЗОВАТЕЛЯ
 СЧ ШФР
 СДА 64+8
 СБР Е16П1
 ИЛИ ФАМ2+1
 ЗП ФИО+1
 СЧ ФАМ2
 ЗП ФИО ПОДГОТОВЛЕНА ИНФОРМАЦИЯ ДЛЯ КАТАЛОГА  Ф И О
 УИА -2(М1)
 ПВ ЗАХ253(М13)
НОВОС1 Э70 ЧТФИО+2(М1)
 УИА -1023(М2)
НОВОС2 СЧ ПОЛФИО+1024(М2)
 РЗБ Е16П1
 ПЕ НОВОС3    НА КОНЕЦ ЦИКЛА ПО М2
 СЧ ФИО
 ЗП ПОЛФИО+1023(М2)
 СЧ ФИО+1
 ЗП ПОЛФИО+1024(М2)    НОВЫЙ ПОЛЬЗОВАТЕЛЬ
 Э70 ЗПФИО+2(М1)
 ПБ НОВОС4
НОВОС3 СЛИА 1(М2)
 ЦИКЛ НОВОС2(М2)
 ЦИКЛ НОВОС1(М1)
 ПВ ОСВОБ(М13)
 УИА НЕСООТ(М1)
 ПВ ВЫВОД(М17)  НЕТ СООТВЕТСТВИЯ БЮДЖЕТА С КАТ. ФИО
НОВОС4 НОП
 ПВ ОСВОБ(М13)
НОВ7 НОП
 ПВ ЗАХВАТ(М13)
 Э70 ЧТСЛЗО
 МОД НОМОТД
 СЧ БЮДОТ2
 СБР Е24П1
 ПВ ПРАРХ(М13)
 СЧ ШФР
 ЗП БЮДЖП+1
 И Е8П1
 ПО НОВ4       ПОЛЬЗОВАТЕЛЬ 4-Х  ЗНАЧН
 СЧ ШФР
 СДА 64+8
 ЗП ГРУППА
 ЗП ЗОНРЯ
 ПВ ПРАВО3(М13)
 ПВ ГРУНАЙ(М13)
 ПО НБЮД
 СЧ ГРУППА+1
 СБР Е24П1
 ПВ ПРАРХ(М13)
 СЧ ГРУППА
 И Е16П1
 ЗП РЯ5
 СЧ RСДВИГ+1
 ЗП РЯ4
 ПВ ЛЮДИ(М13)
 СЧ ГРУППА
 И Е23П17
 СДА 64+16
 ВЧОБ РЯ7
 ПО НБЮД
 ПБ НОВ2
*
*
НОВ4 ПВ ПРАВО1(М13)
 СЧ Е1
 ЗП ВЫХНОМ  ПРИЗНАК: ИЗ "НОВ" ==> В "НОМ"
 СЧ ШФР
 СДА 64+16
 ЗП РЯ5
 МОД НОМОТД
 СЧ БЮДОТ1
 И Е16П9
 СДА 64+8
 ЗП НОМЕР    БЮДЖЕТ ОТДЕЛА  ПО ЧИСЛУ НОМЕРОВ
 СЧ RСДВИГ
 ЗП РЯ4
 ПВ ЛЮДИ(М13)
 СЧ РЯ5
 ЗП РЯ4
 ПВ ЛЮБГР(М13)
 СЧ РЯ5
 СЛЦ РЯ6
 ВЧ НОМЕР
 ПО НБЮД
НОВ2 СЧ ШФР
 ЗП ШФР2
 ПВ ПОЛКЛЮ(М13)
 СЧ РЯДУР
 ПЕ НОВ1
 ЗП КЛЮЧИ+1023(М1)
 СЧ ШФР
 ЗП КЛЮЧИ+1022(М1)
НОВ1 МОД РАБАРХ
 СЧ Е48-1
 ЗП РЯ1
 И КЛЮЧИ+1022(М1)
 ПЕ НОВ3  ТАКОЙ ПОЛЬЗ.ЕСТЬ В АРХИВЕ
 СЧ РЯ1
 ИЛИ КЛЮЧИ+1022(М1)
 ЗП КЛЮЧИ+1022(М1)
 ЗП УПРКЛЮ
 Э70 ЗПКЛЮ ЗАПИСЬ ТЕКУЩЕЙ ЗОНЫ КЛЮЧЕЙ
 ПВ ОСВОБ(М13)
 ПБ ДЛЯВХН  ДЛЯ ВЫХОДА НА ДИР-ВУ "НОМ"
*
НОВ3 УИА ПЕСТЬ(М1)
 ПВ ОСВОБ(М13)
 ПВ ВЫВОД(М17)
ДЛЯВХН СЧ 0
 ЗП БЮДЖП+1
 СЧ ВЫХНОМ
 ПО НАЧБЮД
 ПВ ПОДГОТ(М13)
 ПБ НОМНОВ
*
ПРАРХ НОП  ПРОВЕРКА НА  СВОЙ АРХИВ
 МОД РАБАРХ
 И Е48-1
 ПО ПАРХ     АРХИВ  ЗАКРЫТ
 ПБ (М13)
ПАРХ УИА ЗАРХ(М1)
 ПВ ОСВОБ(М13)
 ПВ ВЫВОД(М17)
 ПБ ББ6
*
*$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
*
НОВГР НОП ,  ЗАВЕДЕНИЯ НОВОЙ ГРУППЫ
*
 ПВ ВЫБДИР(М17)
*
 СЧ ЭТОГРУ
 НТЖ ТЕКДИР
 И   МАСДИР
 ПЕ ОШИФР
*
НОВГ0 ПВ НАЙПРО(М15)
 ПВ ПРОЧИС(М13)
 СЧ РЯВС
 ЗП ЗОНРЯ
 УИА -3(М5)
 ПВ ЧИСЛА(М13)
 СЧ ЗОНРЯ
 ЗП ГРУППА
 ПВ ПРАВО2(М13)
 ПВ ЗАХВАТ(М13)
 СЧ ГРУППА
 И Е16П1
 СДА 64+8
 ЗП РЯ4
 ЗП ЗОНРЯ
 ПВ ИЗ10В8(М13)
 СЧ РЯ2
 ЗП НОМОТД   НОМЕР ОТДЕЛА В 8-ИЧН
 ПВ ЛЮБГР(М13)
 МОД НОМОТД
 СЧ БЮДОТ2
 СБР Е24П1
 ПВ ПРАРХ(М13)     ПРОВЕРКА НА РАЗРЕШЕННЫЙ АРХИВ
 ПВ ГРУНАЙ(М13)
 ПО ННГР  НОВАЯ ГРУППА
 МОД РАБАРХ
 СЧ Е24-1      ПРИЗНАК ОТКРЫТОГО АРХИВА
 ИЛИ БЮДГР2+99(М2)
 ЗП БЮДГР2+99(М2)
 ЗП ГРУППА+1
 Э70 ЗПСЛЗО
 ПВ ОСВОБ(М13)
 ПБ ББ6
ННГР НОП
 МОД НОМОТД
 СЧ БЮДОТ1
 И Е21П17
 СДА 64+16
 ВЧОБ РЯ12
 ПО НБЮД
 УИА -99(М2)
ИЩУГР1 СЧ БЮДГР1+99(М2)
 И Е16П1
 ПЕ ИЩУГР2  НА КОНЕЦ ЦИКЛА
 СЧ ГРУППА
 ЗП БЮДГР1+99(М2)
 МОД РАБАРХ
 СЧ Е24-1
 ЗП БЮДГР2+99(М2)
 Э70 ЗПСЛЗО
 ПВ ОСВОБ(М13)
 ПБ ББ6
ИЩУГР2 ЦИКЛ ИЩУГР1(М2)
 ПВ ОСВОБ(М13)
 УИА АВАРГР(М1)
 ПВ ВЫВОД(М17)
 ПБ КОНЕЦ
*
*$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
*
ИНФ НОП ,  УСТАНОВКА ПРИЗНАКА: "ВЫПОЛНИТЕ ДИР. ИНФ"
*
*
 СЧ ФЛОТД
 ПЕ ОШИФР
 СЧ НОМРЯ
 УИ М10
 СЧ МАСПР
 СДА 64+8
 И Е8П1
 НТЖ В
 ПО ИНФВСЕ
 СЧ БЮДЖП+1
 ПО НЕТРП    НА РАБОЧЕМ ПОЛЕ НЕТ БЮДЖЕТА
 СЧ БЮДЖП+2
 ИЛИ Е1
 ЗП БЮДЖП+2
 ПБ ЗАПШМ
ИНФВСЕ НОП
 СЧ Е1
 ЗП РЯ1
 СЧ ВСЕЕД
 ЗП РЯ2
*
ИНФШТР НОП ,  ВХОД ДЛЯ СНЯТИЯ ВСЕХ ШТРАФОВ И
*             ПРИЗНАКА "ИНФ" У ВСЕХ ПОЛЬЗОВАТЕЛЕЙ
 УИА -3(М2) ПРИЗНАКЧ
 ПВ ЗАХВАТ(М13)
ИНВ1 Э70 ЧТКЛЮ1+3(М2)
 УИА -1024(М1)
ИНФ1 СЛИА 2(М1)
 СЧ КЛЮЧИ+1022(М1)
 НТЖ ВСЕЕД
 ПЕ ИНФ2
 Э70 ЗПКЛЮ1+3(М2)
 ПБ ИНФ3
ИНФ2 СЧ КЛЮЧИ+1023(М1)
 ИЛИ РЯ1
 И РЯ2
 ЗП КЛЮЧИ+1023(М1)
 ПИНО ИНФ1(М1)
 Э70 ЗПКЛЮ1+3(М2)
 ЦИКЛ ИНВ1(М2)
ИНФ3 ПВ ОСВОБ(М13)
 ПБ ББ1
*
*$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
*
ШТР НОП ,  БЛОК  ШТРАФА (СНЯТИЕ,УСТАНОВКА)
*
*
 СЧ ФЛДИСП  ФЛАГ ДИСПЕТЧЕРА
 ПО ШТР00  ЭТО НЕ ДИСП.
 СЧ Е26
 ЗП РЯ2  ПРИЗНАК ШТРАФА
 НТЖ ВСЕЕД
 ПБ ШТР4
*
ШТР00 НОП ,  КОНЕЦ ПРАВКИ ОТ 02.04.86
 СЧ ФЛОТД
 ПЕ ШТР3
*
*  РЕЖИМ АДМИНИСТРАТОРА
*
 СЧ МАСПР
 СДА 64+8
 И Е8П1
 УИ М16
 СЛИА -П'В'(М16)
 ПИНО ШТР00А(М16)  ШТРАФ КОНКРЕТНОГО ПОЛЬЗОВАТЕЛЯ
*
* СНЯТИЕ ШТРАФА У ВСЕХ
*
 СЧ 0
 ЗП РЯ1
 СЧ ВСЕЕД
 НТЖ Е28  ШТРАФ ГРУППЫ
 НТЖ Е27  ШТРАФ ОТДЕЛА
 НТЖ Е26  ШТРАФ ДИСПЕТЧЕРА
 НТЖ Е25  ШТРАФ АДМИНИСТРАТОРА
 НТЖ Е1   ПРИЗНАК "ИНФ"
 ЗП РЯ2
 ПБ ИНФШТР
*
ШТР00А НОП
 СЧ Е25
 ЗП РЯ2
 НТЖ ВСЕЕД
 ПБ ШТР4
ШТР3 СЧ БЮДЖП+1
 И Е24П1
 СДА 64+16
 НТЖ ФЛОТД
 ПЕ ШТР33
 СЧ Е27
 ЗП РЯ2
 НТЖ ВСЕЕД
 ПБ ШТР4
ШТР33 ПВ ПРАВО3(М13)
 СЧ Е28
 ЗП РЯ2
 НТЖ ВСЕЕД
ШТР4 ЗП РЯ3
 СЧ БЮДЖП+1
 ПО НЕТРП    НА РАБОЧЕМ ПОЛЕ НЕТ БЮДЖЕТА
 СЧ МАСПР
 СДА 64+8
 И Е8П1
 ЗП РЯ1
 НТЖ Д
 ПЕ ШТР1
 СЧ БЮДЖП+2
 ИЛИ РЯ2
 ЗП БЮДЖП+2
 ПБ ЗАПШМ
ШТР1 СЧ РЯ1
 НТЖ Н
 ПЕ ШТР2
 СЧ БЮДЖП+2
 И РЯ3
 ЗП БЮДЖП+2
 ПБ ЗАПШМ
ШТР2 ПВ ОШВД(М13)
 ПБ ББ1

*
*
*23456789012345678901234567890123456789012345678901234
**********************************************
*                                            *
*    Д И А Л О Г О В А Я    С И С Т Е М А    *
*                К  Р  А  Б                  *
*                                            *
*--------------------------------------------*
*  БЮДЖЕТНО-СТАТИСТИЧЕСАЯ ПОДСИСТЕМА.        *
*  Ч А С Т Ь  4                              *
*  БЛОК БЮДЖЕТА                              *
**********************************************
*
*$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
*
УНЧ    НОП ,  БЛОК  У Н И Ч Т О Ж Е Н И Я  ГРУППЫ
*
 СЧ ГРУППА
 ПО НЕТРП
 И Е16П1
 ЗП ЗОНРЯ     ШИФР ГРУППЫ
 ПВ ПРАВО2(М13)
 ПВ ЗАХВАТ(М13)
 УИА -3(М2)
УНЧГ1 Э70 ЧТКЛЮ1+3(М2)
 УИА -1024(М1)
УНЧГК1 СЛИА 2(М1)
 СЧ КЛЮЧИ+1022(М1)
 НТЖ ВСЕЕД
 ПО УКОНГК     КОНЕЦ РАБОТЫ КЛЮЧАМИ
 СЧ КЛЮЧИ+1022(М1)
 И Е24П1
 ЗП ШФР
 И Е8П1
 ПО УНЧГК2    НА КОНЕЦ ЦИКЛА
 СЧ ШФР
 СДА 64+8
 НТЖ ЗОНРЯ    ШИФР ГРУППЫ
 ПЕ УНЧГК2
 МОД РАБАРХ
 СЧ Е48-1
 НТЖ ВСЕЕД
 И КЛЮЧИ+1022(М1)
 ЗП КЛЮЧИ+1022(М1)
 И Е48П25
 ПЕ УНЧГК2
 ЗП КЛЮЧИ+1023(М1)
 ЗП КЛЮЧИ+1022(М1)
УНЧГК2 ПИНО УНЧГК1(М1)
 Э70 ЗПКЛЮ1+3(М2)
 ЦИКЛ УНЧГ1(М2)
УКОНГК Э70 ЗПКЛЮ1+3(М2)
 Э70 ЧТСЛЗО
 УИА -99(М1)
УГ СЧ БЮДГР1+99(М1)
 И Е16П1
 НТЖ ЗОНРЯ
 ПЕ УГ2
 СЧ БЮДГР2+99(М1)
 МОД РАБАРХ
 ИЛИ Е24-1
 МОД РАБАРХ
 НТЖ Е24-1
 ЗП БЮДГР2+99(М1)
 И Е24П1
 ПО УГ3  ГРУППА ПОЛНОСТЬЮ УНИЧТОЖЕНА
 СЧ БЮДГР1+99(М1)
УГ3 ЗП БЮДГР1+99(М1)
 Э70 ЗПСЛЗО
 ПБ УГК
УГ2 ЦИКЛ УГ(М1)
УГК НОП 
 ПВ ОСВОБ(М13)
 ПВ ХВАТЬ(М13)
 ПВ НАСАРХ(М17)
 МОД ДЛНАРХ
 УИА -1(М1)  ДЛИНА АРХ. - 1
УНЧФ1 НОП
 СЧ ПОЛКТ2(М1)
 И Е24П1
 ЗП ШФР
 И Е8П1
 ПО УНЧФ3  НА КОНЕЦ ЦИКЛА
 СЧ ШФР
 СДА 64+8
 НТЖ ЗОНРЯ
 ПЕ УНЧФ3    НЕ ТОТ ФАЙЛ
 ЗП ПОЛКТ1(М1)
 СЧ ШФР
 И Н16П1
 ИЛИ КОНОТД
 ЗП ПОЛКТ2(М1)
УНЧФ3 НОП
 ПИО УНЧФ2(М1)
 СЛИА -1(М1)
 ПБ УНЧФ1
УНЧФ2 НОП
 ПВ ЗКТАРХ(М16)
 ПВ ОСВОБ(М13)
 ПБ НАЧБЮД
*
*$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
*
ДАЙ    НОП ,  ПОЛУЧЕНИЕ СВОДКИ ПО   Б Ю Д Ж Е Т У
*             ПОЛЬЗОВАТЕЛЯ ИЛИ ГРУППЫ
*
 СЧ ГРУППА
 ПЕ ДАЙГР    ДАЙ ДЛЯ БЮДЖЕТА ГРУППЫ
*
*  ДАЙ ДЛЯ БЮДЖЕТА ПОЛЬЗОВАТЕЛЯ
*
 СЧ БЮДЖП+1
 ПО НЕТРП  БЮДЖЕТ НЕ ВЫЗВАН НА Р.П.
*
 УИА ЗГБПОЛ+1(М1)  ПОЛЬЗОВАТЕЛЯ
 ПВ ВЕРТУШ(М13)
 ПВ ФОРМ(М11)
 УИА БЮДПОЛ+1(М1)
 ПВ ВЕРТУВ(М13)
 ПБ ББ1
*
******************************************************
*
ДАЙАРХ НОП
*
*    ПОЛУЧЕНИЕ АРХИВОВ ПОЛЬЗОВТЕЛЕЙ, ОТДЕЛОВ И ГРУПП
*
*
*    СМ         : ШКАЛА РАЗРЕШЕННЫХ АРХИВОВ Е48П25
*    РЕЗУЛЬТАТ  : НАХОДИТСЯ В  ШКАРХ  И  ШКАРХ+1
*    РЕГ.ВОЗВР. : М17
*
 И Е48П25
 ЗП РЯ14    ШКАЛА АРХИВОВ
 НТЖ Е48П25
 ПЕ НЕВСЕ  НЕ ВСЕ АРХИВЫ ОТКРЫТЫ
*
*  ВЫДАЕМ ТЕКСТ  1-24
*
 СЧ ТАРХ0
 ЗП ШКАРХ
 СЧ ВСЕ143
 ЗП ШКАРХ+1
 ПБ (М17)  ВОЗВРАТ
*
НЕВСЕ НОП ,  ЕСТЬ И ЗАКРЫТЫЕ АРХИВЫ
 СЧ РЯ14
 ПЕ ЕСТЬАР  ЕСТЬ АРХИВЫ
 СЧ ТАРХ1
 ЗП ШКАРХ
 СЧ ВСЕ143
 ЗП ШКАРХ+1  ВЫДАЕМ "НЕТ"
 ПБ (М17)  ВОЗВРАТ
*
ЕСТЬАР НОП ,  0 < КОЛИЧ. АРХИВОВ < 24
 СЧ РЯ14
 ЧЕД 0
 ВЧОБ Ф4
 ПО МЕНЕЕ5  АРХИВОВ НЕ БОЛЕЕ ЧЕМ  4
*
 СЧ РЯ14
 РЗБ ООСССС   П'007777'
 ИЛИ ППОООО  П'  0000'
 ЗП ШКАРХ    ВЫДАЕМ В ВОСЬМИРИЧНОМ ВИДЕ
 СЧ РЯ14
 СДА 64-12
 РЗБ ССССОО   П'777700'
 ИЛИ ООООПП  П'0000  '
 ЗП ШКАРХ+1
 ПБ (М17)
*
МЕНЕЕ5 НОП ,    0 < КОЛ.АРХИВОВ < 4
*
*  ВЫДАЕМ В ЧЕЛОВЕЧЕСКОМ ВИДЕ : А1,А2,А3,А4
*
 СЧИ М13
 ЗП РАР13
 СЧИ М16
 ЗП РАР16
 СЧИ М17
 ЗП РАР17
*
 СЧ ВСЕ143
 ЗП ШКАРХ+1
 И Е24П1
 ЗП ШКАРХ
*
 ПВ ОЧЕАРХ(М17)  ЗА ОЧЕРЕДНЫМ НОМЕРОМ АРХИВА
 СДА 64-24
 ИЛИ ПООООО
 ИЛИ ШКАРХ
 ЗП ШКАРХ
*
 ПВ ОЧЕАРХ(М17)
 ИЛИ ЗПТ4   П'000,00'
 ЗП РЯ2
 СЧ ШКАРХ
 ИЛИ Е24П1
 НТЖ Е24П1
 ИЛИ РЯ2
 ЗП ШКАРХ
*
 ПВ ОЧЕАРХ(М17)
 ИЛИ ЗПТ4
 СДА 64-24
 ЗП РЯ2
 СЧ ШКАРХ+1
 СДА 64+24
 ИЛИ РЯ2
 ЗП ШКАРХ+1
*
 ПВ ОЧЕАРХ(М17)
 ИЛИ ЗПТ4
 ЗП РЯ2
 СЧ ШКАРХ+1
 ИЛИ Е24П1
 НТЖ Е24П1
 ИЛИ РЯ2
 ЗП ШКАРХ+1
 ПБ ГОТОВО  ВСЕ СДЕЛАЛИ. ПОРА УХОДИТЬ
*
*-----------------------------------------
*
*  ВНУТРЕННЯЯ П/П "ДАЙАРХ"
*
ОЧЕАРХ НОП ,  ВЫДАЧА В СИМВОЛЬНОМ ВИДЕ ОЧЕРЕД. АРХ.
 СЧИ М17
 ЗП РАР17А
*
 СЧ РЯ14
 ПО ГОТОВО  УХОД ИЗ ОЧЕАРХ И ДАЙАРХ
 НЕД
 УИ М13
 СЧ РЯ14
 НТЖ Е48-1(М13)
 ЗП РЯ14 АРХИВ ПОГАШЕН В ШКАЛЕ
*
 СЧИ М13
 ПВ ПЕРЕВ(М13)
*
 МОД РАР17А
 УИА (М17)  АДРЕС ВОЗВРАТА
*
 ЗП РЯ2
 И Е16П9
 ПЕ УХОД  СТАРШАЯ ЦИФРА НЕ "0"
 СЧ РЯ2
 ИЛИ М8В143
 ПБ (М17)  ЗАБИЛИ СТАРШИЙ НЕ ЗНАЧАЩИЙ "0"
*
УХОД НОП
 СЧ РЯ2
 ПБ (М17)  ДВУЗНАЧНОЕ ЧИСЛО
*
*-----------------------------------------
*
ГОТОВО НОП ,  ВЫХОД ИЗ ДАЙАРХ
 МОД РАР13
 УИА (М13)
 МОД РАР16
 УИА (М16)
 МОД РАР17
 ПБ ,  ВОЗВРАТ
*
******************************************************
*
ПОЛЬЗ НОП
 СЧ ШФР
 СДА 64+8
 ЗП ФАМ
 УИА ФАМ(М7)
 СЧИ М7
 Э50 '104'
 УИА ТФАМ(М1)   ФАМИЛИЯ ДЛЯ ВЫВОДА
 СЧ ФАМ
 ПЕ ПОЛБЭС   ЭТО ПОЛЬЗОВАТЕЛЬ ОС ДИСПАК
*СЧ ФЛОТД
*ПЕ НЕПОЛ
 УИА ТБЭСМ(М1)    РАБОТА  С НЕ ПОЛЬЗОВАТЕЛЕМ БЭСМ
ПОЛБЭС ПВ ВЫВОД(М17)
 ПБ (М13)
НЕПОЛ НОП
 ПВ ОСВОБ(М13)
 УИА ТБЭСМ(М1)
 ПВ ВЫВОД(М17)
 ПБ НАЧБЮД
*
******************************************************
*
ПОЛКЛЮ НОП ,  ПОЛУЧЕНИЕ БЮДЖЕТА ПОЛЬЗОВАТЕЛЯ
*
 СЧ Е8
 ЗП РЯДУР   УСТАНОВЛЕН ПРИЗНАК
 УИА -3(М2)
ПБ12 Э70 ЧТКЛЮ1+3(М2)
 УИА -1024(М1)
 СЧ ЗПКЛЮ1+3(М2)
 ЗП ЗПКЛЮ
 СЧ ЧТКЛЮ1+3(М2)
 ЗП ЧТКЛЮ
ПБ1 СЛИА 2(М1)
 СЧ КЛЮЧИ+1022(М1)
 НТЖ ВСЕЕД
 ПО НЕТКЛ    НЕ ПОЛЬЗОВАТЕЛЬ
 СЧ КЛЮЧИ+1022(М1)
 И Е24П1
 НТЖ ШФР
 ПО ПОЛКРБ  ПОЛЬЗ.КРАБА
 ПИНО ПБ1(М1)
 ЦИКЛ ПБ12(М2)
 УИА КЛПЕРП(М1)
 ПБ ДИАГН  КЛЮЧИ ПЕРЕП.
ПОЛКРБ СЧ КЛЮЧИ+1022(М1)
 ЗП БЮДЖП+1
 СЧ КЛЮЧИ+1023(М1)
 ЗП БЮДЖП+2
 ПБ (М13)
НЕТКЛ СЧ
 ЗП РЯДУР
 ПБ (М13)
ДИАГН ПВ ВЫВОД(М17)
 ПБ НАЧБЮД
НЕТПОЛ УИА ПОЛНЕТ(М1)
 ПБ ДИАГН
НЕТВАР УИА НЕПАРХ(М1)
 ПБ ДИАГН
ОШБ УИА ТОШСЧ(М1)
 ПБ ДИАГН
*
*
*     Д И А Г Н О С Т И К И 
*
*
НЕТРП НОП  НЕТ РАБОЧЕГО ПОЛЯ
 УИА НЕТРАБ(М1)
 ПВ ВЫВОД(М17)
 ПБ ББ1
*
*
ОШИФР ПВ ОШВД(М13)
 ПБ ББ1
ОШВД УИА ТЕКСТО(М1)
 Э71 ИСВЫ
 Э71 ИССОСТ
 И Е2526
 НТЖ Е2526
 ПО КОНЕЦ
 ПБ (М13)
*
******************************************************
*
ЗОНЫ НОП 
 ПВ ПОДГОТ(М13)
 ПВ НАЙПРО(М15)
 УИА -4(М5)
 СЧ РЯВС
 ЗП ЗОНРЯ
 ПВ ЧИСЛА(М13)
 ПВ ИЗ10В8(М13)
 СЧ РЯ2
 ЗП БОБ
 ПБ (М17)
*
******************************************************
*
ЗАПШМ ПВ ЗАХВАТ(М13) ЗПИСЬ КЛЮЧЕЙ
 УИА -3(М13)
 СЧ БЮДЖП+1
 И Е24П1
 ЗП ШФР
ЗА7 УИА -1024(М1)
 Э70 ЧТКЛЮ1+3(М13)
ЗА1 СЛИА 2(М1)
 СЧ КЛЮЧИ+1022(М1)
 НТЖ ВСЕЕД
 ПО ЗА2
 СЧ КЛЮЧИ+1022(М1)
 И Е24П1
 НТЖ ШФР
 ПЕ ЗА3
 СЧ БЮДЖП+1
 ЗП КЛЮЧИ+1022(М1)
 СЧ БЮДЖП+2
 ЗП КЛЮЧИ+1023(М1)
 Э70 ЗПКЛЮ1+3(М13)
 ПВ ОСВОБ(М13)
 ПБ ББ1
ЗА3 ПИНО ЗА1(М1)
 ЦИКЛ ЗА7(М13)   ЧТЕНИЕ СЛЕД.ЗОНУ
ЗА2 ПВ ОСВОБ(М13)
ЗА6 УИА ИСКЛЮ(М1)
 ПВ ВЫВОД(М17)
 ПБ НАЧБЮД
БУФВЫВ СЧ NАМЕ   ФОРМИР.БУФ.ВЫВ.ДЛЯ БИБ.
 ЗП NАМЕ5+12(М11)
 СЧ NАМЕ+1
 ЗП NАМЕ5+13(М11)
 СЧ NАМЕ+2
 ЗП NАМЕ5+14(М11)
 СЛИА 2(М11)
 ПИНО ФОРБУФ(М11)
 УИА -12(М11)
 УИА NАМЕ5(М1)
 ПВ ВЫВОД(М17)
 СЧ
 ЗП NАМЕ5+1
ФОРБУФ ПБ (М13)
*
******************************************************
*
*     ФОРМИРОВАНИЕ БУФЕРА ВЫВОДА
*
*  ИСПОЛЬЗУЕМЫЕ РЕГИСТРЫ И ПЕРЕМЕННЫЕ:
*    М1 - АДРЕС НАЧАЛА ТЕКСТА
*    М16 - МАКСИМАЛЬНАЯ ДЛИНА СТРОКИ
*    М4 - ШАГ ВЫДАЧИ
*    М2,М3 - ДЛЯ  В С (УПРЯТЫВАЮТСЯ В УПВЕР2, УПВЕР3)
*    М5,М6 - ДЛЯ  З С (УПРЯТЫВАЮТСЯ В УПВЕР5, УПВЕР6)
*    М17, М13 - ДЛЯ РАБОТЫ С СЛУЖЕБНЫМИ БЛОКАМИ
*    КООБЗ1 - КООРДИНАТЫ КОНЦА БУФЕРА. НОМЕР БАЙТА
*    КООБЗ2 - КООРДИНАТЫ КОНЦА БУФЕРА. НОМЕР СЛОВА
*
ВЕРТУВ НОП ,  ВХОД ДЛЯ ЗАПИСИ СТРОКИ В БУФЕР ВЫВОДА
*           И ВЫДАЧИ ВСЕГО НАКОПЛЕННОГО БУФЕРА.
*           ПРИГЛАШЕНИЯ ДЛЯ ПРОДОЛЖЕНИЯ ВЫДАЧИ НЕТ.
*
 СЧ Е1  ПРИЗНАК : "ВЫДАВАТЬ"
 ПБ НАЧВЕР
*
ВЕРТУШ НОП , ВХОД ДЛЯ ЗАПИСИ СТРОКИ В БУФЕР, НО ВЫДАЧА
*           ДЕЛАЕТСЯ ЕСЛИ БУФЕР ЗАПОЛНЕН, Т.Е. НАБРАНО
*           НУЖНОЕ ЧИСЛО СТРОК.
*
 СЧ 0   ПРИЗНАК : "КОПИТЬ"
*
НАЧВЕР НОП ,  НАЧАЛО СОБСТВЕННО ВЕРТУШКИ
 ЗП ВЫДАТЬ  ПРИЗНАК : ВЕРТУВ/ВЕРТУШ
*
 СЧИ М2
 ЗП УПВ2
 СЧИ М5
 ЗП УПВ5
 СЧИ М6
 ЗП УПВ6
 СЧИ М13
 ЗП УПВ13
 СЧИ М17
 ЗП УПВ17
 УИИ М2(М1)
 УИА -5(М1)    ДЛЯ БЛОКА ВС
 УИА -78(М16)    ГРАНИЦА СТРОКИ
 СЧ КООБЗ1
 УИ М5
 СЧ КООБЗ2
 УИ М6
 СЧ П214
 ПВ ЗС(М13)
 СЧ КООБЗ2
 ПЕ ПРОДВЫ   НА БУФЕРЕ ЧТО-ТО ЕСТЬ
 СЧ ШАГ
 УИ М4  УСТАНОВЛЕН ШАГ ВЫДАЧИ
 УИА БВЫВ(М6)
 УИА -5(М5)   ДЛЯ БЛОКА ЗС
*
ПРОДВЫ НОП
 ПВ ВС(М13)
 НТЖ Е8П1
 ПО БВКОН   КОНЕЦ ОЧЕРЕДНОЙ СТРОКИ
*
 СЧ РЯВС
 НТЖ В143
 ПО ПРОДВЫ  '143'  ПРОПУСКАЕМ
*
 СЧ РЯВС
 ПВ ЗС(М13)
 ЦИКЛ ПРОДВЫ(М16)
 УИА СТРПЕР(М1)
 ПВ ВЫВОД(М17)
 ПБ НАЧБЮД СТРОКА ПРЕВЫСИЛА ГРАНИЦУ
*
БВКОН НОП
 СЧИ М5
 ЗП КООБЗ1
 СЧИ М6
 ЗП КООБЗ2   КООРДИНАТЫ КОНЦА БУФЕРА ВЫВОДА
 СЧ Е8П1
 ПВ ЗС(М13)
*
 СЧ ВЫДАТЬ
 ПЕ ВЫДАЮ  ВЕРТУШКА ДОЛЖНА ОБЯЗАТЕЛЬНО ВЫДАТЬ
 ЦИКЛ БВКОН2(М4)
*
ВЫДАЮ НОП
 СЧ 0
 ЗП КООБЗ2
 УИА -5(М5)
 СЧИ М5
 ЗП КООБЗ1
 УИА БВЫВ(М1)
 ПВ ВЫВОД(М17)
 СЧ РЕЖВЫВ
 И Е1
 ПО НАЦПУ    НЕТ ВЫВОДА НА ТЕРМИНАЛ
 СЧ ВЫДАТЬ
 ПЕ БВКОН2  ВЫДАВАТЬ И НЕ ДЕЛАТЬ ПРИЕМ
*
 СЧ НРБОЛЬ
 ПВ ПРИЕМ1(М13)
 СЧ МАСПР
 СДА 64+40
 НТЖ Е8П1
 ПО БВКОН2
 СЧ РЯБОЛЬ
 НТЖ СБОЛЬ
 ПО СТАТ1   РАБОТА ИЗ БЛОКА СТАТИСТИКИ
 ПБ ББ7
НАЦПУ НОП
БВКОН2 НОП 
 СЧ УПВ17
 УИ М17
 СЧ УПВ2
 УИ М2
 СЧ УПВ5
 УИ М5
 СЧ УПВ6
 УИ М6
 МОД УПВ13
 ПБ
*    ПОЛЯ ДЛЯ УПРЯТЫВАНИЯ ИР-ОВ БЛОКА ВЕРТУШ
*
УПВ2 КОНД В'0'
УПВ5 КОНД В'0'
УПВ6 КОНД В'0'
УПВ13 КОНД В'0'
УПВ17 КОНД В'0'
*
*
Э64БСС НОП
 СЧ РЕЖВЫВ
 И Е2
 ПО (М13)
 Э64 ИНФВЫВ
 ПБ НАЧБЮД
*
******************************************************
*
ФОРМ НОП , ФОРМИРУЕМ СТРОКУ БЮДЖЕТА ПОЛЬЗОВАТЕЛЯ
*
 СЧ ТСИСТ
 ЗП ШФР
 СЧ ТСИСТ+1
 ЗП ФАМ2
 СЧ ТСИСТ+2
 ЗП ФАМ2+1
*
 СЧ СЧПОЛ
 СЛЦ Е1
 ЗП СЧПОЛ  ЭТО СЧЕТЧИК ПОЛЬЗОВАТЕЛЕЙ
 ПВ ПЕРЕВ(М13)
 СДА 64-16
 ИЛИ ООООПП
 ЗП НОМЕР
*
 СЧ ППППЗЗ
 ЗП ФАМ2+2    ДЛЯ СИС.ВХОДА - НОМЕР В ГРУППЕ
 СЧ БЮДЖП+1
 И Е24П1
 ЗП ЗОНРЯ      ШИФР ПОЛЬЗОВАТЕЛЯ
 НТЖ Е8П1
 ПО ФОРМ1     СИСТЕМНЫЙ ВХОД
 СЧ ЗОНРЯ
 СДА 64+8
 ЗП ФАМ2
 УИА ФАМ2(М7)
 СЧИ М7
 Э50 '104'  ФИО ПОПАДЕТ В ФАМ2, ФАМ2+1
 СЧ ЗОНРЯ
 СБР Е24П1
 РЗБ ПППППП
 ЗП ШФР
 И Е16П1
 СДА 64-16
 ИЛИ МАСГР
 ЗП ФАМ2+2
 СЧ ШФР
 И Е48П17
 ИЛИ МАСКШФ
 ЗП ШФР
*
* ПОЛУЧАЕМ ЗОНЫ ЧЕРЕЗ П/П  ПОЛЗОН
*
ФОРМ1 ПВ ПОЛЗОН(М17)
 СЧ БЮДЖП+2
 И Е7П5
 СДА 64-4
 ИЛИ ЗЗППОП
 ЗП ДЖТ   КАТЕГОРИЯ ПОЛЬЗОВАТЕЛЯ
*
 СЧ БЮДЖП+1  ПОЛУЧЕНИЕ АРХИВОВ ПОЛЬЗОВАТЕЛЯ
 ПВ ДАЙАРХ(М17)  АРХИВЫ УЖЕ В ШКАРХ, ШКАРХ+1
*
ПП14 СЧ БЮДЖП+2  ОБРАБОТКА  ШТРАФА
 СБР Е28П25
 УИ М7
 ПО ПП14А
 НЕД
 УИ М7
ПП14А СЧ ПШТРАФ(М7)
      ЗП ДШТР
      ПБ (М11) ВОЗВРАТ
*
******************************************************
*
ПОЛЗОН НОП , ПОДПРОГРАММА ПОЛУЧЕНИЯ ЗОН ПОЛЬЗ.
 СЧ БЮДЖП+1
 И Е24П1
 ЗП РЯ5
 ПВ НСПОЛ(М13)  ПОДСЧЕТ ЗОН
 СЧ РЯ7   РАСПР.
 СЛЦ ИТБЗОН
 ЗП  ИТБЗОН   ИТГ : БЗОН   СУММ. ЗОНЫ  ПО БЮДЖ.
 СЧ РЯ7
 ПВ ПЕРЕВ(М13)
 СДА 64-8
 ИЛИ МАСЗОН
 ЗП ЗОНИМ
 СЧ РЯ8    ЗАНЯТО
 СЛЦ ИТПЗОН
 ЗП  ИТПЗОН   ИТГ : ЗЗОН   СУММ. ЗОН  ЗАНЯТО
 СЧ РЯ8
 ПВ ПЕРЕВ(М13)
 СДА 64-8
 ИЛИ МАСЗОН
 ЗП ЗОНИС
 ПБ (М17)
*
*$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
*
ПАРДИС НОП ,  УСТАНОВКА ПАРОЛЯ ДИСПЕТЧЕРА
*
 ПВ ПРАВ04(М13)  АДМ.СИС. ИЛИ ДИСП. ДРУГИЕ - НЕТ
*
 СЧ Е9П1  '777' -  ОТНОС. АДРЕС ПАРОЛЯ
 ЗП РЯ8
 ПБ ПР10  ДАЛЕЕ СТАНДАРТНО
*
*$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
*
ПАР    НОП ,  БЛОК УСТАНОВКИ ПАРОЛЕЙ
*
 ПВ ПОДГОТ(М13)
 ПВ НАЙПРО(М15)
 СЧ РЯВС
 ЗП ЗОНРЯ
 НТЖ Е8П1
 ЗП РЯ8   ДЛЯ СИСТЕМНОГО ВХОДА
 ПО ПР11      ПАРОЛЬ СИСТЕМНОГО ВХОДА
 УИА -1(М5)
 ПВ ЧИСЛА(М13)
 ЗП РЯ9
 ПВ ИЗ10В8(М13)
 СЧ РЯ2
 ЗП РЯ8
 СЧ РЯ9
 НТЖ ФЛОТД
 ПО ПР10
ПР11 ПВ ПРАВО0(М13)
ПР10 СЧ РЯБОЛЬ
 ЗП РЯ10
 СЧ ПБОЛЬ
 ЗП РЯБОЛЬ
ПР1 ПВ ПРИЕМ(М13)
 СЧ МАСПР
 ЗП ЗОНРЯ
 ПВ ИЗ10В8(М13)
 СЧ РЯ2
 ЗП РЯ1
 ПО ПАРЕСТ
 ПВ ЗАХВАТ(М13)
 Э70 ЧТСЛЗО
 СЧ РЯ1
 МОД РЯ8
 ЗП ТПАРБ
 Э70 ЗПСЛЗО
 ПВ ОСВОБ(М13)
 СЧ РЯ10
 ЗП РЯБОЛЬ
 ПБ НАЧБЮД
*
*
ПАРЕСТ НОП  
 УИА ПАЕСТ(М1)  ТАКОЙ ПАР.ЕСТЬ
 ПВ ВЫВОД(М17)
 ПБ ПР1
ПРФЛАГ СЧ ФЛОТД
 ПО ВОЗФЛ
 НТЖ ФЛОТД1
 ПО ВОЗФЛ
 ПБ ОШИФР
ВОЗФЛ ПБ (М13)
*
*
*     ПОЛУЧЕНИЕ ОТДЕЛА ПОЛЬЗОВАТЕЛЯ
*     НА  СМ:   ШИФР ПОЛЬЗОВАТЕЛЯ
*
*
ПОЛОТД СДА 64+16
 ЗП ЗОНРЯ
 ПВ ИЗ10В8(М13)
 СЧ РЯ2
 ЗП ФЛОТД1
 ПБ (М17)
*
*$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
*
КБЮ    НОП ,  КОНЕЦ РАБОТЫ В БЛОКЕ "БЮДЖЕТ"
 ПВ ДИСОТК(М13)
 ПБ РАЗД1
*
*$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
*
ГРУ    НОП ,  БЛОК ВЫЗОВА НА РОБ. ПОЛЕ БЮДЖЕТА ГРУППЫ
*
 СЧ
 ЗП БЮДЖП
 ЗП ШФР
*
 ПВ ПОДГОТ(М13)
 ПВ ШИФНОВ(М15)
 ПО ОШИФР
 СЧ ШФР
 СДА 64+8
 ЗП ГРУППА
 ПВ ГРУНАЙ(М13)
 ПЕ ББ6
ГРН УИА НЕТ(М1)
 ПВ ВЫВОД(М17)
 ПБ НАЧБЮД
ГРУНАЙ НОП ПОИСК ГРУП.БЮД.
 УИА -99(М2)
 Э70 ЧТСЛЗО
ГР НОП
 СЧ БЮДГР1+99(М2)
 ПО ГРЦ   НА КОНЕЦ ЦИКЛА
 И Е16П1
 НТЖ ГРУППА
 ПО ГР0   ГРУППА НАЙДЕНА
ГРЦ ЦИКЛ ГР(М2)
 СЧ 0   ГРУППЫ НЕТ
 ПБ (М13)
ГР0 СЧ БЮДГР2+99(М2)
 ЗП ГРУППА+1
 СЧ БЮДГР1+99(М2)
 ЗП ГРУППА
 ПБ (М13)
*
******************************************************
*
ДАЙГР НОП
 УИА ЗГБГРУ(М1)
 ПВ ВЕРТУШ(М13)
 ПВ ФОРГР(М11)
 УИА БЮДГРУ(М1)
 ПВ ВЕРТУВ(М13)
 ПБ ББ6
*
*    ФОРМИРОВАНИЕ СТРОКИ - БЮДЖЕТ ГРУППЫ
*
ФОРГР СЧ ГРУППА
 И Е16П1
 ЗП ЗОНРЯ
 ЗП РЯ6  ПАРАМЕТР(ШИФР ГРУППЫ)
 СДА 64-24
 РЗБ ПППППП
 СДА 64-8
 ИЛИ ПООООП
 ЗП ШИФГРУ       ШИФР В  Б-Ц
*
 ПВ НСГР(М13)
 СЧ РЯ7
 СЛЦ РЯ8    ПОЛУЧИЛИ БЮДЖЕТ ГРУППЫ
 ПВ ПЕРЕВ(М13)
 СДА 64-8
 ИЛИ МАСЗОН
 ЗП БЗГРУ     БЮДЖ.ГРУП.В БУКВ.
*
 СЧ РЯ8
 ПВ ПЕРЕВ(М13)
 СДА 64-8
 ИЛИ МАСЗОН
 ЗП ЗЗГРУ     ЗАНЯТО ЗОНЫ
*
 СЧ ГРУППА
 И Е23П17
 СДА 64+16
 ПВ ПЕРЕВ(М13)
 СДА 64-16
 ИЛИ МАСБНГ
 ЗП БЧЕЛ     ЧИСЛО ЛЮДЕЙ ПО БЮДЖЕТУ
*
 СЧ ГРУППА
 И Е16П1
 ЗП РЯ5
 СЧ RСДВИГ+1
 ЗП РЯ4
 ПВ ЛЮДИ(М13)
 СЧ РЯ7
 ПВ ПЕРЕВ(М13)
 СДА 64-16
 ИЛИ МАСБНГ
 ЗП  ЗЧЕЛ  РЕАЛЬНО ПОЛЬЗОВАТЕЛЕЙ ГРУППЫ
*
 СЧ ГРУППА
 И Е47П45
 СДА 64-12
 СЧМР
 ИЛИ ЗЗППОП
 ЗП КТГРУ  КАТЕГОРИЯ ГРУППЫ
*
 СЧ ГРУППА+1
 СДА 64-24  ШКАЛА АРХИВОВ
 ПВ ДАЙАРХ(М17)
 СЧ ШКАРХ
 ЗП АРХГРУ
 СЧ ШКАРХ+1
 ЗП АРХГРУ+1
 ПБ (М11)
*
*
ЗОНГР НОП ЗОНЫ ДЛЯ ГРУППЫ
*
*
 ПВ ПРАВО2(М13)
 ПВ ЗОНЫ(М17)
 СЧ ГРУППА
 И Е16П1
 ЗП РЯ6     ПАРАМЕТР(ШИФР ГРУППЫ)
*
 ПВ ХВАТЬ(М13)
 ПВ ЧКТАРХ(М16)
 ПВ НСГР(М13)
*
 СЧ БОБ  НОВ.БЮДЖЕТ
 ВЧ РЯ7  СВОБОДН.ЗОНЫ ГРУППЫ
 ВЧ РЯ8  РАСПРЕДЕЛЕН.ЗОНЫ
 ПО ЗНГР1   НОВ НЕ МЕНЬШЕ СТАРОГО БЮДЖ.
 ЗП РЯ1
 СЧ БОБ
 ВЧ РЯ8
 ПЕ МАЛО  БЮДЖ<РАСПРЕД
 СЧ РЯ1
 ЗП РЯ7
 СЧ РЯ5
 ЗП РЯ6    ПОДГОТОВКА ПАРАМЕТРЫ
 ПВ ЗОННЕТ(М13)
 ПВ ЗКТАРХ(М16)
 ПВ ОСВОБ(М13)
 ПБ ББ6
ЗНГР1 ЗП РЯ1    ИЗМЕНЕНИЕ ЧИСЛА ЗОН
 СЧ РЯ6
 СДА 64+8
 ЗП РЯ6    НОМЕР ОТДЕЛА
 ПВ НСОТ(М13)
 СЧ РЯ1
 ВЧОБ РЯ7  СВОБОДНЫЕ ЗОНЫ
 ПЕ НБЮД
 СЧ РЯ1
 ЗП РЯ7
 СЧ РЯ5
 ЗП РЯ6   ПРИЗНАК СВОБ.ЗОНЫ  ДЛЯ ГРУППЫ
 СЧ ГРУППА
 И Е16П1
 СДА 64-8
 ИЛИ П376
 ЗП РЯ5    КОД ГРУППЫ
 ПВ ЗОНДАЙ(М13)
 ПВ ЗКТАРХ(М16)
 ПВ ОСВОБ(М13)
 ПБ ББ6
*
*
*      БЛОК ВЫДЕЛЕНИЯ  Л Ю Д Е Й  ДЛЯ ГРУППЫ
*
*
ЧЕЛ НОП
 СЧ ГРУППА
 ПО НЕТРП
 И Е16П1
 ЗП ШФР
 ПВ ПРАВО2(М13)
 ПВ ЗОНЫ(М17)
 ВЧОБ П100
 ПЕ МНГЧЕЛ
 ПВ ЗАХВАТ(М13)
 Э70 ЧТСЛЗО
 СЧ ГРУППА
 И Е23П17
 СДА 64+16
 ВЧОБ БОБ
 ПО ЧЕЛ1   НОВ > СТАР
 СЧ RСДВИГ+1
 ЗП РЯ4
 СЧ ШФР
 ЗП РЯ5
 ПВ ЛЮДИ(М13)
 СЧ РЯ7
 ВЧОБ БОБ
 ПО ЧЕЛ2
 ПВ ОСВОБ(М13)
 ПБ МАЛО
ЧЕЛ2 СЧ ГРУППА
 И Н24П17
 ЗП ГРУППА
 СЧ БОБ
 СДА 64-16
 ИЛИ ГРУППА
 ЗП ГРУППА
 ПВ ЗПГР(М13)
 ПВ ОСВОБ(М13)
 ПБ ББ6
ЧЕЛ1 ЗП НОМЕР
 СЧ ШФР
 СДА 64+8
 ЗП РЯ5
 ЗП ЗОНРЯ
 СЧ RСДВИГ
 ЗП РЯ4
 ПВ ЛЮДИ(М13)
 ПВ ИЗ10В8(М13)
 СЧ РЯ5
 ЗП РЯ4
 ПВ ЛЮБГР(М13)
 МОД РЯ2
 СЧ БЮДОТ1
 И Е16П9
 СДА 64+8
 ВЧ РЯ6
 ВЧ РЯ5
 ВЧ НОМЕР
 ПО ЧЕЛ2    ЗАПИШЕМ
 ПВ ОСВОБ(М13)
 ПБ НБЮД
*
*
*     ЗАПИСЬ БЮДЖЕТА ГРУППЫ
*
*
ЗПГР УИА -99(М1)
 СЧ ГРУППА
 И Е16П1
 ЗП ШФР
 Э70 ЧТСЛЗО
ЗПГ1 СЧ БЮДГР1+99(М1)
 И Е16П1
 НТЖ ШФР
 ПЕ ЗПГ2
 СЧ ГРУППА
 ЗП БЮДГР1+99(М1)
 СЧ ГРУППА+1
 ЗП БЮДГР2+99(М1)
 Э70 ЗПСЛЗО
 ПБ (М13)
ЗПГ2 ЦИКЛ ЗПГ1(М1)
 ПБ КОНЕЦ
МНГЧЕЛ УИА МНЧЕЛ(М1)
 ПВ ВЫВОД(М17)
 ПБ ББ6
*
******************************************************
*
НСОТ   НОП ,  ПОДСЧЕТ ЗОН ОТДЕЛА
*      РЯ5   - NО'377''376'
*      РЯ6   - NО
*      РЯ7   - ЧИСЛО СВОБОДНЫХ ЗОН ОТД.
*      РЯ8   - РАСПРЕДЕЛЕНО ПОЛЬЗОВАТЕЛЯМ
*      РЯ9   - ЗАНЯТЫЕ ФАЙЛАМИ ЗОНЫ
*      РЯ11  - СВОБОДНЫЕ ЗОНЫ АРХИВА
*      РЯ12  - ЗОНЫ РАСПРЕД.ГРУППАМ
*
 МОД ДЛНАРХ
 УИА -1(М5)  ЧИСЛО ЗОН В ТЕК. АРХИВЕ - 1
*
 СЧ
 ЗП РЯ7
 ЗП РЯ8
 ЗП РЯ11
 ЗП РЯ12
 ЗП РЯ9
*
 СЧ РЯ6
 СДА 64-16
 ИЛИ КОНОТД
 ЗП РЯ5
*
НСОТ1 НОП 
 СЧ ПОЛКТ2(М5)
 И Е24П1
 ЗП РЯ10   ВЫДЕЛЕНИЕ ПРИЗНАКА
 ПЕ НСОТ5     ЗОНА НЕ СВОБОДНА
 СЧ ОДИН
 СЛЦ РЯ11    ЧТЕНИЕ СВОБОДН.ЗОНЫ АРХИВА
 ЗП РЯ11
 ПБ НСОТ4
НСОТ5 СЧ РЯ10
 НТЖ РЯ5
 ПЕ НСОТ3   ЗОНА НЕ СВОБОДНА ИЛИ НЕ ПРИНАДЛ.ОТДЕЛУ
 СЧ ОДИН
 СЛЦ РЯ7
 ЗП РЯ7
 ПБ НСОТ4 НА КОНЕЦ ЦИКЛА
НСОТ3 СЧ РЯ10  ЧЬЯ ЗОНА
 СДА 64+16
 НТЖ РЯ6
 ПЕ НСОТ4  НЕ НАША
 СЧ ОДИН
 СЛЦ РЯ8
 ЗП РЯ8    РАСПРЕДЕЛЕН.ПОЛЬЗОВАТЕЛЯМ
 СЧ ПОЛКТ2(М5)
 И Е8П1      ЧТЕНИЕ ЗОНЫ РАСПРЕД.ГРУППЕ
 ПО НСОТ6    ЗОНА ПОЛЬЗОВАТЕЛЯ
 СЧ ОДИН
 СЛЦ РЯ12
 ЗП РЯ12  БЮДЖЕТ ГРУПП(ВСЕХ)
НСОТ6 СЧ ПОЛКТ1(М5)
 ПО НСОТ4  ЗОНА СВОБОДНА
 СЧ ОДИН
 СЛЦ РЯ9
 ЗП РЯ9   ЗАНЯТЫЕ ЗОНЫ
НСОТ4 ПИО НСОТ2(М5)
 СЛИА -1(М5)
 ПБ НСОТ1
НСОТ2 ПБ (М13)
*
******************************************************
*
НСГР   НОП ,  ПОДСЧЕТ ЗОН ГРУППЫ
*      РЯ5  - 7398'376'
*      РЯ6  - 7398
*      РЯ7  - СВОБОДНЫЕ ЗОНЫ ГРУППЫ
*      РЯ8  - РАСПРЕДЕЛЕННЫЕ ЗОНЫ
*      РЯ9  - ЗАНЯТЫЕ ПОД ФАЙЛЫ
*
 МОД ДЛНАРХ
 УИА -1(М5)  ДЛИНА АРХ. - 1
*
 СЧ
 ЗП РЯ7
 ЗП РЯ8
 ЗП РЯ9
 СЧ РЯ6
 СДА 64-8
 ИЛИ П376
 ЗП РЯ5
НСГР1 НОП 
 СЧ ПОЛКТ2(М5)
 И Е24П1
 ЗП РЯ10
 НТЖ РЯ5
 ПЕ НСГР3    ЗОНА НЕ ЯВЛЯЕТСЯ СВОБОДНОЙ ГРУППЫ
 СЧ ОДИН
 СЛЦ РЯ7
 ЗП РЯ7
 ПБ НСГР4 НА КОНЕЦ ЦИКЛА
НСГР3 СЧ РЯ10
 И Е8П1
 ПО НСГР4  ЗОНА НЕ ПРИНАДЛЕЖИТ ГРУППЕ
 СЧ РЯ10
 СДА 64+8
 НТЖ РЯ6
 ПЕ НСГР4  НЕ НАША ЗОНА
 СЧ ОДИН
 СЛЦ РЯ8
 ЗП РЯ8
 СЧ ПОЛКТ1(М5)
 ПО НСГР4 НЕ ЗАНЯТА ПОД ФАЙЛ
 СЧ ОДИН
 СЛЦ РЯ9
 ЗП РЯ9
НСГР4 ПИО НСГР2(М5)
 СЛИА -1(М5)
 ПБ НСГР1
НСГР2 ПБ (М13)
*
******************************************************
*
НСПОЛ НОП ПОДСЧЕТ ЗОН ДАННОГО ПОЛЬЗОВАТЕЛЯ
*     РЯ5  - ШИФР ПОЛЬЗОВАТЕЛЯ
*     РЯ7  - РАСПРЕДЕЛЕНО ЗОН
*     РЯ8  - ЗАНЯТО ЗОН
*
*
 МОД ДЛНАРХ
 УИА -1(М5)  ДЛИНА ТЕКУЩЕГО АРХИВА - 1
*
 СЧ
 ЗП РЯ7
 ЗП РЯ8
*
НСПОЛ1 НОП 
 СЧ ПОЛКТ2(М5)
 И Е24П1
 НТЖ РЯ5
 ПЕ НСПОЛ3  НЕ НАШЕ
 СЧ РЯ7
 СЛЦ Е1
 ЗП РЯ7
*
 СЧ ПОЛКТ1(М5)
 ПО НСПОЛ3  СВОБОДНА
 СЧ РЯ8
 СЛЦ Е1
 ЗП РЯ8
НСПОЛ3 ПИО НСПОЛ2(М5)
 СЛИА -1(М5)
 ПБ НСПОЛ1
НСПОЛ2 ПБ (М13)
*
******************************************************
*
ЗОННЕТ НОП ,  ИСКЛЮЧЕНИЕ СВОБОДНЫХ ЗОН
*
*       ВНИМАНИЕ !!!    ВСЕ ДЕЛАТЬ ПРИ ЗАХВАЧЕННОМ МД
*
*     РЯ5  - КОД ПОДРАЗДЕЛЕНИЯ:
*            0              - ДЛЯ ОТДЕЛА
*            73 '377''376'  - ДЛЯ ГРУППЫ
*            7398'376'      - ДЛЯ ПОЛЬЗ.ГРУППЫ
*            73 '377''376'  - ДЛЯ ПОЛЬЗОВАТЕЛЯ
*     РЯ6  - ПРИЗНАК СВОБОДНОЙ ЗОНЫ
*     РЯ7  - ИЗМЕНЕНИЕ ЧИСЛА ЗОН
*
 СЧ РЯ7
 УИ М5
*
 МОД ДЛНАРХ
 УИА -1(М6)  ДЛИНА АРХИВА - 1
*
 СЧ РЯ6
 НТЖ Е8П1
 ПЕ ЗНН9   НЕ СИС.ВХОД
 ЗП РЯ5
 ПБ ЗННН1
ЗНН9 СЧ РЯ6
 И Е16П1
 НТЖ КОНОТД
 ПЕ ЗНН6     РАБОТА НЕ С ОТДЕЛОМ
 ЗП РЯ5
 ПБ ЗННН1
ЗНН6 СЧ РЯ6
 И Е8П1
 НТЖ П376
 ПЕ ЗНН7   НЕ  ГРУППА
 СЧ РЯ6
 ИЛИ КОНОТД
 ЗП РЯ5
 ПБ ЗННН1
ЗНН7 СЧ РЯ6
 И Е8П1
 ПЕ ЗНН8   ПОЛЬЗОВАТЕЛЬ ГРУППЫ
 СЧ РЯ6
 И Н16П1
 ИЛИ КОНОТД
 ЗП РЯ5
 ПБ ЗННН1
ЗНН8 СЧ РЯ6
 И Н377
 ИЛИ П376
 ЗП РЯ5
ЗННН1 СЧ ПОЛКТ2(М6)
 И Е24П1
 НТЖ РЯ6
 ПЕ ЗННН2
 СЧ РЯ6      ПРОВЕРКА НА ПРОСТОГО ПОЛЬЗОВАТЕЛЯ
 И Е8П1
 НТЖ П376
 ПО ЗНН4  НЕ ПРОСТ
 СЧ ПОЛКТ1(М6)     ПОЛЗОВАТЕЛЬ
 ПЕ ЗННН2  ЗОНА ЗАНЯТА
ЗНН4 СЧ
 ЗП ПОЛКТ1(М6) РАСЧИСТКА КАТАЛОГОВ
 СЧ РЯ5
 ЗП ПОЛКТ2(М6)
 СЛИА 1(М5)
 ПИНО ЗННН2(М5)
 ПБ (М13)     ВОЗВРАТ
ЗННН2 СЛИА -1(М6)
 ПБ ЗННН1
*
******************************************************
*
ЗОНДАЙ НОП ,  ВЫДЕЛЕНИЕ ЗОН ДЛЯ ПОДРАЗДЕЛЕН.ПОЛЬЗ.
*
*       ВНИМАНИЕ !!!    ВСЕ ДЕЛАТЬ ПРИ ЗАХВАЧЕННОМ МД
*
*     РЯ5  - КОД ПОДРАЗДЕЛЕНИЯ
*     РЯ6  - ПРИЗНАК СВОБОДНОЙ ЗОНЫ
*     РЯ7  - ИЗМЕНЕНИЕ ЧИСЛА ЗОН
*
 СЧ РЯ7
 УИ М5
 УИА -1020(М6)
ЗНН1 ПИО ЗНН2(М5)
 СЧ ПОЛКТ2+1020(М6)
 НТЖ РЯ6
 ПЕ ЗНН3  ЗОНА ЗАНЯТА
 ЗП ПОЛКТ1+1020(М6)
 СЧ РЯ5
 ЗП ПОЛКТ2+1020(М6)
 СЛИА -1(М5)
ЗНН3 ЦИКЛ ЗНН1(М6)
ЗНН2 ПБ (М13)
*
******************************************************
*
ГРУЛЮД НОП 
*
*    ПОДСЧЕТ ЧИСЛА ПОЛЬЗОВАТЕЛЕЙ
*    РАСПРЕДЕЛЕННЫХ ГРУППАМ ОТДЕЛА
*    И КОЛИЧЕСТВА ГРУПП В ОТДЕЛЕ
*
*    РЯ6 - НОМЕР ОТДЕЛА В Д.Д.
*    М2 - ЧИСЛО ГРУПП В ОТДЕЛЕ
*    РЯ1 - МАКСИМ.ЧИСЛО ГРУППОВЫХ ПОЛЬЗОВАТЕЛЕЙ
*    М13 - АДРЕС ВЫХОДА
*
*
 УИА -99(М1)
 СЧ 
 ЗП РЯ1
 УИА (М2)
ПРГР1 СЧ БЮДГР1+99(М1)  СКОЛЬКО ГРУПП В ОТДЕЛЕ
 ПО ПРГР3  НА КОНЕЦ ЦИКЛА
 И Е16П1
 СДА 64+8
 НТЖ РЯ6
 ПЕ ПРГР3  НА КОНЕЦ ЦИКЛА
 СЛИА 1(М2)
 СЧ БЮДГР1+99(М1)
 И Е23П17
 СДА 64+16
 СЛЦ РЯ1
 ЗП РЯ1
ПРГР3 ЦИКЛ ПРГР1(М1)
 ПБ (М13)
*
******************************************************
*
ЛЮДИ НОП ПОДСЧЕТ ЧИСЛА ПОЛЬЗОВАТЕЛЕЙ
*     РЯ4  - СДВИГ
*              RСДВИГ    ДЛЯ ОТДЕЛА
*              RСДВИГ+1    ДЛЯ ГРУППЫ
*     РЯ5  - КОД ПОДРАЗДЕЛЕНИЯ
*     РЯ6  - ЧИСЛО ПОЛЬЗОВАТЕЛЕЙ  4-Х  ЗНЧ
*     РЯ7  - ЧИСЛО ПОЛЬЗОВАТЕЛЕЙ  6-ТИ  ЗНЧ
*     РЯ10  - ЧИСЛО ПОЛЬЗОВАТЕЛЕЙ  К Р А Б  'А
*     РЯ17 - ЧИСЛО ПОЛЬЗОВАТЕЛЕЙ ТЕКУЩЕГО АРХИВА
 СЧИ М1
 ЗП РЯ8
 СЧИ М2
 ЗП РЯ9
 СЧ 
 ЗП РЯ6
 ЗП РЯ10
 ЗП РЯ7
 ЗП РЯ17
*
 УИА -3(М2)
ЛЮД1 УИА -1024(М1)
 Э70 ЧТКЛЮ1+3(М2)
ЛЮД2 СЛИА 2(М1)
 СЧ КЛЮЧИ+1022(М1)
 ПО ЛЮД3
 НТЖ ВСЕЕД
 ПО ЛЮД4
 СЧ ОДИН
 СЛЦ РЯ10
 ЗП РЯ10
 СЧ КЛЮЧИ+1022(М1)
 МОД РАБАРХ
 И Е48-1
 ПО ЛЮД7  НЕТ В ТЕК.АРХ.
 СЧ РЯ17
 СЛЦ Е1
 ЗП РЯ17
ЛЮД7 СЧ КЛЮЧИ+1022(М1)
 И Е24П1
 ЗП РЯ11
 СД РЯ4
 НТЖ РЯ5
 ПЕ ЛЮД3   НА КОНЕЦ ЦИКЛА
 СЧ РЯ11
 И Е8П1
 ПЕ ЛЮД5
 СЧ ОДИН
 СЛЦ РЯ6
 ЗП РЯ6
 ПБ ЛЮД3
ЛЮД5 СЧ ОДИН
 СЛЦ РЯ7
 ЗП РЯ7
ЛЮД3 ПИНО ЛЮД2(М1)
 ЦИКЛ ЛЮД1(М2)
ЛЮД4 СЧ РЯ8
 УИ М1
 СЧ РЯ9
 УИ М2
 ПБ (М13)
*
******************************************************
*
ЛЮБГР НОП  ПОДСЧЕТ НОМЕРОВ РАСПР.ГРУППАМ ОТДЕЛА
*
*
*     РЯ4  -  НОМЕР ОТДЕЛА
*     РЯ5  -  РЕЗУЛЬТАТ
*     РЯ9  -  ВСЕГО ГРУПП В КРАБ'Е
*     РЯ12 -  ЧИСЛО ГРУПП В ОТДЕЛЕ
*
 СЧ  
 ЗП РЯ5
 ЗП РЯ9
 ЗП РЯ12
 УИА -99(М1)
ЛЮБГ1 СЧ БЮДГР1+99(М1)
 ПО ЛЮБГ2
 СЧ ОДИН
 СЛЦ РЯ9
 ЗП РЯ9
 СЧ БЮДГР1+99(М1)
 И Е16П1
 СДА 64+8
 НТЖ РЯ4
 ПЕ ЛЮБГ2
 СЧ ОДИН
 СЛЦ РЯ12
 ЗП РЯ12
 СЧ БЮДГР1+99(М1)
 И Е23П17
 СДА 64+16
 СЛЦ РЯ5
 ЗП РЯ5
ЛЮБГ2 ЦИКЛ ЛЮБГ1(М1)
 ПБ (М13)
*
******************************************************
*
*     П.П.  ОБРАБОТКИ ЧИСЕЛ 
*     ИР 5  МАХ КОЛ-ВО ЦИФР
*
ЧИСЛА НОП ОБРАБОТКА ЦИФР
 СЧИ М13
 ЗП РЯ10  АДРЕС ВОЗВРАТА
ЧИС1 ПВ ВС(М13)
 ВЧОБ П11
 ПЕ ЧИС2   НА КОНЕЦ
 СЧ ЗОНРЯ
 СДА 64-8
 ИЛИ РЯВС
 ЗП ЗОНРЯ
 ЦИКЛ ЧИС1(М5)
 СЧ РЯБОЛЬ
 НТЖ СБОЛЬ
 ПО УСТОШ
 ПБ ОШИФР
ЧИС2 СЧ ЗОНРЯ ПО (М13)
 СБР ПППППП
 СДА 64+24
 ЗП ЗОНРЯ   В Д-Д
 МОД РЯ10
 ПБ
*
******************************************************
*
*     П Р А В О В О Й   БЛОК 
*
*
ДИСПРА НОП , ПРАВО НЕ НИЖЕ ДИСПЕТЧЕРА ==>  СМ=0
  СЧ ФЛДИСП
  ПО АДМПРА
  СЧ 0
  ПБ (М13)  ЕСТЬ ПРАВО
*
АДМПРА НОП , ПРАВО НЕ НИЖЕ АДМ.СИС. ==>  СМ=0
 СЧ ФЛОТД
 ПБ (М13)
*
* В ОСТАЛЬНЫХ БЛОКАХ : ЕСЛИ ПРАВО НЕ ПОДТВЕРЖДЕНО
* ТО УХОД НА "ЗАПР.ДИР."
*
*   ПРАВО0  ПРИНАДЛЕЖНОСТЬ ГЛАВНОМУ АДМИНИСТРАТОРУ
*   ПРАВ04  ПРИНАДЛЕЖНОСТЬ АДМ.СИС. ИЛИ ДИСПЕТЧЕРУ
*   ПРАВО1  АДМИНИСТРАТОР ОТДЕЛА ДЛЯ РАБ.С ПОЛЬЗ.
*   ПРАВО2  АДМИНИСТРАТОР ОТДЕЛА ДЛЯ РАБ.С ГРУП.
*   ПРАВО3  АДМИНИСТРАТОР ГРУППЫ.
*
*
ПРАВО0 НОП 
 СЧ ФЛОТД
 ПЕ ЗАПДИР
 ПБ (М13)
*
ПРАВ04 НОП , АДМ.СИС. ИЛИ ДИСПЕТЧЕР
 СЧ ФЛОТД
 ПО (М13)  АДМ.СИС.
 СЧ ФЛДИСП
 ПЕ (М13)  ЭТО ДИСП.
 ПБ ЗАПДИР  ЗАПРЕЩЕННАЯ ДИРЕКТИВА
*
ПРАВО1 НОП 
 СЧ ФЛОТД
 ПО (М13)
 СЧ БЮДЖП+1
 И Е24П1
 СДА 64+16
 НТЖ ФЛОТД
 ПЕ ЗАПДИР
 ПБ (М13)
ПРАВО2 НОП 
 СЧ ФЛОТД
 ПО (М13)
 СЧ ГРУППА
 И Е16П1
 СДА 64+8
 НТЖ ФЛОТД
 ПЕ ЗАПДИР
 ПБ (М13)
ПРАВО3 НОП 
 СЧ ФЛОТД
 ПО (М13)
 СЧ БЮДЖП+1
 И Е24П1
 ЗП БЮДЖП
 СДА 64+16
 НТЖ ФЛОТД
 ПО (М13)
 СЧ РАБШИФ
 И Е8П1
 ПЕ ЗАПДИР
 СЧ БЮДЖП
 И Н377
 НТЖ РАБШИФ
 ПЕ ЗАПДИР
 ПБ (М13)
ЗАПДИР НОП 
 ПВ ОСВОБ(М13)
 УИА ЗАПД(М1)
 ПВ ВЫВОД(М17)
 ПБ НАЧБЮД
*
******************************************************
*
ГЛАДИМ НОП   СГЛАЖИВАНИЕ КАТЕГОРИЙ ПОЛЬЗОВАТЕЛЕЙ
*
*
*  РЯ4  -  СДВИГ
*          RСДВИГ - ДЛЯ ОТДЕЛА
*          RСДВИГ+1 - ДЛЯ ГРУППЫ
*  РЯ5  -  КОД ПОДРАЗДЕЛЕНИЯ (НОМЕР ОТД.ИЛИ ШИФ.ГР.)
*  РЯ6  -  СГЛАЖИВАЮЩАЯ КАТЕГОРИЯ
*
*
*  В Н И М А Н И Е   ВСЕ ДЕЛАТЬ НА ЗАХВАТЕ
*
 СЧ РЯ6
 СДА 64-4
 ЗП РЯ7   ДЛЯ УСТАНОВКИ КАТЕГОРИЙ В КЛЮЧАХ
 СЧ Е3П1
 СДА 64-4
 НТЖ ВСЕЕД
 ЗП РЯ8   ===  Н28П27
 УИА -3(М2)
ГЛ1 УИА -1024(М1)
 Э70 ЧТКЛЮ1+3(М2)
ГЛ2 СЛИА 2(М1)
 СЧ КЛЮЧИ+1022(М1)
 ПО ГЛ3   НА КОЙЕЦ ЦИКЛА
 НТЖ ВСЕЕД
 ПО ГЛКОН    ЗАКОНЧИЛИ
 СЧ КЛЮЧИ+1022(М1)
 И Е24П1
 ЗП РЯ9  Ш И Ф Р 
 СД РЯ4
 НТЖ РЯ5
 ПЕ ГЛ3 НА КОНЕЦ ЦИКЛА
 СЧ РЯ4   ПРИЗНАК СДВИГА
 НТЖ RСДВИГ+1
 ПЕ ГЛ4   РАБОТА С ОТДЕЛОМ
 СЧ РЯ9
 И Е8П1
 ПО ГЛ3  НЕ ПОЛЬЗОВАТЕЛЬ ГРУППЫ
ГЛ4 СЧ КЛЮЧИ+1023(М1)
 И Е29П27
 СДА 64+26
 ВЧОБ РЯ6     МАКСИМАЛЬЙАЯ КАТЕГОРИЯ
 ПО ГЛ3
 СЧ КЛЮЧИ+1023(М1)
 И РЯ8
 ИЛИ РЯ7
 ЗП КЛЮЧИ+1023(М1)
ГЛ3 ПИНО ГЛ2(М1)
 Э70 ЗПКЛЮ1+3(М2)
 ЦИКЛ ГЛ1(М2)
 ПБ (М13)
ГЛКОН Э70 ЗПКЛЮ1+3(М2)
 ПБ (М13)

*^
*^
*23456789012345678901234567890123456789012345678901234
**********************************************^
*                                            *^
*    Д И А Л О Г О В А Я    С И С Т Е М А    *^
*                К  Р  А  Б                  *^
*                                            *^
*--------------------------------------------*^
*  БЮДЖЕТНО-СТАТИСТИЧЕСАЯ ПОДСИСТЕМА.        *^
*  ЧАСТЬ2                                    *^
*  БЛОК СТАТИСТИКИ                           *^
**********************************************^
*^
*^
БЛСТАТ НОП^
 СЧ СБОЛЬ^
 ЗП РЯБОЛЬ^
 СЧ^
 ЗП КООБЗ2^
 ПВ УСТФЛГ(М13)^
СТАТ0 ПВ ГАСИТГ(М13)^
 ПВ ФЛГДИР(М13)   ГАСИМ ПРИЗНАКИ ПОДДИРЕКТИВ^
 ПВ ПРИЕМ(М13)^
СТАТ1 СЧ ЧТКТСТ^
 Э50 '105'^
 ПО СТАТ01^
 ПВ ДИСОТК(М13)^
СТАТ01 НОП^
 ПВ ПРИГЛ(М15)^
 ПВ НАЙПРО(М15)^
 СЧ РЯВС^
 НТЖ Е8П1^
 ПО СТАТ0  ОБХОД ПУСТОЙ ПОСЫЛКИ^
 СЧ РЯВС^
 ПВ ВЫБДИР(М17)
 ПВ ПАМВС(М13)^
 УИА -10(М1)^
*^
НАЧ СЧ ПЕРК1+10(М1)^
 НТЖ ТЕКДИР
 И МАСДИР
 ПО ПЕРК1+10(М1)^
 ЦИКЛ НАЧ(М1)^
 ПВ НЕТДИР(М13)^
 ПБ СТАТ0^
*^
*^
*     ВЫПОЛНЕНИЕ ДИРЕКТИВЫ  НОВ^
*  (ПОДГОТОВКА МЛ СТАТИСТИКИ)^
*^
*^
НОВМЛ НОП^
*^
 ПВ ПОДГОТ(М13)^
 ПВ НАЙПРО(М15)^
 ПВ ПРЧСТ(М13)^
 СЧ РЯВС^
 ЗП ЗОНРЯ^
 УИА -3(М5)^
 ПВ ЧИСЛА(М13)^
 ПВ ДИСТАТ(М13)^
 СЧ^
 ЗП РЯ11  ПРИЗНАК АДМИНИСТРАТОРА СИСТЕМЫ^
 ПВ КТОАДМ(М10)   ЗАПРОС ПАРОЛЯ АДМИНИСТРАТОРА^
 ПЕ КОНЕЦ  НЕ ПОДТВЕРЖДЕН^
 СЧ СБОЛЬ^
 ЗП РЯБОЛЬ^
 УИА -1022(М1)^
 СЧ^
НОВМЛ0 ЗП КАТСТ+1023(М1)^
 ЦИКЛ НОВМЛ0(М1)^
 СЧ ВСЕЕД^
 ЗП КАТСТ^
 Э70 ЗПКТСТ^
 ПВ ДИСОТК(М13)^
 ПБ БЛСТАТ^
*^
*^
*    ДИРЕКТИВА  К Т Л^
*^
*^
КТЛ НОП^
*^
 СЧ^
 ЗП КООБЗ2   ПРИЗНАК НАЧАЛА БУФЕРА ВЫВОДА^
 СЧ УСБОБ^
 НТЖ ВСЕЕД^
 ПО КТЛСТН   МЛ НАКОПЛЕНИЯ СТАНДАРТНАЯ^
 СЧ УСБОБ^
 ПБ КТЛ0  НА ДОЗАКАЗ^
КТЛСТН СЧ МЛСТАТ^
 И Е16П1^
КТЛ0 ПВ ДИСТАТ(М13)^
 Э70 ЧТКТСТ^
 СЧ КАТСТ^
 НТЖ ВСЕЕД^
 ПО КТЛ1  НА ПОЛУЧЕНИЕ КАТАЛОГА^
 ПВ ДИСОТК(М13)^
 ПБ ЗАТКАТ^
КТЛ1 НОП^
 УИА -1022(М3)^
 УИА -10(М2)^
КТЛ11 СЧ КАТСТ+1023(М3)^
 ПО КТЛ91  ПУСТАЯ ЗОНА ==> НА КОНЕЦ ЦИКЛА ПО М2^
 СБР Е24П1^
 РЗБ ПППППП
 ПВ ПЕРВД(М17)^
 ИЛИ МАСВД1^
 ЗП МАСПР+10(М2)^
 УИИ М5(М3)^
 СЛИА 1023(М5)^
 СЧИ М5^
 СБР Е12П1^
 РЗБ СССССС
 СДА 64+8^
 ИЛИ МАСКТЛ  МАСКА ВИДА: ' 0000;'^
 ЗП МАСПР+11(М2)^
 СЧ М40377^
 ЗП МАСПР+12(М2)^
 СЛИА 2(М2)^
 ПИНО КТЛ91(М2)^
 УИА МАСПР(М1)^
 ПВ ВЕРТУШ(М13)^
 УИА -10(М2)^
КТЛ91 ЦИКЛ КТЛ11(М3)^
 СЛИА 10(М2)^
 ПИО КПЕЧСТ(М2)^
 УИА МАСПР(М1)^
 ПВ ВЕРТУШ(М13)^
 ПБ КПЕЧСТ  НА ОБРАБОТКУ КОНЦА ПЕЧАТИ^
*^
*^
*     РАЗБОР И ВЫПОЛНЕНИЕ  ДИРЕКТИВЫ^
*         У  С  Т^
*^
*^
УСТ НОП^
 ПВ ПОДГОТ(М13)^
 ПВ НАЙПРО(М15)^
ВОЗУСТ СЧ РЯВС^
 НТЖ Е8П1^
 ПО УСТ00  НА ПРОСМОТР  ТЕКУЩИХ УСТАНОВОК^
 СЧ РЯВС^
 ПВ ВЫБДИР(М17)^
 УИА -13(М4)^
УСТ1 СЧ ПЕРК2+13(М4)^
 НТЖ ТЕКДИР
 И МАСДИР

 ПО ПЕРК2+13(М4)^
 ЦИКЛ УСТ1(М4)^
УСТОШ ПВ ОШДИР(М13)^
 ПБ БЛСТАТ^
УСТ00 НОП^
 СЧ^
 ЗП КООБЗ2^
 ПВ УСТПОК(М13)^
 ПБ КПСТ3^
*^
*^
УСТПОК НОП^
*^
*     ПРОСМОТР ТЕКУЩИХ УСТАНОВОК^
*^
*^
 СЧИ М13^
 ЗП ИРС13^
 УИА ТЕКУС(М1)^
 ПВ ВЕРТУШ(М13)^
 СЧ УСОТД^
 НТЖ ВСЕЕД^
 ПО ПОКГ  НА ПРОСМОТР УСТАНОВКИ ГРУППЫ^
 СЧ УСОТД^
 СБР Е24П1^
 РЗБ ПППППП
 ИЛИ ППППОО
 ЗП РЯ15^
 СЧ СОТДЕЛ^
 ЗП РЯ14^
 УИА РЯ14(М1)^
 ПВ ВЕРТУШ(М13)^
ПОКГ СЧ УСГРУ  ПРОСМОТР ГРУППУ^
 НТЖ ВСЕЕД^
 ПО ПОКШ   НА ПРОСМОТР УСТАНОВКИ ШИФРА^
 СЧ УСГРУ^
 СБР Е24П1^
 РЗБ ПППППП
 ИЛИ МАГРУС  МАСКА ВИДА:  '- 0000'^
 ЗП РЯ15^
 СЧ СГРУП^
 ЗП РЯ14^
 УИА РЯ14(М1)^
 ПВ ВЕРТУШ(М13)^
ПОКШ СЧ УСШИФ НА ПРОСМОТР УСТАНОВКИ ШИФРА^
 НТЖ ВСЕЕД^
 ПО ПОКТ^
 СЧ УСШИФ^
 НТЖ Е8П1^
 ПЕ ПОКШ1   УСТАНОВКА ПРОСТОГО ПОЛЬЗОВАТЕЛЯ^
 УИА ТСИСТ(М1)^
 ПВ ВЕРТУШ(М13)^
 ПБ ПОКТ НА ПРОСМОТР УСТАНОВКИ ТЕРМИНАЛОВ^
ПОКШ1 НОП^
 СЧ УСШИФ^
 СБР Е24П1^
 РЗБ ПППППП
 ЗП РЯ15^
 СЧ СШИФР^
 ЗП РЯ14^
 УИА РЯ14(М1)^
 ПВ ВЕРТУШ(М13)^
ПОКТ СЧ УСТЕР^
 НТЖ ВСЕЕД^
 ПО ПОКБ НА ПРОСМОТР УСТАНОВКИ NМАШ^
 СЧ УСТЕР^
 СБР Е6П1^
 РЗБ СССССС
 СДА 64+32^
 ИЛИ СТЕР^
 ЗП РЯ15^
 УИА РЯ15(М1)^
 ПВ ВЕРТУШ(М13)^
ПОКБ СЧ УСМАШ ПРОСМОТР УСТАНОВКИ МАШИНЫ^
 ИЛИ СБЭС^
 ЗП РЯ15^
 НТЖ ВСЕЕД^
 ПО ПОКАЦ   НА ПРОСМОТР УСТАНОВКИ АЦПУ^
 УИА РЯ15(М1)^
 ПВ ВЕРТУШ(М13)^
ПОКАЦ СЧ АЦП^
 ПО ПОКСНЯ  НА ПРОСМОТР УСТАНОВКИ СНЯТИЯ^
 СЧ САЦДА^
 ЗП РЯ15^
 УИА РЯ15(М1)^
 ПВ ВЕРТУШ(М13)^
ПОКСНЯ СЧ УССНЯ^
 НТЖ ВСЕЕД^
 ПО ПОКРЕС НА ПРОСМОТР УСТАНОВКИ РЕСУРСА^
 СЧ УССНЯ^
 СДА 64-24^
 ИЛИ МАССНЯ^
 ЗП РЯ15^
 СЧ ССНЯ^
 ЗП РЯ14^
 УИА РЯ14(М1)^
 ПВ ВЕРТУШ(М13)^
ПОКРЕС СЧ УСРЕС^
 НТЖ ВСЕЕД^
 ПО ПОКБОБ^
 СЧ УСРЕС^
 ПВ ПЕРЕВ(М13)^
 СДА 64-8^
 ИЛИ МАСЗОН
 ЗП РЯ15^
 СЧ СРЕС^
 ЗП РЯ14^
 УИА РЯ14(М1)^
 ПВ ВЕРТУШ(М13)^
ПОКБОБ СЧ УСБОБ^
 НТЖ ВСЕЕД^
 ПО ПБОБ1  МЛ НАКОПЛЕНИЯ СТАНДАРТНАЯ^
 СЧ УСБОБ^
ПБОБ2 СБР Е24П1^
 РЗБ ПППППП
 СДА 64-8^
 ИЛИ ПООООП
 ЗП РЯ15^
 СЧ СБОБ^
 ЗП РЯ14^
 УИА РЯ14(М1)^
 ПВ ВЕРТУШ(М13)^
 ПБ ПОКЗАД  НА ПОКАЗ УСТАНОВКИ ВРЕМЕНИ^
ПБОБ1 СЧ МЛСТАТ^
 И Е16П1   СТАНДАРТНАЯ МЛ НАКОПЛЕНИЯ^
 ПБ ПБОБ2^
ПОКЗАД СЧ УСЗАД^
 НТЖ ВСЕЕД^
 ПО ПОКВРЕ  НА ПОКАЗ УСТАНОВКИ ВРЕМЕНИ^
 СЧ УСЗАД^
 ПВ ПЕРЕВ(М13)^
 СДА 64-32^
 ИЛИ ООПППП
 ЗП РЯ15^
 СЧ СЗАД^
 ЗП РЯ14^
 УИА РЯ14(М1)^
 ПВ ВЕРТУШ(М13)^
ПОКВРЕ НОП^
 СЧ УСВРЕ^
 НТЖ ВСЕЕД^
 ПО ПОКДАТ НА ПРОСМОТР УСТАНОВКИ ДАТЫ^
 СЧ СВРЕ^
 ЗП РЯ13^
 УИА УСВРЕ(М6)^
 ПВ ПВД(М13)^
 СЧ УСВРЕ+1^
 НТЖ ВСЕЕД^
 ПЕ ПОКВР1^
 СЧ ВРЕКОН^
 ЗП РЯ15^
ПОКВР1 ПВ ВЕРТУШ(М13)^
ПОКДАТ НОП^
 СЧ УСДАТ^
 НТЖ ВСЕЕД^
 ПО ВЫХУСТ^
 СЧ СДАТ^
 ЗП РЯ13^
 УИА УСДАТ(М6)^
 ПВ ПВД(М13)^
 ПВ ВЕРТУШ(М13)^
ВЫХУСТ МОД ИРС13^
 ПБ^
*^
ПВД НОП^
*^
*^
*     ОБРАБОТКА ВРЕМЕНИ И ДАТЫ^
*^
*    ВОЗВРАТ ПО М13^
*    АДРЕС УСТАНОВКИ ПО М6^
*^
*^
 УИА РЯ13(М1)     ДЛЯ П.П. ВЫВОД^
 СЧ (М6)^
 СБР Е24П1^
 РЗБ ПППППП
 ПВ ПЕРВД(М17)  ПЕРЕВОД ИЗ ВИДА: ЧЧММ  В ВИД: ЧЧ.ММ
 ИЛИ МАСВД1^
 ЗП РЯ14^
 СЧ РЯ1^
 ИЛИ МАСВД2^
 ЗП РЯ15^
 СЧ 1(М6)^
 НТЖ ВСЕЕД^
 ПО (М13)   ВОЗВРАТ^
 СЧ 1(М6)^
 СБР Е24П1^
 РЗБ ПППППП
 ПВ ПЕРВД(М17)^
 ИЛИ МАСВД2^
 ЗП РЯ15^
 ПБ (М13)^
*^
*^
ПЕРВД НОП^
*^
*    ПЕРЕВОД ВРЕМЕНИ ИЗ ВИДА  '00ЧЧММ'==>'0ЧЧ.ММ'^
*    ВХОДНЫЕ ДАННЫЕ НА СМ;^
*    РЕЗУЛЬТАТЫ НА  СМ  И В РЯ1^
*^
 ЗП РЯ1^
 СЧ ВСЕЕД^
 НТЖ Е16П1^
 И РЯ1^
 СДА 64-8^
 ЗП РЯ2^
 СЧ РЯ1^
 И Е16П1^
 ИЛИ РЯ2^
 ЗП РЯ1^
 ПБ (М17)^
*^
*^
*     РАЗБОР ДИРЕКТИВЫ  П  Е  Ч , И Т Г^
*^
*^
ИТГСТ НОП^
 СЧ^
 ПБ ПСТ00^
*^
ПЕЧСТ НОП^
 СЧ ВСЕЕД^
ПСТ00 НОП^
 ЗП НАПР   ПРИЗНАК ДИРЕКТИВЫ.^
 УИА (М5)^
 ПВ ПОДГОТ(М13)^
 СЧ ВСЕЕД^
 ЗП РЯ3^
ПСТ0 ПВ ВС(М13)^
 НТЖ ОООООП
 ПО ПСТ0^
 СЧ РЯВС^
 НТЖ Е8П1^
 ПО ВЫППЕЧ     НА ИСПОЛНЕНИЕ ДИР-ВЫ  П Е Ч^
 СЧ РЯВС^
 ЗП ТЕКДИР^
 УИА -1(М3)^
ПСТ11 ПВ ВС(М13)^
 ПО ПСТ11^
 НТЖ Е8П1^
 ПО ПСТ01^
 СЧ РЯВС^
 НТЖ ОООООП
 ПО ПСТ01   ИДЕНТИФИКАТОР ВЫБРАН^
 СЧ ТЕКДИР^
 СДА 64-8^
 ИЛИ РЯВС^
 ЗП ТЕКДИР^
 ЦИКЛ ПСТ11(М3)^
*^
ПСТ01 СЧ РЯ3^
 ПЕ ПСТ02  НЕТ ПАРАМЕТРОВ  'ЗДЧ'^
 УИА -3(М3)^
ПСТ03 СЧ ПЕРК31+3(М3)^
 И Е24П1^
 ЗП РЯ3^
 НТЖ ТЕКДИР^
 ПЕ ПСТ33^
 МОДА ПЕРК31+3-ПЕРК3(М3)^
 УИА (М3)^
 СЧИ М3^
 ЗП ДИРСТ-1(М5)^
 СЧ РЯВС^
 НТЖ Е8П1^
 ПО ВЫППЕЧ^
 ПБ ПСТ0  ЗА НОВЫМ ИДЕНТИФИКАТОРОМ^
ПСТ33 ЦИКЛ ПСТ03(М3)^
*^
ПСТ02 НОП^
 УИА -8(М3)^
ПСТ1 СЧ ПЕРК3+8(М3)^
 И Е24П1^
 ЗП РЯ3^
 НТЖ ТЕКДИР^
 ПЕ ПСТ2^
 СЧ ЗДЧ^
 И Е24П1^
 НТЖ РЯ3^
 ЗП РЯ3^
 МОДА 8(М3)^
 УИА (М3)^
 СЧИ М3^
 ЗП ДИРСТ(М5)^
 СЛИА 1(М5)^
 ПБ ПСТ0^
ПСТ2 ЦИКЛ ПСТ1(М3)^
 ПВ ОШДИР(М13)^
 ПВ ФЛГДИР(М13)^
 ПБ СТАТ0^
ВЫППЕЧ НОП^
 УИА -17(М1)^
 СЧ^
 ЗП ДЛБФСТ^
ВП1 СЧ ДИРСТ+17(М1)^
 НТЖ ВСЕЕД^
 ПО ВП2^
 МОД ДИРСТ+17(М1)^
 ПБ ПЕРК3^
ВП3 ПВ ПЛЮС1(М13)^
 ПВ СКЛКОН(М13)^
 ЦИКЛ ВП1(М1)^
ВП2 УИА ЗАГСТ(М1)^
 УИА -13(М4)^
 СЧ НАПР^
 ПО НАЧРБЗ   ДИРЕКТИВА  И Т Г^
 ПВ ВЕРТУШ(М13)^
*^
*^
*     НАЧАЛО РАБОТЫ С ЗОНАМИ СТАТИСТИКИ^
*^
НАЧРБЗ НОП^
 СЧ УСДАТ^
 НТЖ ВСЕЕД^
 ПЕ СТНАК  СТАТИСТИКА С МЛ НАКОПЛЕНИЯ^
 СЧ ЯЧСТАТ^
 СДА 64+30^
 И Е12П1^
 ИЛИ СТАОБМ^
 ЗП РЯ12    КОНСТАНТА ОБМЕНА^
 Э70 РЯ12^
 СЧ СТАТКА^
 И Е24^
 ПО КРУТИМ    НАЧАЛО ОБРАБОТКИ^
 СЧ РЯ12^
 СЛЦ Е1^
 ЗП РЯ12^
 Э70 РЯ12^
 СЧ СТАТКА^
 И Е24^
 ПЕ ОШБЛСТ    ДИАГН. ОШ.БЛ.СТАТ.^
КРУТИМ НОП^
 СЧ СТАТКА^
 СДА 64+40^
 НТЖ Е8П1^
 ПЕ ИСПСТ    СТАТИСТИКА ИСПОРЧЕНА^
 ПВ ОБРАБ(М15)    ОБРАБОТКА СЧИТАН. ЗОНЫ^
 СЧ ЯЧСТАТ^
 УИ М1   ЧИСЛО КОМПЛЕКТОВ -1 В ОБРАТН.КОДЕ^
 СЛИА 1(М1)^
КРУТ0 СЧ РЯ12^
 СЛЦ Е2^
 ЗП РЯ12^
 Э70 РЯ12^
 СЧИ М1^
 ЗП ИРС1^
 ПВ ОБРАБ(М15)^
 СЧ ИРС1^
 УИ М1^
 ЦИКЛ КРУТ0(М1)^
*^
*    ОБРАБОТКА ИТОГОВЫХ ПАРАМЕТРОВ^
*^
КПЕЧСТ НОП^
 ПВ УСТПОК(М13)      ВЫВОД ТЕКУШИХ УСТАНОВОК^
 СЧ ШИТП ИТОГО ВХОДОВ В СИСТЕМУ^
 ПО КПСТ0^
 ПВ ПЕРЕВ(М13)^
 СДА 64-16^
 ИЛИ МАСКШФ^
 ЗП РЯ17^
 УИА ИТВХОД(М1)^
 ПВ ВЕРТУШ(М13)^
КПСТ0 СЧ ШИТП+1^
 ПО КПСТ2  НЕТ СФОРМИРОВАННЫХ ЗАДАЧ^
 ПВ ПЕРЕВ(М13)^
 ИЛИ МАСФЗД^
 ЗП РЯ16^
 УИА СФЗАД(М1)^
 ПВ ВЕРТУШ(М13)^
КПСТ2 СЧ ШИТП+2^
 ПО КПСТ6   РЕСУРС НЕ ИСПОЛЬЗОВАЛСЯ^
 ПВ ПЕРЕВ(М13)^
 ЗП РЯ18^
 УИА ИТРЕС(М1)^
 ПВ ВЕРТУШ(М13)^
*^
КПСТ6 НОП^
 СЧ ШИТП+3^
 ПО КПСТ4  СУММРАНОЕ ВРЕМЯ - 0^
 И Е24П1^
 ПВ ПЕРЕВ(М13)^
 СДА 64-8^
 ИЛИ МИТГВ2^
 ЗП ВРЕСУМ+3^
 СЧ ШИТП+3^
 СДА 64+24^
 ПВ ПЕРЕВ(М13)^
 ИЛИ МИТГВ1^
 ЗП ВРЕСУМ+2^
 УИА ВРЕСУМ(М1)^
 ПВ ВЕРТУШ(М13)^
*^
КПСТ4 НОП  ОБРАБОТКА ТЕРМИНАЛОВ^
 УИА -6(М5)^
 СЧ Е24П1^
 ЗП РЯРЗБ^
 СЧ НОММАШ^
 ЗП ЗАГСТ^
 УИА (М6)^
 СЧ^
 ЗП РЯ1^
*^
ТЕРИТГ НОП^
 СЧ ШИТП+4+6(М5)^
 ПО НЕТМАШ     НА ДАННОЙ МАШИНЕ РАБОТЫ НЕТ^
ТЕРИТ4 НЕД^
 УИ М13   НОМЕР ТЕРМИНАЛА^
 СБР Е6П1^
 РЗБ СССССС
 СДА 64+24^
 ИЛИ ОООООП
 СБР РЯРЗБ^
 ИЛИ РЯ1^
 ЗП РЯ1^
 ЗП ЗАГСТ+1(М6)^
 СЧ М40377^
 ЗП ЗАГСТ+2(М6)^
 СЧ РЯРЗБ^
 НТЖ ВСЕЕД^
 ПЕ ТЕРИТ1    НЕ ВСЕ СЛОВО ЗАПОЛНЕНО^
 СЧ^
 ЗП РЯ1^
 СЛИА 1(М6)^
 СЧ Е24П1^
 ПБ ТЕРИТ0^
ТЕРИТ1 СЧ ВСЕЕД^
ТЕРИТ0 ЗП РЯРЗБ^
 СЧ ШИТП+4+6(М5)^
 НТЖ Е48-1(М13)^
 ЗП ШИТП+4+6(М5)^
 ПЕ ТЕРИТ4  НА ДОРАБОТКУ ДАННОЙ ШКАЛЫ^
 УИА ЗАГСТ(М1)^
 СЧ РЯРЗБ^
 НТЖ ВСЕЕД^
 ПЕ ТЕРИТ3^
 СЧ ЗАГСТ+1(М6)^
 ИЛИ ООПППП
 ЗП ЗАГСТ+1(М6)^
ТЕРИТ3 ПВ ВЕРТУШ(М13)^
 СЧ^
 ЗП РЯ1^
НЕТМАШ СЧ ЗАГСТ^
 СЛЦ Е25^
 ЗП ЗАГСТ^
 УИА (М6)^
 СЧ Е24П1^
 ЗП РЯРЗБ^
 ЦИКЛ ТЕРИТГ(М5)^
*^
*  ИТОГ ПО СНЯТИЯМ^
*^
ИТГСНЯ НОП^
 УИА -7(М1)^
ИТГС1 СЧ ШИТП+11+7(М1)^
 ПЕ ИТГС2^
 ЦИКЛ ИТГС1(М1)^
 ПБ КПСТ3^
*^
ИТГС2 УИА ВЫХСИС(М1)^
 ПВ ВЕРТУШ(М13)^
 УИА -7(М5)^
 УИА (М6)^
ИТГС3 СЧ ШИТП+11+7(М5)^
 ПО ИТГС4^
 ПВ ПЕРЕВ(М13)^
 СДА 64-16^
 ИЛИ МИТГСН^
 ЗП ИТОГСН+2^
 СЧ ТСНЯТ(М6)^
 ЗП ИТОГСН^
 СЧ ТСНЯТ+1(М6)^
 ЗП ИТОГСН+1^
 УИА ИТОГСН(М1)^
 ПВ ВЕРТУШ(М13)^
ИТГС4 СЛИА 2(М6)^
 ЦИКЛ ИТГС3(М5)^
*^
*    ДОРАБОТКА БУФЕРА ВЫВОДА СТАТИСТИКИ^
*^
КПСТ3 СЧ РЕЖВЫВ^
 И Е2^
 ПО КПСТ1^
 УИА ВСЕ214(М1)^
 ПВ ВЕРТУШ(М13)^
КПСТ1 СЧ КООБЗ2^
 ПО СТАТ0^
 УИА БВЫВ(М1)^
 ПВ ВЫВОД(М17)^
 СЧ^
 ЗП КООБЗ2   ГАСИМ ПРИЗНАК ДОРАБОТКИ ВЫВОДА^
 ПБ СТАТ0^
*^
*     АВАРИЙНЫЕ ДИАГНОСТИКИ^
*^
ИСПСТ НОП СТАТИСТИКА ИСПОРЧЕНА^
 УИА СТАИСП(М1)^
 ПВ ВЫВОД(М17)^
 ПБ БЛСТАТ^
ОШБЛСТ НОП ОШИБКА БЛОКА СТАТИСТИКИ^
 УИА ОШСТАТ(М1)^
 ПВ ВЫВОД(М17)^
 ПБ БЛСТАТ^
*^
*^
СТНАК НОП  РАБОТА С МЛ НАКОП^
 СЧ УСБОБ^
 НТЖ ВСЕЕД^
 ПО СТНАК7^
 СЧ УСБОБ^
 ПБ СТНАК1^
СТНАК7 СЧ МЛСТАТ^
 И Е16П1^
СТНАК1 ПВ ДИСТАТ(М13)^
 Э70 ЧТКТСТ^
 СЧ КАТСТ^
 НТЖ ВСЕЕД^
 ПЕ ЗАТКАТ    ЗАТЕРТ КАТАЛОГ МЛ НАКОПЛЕНИЯ^
 УИА -1022(М1)^
 СЧ^
 ЗП РЯ12^
СТНАК2 СЧ РЯ12^
 СЛЦ Е1^
 ЗП РЯ12^
 СЧ УСДАТ+1^
 НТЖ ВСЕЕД^
 ПО СТНАК3   УСТАНОВКА ДАТЫ ОДНА^
 СЧ КАТСТ+1023(М1)^
 ПО СТНАК6    КОНЕЦ КАТАЛОГА^
 ПВ ДАТХЛМ(М15)^
 ЗП КАТСТ+1023(М1)^
 СЧ УСДАТ^
 ПВ ДАТХЛМ(М15)^
 ВЧОБ КАТСТ+1023(М1)^
 ПЕ СТНАК4^
 СЧ УСДАТ+1^
 ПВ ДАТХЛМ(М15)^
 ВЧ КАТСТ+1023(М1)^
 ПЕ СТНАК4^
 ПБ СТНАК5     НА ОБРАБОТКУ^
СТНАК3 СЧ КАТСТ+1023(1)^
 ПО СТНАК6    КОНЕЦ КАТАЛОГА^
 НТЖ УСДАТ^
 ПЕ СТНАК4^
СТНАК5 СЧИ М1^
 ЗП ИРС1^
 СЧ РЯ12^
 ИЛИ ЧТМЛСТ^
 ЗП РЯ11^
 Э70 РЯ11^
 ПВ ОБРАБ(М15)^
 СЧ ИРС1^
 УИ М1^
СТНАК4 ЦИКЛ СТНАК2(М1)^
СТНАК6 ПВ ДИСОТК(М13)^
 ПБ КПЕЧСТ   НА ОБРАБОТКУ КОНЦА ПЕЧАТИ^
ЗАТКАТ УИА КАТЗАТ(М1)^
 ПВ ВЫВОД(М17)^
 ПБ СТНАК6^
*^
*^
********************************************^
*                                          *^
*    Б Л О К  ПЕРЕКАЧИВАНИЯ СТАТИСТИКИ     *^
*    ---------------------------------     *^
*                                          *^
********************************************^
*^
*^
ОТКАЧ НОП^
*^
*^
 ПВ ПЕЧЗАГ(М13)^
 СЧ ЯЧСТАТ^
 СДА 64+30^
 И Е12П1^
 ИЛИ СТАОБМ^
 ЗП РЯ12^
 Э70 РЯ12^
 СЧ СТАТКА^
 И Е24^
 ПЕ ПИШЕМ   НА МЛ НАКОПЛЕНИЯ^
 СЧ РЯ12^
 СЛЦ Е1^
 ЗП РЯ12^
 Э70 РЯ12^
 СЧ СТАТКА^
 И Е24^
 ПО НЕПИШ2  СТАТИСТИКА ИСПОРЧЕНА^
ПИШЕМ НОП^
 СЧ СТАТКА^
 СДА 64+40^
 НТЖ Е8П1^
 ПЕ НЕПИШ2  СТАТИСТИКА ИСПОРЧЕНА^
 Э70 ЧТКТСТ   ЧТЕНИЕ КАТАЛОГА МЛ СТАТИСТИКИ^
 СЧ КАТСТ^
 НТЖ ВСЕЕД^
 ПЕ НЕПИШ1    ЗАТЕРТ КАТАЛОГ МЛ НАКОП.^
 СЧ ЯЧСТАТ^
 УИ М3^
ПИШЕМ1 Э70 РЯ12^
 СЧ СТАТКА^
 И Е15П1^
 НТЖ Е1^
 ПО ПИШЕМ2^
 ПВ НАМЛ(М13)^
 СЧ РЯ12^
 СЛЦ Е2^
 ЗП РЯ12^
ПИШЕМ2 ЦИКЛ ПИШЕМ1(М3)^
 Э70 ЗПКТСТ^
 ПБ ПИШ  О'КЕУ^
*^
*     ДИАГНОСТИКИ БЛОКА ОТКАЧИВАНИЯ СТАТИСТИКИ^
*^
*^
НЕПИШ1 УИА КАТЗАТ(М1)^
 ПБ Э6474^
НЕПИШ2 УИА ОШСТАТ(М1)^
 ПБ Э6474^
НЕПИШ3 УИА НЕТМЕС(М1)^
 ПБ Э6474^
ПИШ УИА ОКЕУ(М1)^
 ПБ Э6474^
Э6474 Э62 '76'^
 Э64 ИНФВЫВ^
 Э74^
*^
*^
*^
*------------------------------------------^
*                                         I^
*                                         I^
*                                         I^
*    ПЕРЕКЛЮЧАТЕЛИ И СЛУЖЕБНЫЕ П.П.       I^
*       БЛОКА  С Т А Т И С Т И К А        I^
*                                         I^
*                                         I^
*                                         I^
*------------------------------------------^
*^
*^
ПРЧСТ НОП^
*^
*      ПРОВЕРКА НА ЧИСЛО^
*^
 СЧ РЯВС^
 ВЧОБ П11^
 ПО (М13)^
 ПБ УСТОШ^
ДИСТАТ НОП^
*      НА СМ- НОМЕР БОБИНЫ^
 ЗП РЯ1^
 ИЛИ КЗАКБ^
 Э50 '131'^
 ПЕ ДИСТ  ЗАКАЗ НЕ ПРОШЕЛ^
 УИА (М15)^
 СЧ РЯ1^
 Э50 '121'^
 СЧ Е15^
 УИ М15^
 СЧ РЯ1^
 Э50 '121'^
 ПБ (М13)^
ДИСТ НОП^
 СЧ РЯ1^
 СБР Е16П1^
 РЗБ ПППППП
 СДА 64+16^
 ИЛИ НМД1^
 ЗП НЕТМД+1^
 УИА НЕТМД(М1)^
 ПВ ВЫВОД(М17)^
 ПБ СТАТ0^
*^
*^
ДАТХЛМ НОП^
*^
 ЗП РЯ1^
 СДА 64+8^
 ЗП РЯ1^
 СЧМР^
 РЗБ Е16П9^
 ИЛИ РЯ1^
 ПБ (М15)^
*^
*^
НАМЛ НОП^
*^
*     ЗАПИСЬ ОЧЕРЕДНОЙ ЗОНЫ НА МЛ СТАТИСТИКИ^
*^
 УИА -1022(М1)^
 УИА 1(М2)^
НАМЛ0 СЧ КАТСТ(М2)^
 ПЕ НАМЛ1^
 СЧИ М2^
 ИЛИ ЗПМЛСТ^
 ЗП РЯ7^
 Э70 РЯ7     ЗАПИСЬ ЗОНЫ НА МЛ^
 СЧ СТАТКА^
 СДА 64+24^
 И Е16П1^
 ЗП КАТСТ(М2)     ЗАНЕСЕНИЕ ДАТЫ В КАТАЛОГ^
 ПБ (М13)^
НАМЛ1 СЛИА 1(М2)^
 ЦИКЛ НАМЛ0(М1)^
*^
*^
 ПБ НЕПИШ3   НЕТ МЕСТА НА МЛ НАКОПЛЕНИЯ^
*^
*^
ОБРАБ НОП^
*^
*^
*     РАБОТА С ЗОНОЙ СТАТИСТИКИ^
 СЧ СТАТКА^
 И Е15П1^
 ЗП РЯ10   НОМЕР СВОБОДНОЙ ЯЧЕЙКИ^
 НТЖ Е1^
 ПО (М15)    ЗОНА СВОБОДНА^
 СЧИ М15^
 ЗП ИРС15   ЗАПОМНИ АДРЕС ВОЗВРАТА^
 УИА 1(М2)^
ОБРАБ0 СЧИ М2^
 ВЧ РЯ10^
 ПЕ ОБРАБ1^
 МОД ИРС15^
 ПБ^
ОБРАБ1 СЧ СТАТКА(М2)^
 ЗП СТАПОЛ^
 СЧ СТАТКА+1(М2)^
 ЗП СТАПОЛ+1^
*^
ПРУСТ НОП^
*^
*    КОНТРОЛЬ УСТАНОВОК^
*^
 СЧИ М2^
 ЗП ИРС2^
 СЧ УСШИФ^
 НТЖ ВСЕЕД^
 ПО ПРУСТ1  НА КОНТРОЛЬ ВРЕМЕНИ^
 СЧ СТАПОЛ^
 И Е24П1^
 НТЖ УСШИФ^
 ПЕ ОБРАБ2    КОНРОЛЬ НЕ ПРОШЕЛ^
ПРУСТ1 СЧ УСВРЕ^
 НТЖ ВСЕЕД^
 ПО ПРУСТ2   НА КОНРОЛЬ ТЕРМИНАЛА^
 СЧ СТАПОЛ^
 СДА 64+32^
 И Е16П1    ВРЕМЯ ВХОДА^
 ВЧ УСВРЕ^
 ПЕ ОБРАБ2^
 СЧ УСВРЕ+1^
 НТЖ ВСЕЕД^
 ПО ПРУСТ2^
 СЧ СТАПОЛ+1^
 СДА 64+32^
 И Е16П1     ВРЕМЯ ВЫХОДА^
 ВЧОБ УСВРЕ+1^
 ПЕ ОБРАБ2^
ПРУСТ2 СЧ УСТЕР^
 НТЖ ВСЕЕД^
 ПО ПРУСТ3    НА КОНТРОЛЬ ЭВМ^
 СЧ СТАПОЛ+1^
 СДА 64+3^
 И Е6П1^
 НТЖ УСТЕР^
 ПЕ ОБРАБ2^
ПРУСТ3 СЧ УСМАШ^
 НТЖ ВСЕЕД^
 ПО ПРУСТ4   НА КОНТРОЛЬ ЗАДАЧ^
 СЧ СТАПОЛ+1^
 И Е3П1^
 НТЖ УСМАШ^
 ПЕ ОБРАБ2^
ПРУСТ4 СЧ УСЗАД^
 НТЖ ВСЕЕД^
 ПО ПРУСТ5   КОНТРОЛЬ СНЯТИЯ^
 УИА -2(М1)^
 СЧ СТАПОЛ+1^
 СДА 64+12^
 ЗП РЯ1^
 И Е5П1^
 ЗП РЯ2^
ПРУЗДЧ СЧ РЯ1^
 СДА 64+5^
 ЗП РЯ1^
 И Е5П1^
 СЛЦ РЯ2^
 ЗП РЯ2^
 ЦИКЛ ПРУЗДЧ(М1)^
 ВЧ УСЗАД^
 ПЕ ОБРАБ2^
ПРУСТ5 СЧ УССНЯ^
 НТЖ ВСЕЕД^
 ПО ПРУСТ6    КОНТРОЛЬ РЕСУРСОВ^
 СЧ СТАПОЛ+1^
 СДА 64+9^
 И Е3П1^
 НТЖ УССНЯ^
 ПЕ ОБРАБ2^
ПРУСТ6 СЧ УСРЕС^
 НТЖ ВСЕЕД^
 ПО ПРУСТ7^
 СЧ СТАПОЛ^
 СДА 64+24^
 И Е8П1^
 ВЧ УСРЕС^
 ПЕ ОБРАБ2^
ПРУСТ7 НОП^
 СЧ УСОТД ОБРАБ.ОТД^
 НТЖ ВСЕЕД^
 ПО ПРУСТ8   НА ГРУППУ^
 СЧ СТАПОЛ^
 И Е24П1^
 СДА 64+16^
 НТЖ УСОТД^
 ПЕ ОБРАБ2^
ПРУСТ8 НОП^
 СЧ УСГРУ^
 НТЖ ВСЕЕД^
 ПО ПРУСТ9^
 СЧ СТАПОЛ^
 И Е24П1^
 ЗП РЯ0^
 И Е8П1^
 ПО ОБРАБ2   НЕ ГРУПП.^
 СЧ РЯ1^
 СДА 64+8^
 НТЖ УСГРУ^
 ПЕ ОБРАБ2   НЕ ТА ГРУППА^
ПРУСТ9 НОП^
*      ОБРАБОТКА  С Т А П О Л^
 УИА -17(М1)^
 СЧ^
 ЗП ДЛБФСТ^
ВП4 СЧ ДИРСТ+17(М1)^
 НТЖ ВСЕЕД^
 ПО ВП5^
 МОД ДИРСТ+17(М1)^
 ПБ ПЕРК4^
ВОЗПЕЧ ПВ ПЛЮС1(М13)^
 ПВ СКЛКОН(М13)^
 ЦИКЛ ВП4(М1)^
ВП5 СЧ НАПР^
 ПО ОБРАБ2   ДИРЕКТИВА  И Т Г^
 УИА ЗАГСТ(М1)^
 ПВ ВЕРТУШ(М13)^
ОБРАБ2 СЧ ИРС2^
 СЛЦ Е2^
 УИ М2^
 ПБ ОБРАБ0^
*^
*^
УСТФЛГ НОП^
*     ГАШЕНИЕ ВСЕХ ФЛАЖКОВ^
 УИА -70(М5)^
 СЧ ВСЕЕД^
УФГ ЗП УСШИФ+70(М5)^
 ЦИКЛ УФГ(М5)^
 ПБ (М13)^
*^
*^
ФЛГДИР НОП^
*     ГАШЕНИЕ ФЛАЖКОВ ДИР-В.^
 УИА -40(М1)^
 СЧ ВСЕЕД^
УФД ЗП ДИРСТ+40(М1)^
 ЦИКЛ УФД(М1)^
 ПБ (М13)^
*^
*^
ПЛЮС1 НОП^
 СЧ ДЛБФСТ^
 СЛЦ Е1^
 ЗП ДЛБФСТ^
 ПБ (М13)^
*^
*^
СКЛКОН НОП^
*     '377' В КОНЕЦ БУФЕРА^
 СЧ М40377^
 МОД ДЛБФСТ^
 ЗП ЗАГСТ^
 ПБ (М13)^
*^
*     ПЕРЕКЛЮЧАТЕЛЬ ОСН.ДИР-В^
*^
ПЕРК1 ПБ КОНЕЦ^
 КОНК П'КОН'^
 ПБ КНЦ^
 КОНК П'КНЦ'^
 ПБ РАЗД1^
 КОНК П'КСТ'^
 ПБ ПЕЧСТ^
 КОНК П'ПЕЧ'^
 ПБ УСТ^
 КОНК П'УСТ'^
 ПБ НОВМЛ^
 КОНК П'НОВ'^
 ПБ КТЛ^
 КОНК П'КТЛ'^
 ПБ ИТГСТ^
 КОНК П'ИТГ'^
 ПБ СТААЦП^
 КОНК П'АЦП'^
 ПБ СТАЭКР^
 КОНК П'ЭКР'^
*^
*    ПЕРЕКЛЮЧАТЕЛЬ ПОДДИРЕКТИВ  УСТАНОВКИ^
*^
ПЕРК2 ПБ УШИФ^
 КОНК П'ШИФ'^
 ПБ УВРЕ^
 КОНК П'ВРЕ'^
 ПБ УТЕР^
 КОНК П'ТЕР'^
 ПБ УМАШ^
 КОНК П'МАШ'^
 ПБ УСНЯ^
 КОНК П'СНЯ'^
 ПБ УЗАД^
 КОНК П'ЗДЧ'^
 ПБ УАЦП^
 КОНК П'АЦП'^
 ПБ УДАТ^
 КОНК П'ДАТ'^
 ПБ УРЕС^
 КОНК П'РЕС'^
 ПБ УБОБ^
 КОНК П'БОБ'^
 ПБ УОТД^
 КОНК П'ОТД'^
 ПБ УГРУ^
 КОНК П'ГРУ'^
 ПБ УСИС^
 КОНК П'СИС'^
 ПБ УНАЧ^
 КОНК П'НАЧ'^
*^
*     ПЕРЕКЛЮЧАТЕЛЬ ПОДДИРЕКТИВ ПЕЧАТИ^
*^
ПЕРК3 ПБ ЗАГШИФ^
 КОНК П'ШИФ'^
 ПБ ЗАГФИО^
 КОНК П'ФИО'^
ЗДЧ ПБ ЗАГЗАД^
 КОНК П'ЗДЧ'^
 ПБ ЗАГТЕР^
 КОНК П'ТЕР'^
 ПБ ЗАГМАШ^
 КОНК П'МАШ'^
 ПБ ЗАГСНЯ^
 КОНК П'СНЯ'^
 ПБ ЗАГРЕС^
 КОНК П'РЕС'^
 ПБ ЗАГВРЕ^
 КОНК П'ВРЕ'^
 ПБ ЗАГДАТ^
 КОНК П'ДАТ'^
ПЕРК31 ПБ ЗАГПЗД^
 КОНК П'0ПЗ'^
 ПБ ЗАГБУФ^
 КОНК П'БУФ'^
 ПБ ЗАГВПС^
 КОНК П'ВПС'^
 ПБ ЗАГПАК^
 КОНК П'ПАК'^
*^
*^
ПЕРК4 ПБ ПШИФ^
 КОНК П'ШИФ'^
 ПБ ПФИО^
 КОНК П'ФИО'^
 ПБ ПЗАД^
 КОНК П'ЗДЧ'^
 ПБ ПТЕР^
 КОНК П'ТЕР'^
 ПБ ПМАШ^
 КОНК П'МАШ'^
 ПБ ПСНЯ^
 КОНК П'СНЯ'^
 ПБ ПРЕС^
 КОНК П'РЕС'^
 ПБ ПВРЕ^
 КОНК П'ВРЕ'^
 ПБ ПДАТ^
 КОНК П'ДАТ'^
*^
ПЕРК41 ПБ ППЗД^
 КОНК П'0ПЗ'^
 ПБ ПБУФ^
 КОНК П'БУФ'^
 ПБ ПВПС^
 КОНК П'ВПС'^
 ПБ ППАК^
 КОНК П'ПАК'^
*-----------------------------------------^
*                                        I^
*      ВЫПОЛНЕНИЕ  ПОДДИРЕКТИВ           I^
*                                        I^
*-----------------------------------------^
*^
*^
*     ПОДДИРЕКТИВЫ ПЕЧАТИ^
*  ФОРМИРОВАНИЕ ЗАГОЛОВКА^
ЗАГФИО НОП^
 СЧ ФИО^
 МОД ДЛБФСТ^
 ЗП ЗАГСТ^
 СЧ ФИО+1^
 МОД ДЛБФСТ^
 ЗП ЗАГСТ+1^
 ПВ ПЛЮС1(М13)^
 ПБ ВП3^
*^
ЗАГДАТ НОП^
 СЧ ДАТ^
 МОД ДЛБФСТ^
 ЗП ЗАГСТ^
 СЧ ДАТ+1^
 МОД ДЛБФСТ^
 ЗП ЗАГСТ+1^
 ПВ ПЛЮС1(М13)^
 ПБ ВП3^
ЗАГШИФ НОП^
 СЧ ШИФ^
 МОД ДЛБФСТ^
 ЗП ЗАГСТ^
 ПБ ВП3^
ЗАГПЗД НОП^
 СЧ ПЗД^
 МОД ДЛБФСТ^
 ЗП ЗАГСТ^
 ПБ ВП3^
ЗАГПАК НОП^
 СЧ ПАК^
 МОД ДЛБФСТ^
 ЗП ЗАГСТ^
 ПБ ВП3^
ЗАГБУФ НОП^
 СЧ БУФ^
 МОД ДЛБФСТ^
 ЗП ЗАГСТ^
 ПБ ВП3^
ЗАГВПС НОП^
 СЧ ВПС^
 МОД ДЛБФСТ^
 ЗП ЗАГСТ^
 ПБ ВП3^
ЗАГЗАД НОП^
 СЧ ЗАД^
 МОД ДЛБФСТ^
 ЗП ЗАГСТ^
 ПБ ВП3^
ЗАГТЕР НОП^
 СЧ ТЕР^
 МОД ДЛБФСТ^
 ЗП ЗАГСТ^
 ПБ ВП3^
ЗАГМАШ НОП^
 СЧ БЭС^
 МОД ДЛБФСТ^
 ЗП ЗАГСТ^
 ПБ ВП3^
ЗАГСНЯ НОП^
 СЧ СНЯ^
 МОД ДЛБФСТ^
 ЗП ЗАГСТ^
 СЧ СНЯ+1^
 МОД ДЛБФСТ^
 ЗП ЗАГСТ+1^
 ПВ ПЛЮС1(М13)^
 ПБ ВП3^
ЗАГРЕС НОП^
 СЧ РЕС^
 МОД ДЛБФСТ^
 ЗП ЗАГСТ^
 ПБ ВП3^
ЗАГВРЕ НОП^
 УИА -2(М13)^
ЗГВРЕ СЧ ВР+2(М13)^
 МОД ДЛБФСТ^
 ЗП ЗАГСТ+2(М13)^
 ЦИКЛ ЗГВРЕ(М13)^
 ПВ ПЛЮС1(М13)^
 ПВ ПЛЮС1(М13)^
 ПБ ВП3^
*^
*^
*     ПОДДИРЕКТИВЫ ПЕЧАТИ^
*   ОБРАБОТКА СТАТИСТИКИ ПОЛЬЗОВАТЕЛЯ^
*^
ПФИО НОП^
 СЧ СТАПОЛ^
 И Е8П1^
 НТЖ Е8П1^
 ПЕ ПФИО1   НЕ СИС ВХОД^
 СЧ ПППППП
 ЗП БЮДЖП+1^
 СЧ ТСИС^
 ПБ ПФИО2^
ПФИО1 СЧ СТАПОЛ^
 И Е24П1^
 СДА 64+8^
 ЗП БЮДЖП^
 УИА БЮДЖП(М7)^
 СЧИ М7^
 Э50 '104'^
 СЧ БЮДЖП^
 СДА 64-40^
 ЗП РЯ1^
 СЧ БЮДЖП+1^
 СДА 64+8^
 ИЛИ РЯ1^
 ЗП БЮДЖП+1^
 СЧ БЮДЖП^
 СДА 64+8^
 ИЛИ ПООООО
ПФИО2 МОД ДЛБФСТ^
 ЗП ЗАГСТ^
 СЧ БЮДЖП+1^
 МОД ДЛБФСТ^
 ЗП ЗАГСТ+1^
 ПВ ПЛЮС1(М13)^
 ПБ ВОЗПЕЧ^
ПДАТ НОП^
 СЧ СТАТКА^
 СДА 64+24^
 СБР Е16П1^
 РЗБ ПППППП
 СДА 64+16^
 ЗП РЯ1^
 СДА 64+16^
 ИЛИ ЧЗАД^
 МОД ДЛБФСТ^
 ЗП ЗАГСТ^
 СЧ РЯ1^
 СБР Е16П1^
 СДА 64+8^
 ИЛИ ЧВР+2^
 МОД ДЛБФСТ^
 ЗП ЗАГСТ+1^
 ПВ ПЛЮС1(М13)^
 ПБ ВОЗПЕЧ^
*^
ПШИФ НОП^
 СЧ ШИТП^
 СЛЦ Е1^
 ЗП ШИТП   ИТОГОВОЕ ЧИСЛО ЗАХОДОВ^
 СЧ СТАПОЛ^
 И Е8П1^
 НТЖ Е8П1^
 ПЕ ПШИФ1^
 СЧ ЗВЗД^
 ПБ ПШИФ2^
ПШИФ1 СЧ СТАПОЛ^
 СБР Е24П1^
 РЗБ ПППППП
ПШИФ2 МОД ДЛБФСТ^
 ЗП ЗАГСТ^
 ПБ ВОЗПЕЧ^
ППЗД НОП^
 СЧ СТАПОЛ+1^
 СДА 64+12^
 И Е5П1^
 ПВ ПЕРЕВ(М13)^
 СДА 64-8^
 ИЛИ ЧТЕР^
 МОД ДЛБФСТ^
 ЗП ЗАГСТ^
 ПБ ВОЗПЕЧ^
*^
ППАК НОП^
 СЧ СТАПОЛ+1^
 СДА 64+22^
 И Е5П1^
 ПВ ПЕРЕВ(М13)^
 СДА 64-8^
 ИЛИ ЧТЕР^
 МОД ДЛБФСТ^
 ЗП ЗАГСТ^
 ПБ ВОЗПЕЧ^
*^
ПВПС НОП^
 СЧ СТАПОЛ+1^
 СДА 64+17^
 И Е5П1^
 ПВ ПЕРЕВ(М13)^
 СДА 64-8^
 ИЛИ ЧТЕР^
 МОД ДЛБФСТ^
 ЗП ЗАГСТ^
 ПБ ВОЗПЕЧ^
*^
ПБУФ НОП^
 СЧ СТАПОЛ+1^
 СДА 64+27^
 И Е5П1^
 ПВ ПЕРЕВ(М13)^
 СДА 64-8^
 ИЛИ ЧТЕР^
 МОД ДЛБФСТ^
 ЗП ЗАГСТ^
 ПБ ВОЗПЕЧ^
ПЗАД НОП^
 СЧ СТАПОЛ+1^
 СДА 64+12^
 И Е5П1^
 ЗП РЯ1^
 СЧ СТАПОЛ+1^
 СДА 64+27^
 И Е5П1^
 СЛЦ РЯ1^
 ЗП РЯ1^
 СЧ СТАПОЛ+1^
 СДА 64+22^
 И Е5П1^
 СЛЦ РЯ1^
 ЗП РЯ1^
 СЧ СТАПОЛ+1^
 СДА 64+17^
 И Е5П1^
 СЛЦ РЯ1^
 ЗП РЯ1^
 СЛЦ ШИТП+1^
 ЗП ШИТП+1   ИТОГО СФОРМИРОВАНО ЗАДАЧ^
 СЧ РЯ1^
 ПВ ПЕРЕВ(М13)^
 СДА 64-8^
 ИЛИ ЧТЕР^
 МОД ДЛБФСТ^
 ЗП ЗАГСТ^
 ПБ ВОЗПЕЧ^
ПТЕР НОП^
 СЧ СТАПОЛ+1^
 СБР Е9П4^
 ЗП РЯ1^
 РЗБ СССССС
 СДА 64+24^
 ИЛИ ЧТЕР^
 МОД ДЛБФСТ^
 ЗП ЗАГСТ^
 СЧ РЯ1   НОМЕР ТЕРМИНАЛА^
 РЗБ Е6П1^
 УИ М13^
 СЧ СТАПОЛ+1^
 И Е3П1^
 УИ М16   НОМЕР МАШИНЫ^
 СЧ Е48-1(М13)^
 ИЛИ ШИТП+4-1(М16)^
 ЗП ШИТП+4-1(М16)^
 ПБ ВОЗПЕЧ^
ПМАШ НОП^
 СЧ СТАПОЛ+1^
 И Е3П1^
 ПВ ПЕРЕВ(М13)^
 СЧ РЯ2^
 СДА 64-16^
 ИЛИ ЧБЭС^
 МОД ДЛБФСТ^
 ЗП ЗАГСТ^
 ПБ ВОЗПЕЧ^
ПСНЯ НОП^
 СЧ СТАПОЛ+1^
 СДА 64+9^
 И Е3П1^
 УИ М13^
 СДА 64-1^
 УИ М7^
 СЧ ШИТП+11(М13)^
 СЛЦ Е1^
 ЗП ШИТП+11(М13)^
 СЧ ТСНЯТ(М7)^
 МОД ДЛБФСТ^
 ЗП ЗАГСТ^
 СЧ ТСНЯТ+1(М7)^
 МОД ДЛБФСТ^
 ЗП ЗАГСТ+1^
 ПВ ПЛЮС1(М13)^
 ПБ ВОЗПЕЧ^
ПРЕС НОП^
 СЧ СТАПОЛ^
 СДА 64+24^
 И Е8П1^
 ЗП РЯ1^
 СЛЦ ШИТП+2^
 ЗП ШИТП+2   ИТОГО ИСПОЛЬЗОВАНО РЕСУРСА^
 СЧ РЯ1^
 ПВ ПЕРЕВ(М13)^
 СДА 64-8^
 ИЛИ МАСЗОН
 МОД ДЛБФСТ^
 ЗП ЗАГСТ^
 ПБ ВОЗПЕЧ^
ПВРЕ НОП^
 СЧ СТАПОЛ+1^
 СДА 64+9^
 И Е3П1^
 ПО ПВРЕ0   СБОЙ ЭВМ ИЛИ СЕАНС НЕ ОКОНЧЕН^
 СЧ СТАПОЛ^
 СДА 64+40^
 ЗП ЗОНРЯ^
 ПВ ИЗ10В8(М13)^
 СЧ РЯ2^
 ЗП РЯ5   ЧАС ВХОДА^
 СЧ СТАПОЛ^
 СДА 64+32^
 И Е8П1^
 ЗП ЗОНРЯ^
 ПВ ИЗ10В8(М13)^
 СЧ РЯ2^
 ЗП РЯ6   МИНУТА ВХОДА^
 СЧ СТАПОЛ+1^
 СДА 64+40^
 ЗП ЗОНРЯ^
 ПВ ИЗ10В8(М13)^
 СЧ РЯ2^
 ЗП РЯ7   ЧАС ВЫХОДА^
 СЧ СТАПОЛ+1^
 СДА 64+32^
 И Е8П1^
 ЗП ЗОНРЯ^
 ПВ ИЗ10В8(М13)^
 СЧ РЯ2^
 ЗП РЯ8   МИНУТА ВЫХОДА^
*^
 СЧ РЯ7^
 ВЧ РЯ5^
 ПЕ ПВРЕ1  ЧАС ВХОДА > ЧАСА ВЫХОДА^
 СДА 64-24^
 СЛЦ ШИТП+3^
 ЗП ШИТП+3  ИТОГОВОЕ ВРЕМЯ В ЧАСАХ^
 СЧ РЯ8^
 ВЧ РЯ6^
 ПЕ ПВРЕ2  СЕАНС МЕНЬШЕ ЧАСА^
 СЛЦ ШИТП+3^
 ЗП ШИТП+3^
 ПБ ПВРЕ3   НА КОНТРОЛЬ ЧИСЛА МИНУТ^
ПВРЕ2 НОП^
 СЧ Ф60^
 ВЧ РЯ6^
 СЛЦ РЯ8^
 СЛЦ ШИТП+3^
 ВЧ Е25^
 ЗП ШИТП+3^
 ПБ ПВРЕ3^
ПВРЕ1 НОП^
 СЧ Ф24^
 ВЧ РЯ5^
 СЛЦ РЯ7^
 СДА 64-24^
 СЛЦ ШИТП+3^
 ЗП ШИТП+3^
 СЧ Ф60^
 ВЧ РЯ6^
 СЛЦ РЯ8^
 СЛЦ ШИТП+3^
 ЗП ШИТП+3^
ПВРЕ3 НОП^
 СЧ ШИТП+3^
 И Е24П1^
 ВЧ Ф60^
 ПЕ ПВРЕ0 МИНУТ < 60^
 СЧ ШИТП+3^
 ВЧ Ф60^
 СЛЦ Е25^
 ЗП ШИТП+3^
ПВРЕ0 НОП^
 СЧ СТАПОЛ^
 РЗБ ПППППП
 СДА 64+16^
 ЗП РЯ7^
 СДА 64+8^
 И Н377^
 ИЛИ ЧВР^
 МОД ДЛБФСТ^
 ЗП ЗАГСТ^
 СЧ РЯ7^
 СБР Е16П1^
 ЗП РЯ8^
 СЧ СТАПОЛ+1^
 РЗБ ПППППП
 СДА 64+16^
 ЗП РЯ7^
 СДА 64+16^
 ИЛИ РЯ8^
 ИЛИ ЧВР+1^
 МОД ДЛБФСТ^
 ЗП ЗАГСТ+1^
 СЧ РЯ7^
 СБР Е16П1^
 СДА 64+8^
 ИЛИ ЧВР+2^
 МОД ДЛБФСТ^
 ЗП ЗАГСТ+2^
 ПВ ПЛЮС1(М13)^
 ПВ ПЛЮС1(М13)^
 ПБ ВОЗПЕЧ^
*^
*^
*     ПОДДИРЕКТИВЫ  УСТАНОВКИ^
*^
УВРЕ НОП^
 УИА УСВРЕ(М6)^
 ПБ УВД^
УДАТ НОП^
 УИА УСДАТ(М6)^
 ПБ УВД^
*^
*     ОБЩАЯ ЧАСТЬ УВРЕ  УДАТ^
*^
УВД НОП^
 ПВ НАЙРЗД(М15)^
 ПВ ВРЕДАТ(М15)^
 СЧ БЮДЖП^
 ЗП (М6)^
 СЧ РЯВС^
 НТЖ МИН^
 ПЕ КОНУСТ^
 ПВ ВС(М13)^
 ПВ ВРЕДАТ(М15)^
 СЧ БЮДЖП^
 ЗП 1(М6)^
 ПБ КОНУСТ^
ВРЕДАТ НОП^
 ПВ ПРЧСТ(М13)^
 СЧ РЯВС^
 ЗП ЗОНРЯ^
 УИА -1(М5)^
 ПВ ЧИСЛА(М13)^
 СЧ ЗОНРЯ^
 ЗП БЮДЖП^
 СЧ РЯВС^
 НТЖ ТЧК^
 ПЕ УСТОШ^
 ПВ ВС(М13)^
 ПВ ПРЧСТ(М13)^
 СЧ РЯВС^
 ЗП ЗОНРЯ^
 УИА -1(М5)^
 ПВ ЧИСЛА(М13)^
 СЧ БЮДЖП^
 СДА 64-8^
 ИЛИ ЗОНРЯ^
 ЗП БЮДЖП^
 ПБ (М15)^
*^
*^
УАЦП НОП^
 Э62 '76'^
 СЧ ВСЕЕД^
 ПВ ВС(М13)^
 ПБ КОНУСТ^
УЗАД НОП^
 УИА -1(М5)^
 УИА УСЗАД(М6)^
РЕСЗАД ПВ НАЙРЗД(М15)^
 ПВ ПРЧСТ(М13)^
 СЧ РЯВС^
 ЗП ЗОНРЯ^
 ПВ ЧИСЛА(М13)^
 ПВ ИЗ10В8(М13)^
 СЧ РЯ2^
 ЗП (М6)^
 ПБ КОНУСТ^
УРЕС НОП^
 УИА -2(М5)^
 УИА УСРЕС(М6)^
 ПБ РЕСЗАД   НА ОБЩУЮ ЧАСТЬ^
УШИФ НОП^
 ПВ ШИФНОВ(М15)^
 ПО УСТОШ^
 СЧ ШФР^
 ЗП УСШИФ^
 ПБ КОНУСТ^
*^
УСИС НОП^
 СЧ ВСЕЕД^
 ЗП УСГРУ^
 ЗП УСОТД^
 СЧ Е8П1^
 ЗП УСШИФ^
 ПВ ВС(М13)^
 ПБ КОНУСТ^
*^
УБОБ НОП^
 УИА -3(М5)^
 УИА УСБОБ(М6)^
УУУ ПВ НАЙРЗД(М15)^
 ПВ ПРЧСТ(М13)^
 СЧ РЯВС^
 ЗП ЗОНРЯ^
 ПВ ЧИСЛА(М13)^
 ЗП (М6)^
 ПБ КОНУСТ^
*^
УГРУ НОП^
 СЧ ВСЕЕД^
 ЗП УСШИФ^
 ЗП УСОТД^
 УИА УСГРУ(М6)^
 УИА -3(М5)^
 ПБ УУУ^
*^
УОТД НОП^
 СЧ ВСЕЕД^
 ЗП УСГРУ^
 ЗП УСШИФ^
 УИА УСОТД(М6)^
 УИА -1(М5)^
 ПБ УУУ^
*^
УНАЧ НОП^
 ПВ УСТФЛГ(М13)^
 ПВ ВС(М13)^
 ПБ КОНУСТ^
*^
УТЕР НОП^
 ПВ НАЙРЗД(М15)^
 ПВ ПРЧСТ(М13)^
 СЧ РЯВС^
 ЗП УСТЕР^
 ПВ ВС(М13)^
 ВЧОБ П11^
 ПЕ КОНУСТ^
 СЧ УСТЕР^
 СДА 64-8^
 ИЛИ РЯВС^
 СБР Т377^
 СДА 64+40^
 И Е6П1^
 ЗП УСТЕР^
 ПВ ВС(М13)^
 ПБ КОНУСТ^
*^
УМАШ НОП^
 ПВ НАЙРЗД(М15)^
 ПВ ПРЧСТ(М13)^
 СЧ РЯВС^
 ЗП УСМАШ^
 ПВ ВС(М13)^
 ПБ КОНУСТ^
*^
УСНЯ НОП^
 ПВ НАЙРЗД(М15)^
 ПВ ПРЧСТ(М13)^
 СЧ РЯВС^
 ЗП УССНЯ^
 ПВ ВС(М13)^
 ПБ КОНУСТ^
КОНУСТ НОП^
 СЧ РЯВС^
 НТЖ ОООООП
 ПО КНУСТ1^
 СЧ РЯВС^
 НТЖ Е8П1^
 ПО СТАТ0^
 СЧ РЯВС^
 НТЖ ЗПТ   РАЗДЕЛИТЕЛЬ^
 ПВ НАЙПРО(М15)^
 ПБ ВОЗУСТ^
КНУСТ1 ПВ ВС(М13)^
 ПБ КОНУСТ^
*^
*^
*^
КНЦ НОП       С О П Л Я^
 ПБ КОНЕЦ^

*
*
*23456789012345678901234567890123456789012345678901234
******************************************************
*                                                    *
*      Д И А Л О Г О В А Я    С И С Т Е М А          *
*                  К  Р  А  Б                        *
*                                                    *
*----------------------------------------------------*
*  БЮДЖЕТНО-СТАТИСТИЧЕСАЯ ПОДСИСТЕМА.        *
*  Ч А С Т Ь  1                              *
*  ОБЩИЕ БЛОКИ                               *
**********************************************
*
*
*$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
*$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
*
ВХОД НОП , НАЧАЛЬНЫЙ ВХОД В  Б С С
*
*$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
*$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
*
 Э70 ЧТГП2
 Э70 ЧТГП3
 Э70 ЧТГП4
*
 СЧ РАБФИО
 ПЕ ВХОД01  МОЖНО РАБОТАТЬ С КАТ.ФИО
*
*  ЗАПРЕЩЕНО РАБОТАТЬ С КАТАЛОГОМ ФИО
*
 СЧ М40377    ЗАБИВАЕМ ВСЕ О ФИО
 ЗП ЗГБОК0  В ЗАГОЛОВКЕ,
 ЗП БЮДОК0  СТРОКЕ БЮДЖЕТА,
 ЗП РЗБОК0  СТРОКЕ РАЗДЕЛИТЕЛЕЙ
 СЧ БЕЗФИО
 ЗП ИОК0    И СТРОКЕ ИТОГА
 ПБ ВХОД02
*
ВХОД01 НОП , ДОЗАКАЗ МД  2053 ДЛЯ ФИО
 СЧ МДОСДД
 ИЛИ КЗ2053
 Э50 '131'  ДОЗАКАЗ МД ОС
 ПЕ КОНЕЦ
 СЧ ПАРОС
 И Е12П1
 ИЛИ Е15
 УИ М15
 СЧ МДОСДД
 Э50 '121'  ПАРОЛЬ ЧТЕНИЯ НА МД ОС
 СЧ ПАРОС
 СДА 64+12
 И Е12П1
 УИ М15
 СЧ МДОСДД
 Э50 '121'   ПАРОЛЬ ЗАПИСИ НА МД ОС
*
ВХОД02 НОП
 СЧ ПАРОЛЬ
 СДА 64+24
 УИ М15
 СЧ ЧТКЛЮ1
 Э50 '105'
 ЗП ОСНБОБ
 Э50 '121'
 Э71 ВСЕЕД
 НЕД
 И Е6П1
 ИЛИ ИССОСТ
 ЗП ИССОСТ
 И Е6П1
 СДА 64-12
 ЗП РЯ1
 ИЛИ ИСПР
 ЗП ИСПР
 ИЛИ Е38
 ИЛИ Е39
 ЗП ИСПРБ
 СЧ РЯ1
 ИЛИ ИСВЫ
 ЗП ИСВЫ
 УИА ЗАГБСС(М1)
 ПВ ВЫВОД(М17)
 Э50 '100'
 СДА 64+24
 ЗП РАБШИФ   КТО РАБОТАЕТ
 ЗП ШФР
 СЧ ПРИЗНП
 ПЕ НАЧ3  ПЗ
 ПВ ПОЛКЛЮ(М13)
 СЧ РЯДУР
 ПЕ НАЧ1
 УИА НЕЗНКМ(М1)
 ПВ ВЫВОД(М17)
 ПБ КОНЕЦ
НАЧ1 СЧ БЮДЖП+2
 СДА 64+32
 ЗП РАБКЛЮ
 ПО НАЧ3
 СЧ КЛЮЧ
 ЗП МАСПР
 УИА -1(М14)
НАЧ2 УИА МАСПР(М1)
 УИА МАСПР+1(М2)
 Э71 ИСПРБ
 ПВ ОПРОС(М17)
 СЧ МАСПР
 СБР КОНСШФ
 СДА 64+32
 НТЖ РАБКЛЮ
 ПО НАЧ3
 СЧ КЛЮЧ
 ЗП МАСПР
 ЦИКЛ НАЧ2(М14)
 ПБ КОНЕЦ
НАЧ3 НОП
 ПВ ПЕЧЗАГ(М13)
*
РАЗД1 НОП
 СЧ ВБОЛЬ
 ЗП РЯБОЛЬ
 УИА -3(М5)
*
РАЗД2 НОП
 ПВ ПРИЕМ(М13)
 ПВ ПРИГЛ(М15)
 ПВ НАЙПРО(М15)
 СЧ РЯВС
 НТЖ Е8П1
 ПО РАЗД9А
*
 ПВ ВЫБДИР(М17)
 УИА -4(М7)
РАЗД9 СЧ СТАРТП+4(М7)
 НТЖ ТЕКДИР
 И МАСДИР
 ПО СТАРТП+4(М7)
 ЦИКЛ РАЗД9(М7)
 ПВ ОШВД(М13)
*
РАЗД9А НОП
 ЦИКЛ РАЗД2(М5)
 ПБ КОНЕЦ
*
*     ПЕРЕКЛЮЧАТЕЛЬ СТАРТОВЫХ ДИРЕКТИВ
*
СТАРТП ПБ БЛСТАТ
 КОНК П'СТА'
 ПБ БЛОБЩ
 КОНК П'БЮД'
 ПБ УСТШАГ
 КОНК П'ШАГ'
 ПБ КОНЕЦ
 КОНК П'КОН'
*
*
*     ДИРЕКТИВА УСТАНОВКИ ШАГА ВЫДАЧИ
*
*
УСТШАГ НОП
 ПВ ПОДГОТ(М13)
 ПВ НАЙПРО(М15)
 СЧ РЯВС
 НТЖ Е8П1
 ПО ПОКШАГ
 ПВ ПРОЧИС(М13)
 СЧ РЯВС
 ЗП ЗОНРЯ
 УИА -1023(М5)
 ПВ ЧИСЛА(М13)
 ПВ ИЗ10В8(М13)
 СЧ РЯ2
 ПО ПОКШАГ
 ВЧОБ Ф15
 ПЕ ОШШАГ    ШАГ > 15
 СЧ РЯ2
 ВЧОБ Е1
 ЗП ШАГ
 ПБ РАЗД2
ПОКШАГ НОП
 СЧ ШАГ
 ВЧОБ 0
 СЛЦ Е1
 ПВ ПЕРЕВ(М13)
 ЗП РЯ1
 СЧ ШАГРАВ
 И Н16П1
 ИЛИ РЯ1
 ЗП ШАГРАВ
 УИА ШАГРАВ(М1)
 ПВ ВЫВОД(М17)
 ПБ РАЗД2
ОШШАГ НОП
 УИА ШАГ15(М1)
 ПВ ВЫВОД(М17)
 ПБ РАЗД2
*
*
*       ОБЩИЕ  П О Д П Р О Г Р А М М Ы
*
*
*
*     ПЕЧАТЬ ЗАГОЛОВКА НА АЦПУ
*
ПЕЧЗАГ НОП
 УИА ЗАГБСС+1(М1)
 Э64 ИНФВЫВ
 УИА ВСЕ214(М1)
 Э64 ИНФВЫВ
 ПБ (М13)
*
*
НАЙПРО НОП
 СЧ РЯВС
 НТЖ Е8П1
 ПО (М15)  ЭТО "ЕТХ"
*
НАЙПР1 НОП
 ПВ ВС(М13)
 НТЖ ОООООП
 ПО НАЙПР1
 ПБ (М15)
*
*
НАЙРЗД НОП
*
*    П.П. ПРОПУСКА ПРОБЕЛОВ И РАЗДЕЛИТЕЛЕЙ
*    РАЗДЕЛИТЕЛЕМ МОЖЕТ БЫТЬ:  '-' '='
*    ВОЗВРАТ ПО М15
*
 ПВ ВС(М13)
 НТЖ ОООООП
 ПО НАЙРЗД
 СЧ РЯВС
 НТЖ РАВ
 ПО НАЙРЗ1   ПРОПУСКАЕМ ПРОБЕЛЫ ПОСЛЕ РАЗДЕЛИТЕЛЯ
 СЧ РЯВС
 НТЖ МИН
 ПО НАЙРЗ1
 СЧ РЯВС
 НТЖ ДВТЧК
 ПЕ (М15) ВОЗВРАТ
НАЙРЗ1 ПВ ВС(М13)
 НТЖ ОООООП
 ПО НАЙРЗ1
 ПБ (М15)
*
*
ГАСИТГ НОП
*
*    ГАСИМ ШКАЛУ ИТОГОВЫХ ПАРАМЕТРОВ
*
*
 УИА -22(М1)
 СЧ
ГАСИТ0 ЗП ШИТП+22(М1)
 ЦИКЛ ГАСИТ0(М1)
 ПБ (М13)
*
*
ШИФНОВ НОП
 СЧИ М15
 ЗП РЯ11
 ПВ НАЙРЗД(М15)
 СЧ РЯ11
 УИ М15
ШНОВ2 УИА -5(М5)
 СЧ Е8П1
 ЗП МАСКА
 СЧ РЯВС
 ЗП ЗОНРЯ
 ВЧОБ П11
 ПО ШНОВ0
ШНОВ1 СЧ
 ПБ (М15)     ВОЗВРАТ С 0 - М СМ ,ОШИБКА
ШНОВ0 ПВ ВС(М13)
 ВЧОБ П11
 ПЕ ШНОВ3   НА КОНЕЦ
 СЧ ЗОНРЯ
 СДА 64-8
 ИЛИ РЯВС
 ЗП ЗОНРЯ
 СЧ МАСКА
 СДА 64-8
 ИЛИ Е8П1
 ЗП МАСКА
 ЦИКЛ ШНОВ0(М5)
 ПБ ШНОВ1   ОШИБКА
ШНОВ3 СЧ ЗОНРЯ
 СБР МАСКА
 СБР ПППППП
 СДА 64+24
 ЗП ШФР
 СЧ ВСЕЕД
 ПБ (М15)
КОНЕЦ НОП
 СЧ УПРКЛЮ
 ПО Э74
 ПВ УПР(М17)
Э74 СЧ
 Э74
УПВС НОП
 СЧИ М1
 ЗП ИР1
 СЧИ М2
 ЗП ИР2
 ПБ (М13)
ВВС НОП
 СЧ ИР1
 УИ М1
 СЧ ИР2
 УИ М2
 ПБ (М13)
ВС СЧ (М2)
 СД R+5(М1)
 РЗБ Е8П1
 ЗП РЯВС
 ЦИКЛ ВС2(М1)
 СЛИА 1(М2)
 УИА -5(М1)
ВС2 ПБ (М13)
*
*
ЗС НОП
*
*    ЗАПИСЬ ОЧЕРЕДНОГО БАЙТА НА БУФЕР
*
 СБР Е8П1
 РЗБ КОНСЗС+5(М5)
 ЗП РЯЗС
 СЧ (М6)
 ИЛИ КОНСЗС+5(М5)
 НТЖ КОНСЗС+5(М5)
 ИЛИ РЯЗС
 ЗП (М6)
 ЦИКЛ ЗС2(М5)
 СЛИА 1(М6)
 УИА -5(М5)
ЗС2 ПБ (М13)
ПРОЧИС НОП
 СЧ РЯВС
 ВЧОБ П11
 ПЕ ОШИФР
 ПБ (М13)
ВЫВОД НОП
 СЧ РЕЖВЫВ
 И Е1
 ПО ВЫВОДА  ЗАПРЕЩЕН ВЫВОД НА ТЕРМИНАЛ
ВЫВТЕР Э71 ИСВЫ
 Э71 ИССОСТ
 И Е2526
 НТЖ Е2526
 ПО КОНЕЦ
ВЫВОДА НОП
 СЧ РЕЖВЫВ
 И Е2
 ПО (М17)   ЗАПРЕЩЕН ВЫВОД НА АЦПУ
 Э64 ИНФВЫВ
 ПБ (М17)
*
*************************************************
*
ПРОТЯГ НОП , ПРОТЯЖА ПУСТЫХ СТРОК ПРИ ВЫДАЧЕ
*            БЮДЖЕТНЫХ СВОДОК НА АЦПУ
*
 СЧ РЕЖВЫВ
 И Е2
 ПО (М17)  НЕТ ВЫВОДА НА АЦПУ
*
 Э64 ИНФ64П
 ПБ (М17)  ПРОТЯНУЛИ
*
*
ПРИГЛ НОП
*
*    ПРОВЕРКА НА ПОСЫЛКУ С ПРИГЛАШЕНИЕМ
*
 УИА МАСПР(М2)
 УИА -5(М1)
 УИА -3(М3)
ПРИГЛ1 ПВ ВС(М13)
 НТЖ БОЛЬШЕ
 ПО (М15)  НАЧАЛО ТЕКСТА ДИРЕКТИВЫ
 ЦИКЛ ПРИГЛ1(М3)
 СЧ 0
 ЗП РЯВС
 УИА МАСПР(М2)
 УИА -5(М1)
 ПБ (М15)
*
*
ПАМВС НОП
 СЧИ М2
 ЗП КВС2
 СЧИ М1
 ЗП КВС1
 ПБ (М13)
*
*
ПОДГОТ СЧ R+6
 ЗП РЯ1
 УИА -1(М4)
 УИА -7(М3)
 СЧ КВС1
 УИ М1
 СЧ КВС2
 УИ М2
 ПБ (М13)
*
*
ПЕРЕВ УИА -5(М16) ПЕРЕВОД ИЗ 8 В 10
 ЗП РЯ1
ПЕРЕВ1 СДА 64+8
 ЗП РЯ2
 СЧ РЯ1
 УМН К1
 ЗП РЯ1
 СЧМР 64
 УМН К1+1
 СДА 64-40
 ИЛИ РЯ2
 ЦИКЛ ПЕРЕВ1(М16)
 ПБ (М13)
*
*
ОПРОС Э71 ИССОСТ
 И Е2526
 НТЖ Е2526
 ПО КОНЕЦ
 ПБ (М17)
*
*
ИЗ10В8 УИА 20(М16) ПЕРЕВОД ИЗ10 В 8
 УИА (М15)
 СЧ 0
 ЗП РЯ2
ПВЧ СЧ ЗОНРЯ
 СДА 64(М16)
 ЗП РЯ3
 СЧМР 0
 СДА 64+28(М15)
 ЗП ЗОНРЯ
 СЧ РЯ3
 СЛЦ РЯ2
 ЗП РЯ2
 МОДА (М13)
 ПИО (М16)
 СЛИА -4(М16)
 СЛИА 4(М15)
 СЧ РЯ2
 УМН К1+1
 СЧМР 64
 ЗП РЯ2
 ПБ ПВЧ
*
*
ОШДИР НОП  ОШ В ДИР
 УИА ТЕКСТО(М1)
 ПВ ВЫВОД(М17)
 ПБ (М13)
НЕТДИР НОП  НЕТ ДИРЕК
 УИА ТНДИР(М1)
 ПВ ВЫВОД(М17)
 ПБ (М13)
НЕТБДЖ НОП НЕТ БЮД.
 УИА НЕТБЮД(М1)
 ПВ ВЫВОД(М17)
 ПБ (М13)
МАЛБЮД НОП
 УИА НЕЛЬЗЯ(М1)
 ПВ ВЫВОД(М17)
 ПБ (М13)
*
************************************************
*
ВЫБДИР НОП , ВЫБОРКА ДИРЕКТИВЫ С БУФЕРА ПРИЕМА.
*
* ПАРАМЕТРЫ : РЯВС - ПЕРВЫЙ СИМВОЛ ДИРЕКТИВЫ
* РЕЗУЛЬТАТ : ТЕКДИР  Р:1-24 ИМЯ ДИР-ВЫ.
*           : МАСДИР  Р:1-24 МАСКА ДЛЯ ПОИСКА ДИР.
************************************************
*
 СЧ РЯВС
 ЗП ТЕКДИР
 СЧ Е8П1
 ЗП МАСДИР
*
 УИА -1(М3)  ЕЩЕ НЕ БОЛЕЕ 2-Х СИМВОЛОВ
ВЫБД0 НОП
 ПВ ВС(М13)
 НТЖ Е4П1
 ПО ВЫБД1  КОНЕЦ ДИР-ВЫ ПО ПРОБЕЛУ
 СЧ РЯВС
 НТЖ Е8П1
 ПО ВЫБД1  КОНЕЦ ДИР-ВЫ ПО "ЕТХ"
*
 СЧ ТЕКДИР
 СДА 64-8
 ИЛИ РЯВС
 ЗП ТЕКДИР
 СЧ МАСДИР
 СДА 64-8
 ИЛИ Е8П1
 ЗП МАСДИР
*
 ЦИКЛ ВЫБД0(М3)
 ПБ (М17)  ВОЗВРАТ
*
ВЫБД1 НОП ,  КОНЕЦ ДИР-ВЫ, НО НЕТ 3-Х СИМВОЛОВ
 СЧ ТЕКДИР
 СДА 64-8
 ЗП ТЕКДИР
 СЧ МАСДИР
 СДА 64-8
 ЗП МАСДИР
*
 ЦИКЛ ВЫБД1(М3)
 ПБ (М17)  ВОЗВРАТ
*
*+++++++++++++++++++++++++++++++++++++++++++++++++++++
*
ПРИЕМ СЧ РЯБОЛЬ
ПРИЕМ1 ЗП МАСПР
 УИА МАСПР(М1)
 Э71 ИСПР
 Э71 ИССОСТ
 И Е2526
 НТЖ Е2526
 ПО КОНЕЦ
 ПБ (М13)
*
*+++++++++++++++++++++++++++++++++++++++++++++++++++++
*
ЗАХВАТ НОП  ЗАХВАТ МД
 СЧ
 ЗП ФЛЗАХ   ДЛЯ ЛОВЛИ ЗАХВАТА
 СЧ ОСНБОБ
 ЗП БОБИНА
 Э50 '115'
 ПБ (М13)
*
ФЛЗАХ КОНД В'377'    С   О   П  Л  Я  !!!!
*
*+++++++++++++++++++++++++++++++++++++++++++++++++++++
*
ОСВОБ НОП  ОСВОБОЖДЕНИЕ МД
 СЧ ВСЕЕД
 ЗП ФЛЗАХ
 СЧ БОБИНА
 Э50 '116'
 ПБ (М13)
*
*+++++++++++++++++++++++++++++++++++++++++++++++++++++
*
ХВАТЬ НОП  ЗА ДИСКИ
 СЧ
 ЗП ФЛЗАХ   ДЛЯ ЛОВЛИ ЗАХВАТА
 МОД РАБАРХ
 СЧ ТАРМДК-1
 И Е16П1
 ЗП БОБИНА
 Э50 '115'
 ПБ (М13)
*
*+++++++++++++++++++++++++++++++++++++++++++++++++++++
*
ЗАХ253 НОП ,    ЗАХВАТ МД ОС
 СЧ
 ЗП ФЛЗАХ  ДЛЯ ЛОВЛИ ЗАХВАТА
 СЧ МДОС
 ЗП БОБИНА
 Э50 '115'
 ПБ (М13)
*
*+++++++++++++++++++++++++++++++++++++++++++++++++++++
*
ЧКТАРХ НОП ,  П/П ЧТЕНИЕ  КАТАЛОГА АРХИВА.
*             АДРЕС  ВОЗВРАТА  НА  М16
 СЧИ М16
 ЗП RЕТ
 Э70 ЧТКТ1
 Э70 ЧТКТ2
 МОД RЕТ
 ПБ ,  ВОЗВРАТ
*
*+++++++++++++++++++++++++++++++++++++++++++++++++++++
*
ЗКТАРХ НОП ,  П/П ЗАПИСЬ  КАТАЛОГА АРХИВА.
*             АДРЕС  ВОЗВРАТА  НА  М16
 СЧИ М16
 ЗП RЕТ
 Э70 ЗПКТ1
 Э70 ЗПКТ2
 МОД RЕТ
 ПБ ,  ВОЗВРАТ
*
*+++++++++++++++++++++++++++++++++++++++++++++++++++++
*
ТДАНЕТ КОНД М8П'НЕТДА'В'377'    ТЕКСТ КЛЮЧЕВЫХ СЛОВ
*
*
*
ДАНЕТ НОП
*    РАСПОЗНОВАНИЕ  КЛЮЧЕВЫХ СЛОВ (ДА,НЕТ И Т.П)
*
*
*       ВХОД:  М А С П Р   БУФЕР С КЛЮЧЕВЫМ СЛОВОМ
*
*      ВЫХОД:  СОДЕРЖИМОЕ СУМАТОРА:
*              0  -   Н Е Т ;
*              1  -   Д А ;
*              2  -   'Е Т Х' ;
*              4  -   СЛОВО НЕ ИДЕНТИФИЦИРОВАНО
*
*      ИСПОЛЬЗУЮТСЯ РЕГИСТРЫ:
*             М16  -  АДРЕС ВОЗВРАТА
*       М1,М2,М13  -  БЛОК  В С
*              М3  -  РАБОЧИЙ
*
*
ДАНЕТ0 НОП
 ПВ ВС(М13)
 НТЖ ОООООП
 ПО ДАНЕТ0 ПРОПУСК ПРОБЕЛОВ
 СЧ РЯВС
 ЗП ТЕКДИР  НАКОПЛЕНИЕ ТЕКСТА  КЛЮЧЕВОГО СЛОВА
 НТЖ Е8П1
 ПО ВЫХЕТХ    ВЫХОД ПО ПУСТОЙ ПОСЫЛКЕ
 СЧ Е8П1
 ЗП ТЕКМАС
 УИА -1(М3)
ДАНЕТ1 ПВ ВС(М13)
 НТЖ Е8П1
 ПО ДАНЕТ3   ЗАКОНЧЕН ПРИЕМ КЛЮЧЕВОГО СЛОВА
 СЧ ТЕКДИР
 СДА 64-8
 ИЛИ РЯВС
 ЗП ТЕКДИР
 СЧ ТЕКМАС
 СДА 64-8
 ИЛИ Е8П1
 ЗП ТЕКМАС
 ЦИКЛ ДАНЕТ1(М3)
*
ДАНЕТ3 СЧ ТДАНЕТ    'НЕТДА'377'
 СБР Е24П1
 РЗБ ТЕКМАС
 НТЖ ТЕКДИР
 ПО ВЫХДА   КЛЮЧЕВОЕ СЛОВО  Д А
 СЧ ТДАНЕТ
 РЗБ ТЕКМАС
 НТЖ ТЕКДИР
 ПО (М16)
 СЧ Е3
 ПБ (М16)   КЛЮЧ НЕ ОПОЗНАН
ВЫХДА СЧ Е1
 ПБ (М16)
ВЫХЕТХ СЧ Е2
 ПБ (М16)
*
*
*    БЛОК ПЕРЕКЛЮЧЕНИЯ РЕЖИМА ВЫДАЧИ
*
*
*    ИСПОЛЬЗУЮТСЯ:
*          М1,М3,М17;
*          М13 - АДРЕС ВОЗВРАТА
*
ЭКР НОП
 УИ М3   ПРИЗНАК РАБОТЫ
 СЧ Е1
 ЗП РЯ1
 СЧИ М3
 НТЖ Е3
 ПО ВЫДРЖ  ОШИБКА В ЗАДАНИИ РЕЖИМА
 ПБ ПРЖВЫВ(М3) НА ПЕРЕКЛЮЧАТЕЛЬ РЕЖИМОВ ВЫВОДА
*
*
АЦП НОП
 УИ М3   ПРИЗНАК РАБОТЫ
 СЧ Е2
 ЗП РЯ1
 СЧИ М3
 НТЖ Е3
 ПО ВЫДРЖ  ОШИБКА В ЗАДАНИИ РЕЖИМА
 ПБ ПРЖВЫВ(М3) НА ПЕРЕКЛЮЧАТЕЛЬ РЕЖИМОВ ВЫВОДА
*
*
ПРЖВЫВ ПБ ЗАПРЖ  ЗАПРЕТ ВЫВОДА НА УСТ-ВО
ПРЖ1 ПБ РАЗРЖ  РАЗРЕШЕНИЕ ВЫВОДА НА УСТРОЙСТВО
ПРЖ2 ПБ ВЫДРЖ  ВЫВОД РЕЖИМА УСТРОЙСТВА
*
ЗАПРЖ НОП
 СЧ РЕЖВЫВ ШКАЛА РЕЖИМОВ ВЫВОДА
 ИЛИ РЯ1
 НТЖ РЯ1
 ЗП РЕЖВЫВ
 ПБ (М13)
*
РАЗРЖ НОП
 СЧ РЕЖВЫВ
 ИЛИ РЯ1
 ЗП РЕЖВЫВ
 И Е2
 ПО (М13)  ЭТО НЕ "АЦП ДА"
 СЧ 0
 Э62 '76'
 ПБ (М13)
*
ВЫДРЖ НОП
 УИА ЗАПВЫВ(М1)
 СЧ РЕЖВЫВ
 И РЯ1
 ПЕ ВЫДРЖ0   НА ВЫВОД ДИАГНОСТИКИ
 УИА РАЗВЫВ(М1)
ВЫДРЖ0 ПВ ВЫВОД(М17)
 ПБ (М13)
*
*
*
КТОАДМ НОП
*
*    ЗАПРОС ПАРОЛЯ АДМИНИСТРАТОРА
*    ВЫХОД ПО М10
*    РЯ11  -  НОМЕР ОТДЕЛА
*    ПОСЛЕ ВЫХОДА НА СМ:*    0 - ВСЕ ХОРОШО
*   <>0 -  ПАРОЛЬ НЕ ПОДТВЕРЖДЕН
*
 Э70 ЧТСЛЗО
 МОД РЯ11
 СЧ ТПАРБ
 ПО НОЛПАР
 ЗП РАБКЛЮ
 СЧ ББОЛЬ
 ЗП РЯБОЛЬ
 СЧ ТПАР
 ЗП МАСПР
 СЧ ТПАР+1
 ЗП МАСПР+1
 УИА -1(М14)
ББ2 УИА МАСПР(М1)
 УИА МАСПР+1(М2)
 Э71 ИСПРБ
 ПВ ОПРОС(М17)
 СЧ МАСПР
 ЗП ЗОНРЯ
 ПВ ИЗ10В8(М13)
 СЧ РЯ2
 ЗП МАСПР
 НТЖ РАБКЛЮ
 ПО (М10)
 СЧ ТПАР
 ЗП МАСПР
 СЧ ТПАР+1
 ЗП МАСПР+1
 ЦИКЛ ББ2(М14)
 ПБ (М10)
*
*+++++++++++++++++++++++++++++++++++++++++++++++++++
*
УПР НОП ,  УПОРЯДОЧИВАНИЕ КЛЮЧЕЙ
*
 СЧ УПРКЛЮ
 ПО (М17)
 УИА -3(М3)
УПР16 Э70 ЧКЛЮ+3(М3)
 ЦИКЛ УПР16(М3)
 УИА -2(М2)
УПР1 СЛИА 2(М2)
 СЧИ М2
 НТЖ Ф4096
 ПО УПРКОН  ВСЕ УПОРЯДОЧЕНО
 СЧ КЛЮЧИ(М2)
 НТЖ ВСЕЕД
 ПО УПРКОН
 СЧ КЛЮЧИ(М2)
 ЗП РЯ2
 И Е24П1
 ЗП РЯ4   ШИФР
 СЧ КЛЮЧИ+1(М2)
 ЗП РЯ3
 УИИ М3(М2)
УПР2 СЛИА 2(М3)
 СЧИ М3
 НТЖ Ф4096
 ПО УПР1   НА СЛЕДУЮЩУЮ ПАРУ
 СЧ КЛЮЧИ(М3)
 НТЖ ВСЕЕД
 ПО УПР1
 СЧ КЛЮЧИ(М3)
 И Е24П1
 НТЖ РЯ4
 ПЕ УПР22 НЕ "ДВОЙНИК"
 ЗП КЛЮЧИ(М3)
 ЗП КЛЮЧИ+1(М3)
 ПБ УПР2
УПР22 НОП
 СЧ КЛЮЧИ(М3)
 И Е24П1
 ВЧ РЯ4
 ПО УПР2  КОНЕЦ МАЛОГО ЦИКЛА
 СЧ КЛЮЧИ(М3)
 ЗП КЛЮЧИ(М2)
 СЧ КЛЮЧИ+1(М3)
 ЗП КЛЮЧИ+1(М2)
 СЧ РЯ2
 ЗП КЛЮЧИ(М3)
 СЧ РЯ3
 ЗП КЛЮЧИ+1(М3)
*  ВЫНЕСТИ В П.П.
 СЧ КЛЮЧИ(М2)
 ЗП РЯ2
 И Е24П1
 ЗП РЯ4
 СЧ КЛЮЧИ+1(М2)
 ЗП РЯ3
 ПБ УПР2
УПРКОН НОП
 УИА -2(М2)
 УИА -2(М3)
УПР9 СЛИА 2(М3)
 СЧИ М3
 НТЖ Ф4096
 ПО УПР11   НА ЗАПОЛНЕНИЕ КОНЦА ЗОН
 СЧ КЛЮЧИ(М3)
 И Е24П1
 ПО УПР9   НУЛЕВОЙ КЛЮЧ
 ЗП РЯ3
 СДА 64+8
 ПЕ УПР90
 СЧ РЯ3
 НТЖ Е8П1
 ПЕ УПР9
УПР90 НОП
 СЧ КЛЮЧИ(М3)
 НТЖ ВСЕЕД
 ПО УПР11
 СЛИА 2(М2)
 СЧ КЛЮЧИ(М3)
 ЗП КЛЮЧИ(М2)
 СЧ КЛЮЧИ+1(М3)
 ЗП КЛЮЧИ+1(М2)
 ПБ УПР9
УПР11 СЛИА 2(М2)
 СЧИ М2
 НТЖ Ф4096
 ПО УПР12  НА ЗАПИСЬ ЗОН КЛЮЧЕЙ
 СЧ КЛЮЧИ(М2)
 НТЖ ВСЕЕД
 ПО УПР12  НА ЗАПИСЬ ЗОН КЛЮЧЕЙ
 СЧ ВСЕЕД
 ЗП КЛЮЧИ(М2)
 ЗП КЛЮЧИ+1(М2)
 ПБ УПР11
УПР12 НОП
 УИА -3(М3)
УПР17 Э70 ЗКЛЮ+3(М3)
 ЦИКЛ УПР17(М3)
 Э70 ЧТСЛЗО
 ПВ ЧКТАРХ(М16)
 СЧ
 ЗП УПРКЛЮ
 ПБ (М17)
